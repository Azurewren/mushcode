@@ NOTE: This is a heavily modified Myrddin's BBS 4.0.6. It has been considerably cleaned-up and optimized for speedier responses,
@@ neater appearance, and pueblo support. It requires the Core code suite to function.
@@ The +bbnext is from Multiverse Crisis MUSH. It does not ignore board locks.

@switch/inline isdbref(u(bbs))=0,{@tel create(Bulletin Board System <BBS>)=config(master_room)}
&bbs u(coi)=locate(config(master_room),Bulletin Board System <BBS>,TXxi)
@parent u(bbs)=u(coi)
@set u(bbs)=WIZARD !NO_COMMAND

@switch/inline isdbref(u(bbp))=0,{@switch/inline isdbref(u(bbs))=1,{@tel create(bbpocket)=u(bbs)}}
&bbp u(coi)=locate(u(bbs),bbpocket,TXxi)
@tel u(bbp)=u(bbs)
@set u(bbp)=SAFE WIZARD

@lock u(bbp)=me
@Desc u(bbp)=iter(setdiff(lattr(me),Desc),ljust(%i0,18))
&BBTIME u(bbp)=extract(time(), 2, 2) [last(time())]
&GET_GROUP u(bbp)=switch(isnum(%0),1,extract(v(groups),%0,1),locate(u(bbp),%0,ni))
&NXT_MESS u(bbp)=1
&VALID_GROUPS u(bbp)=iter(v(groups),switch(and(u(%i0/can%1,%0),not(member(get(%0/bb_omit),%i0))),1,%i0))
&ISGROUPREAD u(bbp)=switch(setdiff(%1,get(%0/bb_read)),,,U)
&UNREADNUMS u(bbp)=sort(iter(setdiff(get(%1/mess_lst),get(%0/bb_read)),member(get(%1/mess_lst),%i0)))
&FN_MAKELIST u(bbp)=map(me/fn_maplist,secure(%0))
&FN_MAPLIST u(bbp)=switch(strmatch(%0,?*-*),1,lnum(before(%0,-),after(%0,-)),%0)
&FN_MAPLIST_2 u(bbp)=switch(strmatch(%0,*-*),1,u(fn_lnum,before(%0,-),after(%0,-)),%0)
&FN_UNREAD_LIST u(bbp)=edit(sort(iter(%0,member(%1,%i0))),%b,\,%b)
&FN_LNUM u(bbp)=iter(lnum(add(sub(%1,%0),1)),add(%i0,%0))
&FN_B36-TO-B10 u(bbp)=baseconv(%0,36,10)
&FN_B10-TO-B36 u(bbp)=baseconv(%0,10,36)
&FN_INC_NEXT_MESS u(bbp)=u(fn_b10-to-b36,add(u(fn_b36-to-b10,%0),1))
&VALID_CONFIGS u(bbp)=anonymous timeout
&VALID_GLOBAL_CONFIGS u(bbp)=timeout autotimeout
&CONFIG_TIMEOUT u(bbp)=2592000
&TR_TIMEOUT u(bbp)=@dolist v(groups)={@trigger me/tr_timeout_group=%i0}
&TR_TIMEOUT_GROUP u(bbp)=@dolist get(%0/mess_lst)={@switch/inline and(extract(get(%0/hdr_%i0),5,1,|),gt(secs(),extract(get(%0/hdr_%i0),5,1,|)))=1,{&hdr_%i0 %0; &bdy_%i0 %0; &mess_lst %0=remove(get(%0/mess_lst),%i0); &master_lst u(bbp)=remove(get(u(bbp)/master_lst),%i0)}}
&DO_TIMEOUTS u(bbp)=@trigger me/tr_timeout; @wait 86400={@trigger me/do_timeouts}
&TR_CONFIG_ANONYMOUS u(bbp)=&anonymous %0=if([%1],[%1],); @pemit %2=Posters on the [name(%0)] group will show up as [if(strlen(%1),'%1',themselves)].
&TR_CONFIG_TIMEOUT u(bbp)=&config_timeout %0=mul(%1,86400); @pemit %2=New messages for group '[name(%0)]' have [if(%1,a %1 day,no)] timeout.
&TR_GCONFIG_TIMEOUT u(bbp)=&config_timeout me=mul(%0,86400); @pemit %1=BB global config parameter 'timeout': [if(%0,%0 days,none)].%rAny new groups will have this value set as their default timeout.
&TR_GCONFIG_AUTOTIMEOUT u(bbp)=@switch/inline eq(hasattr(me,startup),setr(0,switch(%0,on,1,0))):%0=1:*,{@pemit %1=BB autotimeout is already [if(%q0,on,off)].},0:on,{@startup me=@trigger me/do_timeouts; @trigger me/do_timeouts; @pemit %1=BB autotimeout turned on.},{@startup me; @pemit %1=BB autotimeout turned off.; @halt me}
&FN_MSG_FLAGS u(bbp)=switch(member(get(%0/bb_read),%2):[u(fn_timeout_close,index(get(%1/hdr_%2),|,5,1))],0:*,U,*:1,T)
&TR_POST_NOTIFY u(bbp)=@switch/inline hasflag(%0,DARK)=0,{@pemit/list iter(remove(lwho(),%0),switch(or(member(get(%i0/bb_omit) [get(%i0/bb_silent)],%1),not(u(%1/canread,%i0))),0,%i0))=(New BB message \([pueblize(member(v(groups),%1)/%2,+bbread [member(v(groups),%1)]/%2)]\) posted to '[name(%1)]' by [if(hasattr(%1,anonymous),get(%1/anonymous),name(%0))]: %3)}
&FN_TIMEOUT_CLOSE u(bbp)=switch(%0:[lte(sub(%0,secs()),259200)],0:*,0,*:1,1,0)
&FN_HASCONFIG u(bbp)=or(hasattr(%0,anonymous),neq(get(%0/config_timeout),get(u(bbp)/config_timeout)))
&VERSION u(bbp)=4.0.6
&STARTUP u(bbp)=@trigger me/do_timeouts
&BUFFER_SIZE u(bbp)=8000

@lock u(bbs)=me
@Desc u(bbs)=iter(setdiff(lattr(me),Desc),ljust(%i0,18))
@Fail u(bbs)=It's bolted down... you aint budgin' it.

&CMD_+BBNEWGROUP u(bbs)=$+bbnewgroup *:@switch/inline hasflag(%#,wizard)=1, {@switch/inline setr(0,create(%0))=#-1,{@pemit %#=That's not a good name for a group.},{&groups u(bbp)=switch(words(get(u(bbp)/groups)),0,,get(u(bbp)/groups)%b)%q0; &own %q0=%#; @set %q0=safe; @set %q0=inherit; &last_mod %q0=u(u(bbp)/bbtime); @set %q0=NO_WARN; &CANREAD %q0=1; &CANWRITE %q0=1; &config_timeout %q0=get(u(bbp)/config_timeout); @pemit %#=Group number [member(get(u(bbp)/groups),%q0)] added as '%0'. Messages will have [if(get(%q0/config_timeout),a [div(get(%q0/config_timeout),86400)] day,no)] timeout.}}, {@pemit %#=You can't add groups to the message base.}

&CMD_+BBLOCK u(bbs)=$+bblock *=*/*:@switch/inline hasflag(%#,wizard)=1,{@switch/inline member(get(u(bbp)/groups),setr(0,u(u(bbp)/get_group,%0)))=0,{@pemit %#=No such group as '%0'.},{@switch/inline %1=flag,{&CANREAD %q0=\[or(hasflag(\%0,%2),hasflag(\%0,wizard))]},{&CANREAD %q0=\[or(match(get(\%0/%1),%2),hasflag(\%0,wizard))]}; @wait 1={&CANWRITE %q0=get(%q0/CANREAD)}; @pemit %#=Group '%0' locked. Only people with %1=%2 can access.}},{@pemit %#=You cannot lock message groups.}

&CMD_+BBCLEARGROUP u(bbs)=$+bbcleargroup *:@switch/inline and(hasflag(%#, wizard),member(get(u(bbp)/groups),setr(0,u(u(bbp)/get_group,%0))))=1, {&delete_grp u(bbp)=%q0; @pemit %#=%r[space(15)]******** Warning ********%r%rDeleting Group #%0 will also delete the [words(get(%q0/mess_lst))] messages in that group.%rIf you still wish to proceed, type '+bbconfirm %0'%r%r[space(15)][repeat(*,25)]%r}, {&delete_grp u(bbp); @pemit %#=You can't remove that group (either that group does not exist, or you are not the owner).}

&DO_CLEARGROUP u(bbs)=$+bbconfirm *:@switch/inline get(u(bbp)/DELETE_GRP)=setr(0,u(u(bbp)/get_group,%0)), {&groups u(bbp)=remove(get(u(bbp)/groups),%q0); &master_lst u(bbp)=setdiff(get(u(bbp)/master_lst),get(%q0/mess_lst)); @nuke %q0; &delete_grp u(bbp); @pemit %#=Group number %0 removed.}, {&delete_grp u(bbp); @pemit %#=That group has not been marked for deletion. Use '+bbcleargroup <#>' to mark a group for deletion.}

&CMD_+BBPOST u(bbs)=$+bbpost */*=*:@switch/inline member(u(u(bbp)/valid_groups,%#,write),setr(0,u(u(bbp)/get_group,%0)))=0, {@pemit %#=Either you do not subscribe to Group #%0, or are unable to post to it.},{&mess_lst %q0=cat(get(%q0/mess_lst),setr(1,get(u(bbp)/nxt_mess))); &nxt_mess u(bbp)=ulocal(u(bbp)/fn_inc_next_mess,%q1); &master_lst u(bbp)=cat(get(u(bbp)/master_lst),%q1); &hdr_%q1 %q0=mid(%1, 0, 34)|[u(u(bbp)/bbtime)]|[edefault(%q0/anonymous,mid(name(%#),0,24))]|[owner(%#)]|[if(get(%q0/config_timeout),add(get(%q0/config_timeout),secs()),0)]; &last_mod %q0=u(u(bbp)/bbtime); &bdy_%q1 %q0=%2[if(and(hasattr(%#,bb_sig),not(hasattr(%q0,anonymous))),{%r[ulocal(%#/bb_sig)]},)]; @pemit %#=You post your note about '%1' in group [member(get(u(bbp)/groups),%q0)] ([name(%q0)]) as message #[member(get(%q0/mess_lst),%q1)][if(and(hasattr(%#,bb_sig),hasattr(%q0,anonymous)),{%b%bThis is an anonymous group, your BB_SIG was -not- appended.},)]; &bb_read %#=setunion(get(%#/bb_read),%q1); @trigger u(bbp)/tr_post_notify=%#,%q0,member(get(%q0/mess_lst),%q1),mid(%1, 0, 34)}

&CMD_+BBREMOVE u(bbs)=$+bbremove */*:@dolist setq(0,u(u(bbp)/get_group,%0))[revwords(sort(u(u(bbp)/fn_makelist,%1)))]={@switch/inline or(strmatch(index(get(%q0/HDR_[setr(1,extract(get(%q0/MESS_LST),%i0,1))]),|,4, 1),%#), hasflag(%#,wizard))=1, {&HDR_%q1 %q0; &BDY_%q1 %q0; &MESS_LST %q0=remove(get(%q0/MESS_LST),%q1); &MASTER_LST u(bbp)=remove(get(u(bbp)/MASTER_LST),%q1); @pemit %#=Message %i0 removed from group #[member(get(u(bbp)/groups),%q0)] ([name(%q0)]).}, {@pemit %#=Either message %i0 does not exist, or you were not the original poster.}}

&CMD_+BBNEXT u(bbs)=$+bbnext:think setq(0,first(iter(u(u(bbp)/valid_groups,%#,read),switch(trim(u(u(bbp)/unreadnums,%# ,##)),,,##))));@switch/inline words(r(0))=0,@pemit %#=There are no unread postings on the Global Bulletin Board.,{think setq(4,member(get(u(bbp)/groups),%q0))[setq(5,first(u(u(bbp)/unreadnums,%#,%q0 )))];@switch/inline and(lte(%q5,words(get(%q0/MESS_LST))),isnum(%q5))=0,{@pemit %#=No new messages beyond %q4/[setr(6,add(after(%0,/),0))] ([name(%q0)]/%q6).},{@pemit %#=header(BBS - [name(%q0)])%r[ljust(Message: %q4/%q5[switch(u(u(bbp)/fn_timeout_close,index(setr(3,get(%q0/HDR_[extract(get(%q0/MESS_LST),%q5,1)])),|,5,1)),1,{%b(timeout warning)})],35)]Posted[space(8)]Author%r[ljust(index(%q3,|,1,1),35)][ljust(index(%q3,|,2,1),14)][mid(index(%q3,|,3,1)[ifelse(and(hasattr(%q0,anonymous),hasflag(%#,wizard)),%b\([name(index(%q3,|,4,1))]\),)],0,29)]%r[subheader()]%r[get(%q0/BDY_[extract(get(%q0/MESS_LST),%q5,1)])]%r[header()];&bb_read %#=setunion(get(%#/bb_read),extract(get(%q0/MESS_LST),%q5,1))}}


&CMD_+BBREAD2 u(bbs)=$+bbread *:@switch/inline strmatch(%0,*/*)=0,{@switch/inline member(u(u(bbp)/valid_groups,%#,read),setr(0,u(u(bbp)/get_group,%0)))=0,{@pemit %#=switch(%q0,#-2,That Group name is not specific enough.,You do not subscribe to that Group.)},{@pemit %#=header(BBS - [name(%q0)])%r[space(8)]Message[space(28)]Posted[space(8)]By;@dolist get(%q0/MESS_LST)={@pemit %#=ljust(pueblize(member(get(u(bbp)/groups),%q0)/[member(get(%q0/MESS_LST),%i0)],+bbread [member(get(u(bbp)/groups),%q0)]/[member(get(%q0/MESS_LST),%i0)]),6)[ljust(u(u(bbp)/fn_msg_flags,%#,%q0,%i0),2)][ljust(index(setr(1,get(%q0/HDR_%i0)),|,1,1),35)][ljust(index(%q1,|,2,1),14)][mid(index(%q1,|,3,1)[if(and(hasattr(%q0,anonymous),hasflag(%#,wizard)),%b\([name(index(%q1,|,4,1))]\),)],0,21)][switch(member(get(%q0/MESS_LST),%i0),words(get(%q0/MESS_LST)),%r[header()])]}}},{@switch/inline member(u(u(bbp)/valid_groups,%#,read),setr(0,u(u(bbp)/get_group,setr(2,first(%0,/)))))=0,{@pemit %#=switch(%q0,#-2,That Group name is not specific enough.,You do not subscribe to that Group.)},{@dolist setq(4,member(get(u(bbp)/groups),%q0))[switch(rest(%0,/),u,u(u(bbp)/unreadnums,%#,%q0),u(u(bbp)/fn_makelist,rest(%0,/)))]={@switch/inline and(lte(%i0,words(get(%q0/MESS_LST))),isnum(%i0),gt(%i0,0))=0,{@pemit %#=Message %q4/%i0 ([name(%q0)]/%i0) does not exist.},{@pemit %#=header(name(%q0));@nspemit %#=ljust(Message: %q4/%i0[switch(u(u(bbp)/fn_timeout_close,index(setr(3,get(%q0/HDR_[extract(get(%q0/MESS_LST),%i0,1)])),|,5,1)),1,{%b(timeout warning)})],35)Posted[space(8)]Author%r[ljust(index(%q3,|,1,1),35)][ljust(index(%q3,|,2,1),14)][mid(index(%q3,|,3,1)[if(and(hasattr(%q0,anonymous),hasflag(%#,wizard)),%b\([name(index(%q3,|,4,1))]\),)],0,29)]%r[subheader()];@nspemit %#=get(%q0/BDY_[extract(get(%q0/MESS_LST),%i0,1)]);@nspemit %#=header(); &bb_read %#=setunion(get(%#/bb_read),extract(get(%q0/MESS_LST),%i0,1))}}; @wait 2={&bb_read %#=setinter(get(%#/bb_read),get(u(bbp)/master_lst))}}}

&CMD_+BBEDIT u(bbs)=$+bbedit */*=*/*:@switch/inline strmatch(index(get(setr(0,u(u(bbp)/get_group,%0))/HDR_[setr(1,extract(get(%q0/MESS_LST),%1,1))]),|,4, 1),%#)=1,{&BDY_%q1 %q0=edit(get(%q0/BDY_%q1),{%2},{%3}); @pemit %#=header()%rMessage [member(get(u(bbp)/groups),%q0)]/%1 ([name(%q0)]/%1) now reads:%r[subheader()];@nspemit %#=get(%q0/BDY_%q1);@nspemit %#=header()}, {@pemit %#=Either that message does not exist, or you were not the original poster.}

&CMD_+BBLEAVE u(bbs)=$+bbleave *:@switch/inline member(u(u(bbp)/valid_groups,%#,read),setr(0,u(u(bbp)/get_group,%0)))=0,{@pemit %#=You aren't currently subscribing to Board #%0. No ommission necessary.},{&bb_omit %#=setunion(get(%#/bb_omit),%q0); @pemit %#=You have removed yourself from the [name(%q0)] board.}
&CMD_+BBJOIN u(bbs)=$+bbjoin *:@switch/inline member(NULL [get(%#/bb_omit)],setr(0,u(u(bbp)/get_group,%0)))=0,{@pemit %#=switch(member(u(u(bbp)/valid_groups,%#,read),%q0),0,{Sorry, you don't have access to that board.},{You are already a member of the [name(%q0)] board.})},{&bb_omit %#=setdiff(get(%#/bb_omit),%q0); @pemit %#=You have joined the [name(%q0)] board.}

&CMD_+BBSCAN u(bbs)=$+bbscan: @pemit %#=switch(hasattr(u(bbp),BB_POST_HDR_%#),1,** BB Warning: You are in the middle of writing a bbpost. **%r)[switch(setdiff(iter(setr(0,u(u(bbp)/VALID_GROUPS,%#,read)),get(%i0/MESS_LST)),setr(1,get(%#/BB_READ))),,There are no unread postings on the Global Bulletin Board.,header(- Unread Postings on the Global Bulletin Board -)[setq(9,get(u(bbp)/GROUPS))][trim(iter(%q0,switch(setr(2,setdiff(setr(3,get(%i0/MESS_LST)),%q1)),,,%r[name(%i0)] \(#[member(%q9,%i0)]\): [words(%q2)] unread \([u(u(bbp)/FN_UNREAD_LIST,%q2,%q3)]\))),b)]%r[header(BBS at [round(mul(fdiv(strlen(get(u(bbp)/MASTER_LST)),get(u(bbp)/BUFFER_SIZE)),100),1)]\% capacity)])]

&CMD_+BBLIST u(bbs)=$+bblist:@pemit %#=header()%r[ljust(Available Bulletin Board Groups,37)]Member?[space(8)]Timeout (in days)%r[subheader()]%r%b[iter(get(u(bbp)/groups),switch(u(%i0/canread,%#),1,{%r%b[trim(ljust(member(get(u(bbp)/groups),%i0),5)[ljust(name(%i0),32)][ljust(switch(member(get(%#/bb_omit),%i0),0,Yes,No),19)][rjust(if(get(%i0/config_timeout),div(get(%i0/config_timeout),86400),none),4)])]}))]%r[subheader()]%rTo join groups, type '+bbjoin <group number or name>'%r[subheader()]

&CREDITS u(bbs)=Myrddin's BBS was written by Myrddin@Witchcraft/Dreaming/Elysium (email: merlin@firstmagic.com). The most recent version of this code can be found at http://www.firstmagic.com/~merlin/mushcode, or via anonymous ftp from ftp.firstmagic.com in the /pub/merlin directory. Please refer interested parties to myself or those addresses, thank you. :)%R%RThis version of the code was edited by Mouvar@M*U*S*H (email: fubarp@gmail.com).

&CMD_+BBCATCHUP u(bbs)=$+bbcatchup *:@switch/inline lcstr(%0)=all,{&bb_read %#=get(u(bbp)/master_lst); @pemit %#=All postings on all boards marked as read.},{@dolist setq(1,u(u(bbp)/valid_groups,%#,read))[u(u(bbp)/fn_makelist,%0)]={@switch/inline member(%q1,setr(0,u(u(bbp)/get_group,%i0)))=0,{@pemit %#=You do not subscribe to Group %i0.},{ &bb_read %#=[setunion(get(%#/bb_read),get(%q0/MESS_LST))]; @pemit %#=All postings on Group #[member(get(u(bbp)/groups),%q0)] ([name(%q0)]) marked as read.}}}

&CMD_+BBMOVE u(bbs)=$+bbmove */* to *:@switch/inline and(member(u(u(bbp)/valid_groups,%#,read),setr(0,u(u(bbp)/get_group,%0))),member(u(u(bbp)/valid_groups,%#,write),setr(2,u(u(bbp)/get_group,%2))),not(strmatch(%q0,%q2)))=0,{@pemit %#=One of the selected groups is not valid (either it doesn't exist, you can't write to it, or you tried to 'move' to the same group).},{@switch/inline or(strmatch(index(get(%q0/HDR_[setr(1,extract(get(%q0/MESS_LST),%1,1))]),|,4, 1),%#), and(hasflag(%#,wizard),member(get(%q0/MESS_LST),%q1)))=1,{&HDR_%q1 %q2=[get(%q0/HDR_%q1)]; &BDY_%q1 %q2=get(%q0/BDY_%q1); &MESS_LST %q2=cat(get(%q2/mess_lst),%q1); &MESS_LST %q0=remove(get(%q0/MESS_LST),%q1); &HDR_%q1 %q0; &BDY_%q1 %q0; @pemit %#=Message '%1' removed from group '%0' and added to group '%2' as message #[member(get(%q2/mess_lst),%q1)]},{@pemit %#=Not a valid message number for that group.}}


&CMD_+BBWRITELOCK u(bbs)=$+bbwritelock *=*/*:@switch/inline hasflag(%#,wizard)=1,{@switch/inline member(get(u(bbp)/groups),setr(0,u(u(bbp)/get_group,%0)))=0,{@pemit %#=No such group as '%0'.},{@switch/inline %1=flag,{&CANWRITE %q0=\[or(hasflag(\%0,%2),hasflag(\%0,wizard))]},{&CANWRITE %q0=\[or(match(get(\%0/%1),%2),hasflag(\%0,wizard))]}; @pemit %#=Group '%0' locked. Only people with %1=%2 can write.}},{@pemit %#=You cannot lock message groups.}

&CMD_+BBPOST2 u(bbs)=$+bbpost */*:@switch/inline hasattr(u(bbp),bb_post_hdr_%#)=1,{@pemit %#=You are already in the middle of writing a bbpost.},{@switch/inline strmatch(%1,*=*)=0,{@switch/inline member(u(u(bbp)/valid_groups,%#,write),setr(0,u(u(bbp)/get_group,%0)))=0,{@pemit %#=Either you do not subscribe to Group #%0 or are unable to post to it.},{&bb_post_hdr_%# u(bbp)=%q0|[mid(%1,0,34)]; @pemit %#=%rYou start your posting to Group #[member(get(u(bbp)/groups),%q0)] ([name(%q0)]).%rYou can now compose the body of the post by using '+bbwrite <text>'%ror '+bb <text>'. When you are finished, type '+bbpost' by itself.[if(and(hasattr(%#,bb_sig),hasattr(%q0,anonymous)),{%b%bThis is an anonymous group. Your BB_SIG will -not- be appended.},)]}}}


&CMD_+BBWRITE u(bbs)=$+bbwrite *:@switch/inline hasattr(u(bbp),bb_post_hdr_%#)=0,{@pemit %#=You do not have a bbpost in progress.},{&bb_post_bdy_%# u(bbp)=[get(u(bbp)/bb_post_bdy_%#)]%0; @pemit %#=Text added to bbpost.}

&CMD_+BBPROOF u(bbs)=$+bbproof:@switch/inline hasattr(u(bbp),bb_post_hdr_%#)=0,{@pemit %#=You do not have a bbpost in progress.},{@pemit %#=[header(= BB Post in Progress =)]%rGroup: %b[name(index(get(u(bbp)/bb_post_hdr_%#),|,1,1))]%rTitle: %b[index(get(u(bbp)/bb_post_hdr_%#),|,2,1)]%r[subheader()]%r[trim(get(u(bbp)/bb_post_bdy_%#))]%r[header()]}

&CMD_+BBPOST-POST u(bbs)=$+bbpost:@switch/inline hasattr(u(bbp),bb_post_hdr_%#):[hasattr(u(bbp),bb_post_bdy_%#)]=0:0,{@pemit %#=You do not have a bbpost in progress.},1:0,{@pemit %#=Your post is empty. Please add text with the '+bbwrite <text>' command or discard the posting with the '+bbtoss' command.},{&mess_lst setr(0,index(get(u(bbp)/bb_post_hdr_%#),|,1,1))=cat(get(%q0/mess_lst),setr(1,get(u(bbp)/nxt_mess))); &nxt_mess u(bbp)=ulocal(u(bbp)/fn_inc_next_mess,%q1); &master_lst u(bbp)=cat(get(u(bbp)/master_lst),%q1); &hdr_%q1 %q0=index(get(u(bbp)/bb_post_hdr_%#),|,2,1)|[u(u(bbp)/bbtime)]|[edefault(%q0/anonymous,mid(name(%#),0,24))]|[owner(%#)]|[if(get(%q0/config_timeout),add(get(%q0/config_timeout),secs()),0)]; &last_mod %q0=u(u(bbp)/bbtime); &bdy_%q1 %q0={trim(get(u(bbp)/bb_post_bdy_%#))[if(and(hasattr(%#,bb_sig),not(hasattr(%q0,anonymous))),{%r[ulocal(%#/bb_sig)]},)]}; @pemit %#=You post your note about '[index(get(u(bbp)/bb_post_hdr_%#),|,2,1)]' in group '[name(%q0)]' as message #[member(get(%q0/mess_lst),%q1)]; &bb_read %#=setunion(get(%#/bb_read),%q1); @trigger u(bbp)/tr_post_notify=%#,%q0,member(get(%q0/mess_lst),%q1),index(get(u(bbp)/bb_post_hdr_%#),|,2,1); &bb_post_bdy_%# u(bbp); &bb_post_hdr_%# u(bbp)}

&CMD_+BBREAD u(bbs)=$+bbread:@pemit %#=header(mudname() BBS)%r[space(7)]Group Name[space(20)]Last Post[space(6)]# of messages;@nspemit %#=iter(u(u(bbp)/valid_groups,%#,read),rjust(pueblize(member(get(u(bbp)/groups),%i0),+bbread [member(get(u(bbp)/groups),%i0)]),2)[center(switch(get(%i0/CANREAD):[get(%i0/CANWRITE)]:[u(%i0/CANWRITE,%#)],1:1:1,,1:*:1,{(-)},1:*:0,-,*),5)][ljust(pueblize(name(%i0),+bbread [member(get(u(bbp)/groups),%i0)]),30)][ljust(get(%i0/LAST_MOD),20)][rjust(words(setr(0,get(%i0/MESS_LST))),3)]%b[ljust(u(u(bbp)/isgroupread,%#,%q0),2)],%b,%R);@nspemit %#=subheader()%r'*' = restricted[space(5)]'-' = read only[space(5)]'\(-\)' = read only\, but you can write%r[header(if(hasflag(%#,wizard),BBS at [round(mul(fdiv(strlen(get(u(bbp)/master_lst)),get(u(bbp)/buffer_size)),100),1)]\% capacity))]

&CMD_+BBTOSS u(bbs)=$+bbtoss:@switch/inline hasattr(u(bbp),bb_post_hdr_%#)=0,{@pemit %#=You do not have bbpost in progress.},{&bb_post_hdr_%# u(bbp); &bb_post_bdy_%# u(bbp); @pemit %#=Your bbpost has been discarded.}

&CMD_+BB u(bbs)=$+bb *:@switch/inline hasattr(u(bbp),bb_post_hdr_%#)=0,{@pemit %#=You do not have a bbpost in progress.},{&bb_post_bdy_%# u(bbp)=get(u(bbp)/bb_post_bdy_%#)%b%0; @pemit %#=Text added to bbpost.}

&CMD_+BBEDIT2 u(bbs)=$+bbedit *=*/*:@switch/inline strmatch(setr(0,ucstr(%0)),*/*)=0,{@switch/inline %q0=TEXT,{@edit u(bbp)/bb_post_bdy_%#={%1},{%2}},TITLE,{&bb_post_hdr_%# u(bbp)=index(get(u(bbp)/bb_post_hdr_%#),|,1,1)|[edit(index(get(u(bbp)/bb_post_hdr_%#),|,2,1),{%1},{%2})]}; @wait 0={@pemit %#=header(= BB Post in Progress =)%rGroup: %b[name(index(get(u(bbp)/bb_post_hdr_%#),|,1,1))]%rTitle: %b[index(get(u(bbp)/bb_post_hdr_%#),|,2,1)]%r[subheader()]%r[trim(get(u(bbp)/bb_post_bdy_%#))]%r[header()]}}

&CMD_+BBCONFIG u(bbs)=$+bbconfig:@pemit %#=[header(= Myrddin's Global BBS v[get(u(bbp)/version)] =)]%rGlobal Config Parameters:%r%r[rjust(timeout:,15)] [div(get(u(bbp)/config_timeout),86400)] days%r[rjust(autotimeout:,15)] [if(hasattr(u(bbp),startup),on,off)]%r[subheader()]%rBoard Config Parameters:%r%r%b[iter(filter(u(bbp)/fn_hasconfig,get(u(bbp)/groups)),%b%b[name(%i0)]%r[if(hasattr(%i0,anonymous),[space(5)]anonymous: [get(%i0/anonymous)]%r,)][if(neq(get(%i0/config_timeout),get(u(bbp)/config_timeout)),[space(5)]timeout: [div(get(%i0/config_timeout),86400)] days%r,)]%r)][repeat(=,78)]

&CMD_+BBCONFIG2 u(bbs)=$+bbconfig *=*:@switch/inline strmatch(%0,*/*):[hasflag(%#,wizard)]=1:*,{},0:0,{@pemit %#=Huh?%b%b(Type "help" for help.)},{@switch/inline member(get(u(bbp)/valid_global_configs),lcstr(%0))=0,{@pemit %#=Valid global config parameters are: [get(u(bbp)/valid_global_configs)]},{@trigger u(bbp)/tr_gconfig_%0=%1,%#}}

&CMD_+BBCONFIG3 u(bbs)=$+bbconfig */*=*:@switch/first hasflag(%#,wizard)=0,{@pemit %#=Huh?%b%b(Type "help" for help.)},{@switch/inline setr(0,u(u(bbp)/get_group,%0)):[member(get(u(bbp)/valid_configs),lcstr(%1))]=#-1:*,{@pemit %#='%0' is not a valid group name.},#-2:*,{@pemit %#='%0' is not specific enough.},*:0,{@pemit %#=Valid config parameters are: [get(u(bbp)/valid_configs)]},{@trigger u(bbp)/tr_config_%1=%q0,%2,%#}}

&CMD_+BBTIMEOUT u(bbs)=$+bbtimeout */*=*:@dolist sort(u(u(bbp)/fn_makelist,%1))[setq(0,u(u(bbp)/get_group,%0))]={@switch/inline or(strmatch(index(get(%q0/HDR_[setr(1,extract(get(%q0/MESS_LST),%i0,1))]),|,4, 1),%#), hasflag(%#,wizard)):[and(isnum(%2),or(and(gt(%2,0),lte(mul(%2,86400),get(%q0/config_timeout))),and(not(get(%q0/config_timeout)),gte(%2,0)),hasflag(%#,wizard)))]=1:1, {&hdr_%q1 %q0=replace(get(%q0/hdr_%q1),5,if(%2,add(secs(),mul(%2,86400)),0),|); @pemit %#=Message %i0 in group '[name(%q0)]' has [if(%2,a %2 day,no)] timeout.}, 0:*,{@pemit %#=Either message %i0 does not exist, or you were not the original poster.},*:0,{@pemit %#=Sorry, '%2' is not a valid number of days for a timeout on the '[name(%q0)]' board. [if(get(%q0/config_timeout),Maximum timeout is [div(get(%q0/config_timeout),86400)] days.,)]}}

&CMD_+BBSEARCH u(bbs)=$+bbsearch */*:@switch/inline [member(u(u(bbp)/valid_groups,%#,write),setr(0,u(u(bbp)/get_group,%0)))]:[hasattr(%q0,anonymous)]:[isdbref(setr(1,locate(%#,*%1,p)))]=0:*:*,{@pemit %#=BB Group '%0' is either an invalid Group, an unreadable Group, or a Group you do not subscribe to.},1:1:*,{@pemit %#=BB Group '[name(%q0)]' is an anonymous group.},1:0:0,{@pemit %#=No such player '%1'.},{@switch/inline words(setr(2,grep(%q0,hdr_*,|%q1|)))=0,{@pemit %#=[name(%q1)] hasn't posted any messages to the '[name(%q0)]' Group.},{@pemit %#=header(**** [name(%q0)] ****)%r[space(8)]Message[space(28)]Posted[space(8)]By%r[subheader()]; @dolist %q2={@pemit %#=ljust([member(get(u(bbp)/groups),%q0)]/[member(get(%q0/mess_lst),last(%i0,_))],6)[ljust(u(u(bbp)/fn_msg_flags,%#,%q0,last(%i0,_)),2)][ljust(index([setr(3,get(%q0/%i0))],|,1,1),35)][ljust(index(%q3,|,2,1),14)][mid([index(%q3,|,3,1)],0,21)][switch(member(%q2,%i0),words(%q2),%r[header()])]}}}

&CMD_+BBNOTIFY u(bbs)=$+bbnotify *=*:@switch/first member(u(u(bbp)/valid_groups,%#,read),setr(0,u(u(bbp)/get_group,%0))):[member(on off,lcstr(%1))]:[lcstr(%1)]=0:*:*,{@pemit %#=switch(%q0,#-2,That Group name is not specific enough.,You do not subscribe to that Group.)},*:0:*,{@pemit %#=Invalid choice '%1'. Choices are 'on' or 'off'.},*:*:off,{&bb_silent %#=setunion(get(%#/bb_silent),%q0); @pemit %#=Post notification for BB Group '[name(%q0)]' turned off. You will no longer be notified of new postings to that Group.},{&bb_silent %#=setdiff(get(%#/bb_silent),%q0); @pemit %#=Post notification for BB Group '[name(%q0)]' turned on. You will now be notified of new postings to that Group.}