@@ DEPENDENCIES - CORE

@switch/inline isdbref(u(who))=0,{@tel create(Who Function Object <WHO>)=config(master_room)}
&who u(coi)=locate(config(master_room),Who Function Object <WHO>,TXxi)
@parent u(who)=u(coi)
@set u(who)=WIZARD !NO_COMMAND

@wipe u(pgo)/CMD`+WHO
@wipe u(pgo)/FUN`WHO
@mvattr u(pgo)/VAR`MAXPLAYERS=u(who)/VAR`MAXPLAYERS

&RFN`MSGHEAD u(who)=msghead(%0)
&RFN`HEADER u(who)=header(%0)

&CMD`+WHO u(who)=$^(?\:\+)?who(?\:/(\S+))?$:@include u(ccs)/INC`PARTIAL=%1,setunion(get(u(who)/VAR`WHO`PLAYFLAGS),if(isadmin(%#),get(u(who)/VAR`WHO`ADMINFLAGS)),|,|),|,v(VAR`WHO`MSGHEAD),switch,switch;@include u(who)/INC`WHO`[strfirstof(%q<switch>,MAIN)]=%q<switch>

&VAR`WHO`PLAYFLAGS u(who)=IDLE

&INC`WHO`MAIN u(who)=@include u(who)/INC`WHO`SORT`MAIN;@include u(who)/INC`WHO`LIST

&FUN`WHO`MAIN u(who)=setunion(mwho(),,,namei)

&INC`WHO`SORT`MAIN u(who)=@assert words(setr(who,u(FUN`WHO`MAIN)))=@nspemit %#=u(RFN`MSGHEAD,WHO) No players to show!

&INC`WHO`IDLE u(who)=@include u(who)/INC`WHO`SORT`IDLE;@include u(who)/INC`WHO`LIST=Idle
&INC`WHO`SORT`IDLE u(who)=@assert words(setr(who,sortkey(#lambda/idle(\%0),u(FUN`WHO`MAIN),-n)))=@nspemit %#=u(RFN`MSGHEAD,WHO) No players to show!

&INC`WHO`LIST u(who)=@nspemit %#=u(RFN`HEADER,Who[if(strlen(%0),%b-%b%0)]);@include u(who)/INC`WHO`HEADER`[strfirstof(%0,MAIN)];@nspemit %#=separator();@dolist %q<who>={@nspemit %#=u(FUN`WHO`PLAYERLINE`[strfirstof(%0,MAIN)],%i0);@switch/inline eq(inum(0),words(%q<who>))=1,{&VAR`MAXPLAYERS u(pco)=setr(max,max(get(u(pco)/VAR`MAXPLAYERS),words(%q<who>)));@nspemit %#=u(RFN`HEADER,words(%q<who>) Characters from [words(%q<who>)] IPs Connected - Record is %q<max>)}}

&INC`WHO`HEADER`MAIN u(who)=@nspemit %#=ansi(custcolor(%#,DEFAULT`COLNAME),align(20 11 3 4  4 1 29,Name,Alias,Fac,Idle,Conn,G,Location))
&INC`WHO`HEADER`IDLE u(who)=@include u(who)/INC`WHO`HEADER`MAIN

&FUN`WHO`PLAYERLINE`MAIN u(who)=align(20 11 -3 >4 >4 1 29,pueblize(name(%0),+finger [name(%0)]),alias(%0),strfirstof(switch(1,hasflag(%0,WIZARD),ansi(hr,WIZ),hasflag(%0,ROYALTY),ansi(hb,ROY)),left(getproperty(%0,faction),3),ansi(hx,N/A)),ansi(ryg(round(mul(fdiv(bound(idle(%0),0,3600),3600),100),0)),etime(idle(%0),4)),ansi(hg,etime(conn(%0),4)),ucstr(left(default(%0/sex,?),1)),switch(isic(%0)[strmatch(type(loc(%0)),ROOM)],11,pueblize(name(loc(%0)),+port [loc(%0)]),name(loc(%0))))
&FUN`WHO`PLAYERLINE`IDLE u(who)=u(FUN`WHO`PLAYERLINE`MAIN,%0)

@@ &STARTUP u(who)=@hook/override WHO=u(who),CMD`+WHO

@@ COMMUNITY - WHO
+help/addmain Community/+who=[u(who)]/HLP`+WHO
&HLP`+WHO u(who)=[ansi(hc,Aliases:)] who%R%R[ansi(hc,Commands)]%R[align(5 [sub(width(%#),6)],,{[ansi(h,+who)] - Show all visible, connected players.%R[ansi(h,+who/idle)] - Like above, but sorts by idle times.})]

&CMD`+WHERE u(who)=$^(?\:\+)?where(?\:/(\S+)?)?(?\: +(.+?))?$:@include u(ccs)/INC`PARTIAL=%1,setunion(get(u(who)/VAR`WHERE`PLAYFLAGS),if(isadmin(%#),get(u(who)/VAR`WHERE`ADMINFLAGS)),|,|),|,v(VAR`WHO`MSGHEAD),switch,switch;@include u(who)/INC`WHERE`[strfirstof(%q<switch>,MAIN)]=%2
@set u(who)/CMD`+WHERE=regexp

&INC`WHERE`MAIN u(who)=@switch/inline strlen(%0)=0,{@nspemit %#=u(RFN`HEADER,Where);@nspemit %#=align(30 1 [sub(width(%#),33)],Location,,Players);@nspemit %#=separator();@dolist setr(where,sort(filterbool(#lambda/isic(room(\%0)),setunion(iter(mwho(),loc(%i0)),)),namei))={@nspemit %#=u(FUN`WHERELINE,%i0);@switch/inline eq(inum(0),words(%q<where>))=1,{@nspemit %#=u(RFn`HEADER)}}},{@include u(ccs)/INC`CHECKPC=%0,1,WHERE;@assert cand(conn(%q<t1>),nor(hasflag(%q<t1>,DARK),hidden(%q<t1>)))=@nspemit %#=u(RFN`MSGHEAD,WHERE) %q<t1name> is not online!;@assert isic(%q<t1>)=@nspemit %#=u(RFN`MSGHEAD,WHERE) %q<t1name> is not IC.;@nspemit %#=u(RFN`HEADER,Where - %q<t1name>);@nspemit %#=align(30 1 [sub(width(%#),33)],Location,,Players);@nspemit %#=separator();@nspemit %#=u(FUN`WHERELINE,loc(%q<t1>));@nspemit %#=u(RFN`HEADER)}

&FUN`WHERELINE u(who)=align(30 1 [sub(width(%#),33)],switch(isic(%0)[strmatch(type(%0),ROOM)],11,pueblize(name(%0),+port %0),name(%0)),ansi(custcolor(%#,DEFAULT`BORDER),-),itemize(iter(sort(lvplayers(%0),namei),pueblize(name(%i0),+finger [name(%i0)]),,|),|,and,\,))

