@@ DEPENDENCIES - CORE

th u(NEWCOBJ,FCList Management System <FCLIST>,fclist,,,,WIZARD SAFE !NO_COMMAND,INHERIT SIDEFX SAFE)
th u(NEWCOBJ,FCList Theme Database <THEMEDB>,themedb,u(cobj,fclist),,1,WIZARD SAFE,INHERIT SIDEFX SAFE)

&CMD`+FCLIST`PENNMUSH [u(cobj,fclist)]=$^(?s)(?\:\+)?(fclist|themes|theme|series)(?\:/(\S+)?)?(?\: +(.+?))?(?\:=(.*))?$:@attach %!/CMD`+FCLIST`MAIN
@set [u(cobj,fclist)]/CMD`+FCLIST`PENNMUSH=regexp
&CMD`+FCLIST`RHOSTMUSH [u(cobj,fclist)]=$^(?s)(?\:\+)?(fclist|themes|theme|series)(?\:/(\\S+)?)?(?\: +(.+?))?(?\:=(.*))?$:@attach %!/CMD`+FCLIST`MAIN
@set [u(cobj,fclist)]/CMD`+FCLIST`RHOSTMUSH=regexp
&CMD`+FCLIST`MAIN [u(cobj,fclist)]=th u(setq`%va,com,lcstr(+%1));@attach %!/INC`GETSWITCH=%2;@include %!/INC`[u(strfirstof`%va,%q<switch>,MAIN)]=%3,%4
@set [u(cobj,fclist)]/CMD`+FCLIST`[switch(v(game),PennMUSH,RHOSTMUSH,RhostMUSH,PENNMUSH)]=no_command

&SWITCHES`PLAYER [u(cobj,fclist)]=INFO|MAIL
&SWITCHES`ADMIN [u(cobj,fclist)]=CREATE|RENAME|DELETE|ASSIGN|REMOVE|DESCRIBE|STATUS|TYPE|SETINFO|CLEARINFO|UNASSIGNED

&SYSTEM`NAME [u(cobj,fclist)]=FCLIST

&INC`MAIN [u(cobj,fclist)]=@select/inline strlen(%0)=>0,{@attach %!/INC`DISPLAY},0,{@attach %!/INC`LISTALL}

&INC`LISTALL [u(cobj,fclist)]=@check words(u(setr`%va,themes,u(sortname,children(u(cobj,themedb)))))=@attach %!/INC`MSG=No inhabited themes to display.;@pemit %#=u(HEADER,mudname() - All Themes);th step(FUN`LISTTHEMES,%q<themes>,div(u(width`%va,%#),37));@pemit %#=u(SUBHEADER)

&FUN`LISTTHEMES [u(cobj,fclist)]=pemit(%#,u(table`%va,iter(lnum(0,sub(%+,1)),u(pueblize,u(setr`%va,name,u(moniker`%va,v(%i0))),+fclist %q<name>),%b,|),37,u(width`%va,%#),|))

&INC`FINDTHEME [u(cobj,fclist)]=@check strlen(%0)=@attach %!/INC`MSG=ERROR: Name field empty.;@select/inline words(u(setr`%va,themecheck,if(isdbref(u(setr`%va,find,u(find_in`%va,u(cobj,themedb),%0))),%q<find>,namegraball(children(u(cobj,themedb)),%0))))=0,{@stop 1=@attach %!/INC`MSG=ERROR: Theme '%0' was not found.},>1,{@stop 1=@attach %!/INC`MSG=ERROR: Multiple Themes found with a similar name. Which do you mean? Matches: [itemize(iter(%q<themecheck>,u(pueblize,name(%i0),+theme [name(%i0)]),%b,|),|,and,\,)]},1,{th u(setq`%va,theme%1,objid(%q<themecheck>));th u(setq`%va,themename%1,name(%q<themecheck>))}

&INC`DISPLAY [u(cobj,fclist)]=@include %!/INC`FINDTHEME=%0,1;@pemit %#=u(HEADER,Theme: %q<themename1>);@pemit %#=get(%q<theme1>/DESCRIBE);@pemit %#=u(separator,Cast);@switch/inline t(words(u(setr`%va,cast,get(%q<theme1>/CAST))))=1,{@include %!/INC`CASTLIST=%q<theme1>};@switch/inline gt(strlen(get(%q<theme1>/INFO)),0)=1,{@pemit %#=u(SUBHEADER,u(pueblize,This theme has info!,+fclist/info %q<themename1>))},0,{@pemit %#=u(subheader)}

&INC`CASTLIST [u(cobj,fclist)]=@select/inline t(%1)=0,{@pemit %#=u(FUN`CASTCOLUMN);@pemit %#=u(separator)};@dolist/inline u(sortname,u(filter,ISCAST,get(%0/CAST)))={@pemit %#=u(FUN`CASTROW,##)}

&FUN`CASTCOLUMN [u(cobj,fclist)]=ansi(u(color`%va,%#,FCLIST,COLUMN_NAMES),align(26 24 8 8 10,Name,Faction,Last On,Type,Available))

&FUN`CASTROW [u(cobj,fclist)]=align(26 24 8 8 10,u(namelink,%0),u(strfirstof`%va,u(getproperty,%0,FIRSTNAMECLICK),Unaffiliated),u(lastidle,%0),default(%0/D`FINGER`TYPE,??),u(capnames,default(%0/D`FINGER`STATUS,Open)))

@@ @pemit %#=ansi(u(color`%va,%#,COLOR,COLUMN_NAMES),align(3 20 1 3 18 21 >6,Typ,Name,G,PL,Faction,Rank,LastOn));

@@ @dolist/inline u(sortname,u(filter,ISCAST,get(%0/CAST)))={@pemit %#=align(3 20 1 3 18 21 >6,default(%i0/D`FINGER`TYPE,??),u(pueblize,u(moniker`%va,%i0),+finger [name(%i0)]),switch(get(%i0/sex),m*,M,f*,F,n*,N,?),default(%i0/level,??),u(strfirstof`%va,u(getproperty,%i0,factions,1,,1),Unaffiliated),u(getproperty,%i0,ranks,,,1),if(cand(gte(conn(%i0),0),not(hidden(%i0))),ansi(hg,ago(idle(%i0))),ansi(hx,elements(get(%i0/LAST),2 3))))}

&FIL`ISCAST [u(cobj,fclist)]=cand(isobjid(%0),!not(u(isadmin`%va,%0)))

&INC`INFO [u(cobj,fclist)]=@include %!/INC`FINDTHEME=%0,1;@pemit %#=u(HEADER,Theme Info: %q<themename1>);@pemit %#=get(%q<theme1>/INFO);@pemit %#=u(SUBHEADER)

&INC`SETINFO [u(cobj,fclist)]=@check strlen(%0)=@attach %!/INC`MSG=ERROR: Name field empty.;@include %!/INC`FINDTHEME=%0,1;@check strlen(%1)=@attach %!/INC`MSG=ERROR: Theme info field empty.;@select/inline u(setr`%va,mail,gt(strlen(u(setr`%va,oldinfo,get(%q<theme1>/INFO))),0))=1,{@mail %#=%q<themename1>/OLD INFO WAS:%R%q<oldinfo>};&INFO %q<theme1>=%1;@attach %!/INC`MSG=You have changed theme %q<themename1>'s info.[if(%q<mail>,%bIts old contents have been @mail'd to you.)];@attach %!/INC`MSG`CHAN=Changed the Theme Info for '[u(pueblize,%q<themename1>,+theme %q<themename1>)]'

&INC`CLEARINFO [u(cobj,fclist)]=@check strlen(%0)=@attach %!/INC`MSG=ERROR: Name field empty.;@include %!/INC`FINDTHEME=%0,1;@check strlen(%q<theme1>/INFO))=@attach %!/INC`MSG=ERROR: Theme has no info.;@mail %#=%q<themename1>/OLD INFO WERE:%R[get(%q<theme1>/INFO)];@attach %!/WIPE=%q<theme1>,INFO;@attach %!/INC`MSG=You have deleted theme %q<themename1>'s info. Its old contents have been @mail'd to you.;@attach %!/INC`MSG=Deleted the Theme info for '[u(pueblize,%q<themename1>,+theme %q<themename1>)]'

&INC`DESCRIBE [u(cobj,fclist)]=@check strlen(%0)=@attach %!/INC`MSG=ERROR: Name field empty.;@include %!/INC`FINDTHEME=%0,1;@check strlen(%1)=@attach %!/INC`MSG=ERROR: Theme description field empty.;@select/inline u(setr`%va,mail,gt(strlen(u(setr`%va,olddesc,get(%q<theme1>/DESCRIBE))),0))=1,{@mail %#=%q<themename1>/OLD THEME WAS:%R%q<olddesc>;@describe %q<theme1>=%1};@attach %!/INC`MSG=You have changed theme %q<themename1>'s description. Its old contents have been @mail'd to you.;@attach %!/INC`MSG`CHAN=Changed the Theme description for '[u(pueblize,%q<themename1>,+fclist %q<themename1>)]'

&INC`CREATE [u(cobj,fclist)]=@check strlen(%0)=@attach %!/INC`MSG=ERROR: Name field empty.;@stop isobjid(u(find_in`%va,u(cobj,themedb),%0))=@attach %!/INC`MSG=ERROR: That theme already exists. Use +fclist/describe to change its description.;@check valid(name,%0)=@attach %!/INC`MSG=ERROR: That would not be a valid name for a THING. Avoid special symbols like &.;@check strlen(%1)=@attach %!/INC`MSG=ERROR: Theme description field empty.;@tel u(setr`%va,theme,create(%0))=u(cobj,themedb);@parent %q<theme>=u(cobj,themedb);@describe %q<theme>=%1;@attach %!/INC`MSG=You have created the '%0' Theme.;@attach %!/INC`MSG`CHAN=Created a new +fclist entry: '[u(pueblize,%0,+fclist %0)]'

&INC`RENAME [u(cobj,fclist)]=@attach %!/INC`FINDTHEME=%0,1;@check strlen(%1)=@attach %!/INC`MSG=ERROR: New name field empty.;@check valid(name,%1)=@attach %!/INC`MSG=ERROR: '%1' is not a valid theme name. Themes are stored as THING objects. Avoid characters like &.;@stop isobjid(u(find_in`%va,u(cobj,themedb),%1))=@attach %!/INC`MSG=ERROR: Theme '%1' already exists.;@attach %!/INC`MSG=Theme '%q<themename1>' Renamed to '%1'!;@attach %!/INC`MSG`CHAN={Theme '%q<themename1>' Renamed to '%1'};@name %q<theme1>=%1

&INC`DELETE [u(cobj,fclist)]=@attach %!/INC`FINDTHEME=%0,1;@attach %!/INC`VERIFY={WARNING: This will delete Theme '%q<themename1>' and all data associated with it. Are you SURE you want to do this? Enter the same command again within ten seconds to verify.},FCLIST DELETE %q<theme1>;@attach %!/INC`MSG=Theme '%q<themename1>' deleted!;@attach %!/INC`MSG`CHAN=Theme '%q<themename1>' Deleted!;@attach %!/DELETE=%q<theme1>

&INC`ASSIGN [u(cobj,fclist)]=@check strlen(%0)=@attach %!/INC`MSG=ERROR: No player entered to assign.;@include %!/INC`CHECKPC=%0,1;@include %!/INC`FINDTHEME=%1,1;@attach %!/INC`ADDTOTHEME=%q<theme1>,%q<t1objid>;@attach %!/INC`MSG=You assign %q<t1name> to Theme '%q<themename1>';@attach %!/INC`MSG`CHAN=Assigned %q<t1name> to Theme '[u(pueblize,%q<themename1>,+fclist %q<themename1>)]'

&INC`ADDTOTHEME [u(cobj,fclist)]=&CAST %0=u(sortname,u(filter,ISOBJID,setunion(get(%0/CAST),objid(%1))));&D`THEME %1=u(sortname,u(filter,isobjid,setunion(get(%1/D`THEME),%0)))
&INC`REMFROMTHEME [u(cobj,fclist)]=&CAST %0=u(sortname,u(filter,ISOBJID,setdiff(get(%0/CAST),objid(%1))));&D`THEME %1=u(sortname,u(filter,isobjid,setdiff(get(%1/D`THEME),%0)))

&INC`REMOVE [u(cobj,fclist)]=@check strlen(%0)=@attach %!/INC`MSG=ERROR: No player entered to assign.;@include %!/INC`CHECKPC=%0,1;@include %!/INC`FINDTHEME=%1,1;@check match(get(%q<t1>/D`THEME),%q<theme1>)=@attach %!/INC`MSG=ERROR: %q<t1name> is not a member of %q<themename1>.;@attach %!/INC`REMFROMTHEME=%q<theme1>,%q<t1objid>;@attach %!/INC`MSG=You remove %q<t1name> from Theme '%q<themename1>';@attach %!/INC`MSG`CHAN=Removed %q<t1name> from Theme '[u(pueblize,%q<themename1>,+fclist %q<themename1>)]'

&INC`MAIL [u(cobj,fclist)]=@include %!/INC`FINDTHEME=%0,1;@check strlen(%1)=@attach %!/INC`MSG=ERROR: Nothing entered to send!;th u(setq`%va,cast,u(filter,isobjid,get(%q<theme1>/CAST)));@force/inplace %#={@mail %q<cast>=%1}

&INC`UNASSIGNED [u(cobj,fclist)]=@check words(u(setr`%va,unassigned,sort(lsearch(all,type,player,elock,!D`THEME:?*&!POWER^GUEST&!(FLAG^WIZARD|FLAG^ROYALTY)),namei)))=@attach %!/INC`MSG=No players lack theme assignments.;@pemit %#=u(HEADER,Unassigned Players);@pemit %#=ansi(u(color`%va,%#,COLOR,COLUMN_NAMES),align(20 30,Name,Creation Time));@dolist/inline %q<unassigned>={@pemit %#=align(20 30,u(pueblize,u(moniker`%va,##),+finger [name(##)]),ctime(##));@switch/inline eq(inum(0),words(%q<unassigned>))=1,{@pemit %#=u(SUBHEADER)}}

&INC`EMPTY [u(cobj,fclist)]=@check words(u(setr`%va,empty,sortname(u(filter,NOCAST,children(u(cobj,themedb))))))=@attach %!/INC`MSG=There are no empty themes.;@attach %!/INC`MSG=The following themes have no cast: [u(itemize`%va,iter(%q<empty>,u(pueblize,name(%i0),+fclist [name(%i0)]),,|),|,and,\,)]

&FIL`NOCAST [u(cobj,fclist)]=not(words(u(filter,ISOBJID,get(%0/CAST))))

&OBJECT`DESTROY [u(cobj,fclist)]=@dolist/inline u(lattr`%va,%!/OBJECT`DESTROY`*)={@trigger %!/##=%0,%1,%2,%3,%4,%5,%6,%7,%8,%9}
&OBJECT`DESTROY`PURGETHEME [u(cobj,fclist)]=@select/inline %2=PLAYER,{@dolist/inline u(filter,INCAST,children(u(cobj,themedb)),%b,%b,%0)={&CAST ##=u(filter,ISOBJID,get(##/CAST))}}

&FIL`INCAST [u(cobj,fclist)]=t(match(get(%0/CAST),objid(%1)))

&INC`STATUS [u(cobj,fclist)]=@attach %!/INC`CHECKPC=%0,1;@check strlen(%1)=@attach %!/INC`MSG=ERROR: No status entered.;@attach %!/INC`PARTIAL=%1,u(game_config,FCLIST,STATUS),|,status,status;@attach %!/INC`MSG=%q<t1name>'s FCList status is now: %q<status>;@attach %!/INC`MSG`CHAN=%q<t1name> is now listed as %q<status>.;&D`FINGER`STATUS %q<t1>=%q<status>

th u(newconf,config,FCLIST,STATUS,OPEN|CLOSING|PLAYED|DEAD|TEMP,Character types available,LIST)

&INC`TYPE [u(cobj,fclist)]=@attach %!/INC`CHECKPC=%0,1;@check strlen(%1)=@attach %!/INC`MSG=ERROR: No type entered.;@attach %!/INC`PARTIAL=%1,u(game_config,FCLIST,TYPES),|,type,type;@attach %!/INC`MSG=%q<t1name>'s Character Type is now: %q<type>;@attach %!/INC`MSG`CHAN=%q<t1name> is now listed as a %q<type> Character.;&D`FINGER`TYPE %q<t1>=%q<type>

th u(newconf,config,FCLIST,TYPES,FC|OC|OFC|EFC|SFC,Character types available,LIST)

&STARTUP [u(cobj,fclist)]=@trigger %!/LOOP`REPORTEMPTY

&LOOP`REPORTEMPTY [u(cobj,fclist)]=@select/inline words(u(setr`%va,empty,u(sortname,u(filter,NOCAST,children(u(cobj,themedb))))))=>0,{@attach %!/INC`MSG`CHAN=EMPTY THEMES: The following themes have no Cast: [u(itemize`%va,iter(%q<empty>,u(pueblize,name(%i0),+fclist [name(%i0)]),,|),|,and,\,)]};@wait mul(60,60,6)=@trigger %!/LOOP`REPORTEMPTY

+help/add +fclist=[u(cobj,fclist)]/HLP`+FCLIST
+help/category +fclist=Community
+help/metatags +fclist=theme roster
&HLP`+FCLIST [u(cobj,fclist)]=The FCList system tracks played characters belonging to themes used in [mudname()].%R%R[ansi(hc,Commands)]%R[align(5 [sub(u(width`%va,%#),6)],,[ansi(hw,+fclist)] - Lists all Themes in use.%R[ansi(hw,+fclist <theme>)] - List the roster of characters in a theme.%R[ansi(hw,+fclist/info <theme>)] - List details about a theme's listing.%R[ansi(h,+fclist/mail <theme>=<message>)] - Send an entire cast a @mail easily. Works just like @mail <cast>=<message> so help @mail!%R%RSTATUS:%ROpen - The character is available to play.%RClosing - The character is closing but staff is still accepting applications.%RPlayed - The character is played.%RDead - The character is dead and cannot be applied for.%RTemp - The character is being played temporarily for a plot or similar purpose.%R%RTYPE:%RFC - Feature Character. Created by official canon.%ROFC - Original Feature Character. Created for the game.%ROC - Original Character. Generally not listed.%REFC - Essential Feature Character. Crucial to the game's storytelling\, such as faction leaders.%RSFC - Special Feature Character.%R%RThis list is general and doesn't cover every game.)]

+shelp/add +fclist=[u(cobj,fclist)]/SHLP`+FCLIST
+shelp/category +fclist=Character
&SHLP`+FCLIST [u(cobj,fclist)]=The FCList system allows characters to be added to one or more groups for listing purposes.%R%R[ansi(hc,Staff Commands)]%R[align(5 [sub(u(width`%va,%#),6)],,[ansi(hw,+fclist/create <listname>=<description>)] - Creates a list to group characters under.%R[ansi(h,+fclist/rename <list>=<newname>)] - Rename a list.%R[ansi(h,+fclist/delete <listname>)] - Deletes a list.%R[ansi(h,+fclist/assign <character>=<list>)] - Assigns a character to a list.%R[ansi(h,+fclist/remove <character>=<listname>)] - Removes a character from a list.%R[ansi(h,+fclist/describe <list>=<description>)] - Explain an FCList.%R[ansi(h,+fclist/setinfo <list>=<info>)] - Extra room to hash out an FClist.%R[ansi(h,+fclist/clearinfo <list>)] - Clears an /info entry.%R[ansi(h,+fclist/status <character>=<status>)] - Change a character's status. Choices are OPEN\, CLOSING\, PLAYED\, DEAD\, and TEMP. Can be changed in +gameconfig.%R[ansi(h,+fclist/type <character>=<type>)] - Choices are FC\, OC\, OFC\, EFC\, and SFC. Can be changed in +gameconfig.)]


@@ @dolist lattr(u(cobj,fclist-db)/F`*)={@tel u(setr`%va,new,create(edit(get(u(cobj,fclist-db)/%i0),&,and)))=u(cobj,themedb);@parent %q<new>=u(cobj,themedb);th u(attrib_set`%va,%q<new>,CAST,u(setr`%va,cast,u(sortname,u(filter,ISOBJID,get(u(cobj,fclist-db)/%i0`LIST)))));@cpattr u(cobj,fclist-db)/%i0`DESC=%q<new>/DESCRIBE;@dolist/inline %q<cast>={th u(attrib_set`%va,%i0,D`THEME,u(sortname,setunion(get(%i0/D`THEME),%q,new>)))}}