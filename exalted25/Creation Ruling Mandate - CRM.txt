@switch/inline isdbref(u(crm))=0,{@tel create(Creation Ruling Mandate <CRM>)=config(master_room)}
&crm u(coi)=locate(config(master_room),Creation Ruling Mandate <CRM>,TXxi)
@parent u(crm)=u(coi)
@set u(crm)=WIZARD !NO_COMMAND SAFE

@switch/inline isdbref(u(crm-db))=0,{@tel create(CRM Database <CRM-DB>)=u(crm)}
&crm-db u(coi)=locate(u(crm),CRM Database <CRM-DB>,TXxi)
@parent u(crm-db)=u(coi)
@set u(crm-db)=WIZARD !NO_COMMAND SAFE
@power u(crm-db)=many_attribs

&CMD`+CRM u(crm)=$^(?s)(?\:\+)?crm(?\:/(\S+)?)?(?\: +(.+?))?(?\:=(.*))?$:@include u(ccs)/INC`PARTIAL=%1,setunion(get(u(crm)/VAR`PLAYFLAGS),if(isadmin(%#),get(u(crm)/VAR`ADMINFLAGS)),|,|),|,CRM,switch,switch;@include u(crm)/INC`[strfirstof(%q<switch>,MAIN)]=%2,%3
@set u(crm)/CMD`+CRM=regexp

&VAR`PLAYFLAGS u(crm)=LOG|HIRE|FIRE|COMMIT|UNCOMMIT|PROMOTE|DEMOTE|ALL|STATS|ALLSTATS
&VAR`ADMINFLAGS u(crm)=RENAME|CHOWN|SET|DESC|INTIMACY|SPECIAL|CREATE|DELETE|PARENT|UNPARENT|PARTNER|UNPARTNER|LEND

&FUN`FINDORG u(crm)=first(filterbool(#lambda/strmatch(get(u(crm-db)/\%0),%0),sort(reglattr(u(crm-db)/^\\d+$),i)))

&INC`TARGET u(crm)=@assert strlen(before(%0,/))=@nspemit %#=msghead(CRM) ERROR: No Organization targeted!;@switch/inline valnum(before(%0,/))=1,{@assert hasattr(u(crm-db)/[before(%0,/)])=@nspemit %#=msghead(CRM) ERROR: An Organization by that ID does not exist.;th setq(id,before(%0,/))},0,{@include u(ccs)/INC`PARTIAL=before(%0,/),squish(iter(sort(reglattr(u(crm-db)/^\\d+$),i),get(u(crm-db)/%i0),%b,|),|),|,CRM,found,found;@assert valnum(setr(id,u(FUN`FINDORG,%q<found>)))=@nspemit %#=msghead(CRM) ERROR: No Organization by that name could be found.};th setq(orgname,get(u(crm-db)/%q<id>))

&INC`CREATE u(crm)=@assert strlen(setr(orgname,before(%0,/)))=@nspemit %#=msghead(CRM) ERROR: Name field empty!;@break valnum(u(FUN`FINDORG,%q<orgname>))=@nspemit %#=msghead(CRM) ERROR: An organization by that name already exists!;@include u(ccs)/INC`CHECKPC=%1,1,CRM;&[setr(id,nextslot(u(crm-db),))] u(crm-db)=%q<orgname>;&%q<id>`LEADER u(crm-db)=%q<t1objid>;@nscemit/noisy u(cmo`staffrep)={ansi(h,CRM:) %n created a new CRM Organization: %q<id> '%q<orgname>'};@nspemit %#=msghead(CRM) You created a new organization: %q<id> '%q<orgname>' and gave it to %q<t1name>;@nspemit %q<t1>=msghead(CRM) %n has created a new organization for you: %q<id> '%q<orgname>';@include u(crm)/INC`MAKELOG=%#,%q<id>,{The Organization was created.}

&INC`DELETE u(crm)=@include u(crm)/INC`TARGET;@include u(ccs)/INC`CHECKPC=%1,1,CRM;@nscemit/noisy u(cmo`staffrep)={ansi(h,CRM:) %n deleted %q<id> '%q<orgname>'};@nspemit %#=msghead(CRM) You deleted %q<id> '%q<orgname>';@nspemit setunion(%#,iter(setunion(get(u(crm-db)/%q<id>`LEADER),get(u(crm-db)/%q<id>`EMPLOYEES)),num(%i0)))=msghead(CRM) %n deleted %q<id> '%q<orgname>';@set u(crm-db)=!SAFE;@wipe u(crm-db)/%q<id>;@wipe u(crm-db)/CTRANS`%q<id>`*;@wipe u(crm-db)/CTRANS`*`%q<id>;@set u(crm-db)=SAFE

&INC`RENAME u(crm)=@include u(crm)/INC`TARGET;@assert strlen(%1)=@nspemit %#=msghead(CRM) ERROR: New name field empty!;@break valnum(u(FUN`FINDORG,%1))=@nspemit %#=msghead(CRM) ERROR: An organization by that name already exists!;@nscemit/noisy u(cmo`staffrep)={ansi(h,CRM:) %n renamed %q<id> '%q<orgname>' to '%1'};@nspemit/list setunion(%#,iter(setunion(get(u(crm-db)/%q<id>`LEADER),get(u(crm-db)/%q<id>`EMPLOYEES)),num(%i0)))=msghead(CRM) %n renamed %q<id> '%q<orgname>' to '%1';&%q<id> u(crm-db)=%1;@include u(crm)/INC`MAKELOG=%#,%q<id>,{The Organization was renamed to: %1}

&INC`CHOWN u(crm)=@include u(crm)/INC`TARGET;@include u(ccs)/INC`CHECKPC=%1,1,CRM;@nscemit/noisy u(cmo`staffrep)={ansi(h,CRM:) %n renamed %q<id> '%q<orgname>' to: %1};@nspemit %#=msghead(CRM) You chown'd %q<id> '%q<orgname>' to: %q<t1name>;@nspemit setunion(%# %q<t1>,iter(setunion(get(u(crm-db)/%q<id>`LEADER),get(u(crm-db)/%q<id>`EMPLOYEES)),num(%i0)))=msghead(CRM) %n chown'd %q<id> '%q<orgname>' to: %q<t1name>;@include u(crm)/INC`MAKELOG=%#,%q<id>,{The Organization was chown'd to: %q<t1name>};&%q<id>`LEADER u(crm-db)=%q<t1objid>

&INC`MAKELOG u(crm)=@assert isdbref(%0);@assert hasattrval(u(crm-db),%1);@assert strlen(%2);&[setr(log,%1`LOG`[nextslot(u(crm-db),%1`LOG)])] u(crm-db)=%2;&%q<log>`ON u(crm-db)=secs();&%q<log>`BY u(crm-db)=objid(%0)

&INC`LOG u(crm)=@include u(crm)/INC`TARGET;@nspemit %#=header(Logs for %q<id> '%q<orgname>');@dolist/inline sortkey(#lambda/last(\%0,`),lattr(u(crm-db)/%q<id>`LOG`*))={@nspemit %#=subheader(By [name(get(u(crm-db)/%i0`BY))] on [convsecs(get(u(crm-db)/%i0`ON))]);@nspemit %#=get(u(crm-db)/%i0)};@nspemit %#=header()

&INC`HIRE u(crm)=@include u(crm)/INC`TARGET;@include u(crm)/INC`CHECKPERM;@include u(ccs)/INC`CHECKPC=%1,1,CRM;@break match(get(u(crm-db)/%q<id>`EMPLOYEES),%q<t1objid>)=@nspemit %#=msghead(CRM) ERROR: %q<t1name> is already an employee.;@nscemit/noisy u(cmo`staffrep)={ansi(h,CRM:) %n hired a new %q<id> '%q<orgname>' Employee: %1};@nspemit %#=msghead(CRM) You hired a new %q<id> '%q<orgname>' Employee: %q<t1name>;@nspemit setunion(%# %q<t1>,iter(setunion(get(u(crm-db)/%q<id>`LEADER),get(u(crm-db)/%q<id>`EMPLOYEES)),num(%i0)))=msghead(CRM) %n hired a new %q<id> '%q<orgname>' Employee: %q<t1name>;@include u(crm)/INC`MAKELOG=%#,%q<id>,{New Employee Hired: %q<t1name>};&%q<id>`EMPLOYEES u(crm-db)=filterbool(#lambda/isobjid(\%0),setunion(get(u(crm-db)/%q<id>`EMPLOYEES),%q<t1objid>))

&INC`FIRE u(crm)=@include u(crm)/INC`TARGET;@include u(crm)/INC`CHECKPERM;@include u(ccs)/INC`CHECKPC=%1,1,CRM;@assert match(get(u(crm-db)/%q<id>`EMPLOYEES),%q<t1objid>)=@nspemit %#=msghead(CRM) ERROR: %q<t1name> is not an employee.;@assert if(match(get(u(crm-db)/%q<id>`MANAGERS),%q<t1objid>),or(isadmin(%#),strmatch(get(u(crm-db)/%q<id>`LEADER),%:)),1)=@nspemit %#=msghead(CRM) ERROR: %q<t1name> is a Manager. Only the Leader or Staff may fire Managers.;@nscemit/noisy u(cmo`staffrep)={ansi(h,CRM:) %n fired a %q<id> '%q<orgname>' Employee: %1};@nspemit %#=msghead(CRM) You fired a %q<id> '%q<orgname>' Employee: %q<t1name>;@nspemit setunion(%#,iter(setunion(get(u(crm-db)/%q<id>`LEADER),get(u(crm-db)/%q<id>`EMPLOYEES)),num(%i0)))=msghead(CRM) %n fired a %q<id> '%q<orgname>' Employee: %q<t1name>;@include u(crm)/INC`MAKELOG=%#,%q<id>,{Employee Fired: %q<t1name>};&%q<id>`EMPLOYEES u(crm-db)=filterbool(#lambda/isobjid(\%0),setdiff(get(u(crm-db)/%q<id>`EMPLOYEES),%q<t1objid>));&%q<id>`MANAGERS u(crm-db)=filterbool(#lambda/isobjid(\%0),setdiff(get(u(crm-db)/%q<id>`MANAGERS),%q<t1objid>))

&INC`PROMOTE u(crm)=@include u(crm)/INC`TARGET;@assert or(isadmin(%#),match(get(u(crm-db)/%q<id>`LEADER),%:))=@nspemit %#=msghead(CRM) ERROR: Only the Leader or Staff may Promote members.;@include u(ccs)/INC`CHECKPC=%1,1,CRM;@break match(get(u(crm-db)/%q<id>`MANAGERS),%q<t1objid>)=@nspemit %#=msghead(CRM) ERROR: %q<t1name> is already a Manager.;@switch/inline t(match(get(u(crm-db)/%q<id>`EMPLOYEES),%q<t1objid>))=0,{@include u(crm)/INC`HIRE};@nscemit/noisy u(cmo`staffrep)={ansi(h,CRM:) %n appointed a new %q<id> '%q<orgname>' manager: %1};@nspemit %#=msghead(CRM) You appointed a new %q<id> '%q<orgname>' manager: %q<t1name>;@nspemit setunion(%# %q<t1>,iter(setunion(get(u(crm-db)/%q<id>`LEADER),get(u(crm-db)/%q<id>`EMPLOYEES)),num(%i0)))=msghead(CRM) %n appointed a new %q<id> '%q<orgname>' manager: %q<t1name>;@include u(crm)/INC`MAKELOG=%#,%q<id>,{New Manager appointed: %q<t1name>};&%q<id>`MANAGERS u(crm-db)=filterbool(#lambda/isobjid(\%0),setunion(get(u(crm-db)/%q<id>`MANAGERS),%q<t1objid>))

&INC`DEMOTE u(crm)=@include u(crm)/INC`TARGET;@assert or(isadmin(%#),match(get(u(crm-db)/%q<id>`LEADER),%:))=@nspemit %#=msghead(CRM) ERROR: Only the Leader or Staff may Demote members.;@include u(ccs)/INC`CHECKPC=%1,1,CRM;@assert match(get(u(crm-db)/%q<id>`MANAGERS),%q<t1objid>)=@nspemit %#=msghead(CRM) ERROR: %q<t1name> is not a Manager.;@nscemit/noisy u(cmo`staffrep)={ansi(h,CRM:) %n demoted a %q<id> '%q<orgname>' manager: %1};@nspemit %#=msghead(CRM) You demoted a %q<id> '%q<orgname>' manager: %q<t1name>;@nspemit setunion(%# %q<t1>,iter(setunion(get(u(crm-db)/%q<id>`LEADER),get(u(crm-db)/%q<id>`EMPLOYEES)),num(%i0)))=msghead(CRM) %n demoted a %q<id> '%q<orgname>' manager: %q<t1name>;@include u(crm)/INC`MAKELOG=%#,%q<id>,{Manager Demoted: %q<t1name>};&%q<id>`MANAGERS u(crm-db)=filterbool(#lambda/isobjid(\%0),setdiff(get(u(crm-db)/%q<id>`MANAGERS),%q<t1objid>))

&VAR`SET`MAIN`STATS u(crm)=COMPETENCE|INFLUENCE|REACH|SIZE|WEALTH|CAPITAL|MOTIVATION|EXTRA
&VAR`SET`DESC`STATS u(crm)=COMPETENCE|INFLUENCE|REACH|SIZE|WEALTH

&INC`SET u(crm)=@include u(crm)/INC`TARGET;@assert strlen(before(%0,/))=@nspemit %#=msghead(CRM) ERROR: Nothing entered to set.;@include u(ccs)/INC`PARTIAL=after(%0,/),v(VAR`SET`MAIN`STATS),|,CRM,choice,choice;@switch/inline strlen(%1)=0,{@nscemit/noisy u(cmo`staffrep)={ansi(h,CRM:) %n cleared %q<id> '%q<orgname>''s %q<choice>};@nspemit %#=msghead(CRM) You cleared %q<id> '%q<orgname>''s %q<choice>;@nspemit setunion(%#,iter(setunion(get(u(crm-db)/%q<id>`LEADER),get(u(crm-db)/%q<id>`EMPLOYEES)),num(%i0)))=msghead(CRM) %n cleared %q<id> '%q<orgname>''s %q<choice>;@include u(crm)/INC`MAKELOG=%#,%q<id>,{Cleared %q<choice>.};&%q<id>`STAT`%q<choice> u(crm-db)},{@nscemit/noisy u(cmo`staffrep)={ansi(h,CRM:) %n set %q<id> '%q<orgname>''s %q<choice> to: %1};@nspemit %#=msghead(CRM) You set %q<id> '%q<orgname>''s %q<choice> to: %1;@nspemit setunion(%#,iter(setunion(get(u(crm-db)/%q<id>`LEADER),get(u(crm-db)/%q<id>`EMPLOYEES)),num(%i0)))=msghead(CRM) %n set %q<id> '%q<orgname>''s %q<choice> to: %1;@include u(crm)/INC`MAKELOG=%#,%q<id>,{Set %q<choice> to: %1};&%q<id>`STAT`%q<choice> u(crm-db)=%1}

&INC`DESC u(crm)=@include u(crm)/INC`TARGET;@assert strlen(before(%0,/))=@nspemit %#=msghead(CRM) ERROR: Nothing entered to set.;@include u(ccs)/INC`PARTIAL=after(%0,/),v(VAR`SET`DESC`STATS),|,CRM,choice,choice;@switch/inline strlen(%1)=0,{@nscemit/noisy u(cmo`staffrep)={ansi(h,CRM:) %n cleared %q<id> '%q<orgname>''s %q<choice> DESC};@nspemit %#=msghead(CRM) You cleared %q<id> '%q<orgname>''s %q<choice> DESC;@nspemit setunion(%#,iter(setunion(get(u(crm-db)/%q<id>`LEADER),get(u(crm-db)/%q<id>`EMPLOYEES)),num(%i0)))=msghead(CRM) %n cleared %q<id> '%q<orgname>''s %q<choice> DESC;@include u(crm)/INC`MAKELOG=%#,%q<id>,{Cleared %q<choice> DESC.};&%q<id>`STAT`%q<choice>`DESC u(crm-db)},{@nscemit/noisy u(cmo`staffrep)={ansi(h,CRM:) %n set %q<id> '%q<orgname>''s %q<choice> DESC to: %1};@nspemit %#=msghead(CRM) You set %q<id> '%q<orgname>''s %q<choice> DESC to: %1;@nspemit setunion(%#,iter(setunion(get(u(crm-db)/%q<id>`LEADER),get(u(crm-db)/%q<id>`EMPLOYEES)),num(%i0)))=msghead(CRM) %n set %q<id> '%q<orgname>''s %q<choice> DESC to: %1;@include u(crm)/INC`MAKELOG=%#,%q<id>,{Set %q<choice> DESC to: %1};&%q<id>`STAT`%q<choice>`DESC u(crm-db)=%1}

&INC`INTIMACY u(crm)=@include u(crm)/INC`TARGET;@assert valnum(setr(int,after(%0,/)))=@nspemit %#=msghead(CRM) ERROR: Intimacies must be handled by their Slots!;@switch/inline strlen(%1)=0,{@nscemit/noisy u(cmo`staffrep)={ansi(h,CRM:) %n cleared %q<id> '%q<orgname>''s %q<int> Intimacy};@nspemit %#=msghead(CRM) You cleared %q<id> '%q<orgname>''s %q<int> Intimacy;@nspemit setunion(%#,iter(setunion(get(u(crm-db)/%q<id>`LEADER),get(u(crm-db)/%q<id>`EMPLOYEES)),num(%i0)))=msghead(CRM) %n cleared %q<id> '%q<orgname>''s %q<int> Intimacy;@include u(crm)/INC`MAKELOG=%#,%q<id>,{Cleared %q<int> Intimacy.};&%q<id>`INTIMACY`%q<int> u(crm-db)},{@nscemit/noisy u(cmo`staffrep)={ansi(h,CRM:) %n set %q<id> '%q<orgname>''s %q<int> Intimacy to: %1};@nspemit %#=msghead(CRM) You set %q<id> '%q<orgname>''s %q<int> Intimacy to: %1;@nspemit setunion(%#,iter(setunion(get(u(crm-db)/%q<id>`LEADER),get(u(crm-db)/%q<id>`EMPLOYEES)),num(%i0)))=msghead(CRM) %n set %q<id> '%q<orgname>''s %q<int> Intimacy to: %1;@include u(crm)/INC`MAKELOG=%#,%q<id>,{Set %q<int> Intimacy to: %1};&%q<id>`INTIMACY`%q<int> u(crm-db)=%1}

&INC`SPECIAL u(crm)=@include u(crm)/INC`TARGET;@assert valnum(setr(int,after(%0,/)))=@nspemit %#=msghead(CRM) ERROR: Intimacies must be handled by their Slots!;@switch/inline strlen(%1)=0,{@nscemit/noisy u(cmo`staffrep)={ansi(h,CRM:) %n cleared %q<id> '%q<orgname>''s %q<int> Special Asset};@nspemit %#=msghead(CRM) You cleared %q<id> '%q<orgname>''s %q<int> Special Asset;@nspemit setunion(%#,iter(setunion(get(u(crm-db)/%q<id>`LEADER),get(u(crm-db)/%q<id>`EMPLOYEES)),num(%i0)))=msghead(CRM) %n cleared %q<id> '%q<orgname>''s %q<int> Special Asset;@include u(crm)/INC`MAKELOG=%#,%q<id>,{Cleared %q<int> Special Asset.};&%q<id>`SPECIAL`%q<int> u(crm-db)},{@nscemit/noisy u(cmo`staffrep)={ansi(h,CRM:) %n set %q<id> '%q<orgname>''s %q<int> Special Asset to: %1};@nspemit %#=msghead(CRM) You set %q<id> '%q<orgname>''s %q<int> Special Asset to: %1;@nspemit setunion(%#,iter(setunion(get(u(crm-db)/%q<id>`LEADER),get(u(crm-db)/%q<id>`EMPLOYEES)),num(%i0)))=msghead(CRM) %n set %q<id> '%q<orgname>''s %q<int> Special Asset to: %1;@include u(crm)/INC`MAKELOG=%#,%q<id>,{Set %q<int> Special Asset to: %1};&%q<id>`SPECIAL`%q<int> u(crm-db)=%1}

&INC`CHECKPERM u(crm)=@assert or(isadmin(%#),match(get(u(crm-db)/%q<id>`PROMOTE),%:),strmatch(get(u(crm-db)/%q<id>`LEADER),%:))=@nspemit %#=msghead(CRM) ERROR: You don't have the authority to do that.

&INC`COMMIT u(crm)=@include u(crm)/INC`TARGET;@include u(crm)/INC`CHECKPERM;@assert valnum(setr(int,after(%0,/)))=@nspemit %#=msghead(CRM) ERROR: Commitments must be whole positive numbers!;@assert or(gte(u(FUN`GET`CAPITAL`CURRENT,%q<id>),%q<int>),isadmin(%#))=@nspemit %#=msghead(CRM) ERROR: That would commit more Capital than the organization has. Admin must do this.;@assert strlen(%1)=@nspemit %#=msghead(CRM) ERROR: Commit reason field empty.;&[setr(attr,%q<id>`COMMIT`[setr(slot,nextslot(u(crm-db),%q<id>`COMMIT))])] u(crm-db)=%q<int>;&%q<attr>`DESC u(crm-db)=%1;@nscemit/noisy u(cmo`staffrep)={ansi(h,CRM:) %n Committed %q<int> of %q<id> '%q<orgname>''s Capital to: %1};@nspemit %#=msghead(CRM) You Committed %q<int> of %q<id> '%q<orgname>''s Capital to: %1;@nspemit setunion(%#,iter(setunion(get(u(crm-db)/%q<id>`LEADER),get(u(crm-db)/%q<id>`EMPLOYEES)),num(%i0)))=msghead(CRM) %n Committed %q<int> of %q<id> '%q<orgname>''s Capital to: %1;@include u(crm)/INC`MAKELOG=%#,%q<id>,{Committed %q<int> Capital to: %1}

&INC`UNCOMMIT u(crm)=@include u(crm)/INC`TARGET;@include u(crm)/INC`CHECKPERM;@assert valnum(setr(int,%1))=@nspemit %#=msghead(CRM) ERROR: Commitment slots must be whole positive numbers!;@assert hasattr(u(crm-db)/%q<id>`COMMIT`%q<int>)=@nspemit %#=msghead(CRM) ERROR: Commitment Slot not found.;@nscemit/noisy u(cmo`staffrep)={ansi(h,CRM:) %n released [get(u(crm-db)/%q<id>`COMMIT`%q<int>)] of %q<id> '%q<orgname>''s Capital from: [get(u(crm-db)/%q<id>`COMMIT`%q<int>`DESC)]};@nspemit %#=msghead(CRM) You released [get(u(crm-db)/%q<id>`COMMIT`%q<int>)] of %q<id> '%q<orgname>''s Capital from: [get(u(crm-db)/%q<id>`COMMIT`%q<int>`DESC)];@nspemit setunion(%#,iter(setunion(get(u(crm-db)/%q<id>`LEADER),get(u(crm-db)/%q<id>`EMPLOYEES)),num(%i0)))=msghead(CRM) %n released [get(u(crm-db)/%q<id>`COMMIT`%q<int>)] of %q<id> '%q<orgname>''s Capital from: [get(u(crm-db)/%q<id>`COMMIT`%q<int>`DESC)];@include u(crm)/INC`MAKELOG=%#,%q<id>,{Released  [get(u(crm-db)/%q<id>`COMMIT`%q<int>)] Capital from: [get(u(crm-db)/%q<id>`COMMIT`%q<int>`DESC)]};@set u(crm-db)=!SAFE;@wipe u(crm-db)/%q<id>`COMMIT`%q<int>;@set u(crm-db)=SAFE

&INC`LEND u(crm)=@include u(crm)/INC`TARGET;@include u(crm)/INC`CHECKPERM;th setq(mid,%q<id>,morgname,%q<orgname>);@assert or(valnum(setr(int,%1)),eq(%q<int>,0),not(strlen(%q<int>)))=@nspemit %#=msghead(CRM) ERROR: Lends must be positive numbers or 0 to clear!;@assert or(gte(u(FUN`GET`CAPITAL`CURRENT,%q<id>),%q<int>),isadmin(%#))=@nspemit %#=msghead(CRM) ERROR: That would lend more Capital than the organization has. Admin must do this.;@include u(crm)/INC`TARGET=after(%0,/);@switch/inline t(%q<int>)=0,{@assert setr(trans,get(u(crm-db)/CTRANS`%q<mid>`%q<id>))=@nspemit %#=msghead(CRM) ERROR: No Capital's being lent to %q<id> '%q<orgname>'. Nothing to clear.;@nscemit/noisy u(cmo`staffrep)={ansi(h,CRM:) %n reclaimed %q<trans> of %q<mid> '%q<morgname>''s Capital from: %q<id> '%q<orgname>'};@nspemit %#=msghead(CRM) You reclaimed %q<trans> of %q<mid> '%q<morgname>''s Capital from: %q<id> '%q<orgname>';@nspemit setunion(%#,setunion(iter(setunion(get(u(crm-db)/%q<id>`LEADER),get(u(crm-db)/%q<id>`EMPLOYEES)),num(%i0)),iter(setunion(get(u(crm-db)/%q<mid>`LEADER),get(u(crm-db)/%q<mid>`EMPLOYEES)),num(%i0))))=msghead(CRM) %n reclaimed %q<trans> of %q<mid> '%q<morgname>''s Capital from: %q<id> '%q<orgname>';@wipe u(crm-db)/CTRANS`%q<mid>`%q<id>;@include u(crm)/INC`MAKELOG=%#,%q<mid>,{Reclaimed %q<trans> Capital from: %q<id> '%q<orgname>'};@include u(crm)/INC`MAKELOG=%#,%q<id>,{Lost %q<trans> Capital from: %q<mid> '%q<morgname>'}},{th setq(trans,%q<int>);@nscemit/noisy u(cmo`staffrep)={ansi(h,CRM:) %n lent %q<trans> of %q<mid> '%q<morgname>''s Capital to: %q<id> '%q<orgname>'};@nspemit %#=msghead(CRM) You lent %q<trans> of %q<mid> '%q<morgname>''s Capital to: %q<id> '%q<orgname>';@nspemit setunion(%#,setunion(iter(setunion(get(u(crm-db)/%q<id>`LEADER),get(u(crm-db)/%q<id>`EMPLOYEES)),num(%i0)),iter(setunion(get(u(crm-db)/%q<mid>`LEADER),get(u(crm-db)/%q<mid>`EMPLOYEES)),num(%i0))))=msghead(CRM) %n lent %q<trans> of %q<mid> '%q<morgname>''s Capital to: %q<id> '%q<orgname>';&CTRANS`%q<mid>`%q<id> u(crm-db)=%q<int>;@include u(crm)/INC`MAKELOG=%#,%q<mid>,{Transferred %q<trans> Capital from: %q<id> '%q<orgname>'};@include u(crm)/INC`MAKELOG=%#,%q<id>,{Gained %q<trans> Capital from: %q<mid> '%q<morgname>'}}

&INC`PARENT u(crm)=@include u(crm)/INC`TARGET;@include u(crm)/INC`CHECKPERM;th setq(mid,%q<id>,morgname,%q<orgname>);@include u(crm)/INC`TARGET=%1;@break gt(get(u(crm-db)/%q<id>`PARENT),0)=@nspemit %#=msghead(CRM) ERROR: That Organization is already a Subsidiary. Please unparent it first.;@break match(get(u(Crm-db)/%q<mid>`PARTNERS),%q<id>)=@nspemit %#=msghead(CRM) ERROR: %q<id> '%q<orgname>' is a partner of %q<mid> '%q<morgname>'!;th setq(trans,%q<int>);@nscemit/noisy u(cmo`staffrep)={ansi(h,CRM:) %n assigned %q<mid> '%q<morgname>' as a Subsidiary of: %q<id> '%q<orgname>'};@nspemit %#=msghead(CRM) You assigned %q<mid> '%q<morgname>' as a Subsidiary of: %q<id> '%q<orgname>';@nspemit setunion(%#,setunion(iter(setunion(get(u(crm-db)/%q<id>`LEADER),get(u(crm-db)/%q<id>`EMPLOYEES)),num(%i0)),iter(setunion(get(u(crm-db)/%q<mid>`LEADER),get(u(crm-db)/%q<mid>`EMPLOYEES)),num(%i0))))=msghead(CRM) %n assigned %q<mid> '%q<morgname>' as a Subsidiary of: %q<id> '%q<orgname>';&%q<mid>`PARENT u(crm-db)=%q<id>;@include u(crm)/INC`MAKELOG=%#,%q<mid>,{Assigned as Subsidiary of: %q<id> '%q<orgname>'};@include u(crm)/INC`MAKELOG=%#,%q<id>,{Gained Subsidiary: %q<mid> '%q<morgname>'}

&INC`UNPARENT u(crm)=@include u(crm)/INC`TARGET;@include u(crm)/INC`CHECKPERM;th setq(mid,%q<id>,morgname,%q<orgname>);@assert gt(get(u(crm-db)/%q<id>`PARENT),0)=@nspemit %#=msghead(CRM) ERROR: That Organization is not a Subsidiary.;@include u(crm)/INC`TARGET=get(u(crm-db)/%q<id>`PARENT);@nscemit/noisy u(cmo`staffrep)={ansi(h,CRM:) %n separated %q<mid> '%q<morgname>''s from: %q<id> '%q<orgname>'};@nspemit %#=msghead(CRM) You separated %q<mid> '%q<morgname>''s from: %q<id> '%q<orgname>';@nspemit setunion(%#,setunion(iter(setunion(get(u(crm-db)/%q<id>`LEADER),get(u(crm-db)/%q<id>`EMPLOYEES)),num(%i0)),iter(setunion(get(u(crm-db)/%q<mid>`LEADER),get(u(crm-db)/%q<mid>`EMPLOYEES)),num(%i0))))=msghead(CRM) %n separated %q<mid> '%q<morgname>''s from: %q<id> '%q<orgname>';@wipe u(crm-db)/%q<mid>`PARENT;@include u(crm)/INC`MAKELOG=%#,%q<mid>,{Seperated from: %q<id> '%q<orgname>'};@include u(crm)/INC`MAKELOG=%#,%q<id>,{Lost Holding: %q<mid> '%q<morgname>'}

&INC`PARTNER u(crm)=@include u(crm)/INC`TARGET;th setq(mid,%q<id>,morgname,%q<orgname>);@include u(crm)/INC`TARGET=%1;@break match(get(u(Crm-db)/%q<mid>`PARTNERS),%q<id>)=@nspemit %#=msghead(CRM) ERROR: That Organization is already a partner of %q<mid> '%q<morgname>';@nscemit/noisy u(cmo`staffrep)={ansi(h,CRM:) %n partnered %q<mid> '%q<morgname>''s to: %q<id> '%q<orgname>'};@nspemit %#=msghead(CRM) You partnered %q<mid> '%q<morgname>''s to: %q<id> '%q<orgname>';@nspemit setunion(%#,setunion(iter(setunion(get(u(crm-db)/%q<id>`LEADER),get(u(crm-db)/%q<id>`EMPLOYEES)),num(%i0)),iter(setunion(get(u(crm-db)/%q<mid>`LEADER),get(u(crm-db)/%q<mid>`EMPLOYEES)),num(%i0))))=msghead(CRM) %n partnered %q<mid> '%q<morgname>''s to: %q<id> '%q<orgname>';&%q<mid>`PARTNERS u(crm-db)=filterbool(#lambda/hasattrval(u(crm-db),\%0),setunion(get(u(crm-db)/%q<mid>`PARTNERS),%q<id>));&%q<id>`PARTNERS u(crm-db)=filterbool(#lambda/hasattrval(u(crm-db),\%0),setunion(get(u(crm-db)/%q<id>`PARTNERS),%q<mid>));@include u(crm)/INC`MAKELOG=%#,%q<mid>,{Partnered to: %q<id> '%q<orgname>'};@include u(crm)/INC`MAKELOG=%#,%q<id>,{Partnered to: %q<mid> '%q<morgname>'}

&INC`UNPARTNER u(crm)=@include u(crm)/INC`TARGET;th setq(mid,%q<id>,morgname,%q<orgname>);@include u(crm)/INC`TARGET=%1;@assert match(get(u(Crm-db)/%q<mid>`PARTNERS),%q<id>)=@nspemit %#=msghead(CRM) ERROR: That Organization is not a partner of %q<mid> '%q<morgname>';@nscemit/noisy u(cmo`staffrep)={ansi(h,CRM:) %n unpartnered %q<mid> '%q<morgname>''s from: %q<id> '%q<orgname>'};@nspemit %#=msghead(CRM) You unpartnered %q<mid> '%q<morgname>''s from: %q<id> '%q<orgname>';@nspemit setunion(%#,setunion(iter(setunion(get(u(crm-db)/%q<id>`LEADER),get(u(crm-db)/%q<id>`EMPLOYEES)),num(%i0)),iter(setunion(get(u(crm-db)/%q<mid>`LEADER),get(u(crm-db)/%q<mid>`EMPLOYEES)),num(%i0))))=msghead(CRM) %n unpartnered %q<mid> '%q<morgname>''s from: %q<id> '%q<orgname>';&%q<mid>`PARTNERS u(crm-db)=filterbool(#lambda/hasattrval(u(crm-db),\%0),setdiff(get(u(crm-db)/%q<mid>`PARTNERS),%q<id>));&%q<id>`PARTNERS u(crm-db)=filterbool(#lambda/hasattrval(u(crm-db),\%0),setdiff(get(u(crm-db)/%q<id>`PARTNERS),%q<mid>));@include u(crm)/INC`MAKELOG=%#,%q<mid>,{Unpartnered from: %q<id> '%q<orgname>'};@include u(crm)/INC`MAKELOG=%#,%q<id>,{Unpartnered from: %q<mid> '%q<morgname>'}


&FUN`BORDER u(crm)=align(1. 74 1.,u(FUN`COLOR,%0,|),%1,u(FUN`COLOR,%0,|)%R)

&FUN`COLOR u(crm)=ansi(m,%1)

&FUN`HEADER u(crm)=u(FUN`COLOR,%0,\})[center(u(FUN`COLOR,%0,/)[ansi(h,%1)][u(FUN`COLOR,%0,/)],76,u(FUN`COLOR,%0,-))][u(FUN`COLOR,%0,\{)]

&INC`MAIN u(crm)=@switch/inline gt(strlen(%0),0)=1,{@include u(crm)/INC`TARGET;@include u(crm)/INC`MAIN`SHOWSHEET=%q<id>},{@include u(crm)/INC`MAIN`LISTORGS}

&INC`MAIN`LISTORGS u(crm)=@nspemit %#=header(CRM Organizations)%R[align(4 17 17 7 17,ID,NAME,LEADER,MEMBERS,SUBSIDIARIES)];@dolist/inline sort(filterbool(#lambda/not(hasattrval(u(crm-db)/\%0`PARENT)),reglattr(u(crm-db)/^\\d+$)),i)={@nspemit %#=align(4 17 17 7 17,pueblize(rjust(%i0,3).,+crm %i0),pueblize(get(u(crm-db)/%i0),+crm %i0),pueblize(name(get(u(crm-db)/%i0`LEADER)),+oocfinger [name(get(u(crm-db)/%i0`LEADER))]),words(filterbool(#lambda/isobjid(\%0),get(u(crm-db)/%i0`EMPLOYEES))),itemize(iter(%q<subs>,pueblize(get(u(crm-db)/%i0),+crm %i0),%b,|),|,and,\,))};@nspemit %#=header(+crm/all for Subsidiaries)

&INC`ALL u(crm)=@nspemit %#=header(CRM Organizations + Subsidiaries)%R[align(4 17 17 7 17,ID,NAME,LEADER,MEMBERS,PARENT)];@dolist/inline sort(reglattr(u(crm-db)/^\\d+$),i)={@nspemit %#=align(4 17 17 7 17,pueblize(rjust(%i0,3).,+crm %i0),pueblize(get(u(crm-db)/%i0),+crm %i0),pueblize(name(get(u(crm-db)/%i0`LEADER)),+oocfinger [name(get(u(crm-db)/%i0`LEADER))]),words(filterbool(#lambda/isobjid(\%0),get(u(crm-db)/%i0`EMPLOYEES))),if(hasattrval(u(crm-db)/%i0`PARENT),pueblize(get(u(crm-db)/[get(u(crm-db)/%i0`PARENT)]),+crm [get(u(crm-db)/%i0`PARENT)])))};@nspemit %#=header(+crm/stats for Stats)

&INC`STATS u(crm)=@nspemit %#=header(CRM Organizations - Stats Mode)%R[align(4 17 17 4 4 4 4 4 4 4,ID,NAME,LEADER,COMP,INFL,REAC,SIZE,WEAL,CAPI,AVAI)];@dolist/inline sort(filterbool(#lambda/not(hasattrval(u(crm-db)/\%0`PARENT)),reglattr(u(crm-db)/^\\d+$)),i)={@nspemit %#=align(4 17 17 4 4 4 4 4 4 4,pueblize(rjust(%i0,3).,+crm %i0),pueblize(get(u(crm-db)/%i0),+crm %i0),pueblize(name(get(u(crm-db)/%i0`LEADER)),+oocfinger [name(get(u(crm-db)/%i0`LEADER))]),strfirstof(get(u(crm-db)/%i0`STAT`COMPETENCE),0),strfirstof(get(u(crm-db)/%i0`STAT`INFLUENCE),0),strfirstof(get(u(crm-db)/%i0`STAT`REACH),0),strfirstof(get(u(crm-db)/%i0`STAT`SIZE),0),strfirstof(get(u(crm-db)/%i0`STAT`WEALTH),0),strfirstof(u(FUN`GET`CAPITAL`MAX,%i0),0),strfirstof(u(FUN`GET`CAPITAL`AVAILABLE,%i0),0))};@nspemit %#=header(+crm/allstats for Subsidiaries)

&INC`ALLSTATS u(crm)=@nspemit %#=header(CRM Organizations - Stats Mode)%R[align(4 17 17 4 4 4 4 4 4,ID,NAME,LEADER,COMP,INFL,REAC,SIZE,WEAL,CAPI,AVAI)];@dolist/inline sort(reglattr(u(crm-db)/^\\d+$),i)={@nspemit %#=align(4 17 17 4 4 4 4 4 4,pueblize(rjust(%i0,3).,+crm %i0),pueblize(get(u(crm-db)/%i0),+crm %i0),pueblize(name(get(u(crm-db)/%i0`LEADER)),+oocfinger [name(get(u(crm-db)/%i0`LEADER))]),strfirstof(get(u(crm-db)/%i0`STAT`COMPETENCE),0),strfirstof(get(u(crm-db)/%i0`STAT`INFLUENCE),0),strfirstof(get(u(crm-db)/%i0`STAT`REACH),0),strfirstof(get(u(crm-db)/%i0`STAT`SIZE),0),strfirstof(get(u(crm-db)/%i0`STAT`WEALTH),0),strfirstof(u(FUN`GET`CAPITAL`MAX,%i0),0),strfirstof(u(FUN`GET`CAPITAL`AVAILABLE,%i0),0))};@nspemit %#=header(+crm/allstats for Subsidiaries)

&INC`MAIN`SHOWSHEET u(crm)=@nspemit %#=u(FUN`HEADER,%0,Policy);@nspemit %#=u(FUN`BORDER,%0,align(36 37,Name: [default(u(crm-db)/%0,Unset)]%RMotivation: [default(u(crm-db)/%0`STAT`MOTIVATION,Unset)],INTIMACIES%R[iter(sortkey(#lambda/last(\%0,`),lattr(u(crm-db)/%0`INTIMACY`*)),rjust(last(%i0,`),2). [default(u(crm-db)/%i0,Unset (???))],%b,%R)]));@switch/inline hasattrval(u(crm-db)/%0`STAT`EXTRA)=1,{@nspemit %#=u(FUN`HEADER,%0,Extra);@nspemit %#=u(FUN`BORDER,%0,get(u(crm-db)/%0`STAT`EXTRA))};@nspemit %#=u(FUN`HEADER,%0,Assets);@nspemit %#=u(FUN`BORDER,%0,ljust(STATS,16)DETAILS%R[iter(Competence Influence Reach Size Wealth,align(>13 1 56,%i0%B[rjust(default(u(crm-db)/%0`STAT`%i0,0),2,0)],-,get(u(crm-db)/%0`STAT`%i0`DESC)),%b,%R)]);@switch/inline t(nattr(u(crm-db)/%0`SPECIAL`*))=1,{@nspemit %#=u(FUN`BORDER,%0,SPECIAL ASSETS:%R[iter(sortkey(#lambda/last(\%0,`),lattr(u(crm-db)/%0`SPECIAL`*)),rjust(last(%i0,`),2). [default(u(crm-db)/%i0,Unset (???))],%b,%R)])};@nspemit %#=u(FUN`HEADER,%0,Resources);@nspemit %#=u(FUN`BORDER,%0,align(13 1 56,CAPITAL,,COMMITMENTS));@nspemit %#=u(FUN`BORDER,%0,align(>13 1 56,iter(Max Current Borrowed Lent Committed Available,%i0: [rjust(u(FUN`GET`CAPITAL`%i0,%0),2,0)],%b,%R),,iter(sortkey(#lambda/last(\%0,`),lattr(u(crm-db)/%0`COMMIT`*)),last(%i0,`): [get(u(crm-db)/%i0)] for: [get(u(crm-db)/%i0`DESC)],%b,%r)[if(u(FUN`GET`CAPITAL`BORROWED,%0),%RBORROWING:%R[iter(filterbool(#lambda/hasattrval(u(crm-db),elements(\%0,2,`)),lattr(u(crm-db)/CTRANS`*`%0)),get(u(crm-db)/%i0) from [pueblize(elements(%i0,2,`)-[get(u(crm-db)/[elements(%i0,2,`)])],+crm [elements(%i0,2,`)])],%b,%r)])][if(u(FUN`GET`CAPITAL`LENT,%0),%RLENDING:%R[iter(filterbool(#lambda/hasattrval(u(crm-db),elements(\%0,3,`)),lattr(u(crm-db)/CTRANS`%0`*)),get(u(crm-db)/%i0) to [pueblize(elements(%i0,3,`)-[get(u(crm-db)/[elements(%i0,3,`)])],+crm [elements(%i0,3,`)])],%b,%r)])]));@nspemit %#=u(FUN`HEADER,%0,Structure);@nspemit %#=u(FUN`BORDER,%0,align(33 40,MEMBERSHIP%RLeader: [name(get(u(crm-db)/%0`LEADER))]%RManagers: [itemize(iter(sort(filterbool(#lambda/isobjid(\%0),get(u(crm-db)/%0`MANAGERS)),namei),name(%i0),%b,|),|,and,\,)]%REmployees: [itemize(iter(setdiff(filterbool(#lambda/isobjid(\%0),get(u(crm-db)/%0`EMPLOYEES)),filterbool(#lambda/isobjid(\%0),get(u(crm-db)/%0`MANAGERS)),%b,namei,%b),name(%i0),%b,|),|,and,\,)],PARENT: [if(hasattrval(u(crm-db)/%0`PARENT),pueblize(get(u(crm-db)/%0`PARENT)-[get(u(crm-db)/[get(u(crm-db)/%0`PARENT)])],+crm [get(u(crm-db)/%0`PARENT)]))]%R%RPARTNERS%R[if(hasattrval(u(crm-db)/%0`PARTNERS),iter(filterbool(#lambda/hasattrval(u(crm-db),\%0),sort(get(u(crm-db)/%0`PARTNERS),n)),pueblize(rjust(%i0,3,0).,+crm %i0) [pueblize(get(u(crm-db)/%i0),+crm %i0)],%b,%r)%R%R)]));@switch/inline gt(words(filterbool(#lambda/strmatch(get(u(crm-db)/\%0`PARENT),%0),reglattr(u(crm-db)/^\\d+$))),0)=1,{@include u(crm)/INC`MAIN`SHOWSHEET`LISTSUBS};@nspemit %#=u(FUN`COLOR,%0,+)[u(FUN`COLOR,%0,repeat(-,76))][u(FUN`COLOR,%0,+)]

&INC`MAIN`SHOWSHEET`LISTSUBS u(crm)=@nspemit %#=u(FUN`HEADER,%0,Subsidiaries)%R[u(FUN`BORDER,%0,align(4 17 17 7 17,ID,NAME,LEADER,MEMBERS,SUBSIDIARIES))];@dolist/inline sort(filterbool(#lambda/strmatch(get(u(crm-db)/\%0`PARENT),%0),reglattr(u(crm-db)/^\\d+$)),i)={@nspemit %#=u(FUN`BORDER,%0,align(4 17 17 7 17,pueblize(rjust(%i0,3).,+crm %i0),pueblize(get(u(crm-db)/%i0),+crm %i0),pueblize(name(get(u(crm-db)/%i0`LEADER)),+oocfinger [name(get(u(crm-db)/%i0`LEADER))]),words(filterbool(#lambda/isobjid(\%0),get(u(crm-db)/%i0`EMPLOYEES))),itemize(iter(%q<subs>,pueblize(get(u(crm-db)/%i0),+crm %i0),%b,|),|,and,\,)))}

&FUN`GET`CAPITAL`MAX u(crm)=sub(add(lmath(add,iter(SIZE COMPETENCE INFLUENCE WEALTH REACH,get(u(crm-db)/%0`STAT`%i0))),u(FUN`GET`CAPITAL`BORROWED,%0)),u(FUN`GET`CAPITAL`LENT,%0))
&FUN`GET`CAPITAL`CURRENT u(crm)=strfirstof(bound(get(u(crm-db)/%0`STAT`CAPITAL),0,u(FUN`GET`CAPITAL`MAX,%0)),u(FUN`GET`CAPITAL`MAX,%0))
&FUN`GET`CAPITAL`COMMITTED u(crm)=lmath(add,iter(lattr(u(crm-db)/%0`COMMIT`*),get(u(crm-db)/%i0)))
&FUN`GET`CAPITAL`AVAILABLE u(crm)=sub(u(FUN`GET`CAPITAL`CURRENT,%0),u(FUN`GET`CAPITAL`COMMITTED,%0))
&FUN`GET`CAPITAL`BORROWED u(crm)=lmath(add,iter(filterbool(#lambda/hasattrval(u(crm-db),elements(\%0,2,`)),lattr(u(crm-db)/CTRANS`*`%0)),get(u(crm-db)/%i0)))
&FUN`GET`CAPITAL`LENT u(crm)=lmath(add,iter(filterbool(#lambda/hasattrval(u(crm-db),elements(\%0,3,`)),lattr(u(crm-db)/CTRANS`%0`*)),get(u(crm-db)/%i0)))