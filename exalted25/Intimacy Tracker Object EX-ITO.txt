@teleport create(Intimacy Tracker Object <EX-ITO>)=config(master_room)
&ex-ito u(coi)=locate(config(master_room),Intimacy Tracker Object <EX-ITO>,TXxi)
@parent u(ex-ito)=u(coi)
@set u(ex-ito)=WIZARD !NO_COMMAND

&CMD`+INTIMACY u(ex-ito)=$^\+intimacy(?\:/(\S+))?(?\: +(.*))?$:@assert or(not(strlen(%1)),match(get(u(ex-ito)/VAR`PLAYFLAGS),%1,|))=@nspemit %#=ERROR: %1 is not a valid flag for +intimacy.;@switch/inline and(strlen(%2),not(strlen(%1)),isadmin(%#))=1,{@include u(ccs)/INC`CHECKPC=%2,1,INTIMACY},0,{@include u(ccs)/INC`CHECKPC=%#,1,INTIMACY};@include u(ex-ito)/INC`[strfirstof(%1,MAIN)]=%2,%q<t1>
@set u(ex-ito)/CMD`+INTIMACY=regexp

&VAR`PLAYFLAGS u(ex-ito)=BUILD|ERODE|GAIN|LOSE|HELP|URGE|MOTIVATION

&INC`CHECKINT u(ex-ito)=@assert strlen(%0)=@nspemit %#=announce(INTIMACY) ERROR: Intimacy field empty.;@assert valnum(%0)=@nspemit %#=announce(INTIMACY) ERROR: Intimacies may only be addressed by their slot number.;@assert hasattr(%1/D`INTIMACIES`%0)=@nspemit %#=announce(INTIMACY) ERROR: Intimacy not found.

&INC`BUILD u(ex-ito)=@include u(ex-ito)/INC`CHECKINT;@break gt(setr(new,add(1,get(%1/D`INTIMACIES`%0`CUR))),getstat(%1/D`VIRTUES,Conviction))=@nspemit %#=announce(INTIMACY) ERROR: That would exceed your Conviction.;&D`INTIMACIES`%0`CUR %1=%q<new>;@nspemit %#=announce(INTIMACY) You reinforced your '[get(%1/D`INTIMACIES`%0`CONTEXT)]' Intimacy.

&INC`ERODE u(ex-ito)=@include u(ex-ito)/INC`CHECKINT;@switch/inline setr(new,sub(1,bound(get(%1/D`INTIMACIES`%0`CONTEXT),0,getstat(%1/D`VIRTUES,Conviction))))=0,{@nspemit %#=announce(INTIMACY) Your '[get(%1/D`INTIMACIES`%0`CONTEXT)]' Intimacy was eroded to 0, and removed.;@wipe %1/D`INTIMACIES`%0},{&D`INTIMACIES`%0 %1=%q<new>;@nspemit %#=announce(INTIMACY) You have Eroded your '[get(%1/D`INTIMACIES`%0`CONTEXT)]' Intimacy.}

&INC`GAIN u(ex-ito)=@assert strlen(%0)=@nspemit %#=announce(INTIMACY) ERROR: Intimacy context field empty.;&[setr(attr,D`INTIMACIES`[nextslot(%1,D`INTIMACIES)])]`CONTEXT %1=%0;&%q<attr>`CUR %1=getstat(%1/D`VIRTUES,Conviction);@nspemit %#=announce(INTIMACY) Intimacy '%0' added.

&INC`LOSE u(ex-ito)=@include u(ex-ito)/INC`CHECKINT;@nspemit %#=announce(INTIMACY) You have removed the Intimacy '[get(%1/D`INTIMACIES`%0`CONTEXT)]';@wipe %1/D`INTIMACIES`%0

&INC`MOTIVATION u(ex-ito)=@include u(ex-ito)/INC`MOTURGE=%0,%1,MOTIVATION
&INC`URGE u(ex-ito)=@include u(ex-ito)/INC`MOTURGE=%0,%1,URGE
&INC`MOTURGE u(ex-ito)=@switch/inline strlen(%0)=0,{@nspemit %#=announce(INTIMACY) Your [capnames(%2)] has been cleared.;@wipe %1/D`INTIMACIES`%2},{@assert strlen(%0)=@nspemit %#=announce(INTIMACY) ERROR: [capnames(%1)] field empty.;&D`INTIMACIES`%2 %1=%0;@nspemit %#=announce(INTIMACY) Your [capnames(%2)] has been set to '%0'}

&INC`MAIN u(ex-ito)=@nspemit %#=header(+intimacies for [name(%1)]);@dolist/inline MOTIVATION URGE={@switch/inline gt(strlen(get(%1/D`INTIMACIES`%i0)),0)=1,{@nspemit %#=rjust(ansi(h,%i0):,11) [get(%1/D`INTIMACIES`%i0)]}};@dolist/inline filterbool(#lambda/valnum(\%0),iter(lattr(%1/D`INTIMACIES`*),last(%i0,`)))=@nspemit %#=ljust(rjust(ansi(h,%i0),3): [get(%1/D`INTIMACIES`%i0`CONTEXT)],60)[ljust([bound(get(%1/D`INTIMACIES`%i0`CUR),0,getstat(%1/D`VIRTUES,Conviction))]/[getstat(%1/D`VIRTUES,Conviction)],5)];@nspemit %#=header()



+help/addmain General IC/+intimacy=ljust(ansi(hw,+intimacy),28)Lists current state of Motivation, Urge, and Intimacies.%r[ljust(ansi(hw,+intimacy/help),28)]Lists this text%r[ljust(ansi(hw,+intimacy/gain <text>),28)]Gain described intimacy.%r[ljust(ansi(hw,+intimacy/lose <#>),28)]Remove an intimacy.%r[ljust(ansi(hw,+intimacy/build <#>),28)]Raise Intimacy Health by 1.%r[ljust(ansi(hw,+intimacy/erode <#>),28)]Erode Intimacy Health by 1, removes at 0.%r[ljust(ansi(hw,+intimacy/motivation <text>),28)]Sets your Motivation.%r[ljust(ansi(hw,+intimacy/urge <text>),28)]Sets your Urge if applicable.%R%R* STAFF ONLY *%R[ljust(ansi(hw,+intimacy <player>),28)]Shows Intimacy data of player.
