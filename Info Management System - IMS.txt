@@ DEPENDENCIES - CORE

th u(NEWCOBJ,Info Management System <IMS>,ims,,,,WIZARD SAFE !NO_COMMAND,INHERIT SIDEFX SAFE)

&CMD`+INFO`PENNMUSH [u(cobj,ims)]=$^(?s)(?\:\+)?(info|pinfo)(?\:/(\S+)?)?(?\: +(.+?))?(?\:/(.+?))?(?\:=(.*))?$:@attach %!/CMD`+INFO`MAIN
@set [u(cobj,ims)]/CMD`+INFO`PENNMUSH=regexp
&CMD`+INFO`RHOSTMUSH [u(cobj,ims)]=$^(?s)(?\:\+)?(info|pinfo)(?\:/(\\S+)?)?(?\: +(.+?))?(?\:/(.+?))?(?\:=(.*))?$:@attach %!/CMD`+INFO`MAIN
@set [u(cobj,ims)]/CMD`+INFO`RHOSTMUSH=regexp
&CMD`+INFO`MAIN [u(cobj,ims)]=@attach %!/INC`GETSWITCH=%2;th u(setq,sysname,ucstr(%1));@attach %!/INC`[u(strfirstof,%q<switch>,MAIN)]=trim(%3),trim(%4),%5
@set [u(cobj,ims)]/CMD`+INFO`[switch(v(game),PennMUSH,RHOSTMUSH,RhostMUSH,PENNMUSH)]=no_command

&SWITCHES`PLAYER [u(cobj,ims)]=GET|RENAME|DELETE
&SWITCHES`ADMIN [u(cobj,ims)]=LOCK|UNLOCK
&SYSTEM`NAME [u(cobj,ims)]=u(strfirstof,%q<sysname>,INFO)

&INC`MAIN [u(cobj,ims)]=@select/inline strlen(%2)=>0,{@attach %!/INC`SET},{@attach %!/INC`VIEW}

&INC`TARGET [u(cobj,ims)]=@select/inline t(strlen(%0))[t(strlen(%1))]=10,{@select/inline isdbref(u(setr,target,pmatch(%0)))=1,{@attach %!/INC`CHECKPC=%q<target>,1;th u(setq,filename,%1)},0,{@attach %!/INC`CHECKPC=%#,1;th u(setq,target,%#);th u(setq,filename,%0)}},00,{th u(setq,target,%#);@attach %!/INC`CHECKPC=%#,1},11,{@attach %!/INC`CHECKPC=%0,1;th u(setq,filename,%1);th u(setq,target,%q<t1>)};th u(setq,filename,iter(%q<filename>,trim(%i0),|,|))

&FUN`LIST [u(cobj,ims)]=localize(u(sortinfo,u(sortattr,u(lattr,u(setr,target,%0)/D`%1FILE`*))))

&SORTINFO [u(cobj,ims)]=u(SORTINFO`[v(game)],%0,if(strlen(%1),%1,%B),if(strlen(%2),%2,%B))
&SORTINFO`PENNMUSH [u(cobj,ims)]=sortkey(#lambda/get(%q<target>/\%0),%0,i,%1,%2)
&SORTINFO`RHOSTMUSH [u(cobj,ims)]=sortby(#lambda/[lit([comp(lcstr(get(%q<target>/%0)),lcstr(get(%q<target>/%1)))])],%0,%1,%2)

&FUN`LISTNAMES [u(cobj,ims)]=iter(u(FUN`LIST,%0,%1),get(%0/%i0),%b,|)
&FUN`FINDFILE [u(cobj,ims)]=first(u(filter,INFOMATCH,u(FUN`LIST,%0,%1),%b,%b,%0,%2))
&FIL`INFOMATCH [u(cobj,ims)]=strmatch(%2,get(%1/%0))

&INC`FINDFILE [u(cobj,ims)]=@check strlen(%0)=@attach %!/INC`MSG=ERROR: Info name empty.;@attach %!/INC`PARTIAL=%0,u(FUN`LISTNAMES,%q<target>,%q<sysname>),|,found,filename;th u(setq,attr,u(FUN`FINDFILE,%q<target>,%q<sysname>,%q<found>));th u(setq,foundname,get(%q<target>/%q<attr>))

&INC`LOCK [u(cobj,ims)]=@attach %!/INC`TARGET;@check strlen(%q<filename>)=@attach %!/INC`MSG=ERROR: Info name empty.;@dolist/inline/nobreak/delimit | %q<filename>={@attach %!/INC`FINDFILE=##;@stop u(getstat,%q<target>,%q<attr>`FLAGS,Locked)=@attach %!/INC`MSG=ERROR: [u(capnames,%q<sysname>)] File %q<foundname> is already Locked!;th u(setstat,%q<target>,%q<attr>`FLAGS,Locked,1);th u(setstat,%q<target>,%q<attr>`FLAGS,LockedBy,u(objid,%#));th u(setstat,%q<target>,%q<attr>`FLAGS,LockedOn,secs());@select/inline %q<target>=%#,{@attach %!/INC`MSG=You Locked your %q<foundname> [u(capnames,%q<sysname>)] File},{@attach %!/INC`MSG=You Locked [name(%q<target>)]'s %q<foundname> [u(capnames,%q<sysname>)] File!;@attach %!/INC`MSG`NOTICE=Your %q<foundname> [u(capnames,%q<sysname>)] File was locked.,%q<target>};@attach %!/INC`MSG`CHAN=Locked %q<t1name>'s %q<foundname> [u(capnames,%q<sysname>)] File.}

&INC`UNLOCK [u(cobj,ims)]=@attach %!/INC`TARGET;@attach %!/INC`FILENAME;@check strlen(%q<filename>)=@attach %!/INC`MSG=ERROR: Info name empty.;@dolist/inline/nobreak/delimit | %q<filename>={@attach %!/INC`FINDFILE=##;@check u(getstat,%q<target>,%q<attr>`FLAGS,Locked)=@attach %!/INC`MSG=ERROR: [u(capnames,%q<sysname>)] File %q<foundname> is not Locked!;th u(setstat,%q<target>,%q<attr>`FLAGS,Locked,0);th u(delstat,%q<target>,%q<attr>`FLAGS,LockedBy);th u(delstat,%q<target>,%q<attr>`FLAGS,LockedOn);@select/inline %q<target>=%#,{@attach %!/INC`MSG=You Unlocked your %q<filename> Info File},{@attach %!/INC`MSG=You Unlocked [name(%q<target>)]'s [u(capnames,%q<sysname>)] File %q<foundname>!;@attach %!/INC`MSG`NOTICE=Your [u(capnames,%q<sysname>)] File %q<foundname> was unlocked.};@attach %!/INC`MSG`CHAN=Unlocked %q<t1name>'s %q<foundname> [u(capnames,%q<sysname>)] File.}

&INC`DELETE [u(cobj,ims)]=@attach %!/INC`TARGET;@check strlen(%q<filename>)=@attach %!/INC`MSG=ERROR: Info name empty.;@dolist/delimit | %q<filename>={@attach %!/INC`FINDFILE=##;@select/inline cor(strmatch(%#,%q<target>),u(isadmin,%#))=0,{@stop 1=@attach %!/INC`MSG=ERROR: You may not delete another's Info files.};@stop cand(u(getstat,%q<target>,%q<attr>`FLAGS,Locked),not(u(isadmin,%#)))=@attach %!/INC`MSG=ERROR: Locked Files must be Unlocked by admin first.;@attach %!/WIPE=%q<target>,%q<attr>;@select/inline %q<target>=%#,{@attach %!/INC`MSG=You deleted your [u(capnames,%q<sysname>)] File %q<foundname>},{@attach %!/INC`MSG=You deleted [name(%q<target>)]'s [u(capnames,%q<sysname>)] File %q<foundname>!;@attach %!/INC`MSG`NOTICE=Your [u(capnames,%q<sysname>)] File %q<foundname> was deleted.,%q<target>}}

&INC`GET [u(cobj,ims)]=@attach %!/INC`TARGET;@dolist/inline/nobreak/delimit | %q<filename>={@attach %!/INC`FINDFILE=##;@pemit %#=u(HEADER,Unformatted Contents of %q<foundname>);@pemit %#=u(decompose,get(%q<target>/%q<attr>`CONTENTS));@pemit %#=u(SUBHEADER)}

&INC`VIEW [u(cobj,ims)]=@attach %!/INC`TARGET;@select/inline %q<sysname>=PINFO,{@check cor(strmatch(%#,%q<target>),u(isadmin,%#))=@attach %!/INC`MSG=ERROR: PInfo files are private.};@select/inline gt(strlen(%q<filename>),0)=1,{@attach %!/INC`VIEW`CONTENTS},0,{@attach %!/INC`VIEW`FILES}

&INC`VIEW`FILES [u(cobj,ims)]=@pemit %#=u(HEADER,%q<t1name>'s Info Files);@pemit %#=u(table,iter(u(FUN`LIST,%q<target>,%q<sysname>),u(mxpmenu,left(get(%q<target>/%i0),18),+info [u(setr,get,%q<t1name>/[get(%q<target>/%i0)])]|+info/get %q<get>,View|Decompile)[if(u(getstat,%q<target>,%i0`FLAGS,Locked),\([ansi(hw,+)]\))],%b,|),24,u(width,%#),|);@pemit %#=u(SUBHEADER,Legend: + - Locked)

&INC`VIEW`CONTENTS [u(cobj,ims)]=@dolist/inline/nobreak/delimit | %q<filename>={@attach %!/INC`FINDFILE=##;@pemit %#=u(HEADER,%q<t1name>'s [get(%q<target>/%q<attr>)] +[lcstr(%q<sysname>)]);@pemit %#=get(%q<target>/%q<attr>`CONTENTS);@select/inline u(isadmin,%#)=1,{@pemit %#=u(separator,Admin Info);@pemit %#=[ansi(h,Last set by:)] [name(u(getstat,%q<target>,%q<attr>`FLAGS,SetBy))] [ansi(h,On:)] [u(fancytime,u(getstat,%q<target>,%q<attr>`FLAGS,SetOn),%#)][if(u(setr,locked,u(getstat,%q<target>,%q<attr>`FLAGS,Locked)),%R[ansi(h,Locked by:)] [name(u(getstat,%q<target>,%q<attr>`FLAGS,LockedBy))] [ansi(h,On:)] [u(fancytime,u(getstat,%q<target>,%q<attr>`FLAGS,LockedOn),%#)])]};@pemit %#=u(HEADER,if(%q<locked>,Admin Locked File))}

&INC`SET [u(cobj,ims)]=@attach %!/INC`TARGET;@stop cand(isdbref(pmatch(%0)),not(strlen(%1)))=@attach %!/INC`MSG=ERROR: '%0' matches player [name(%q<target>)]. Use <name>/<filename> format to be more explicit.;@check strlen(%q<filename>)=@attach %!/INC`MSG=ERROR: File name empty.;@select/inline gt(strlen(u(setr,attr,u(FUN`FINDFILE,%q<target>,%q<sysname>,%q<filename>))),0)=1,{@check cor(strmatch(%#,%q<target>),u(isadmin,%#))=@attach %!/INC`MSG=ERROR: You may not change another's Info files.;@select/inline u(getstat,%q<target>,%q<attr>`FLAGS,Locked)=1,{@check u(isadmin,%#)=@attach %!/INC`MSG=ERROR: That File may not be changed by you.}},0,{@stop gt(strlen(%q<filename>),18)=@attach %!/INC`MSG=ERROR: File names are limited to 18 characters or less.;@stop u(charsearch,%q<filename>,| / \\ %r %t)=@attach %!/INC`MSG=ERROR: The following characters are not permitted in file names: |, / \\, linebreaks\, and indents.};@check strlen(%2)=@attach %!/INC`MSG=ERROR: Text field empty. To delete a file use +[lcstr(%q<sysname>)]/delete.;th u(attrib_set,%q<target>,switch(strlen(%q<attr>),>0,%q<attr>,u(setr,attr2,u(setr,attr,D`%q<sysname>FILE`[u(nextslot,%q<target>,D`%q<sysname>FILE)]))),%q<filename>);th u(attrib_set,%q<target>,%q<attr>`CONTENTS,%2);th u(setstat,%q<target>,%q<attr>`FLAGS,SetBy,u(objid,%#));th u(setstat,%q<target>,%q<attr>`FLAGS,SetOn,secs());@select/inline %q<target>=%#,{@attach %!/INC`MSG=You set your %q<filename> Info File},{@attach %!/INC`MSG=You set %q<t1name>'s [u(capnames,%q<sysname>)] File %q<filename>!;@attach %!/INC`MSG`NOTICE=Your [u(capnames,%q<sysname>)] File %q<filename> was set.,%q<target>}



&INC`RENAME [u(cobj,ims)]=@attach %!/INC`TARGET;@attach %!/INC`FINDFILE=%q<filename>;@select/inline u(getstat,%q<target>/%q<attr>`FLAGS,Locked)=1,{@check u(isadmin,%#)=@attach %!/INC`MSG=ERROR: That File may not be changed by you.};@check strlen(%2)=@attach %!/INC`MSG=ERROR: New name field empty.;@stop gt(strlen(%2),18)=@attach %!/INC`MSG=ERROR: Info names are limited to 18 characters or less.;@stop u(charsearch,%2,| / \\ %r %t)=@attach %!/INC`MSG=ERROR: The following characters are not permitted in Info file names: |\, / \\, linebreaks\, and indents.;@stop strlen(u(setr,attr2,u(FUN`FINDFILE,%q<target>,%q<sysname>,%2)))=@attach %!/INC`MSG=ERROR: An info file already uses that name.;&%q<attr> %q<target>=%2;@select/inline %q<target>=%#,{@attach %!/INC`MSG=File named to '%2'!},{@attach %!/INC`MSG=You renamed %q<t1name>'s %q<foundname> [u(capnames,%q<sysname>)] File to %2;@attach %!/INC`MSG`NOTICE=Your [u(capnames,%q<sysname>)] File %q<foundname> was renamed to %2,%q<target>}

@@ CHARACTER - +INFO
+help/add +info=[u(cobj,ims)]/HLP`+INFO
+help/category +info=Character
+help/metatags +info=notes
&HLP`+INFO [u(cobj,ims)]=The Info system allows Players to store notes about their character. These notes can be used for a number of things, such as tracking details of resources, backgrounds, cheat sheets, or other notes that might be several paragraphs in length. Anything stored in +info is visible to ALL PLAYERS. If you want private files, use +pinfo (the commands are the same.)%R%R[ansi(hc,Concepts)]%R[align(5 [sub(u(width,%#),6)],,[ansi(h,infonames)] - Info files can't be very long\, may not contain special characters\, and are not case sensitive.%R[ansi(h,filelists)] - Some commands let you specify a list of files. Seperate their names with | symbols\, such as file1|file2.%R[ansi(h,locked)] - Locked files have been verified by staff and are read-only.)]%R%R[ansi(hc,Managing Infos)]%R[align(5 [sub(u(width,%#),6)],,[ansi(h,+info <infoname>=<text>)] - Creates or replaces an info file.%R[ansi(h,+info/delete <filelist>)] - Deletes a file or series of files.%R[ansi(h,+info/rename <infoname>=<newname>)] - Renames Info files.)]%R%R[ansi(hc,Viewing Infos)]%R[align(5 [sub(u(width,%#),6)],,[ansi(h,+info)] - Show all of your Info files.%R[ansi(h,+info <filelist>)] - Display an info's contents.%R[ansi(h,+info <player>)] - Show a player's visible files.%R[ansi(h,+info <player>/<filelist>)] - Display a player's info contents.%R%RUse the /get switch as you would reading files to retrieve a decompile'd version for easy editing.%R%RIf your File name matches a player's\, you will have to use +info <yourname>/<filename> to set or view it.)]

+shelp/add +info=[u(cobj,ims)]/SHLP`+INFO
+shelp/category +info=Character
&SHLP`+INFO [u(cobj,ims)]=Staff are able to see, create, edit, delete, rename, and otherwise manage +info and +pinfo files on any character. The commands work the same as for players via <name>/<filename>.%R%R[ansi(hc,Staff Commands)]%R[align(5 [sub(u(width,%#),6)],,[ansi(h,+info/lock <player>/<filelist>)] - Lock one or more info files.%R[ansi(h,+info/unlock <player>/<filelist>)] - Unlock one or more info files.%R%RLocked files cannot be edited by players. This is useful for 'approved information' depending on the game.)]

&MIGRATE`INFO [u(cobj,migrate)]=

&CONFLICT`INFO [u(cobj,migrate)]=@tel u(coi,ifo)=u(cobj,oldbox);@set u(coi,ifo)=NO_COMMAND HALT