@teleport create(Intimacy Tracker Object <EX-ITO>)=config(master_room)
&ex-ito u(coi)=locate(config(master_room),Intimacy Tracker Object <EX-ITO>,TXxi)
@parent u(ex-ito)=u(coi)
@set u(ex-ito)=WIZARD !NO_COMMAND

&CMD`+INTIMACY u(ex-ito)=$^\+intimacy(?\:/(\S+))?(?\: +(.*))?$:@include u(ccs)/INC`PARTIAL=%1,setunion(get(u(ex-ito)/VAR`PLAYFLAGS),if(isadmin(%#),get(u(ex-ito)/VAR`ADMINFLAGS)),|,|),|,INFO,switch,switch;@switch/inline and(strlen(%2),not(strlen(%1)),isadmin(%#))=1,{@include u(ccs)/INC`CHECKPC=%2,1,INTIMACY},0,{@include u(ccs)/INC`CHECKPC=%#,1,INTIMACY};@include u(ex-ito)/INC`[strfirstof(%q<switch>,MAIN)]=trim(%2),%q<t1>
@set u(ex-ito)/CMD`+INTIMACY=regexp

&VAR`MSGHEAD u(ex-ito)=INTIMACY
&RFN`MSGHEAD u(ex-ito)=msghead(v(VAR`MSGHEAD))
&VAR`PLAYFLAGS u(ex-ito)=GAIN|LOSE|STRENGTHEN|WEAKEN|RENAME
&RFN`HEADER u(ex-ito)=header(%0,,INTIMACY`BORDER,INTIMACY`BORDERDOT,INTIMACY`BORDERTEXT,1)
&RFN`SEPARATOR u(ex-ito)=separator(%0,,INTIMACY`BORDER,INTIMACY`BORDERDOT,INTIMACY`BORDERTEXT,1)
&RFN`SUBHEADER u(ex-ito)=subheader(%0,,INTIMACY`BORDER,INTIMACY`BORDERDOT,INTIMACY`BORDERTEXT,1)


&INC`GAIN u(ex-ito)=@assert strlen(%0)=@nspemit %#=u(RFN`MSGHEAD) ERROR: Intimacy context field empty.;&[setr(attr,D`INTIMACIES`[nextslot(%1,D`INTIMACIES)])] %1=%0;&%q<attr>`CUR %1=2;@nspemit %#=u(RFN`MSGHEAD) Intimacy '%0' added.

&INC`LOSE u(ex-ito)=@include u(ex-ito)/INC`CHECKINT;@nspemit %#=u(RFN`MSGHEAD) You have removed the Intimacy '[get(%1/D`INTIMACIES`%0)]';@wipe %1/D`INTIMACIES`%0

&INC`CHECKINT u(ex-ito)=@assert strlen(%0)=@nspemit %#=u(RFN`MSGHEAD) ERROR: Intimacy field empty.;@assert valnum(%0)=@nspemit %#=u(RFN`MSGHEAD) ERROR: Intimacies may only be addressed by their slot number.;@assert hasattr(%1/D`INTIMACIES`%0)=@nspemit %#=u(RFN`MSGHEAD) ERROR: Intimacy not found.

&INC`STRENGTHEN u(ex-ito)=@include u(ex-ito)/INC`CHECKINT;@break gt(get(%1/D`INTIMACIES`%0`CUR),4)=@nspemit %#=u(RFN`MSGHEAD) Intimacies cannot be strengthened past Defining.;&D`INTIMACIES`%0`CUR %1=setr(new,add(get(%1/D`INTIMACIES`%0`CUR),2));@nspemit %#=u(RFN`MSGHEAD) You reinforced your '[get(%1/D`INTIMACIES`%0)]' Intimacy to [switch(%q<new>,2,Minor,4,Major,6,Defining)].

&INC`WEAKEN u(ex-ito)=@include u(ex-ito)/INC`CHECKINT;@break lt(get(%1/D`INTIMACIES`%0`CUR),4)=@nspemit %#=u(RFN`MSGHEAD) Intimacies cannot be weakened past Minor. Use /lose for that.;&D`INTIMACIES`%0`CUR %1=setr(new,sub(get(%1/D`INTIMACIES`%0`CUR),2));@nspemit %#=u(RFN`MSGHEAD) You weakened your '[get(%1/D`INTIMACIES`%0)]' Intimacy to [switch(%q<new>,2,Minor,4,Major,6,Defining)].

&INC`MAIN u(ex-ito)=@nspemit %#=u(RFN`HEADER,+intimacies for [name(%1)]);@nspemit %#=align(2 60 15,ID,NAME,STATUS);@nspemit %#=u(RFN`SEPARATOR);@dolist/inline sortkey(#lambda/last(\%0,`),lattr(%1/D`INTIMACIES`*),n)=@nspemit %#=align(2 60 15,last(%i0,`),get(%1/%i0),switch(get(%1/%i0`CUR),2,Minor,4,Major,6,Defining));@nspemit %#=u(RFN`HEADER)
