@@ DEPENDENCIES - CORE

@tel create(Info Files Object <IFO>)=config(master_room)
&ifo u(coi)=locate(config(master_room),Info Files Object <IFO>,TXxi)
@parent u(ifo)=u(coi)
@set u(ifo)=WIZARD !NO_COMMAND

&CMD`+INFO u(ifo)=$^(?s)(?\:\+)?info(?\:/(\S+))?(?\: +(.+?))?(?\:=(.*))?$:@include u(ccs)/INC`PARTIAL=%1,setunion(get(u(ifo)/VAR`PLAYFLAGS),if(isadmin(%#),get(u(ifo)/VAR`ADMINFLAGS)),|,|),|,INFO,switch,switch;@include u(ifo)/INC`[strfirstof(%q<switch>,MAIN)]=%2,%3
@set u(ifo)/CMD`+INFO=regexp

&VAR`PLAYFLAGS u(ifo)=SET|GET|RENAME|DELETE|CATEGORIES|PUBLISH|UNPUBLISH|PUBLISHED
&VAR`ADMINFLAGS u(ifo)=APPROVE|UNAPPROVE
&VAR`CATEGORIES u(ifo)=Allies|Artifacts|BGStory|Backing|CombatStats|Command|Connections|Contacts|Crafts|Cult|Familiar|Followers|Henchmen|Influence|Manse|Mentor|Resources|Tribal Languages|XP

&INC`TARGET u(ifo)=@switch/inline isdbref(setr(target,pmatch(before(%0,/))))=0,{@switch/inline gt(words(%0,/),1)=1,{@break 1=@nspemit %#=announce(INFO)%BERROR: [before(%0,/)] does not match a player.},0,{th setq(target,%#)}}

&INC`FILENAME u(ifo)=th setq(filename,if(strmatch(%#,%q<target>),%0,after(%0,/)))

&FUN`LIST u(ifo)=iter(switch(1,isadmin(%#),lattr(%0/D`INFOFILE`*),strmatch(%#,%0),filterbool(#lambda/not(getstat(%0/\%0`FLAGS,Hidden)),lattr(%0/D`INFOFILE`*)),filterbool(#lambda/and(getstat(%0/\%0`FLAGS,Publish),not(getstat(%0/\%0`FLAGS,Hidden))),lattr(%0/D`INFOFILE`*))),if(%1,get(%q<target>/%i0),%i0),%b,|)

&FUN`FINDFILE u(ifo)=first(grepi(%0,D`INFOFILE`*,%1))

&INC`APPROVE u(ifo)=@include u(ifo)/INC`TARGET;@include u(ifo)/INC`FILENAME;@assert strlen(%q<filename>)=@nspemit %#=announce(INFO)%BERROR: Info name empty.;@dolist/delimit | %q<filename>={@include u(ifo)/INC`PARTIALMATCH=%i0;@break getstat(%q<target>/%q<attr>`FLAGS,Approved)=@nspemit %#=announce(INFO)%BERROR: Info File %i0 is already approved!;th setstat(%q<target>/%q<attr>`FLAGS,Approved,1);th setstat(%q<target>/%q<attr>`FLAGS,ApprovedBy,%#);th setstat(%q<target>/%q<attr>`FLAGS,ApprovedOn,secs());@switch/inline strmatch(%#,%q<target>)=1,{@nspemit %#=announce(INFO)%BYou approved your %i0 Info File},{@nspemit %#=announce(INFO)%BYou approved [name(%q<target>)]'s %i0 Info File!;@nspemit %q<target>=announce(INFO)%B%n approved your %i0 Info File!};@switch/inline t(v(VAR`ANNOUNCEAPPROVE))=1,{@nscemit/noisy v(VAR`ANNOUNCECHANNEL)=ansi(h,INFO:)%B%n just approved [name(%q<target>)]'s %i0 Info File!}}

&INC`UNAPPROVE u(ifo)=@include u(ifo)/INC`TARGET;@include u(ifo)/INC`FILENAME;@assert strlen(%q<filename>)=@nspemit %#=announce(INFO)%BERROR: Info name empty.;@dolist/delimit | %q<filename>={@include u(ifo)/INC`PARTIALMATCH=%i0;@assert getstat(%q<target>/%q<attr>`FLAGS,Approved)=@nspemit %#=announce(INFO)%BERROR: Info File %i0 is not approved!;th setstat(%q<target>/%q<attr>`FLAGS,Approved,0);th delstat(%q<target>/%q<attr>`FLAGS,ApprovedBy);th delstat(%q<target>/%q<attr>`FLAGS,ApprovedOn);@switch/inline strmatch(%#,%q<target>)=1,{@nspemit %#=announce(INFO)%BYou approved your %i0 Info File},{@nspemit %#=announce(INFO)%BYou unapproved [name(%q<target>)]'s %i0 Info File!;@nspemit %q<target>=announce(INFO)%B%n unapproved your %i0 Info File!};@switch/inline t(v(VAR`ANNOUNCEAPPROVE))=1,{@nscemit/noisy v(VAR`ANNOUNCECHANNEL)=ansi(h,INFO:)%B%n just unapproved [name(%q<target>)]'s %i0 Info File!}}

&VAR`ANNOUNCEAPPROVE u(ifo)=1
&VAR`ANNOUNCECHANNEL u(ifo)=Staff Reports

&INC`CATEGORIES u(ifo)=@nspemit %#=header(list of Categories);@nspemit %#=table(get(u(ifo)/VAR`CATEGORIES),24,78,|);@nspemit %#=header()

&INC`DELETE u(ifo)=@include u(ifo)/INC`TARGET;@include u(ifo)/INC`FILENAME;@assert strlen(%q<filename>)=@nspemit %#=announce(INFO)%BERROR: Info name empty.;@dolist/delimit | %q<filename>={@include u(ifo)/INC`PARTIALMATCH=%i0;@switch/inline or(isadmin(%#),strmatch(%#,%q<target>))=0,{@break 1=@nspemit %#=announce(INFO)%BERROR: You may not delete another's Info files.};@break getstat(%q<target>/%q<attr>`FLAGS,Approved)=@nspemit %#=ERROR: Approved Infos must be unapproved by admin first.;@wipe %q<target>/%q<attr>;@switch/inline strmatch(%#,%q<target>)=1,{@nspemit %#=announce(INFO)%BYou deleted your %i0 Info File},{@nspemit %#=announce(INFO)%BYou deleted [name(%q<target>)]'s %i0 Info File!;@nspemit %q<target>=announce(INFO)%B%n deleted your %i0 Info File!}}

&INC`PARTIALMATCH u(ifo)=@assert strlen(%0)=@nspemit %#=announce(INFO)%BERROR: Info name empty.;@switch/inline gt(match(setr(list,u(u(ifo)/FUN`LIST,%q<target>,1)),%0,|),0)=0,{@assert strlen(setr(filename,grab(%q<list>,%0*,|)))=@nspemit %#=announce(INFO)%BERROR: File not found.};th setq(attr,u(u(ifo)/FUN`FINDFILE,%q<target>,%q<filename>))

&INC`GET u(ifo)=@include u(ifo)/INC`TARGET;@include u(ifo)/INC`FILENAME;@include u(ifo)/INC`PARTIALMATCH=%q<filename>;@nspemit %#=header(Unformatted Contents of %q<filename>);@nspemit %#=decompose(get(%q<target>/[u(u(ifo)/FUN`FINDFILE,%q<target>,%q<filename>)]`CONTENTS));@nspemit %#=header()

&INC`MAIN u(ifo)=@include u(ifo)/INC`TARGET;@switch/inline gt(strlen(%1),0)=1,{@include u(ifo)/INC`SET},0,{@include u(ifo)/INC`LIST}

&INC`LIST u(ifo)=@include u(ifo)/INC`FILENAME;@switch/inline gt(strlen(%q<filename>),0)=1,{@include u(ifo)/INC`LIST`CONTENTS},0,{@include u(ifo)/INC`LIST`FILES}

&INC`LIST`FILES u(ifo)=@nspemit %#=header(name(%q<target>)'s Info Files);@nspemit %#=table(iter(u(u(ifo)/FUN`LIST,%q<target>),pueblize(left(if(getstat(%q<target>/%i0`FLAGS,Hidden),ansi(hx,get(%q<target>/%i0)),get(%q<target>/%i0)),18),+info [if(strmatch(%#,%q<target>),get(%q<target>/%i0),[name(%q<target>)]/[get(%q<target>/%i0)])])[if(getstat(%q<target>/%i0`FLAGS,Approved),\([ansi(hw,+)]\))][if(getstat(%q<target>/%i0`FLAGS,Published),\([ansi(hw,P)]\))],|,|),24,78,|);@nspemit %#=Header(Legend: + - approved\, P - Published)

&INC`LIST`CONTENTS u(ifo)=@include u(ifo)/INC`PARTIALMATCH=%q<filename>;@nspemit %#=[header(name(%q<target>)'s [get(%q<target>/%q<attr>)] +info)];@nspemit %#=get(%q<target>/%q<attr>`CONTENTS);@nspemit %#=subheader()%R[ansi(h,Last set by:)] [name(getstat(%q<target>/%q<attr>`FLAGS,SetBy))] [ansi(h,On:)] [convsecs(getstat(%q<target>/%q<attr>`FLAGS,SetOn))];@switch/inline gt(getstat(%q<target>/%q<attr>`FLAGS,Approved),0)=1,{@nspemit %#=ansi(h,Approved by:) [name(getstat(%q<target>/%q<attr>`FLAGS,ApprovedBy))] [ansi(h,On:)] [convsecs(getstat(%q<target>/%q<attr>`FLAGS,ApprovedOn))]};@nspemit %#=[header()]

&INC`PUBLISH u(ifo)=@include u(ifo)/INC`TARGET;@include u(ifo)/INC`FILENAME;@assert strlen(%q<filename>)=@nspemit %#=announce(INFO)%BERROR: Info name empty.;@dolist/delimit | %q<filename>={@include u(ifo)/INC`PARTIALMATCH=%i0;@switch/inline or(isadmin(%#),strmatch(%#,%q<target>))=0,{@break 1=@nspemit %#=announce(INFO)%BERROR: You may not publish another's Info files.};@break getstat(%q<target>/%q<attr>`FLAGS,Published)=@nspemit %#=announce(INFO)%BERROR: File %i0 is already published.;th setstat(%q<target>/%q<attr>`FLAGS,Published,1);@switch/inline strmatch(%#,%q<target>)=1,{@nspemit %#=announce(INFO)%BYou published your %i0 Info File},{@nspemit %#=announce(INFO)%BYou published [name(%q<target>)]'s %i0 Info File!;@nspemit %q<target>=announce(INFO)%B%n published your %i0 Info File!}}

&INC`UNPUBLISH u(ifo)=@include u(ifo)/INC`TARGET;@include u(ifo)/INC`FILENAME;@assert strlen(%q<filename>)=@nspemit %#=announce(INFO)%BERROR: Info name empty.;@dolist/delimit | %q<filename>={@include u(ifo)/INC`PARTIALMATCH=%i0;@switch/inline or(isadmin(%#),strmatch(%#,%q<target>))=0,{@break 1=@nspemit %#=announce(INFO)%BERROR: You may not unpublish another's Info files.};@assert getstat(%q<target>/%q<attr>`FLAGS,Published)=@nspemit %#=announce(INFO)%BERROR: File %i0 is not published.;th setstat(%q<target>/%q<attr>`FLAGS,Published,0);@switch/inline strmatch(%#,%q<target>)=1,{@nspemit %#=announce(INFO)%BYou unpublished your %i0 Info File},{@nspemit %#=announce(INFO)%BYou unpublished [name(%q<target>)]'s %i0 Info File!;@nspemit %q<target>=announce(INFO)%B%n unpublished your %i0 Info File!}}

&INC`SET u(ifo)=@include u(ifo)/INC`TARGET;@include u(ifo)/INC`FILENAME;@assert strlen(%q<filename>)=@nspemit %#=announce(INFO)%BERROR: Info file name empty.;@switch/inline gt(strlen(setr(attr,u(u(ifo)/FUN`FINDFILE,%q<target>,%q<filename>))),0)=1,{@assert or(isadmin(%#),strmatch(%q<target>,%#))=@nspemit %#=announce(INFO)%BERROR: You may not change another's Info files.;@switch/inline or(getstat(%q<target>/%q<attr>`FLAGS,Hidden),getstat(%q<target>/%q<attr>`FLAGS,Approved))=1,{@assert isadmin(%#)=@nspemit %#=announce(INFO)%BERROR: That Info File may not be changed by you.}},0,{@break gt(strlen(%q<filename>),18)=@nspemit %#=ERROR: Info names are limited to 18 characters or less.;@break regmatchi(%q<filename>,\\|)=@nspemit %#=ERROR: Pipe symbols are not allowed in info names.;@break regmatchi(%q<filename>,\/)=@nspemit %#=ERROR: Slashes symbols are not allowed in info names.;@assert strlen(%1)=ERROR: Text field empty. To delete an +info file, use +info/delete.};&[strfirstof(%q<attr>,setr(attr,D`INFOFILE`[nextslot(%q<target>,D`INFOFILE)]))] %q<target>=%q<filename>;&%q<attr>`CONTENTS %q<target>=%1;th setstat(%q<target>/%q<attr>`FLAGS,SetBy,%#);th setstat(%q<target>/%q<attr>`FLAGS,SetOn,secs());@switch/inline strmatch(%#,%q<target>)=1,{@nspemit %#=announce(INFO)%BYou set your %q<filename> Info File},{@nspemit %#=announce(INFO)%BYou set [name(%q<target>)]'s %q<filename> Info File!;@nspemit %q<target>=announce(INFO)%B%n set your %q<filename> Info File!}

&INC`PUBLISHED u(ifo)=@assert words(setr(players,lsearch(all,eplayer,\[words(wildgrepi(##,D`INFOFILE`*`FLAGS,*Published~1*))\])))=@nspemit %#=announce(INFO)%BERROR: No Players have published Infos.;@nspemit %#=header(Published Infos);@nspemit %#=table(iter(%q<players>,left(pueblize(name(%i0),+info [name(%i0)]),22)\([words(wildgrepi(%i0,D`INFOFILE`*`FLAGS,*Published~1*))]\)),25,78,|);@nspemit %#=header()

