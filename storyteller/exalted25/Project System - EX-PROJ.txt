@switch/inline isdbref(u(ex-proj))=0,{@tel create(Project System <EX-PROJ>)=config(master_room)}
&ex-proj u(coi)=locate(config(master_room),Project System <EX-PROJ>,TXxi)
@parent u(ex-proj)=u(coi)
@set u(ex-proj)=WIZARD !NO_COMMAND

&CMD`+PROJECT u(ex-proj)=$^(?s)(?\:\+)?project(?\:/(\S+)?)?(?\: +(.+?))?(?\:=(.*))?$:@include u(ccs)/INC`PARTIAL=%1,setunion(get(u(ex-proj)/VAR`PLAYFLAGS),if(isadmin(%#),get(u(ex-proj)/VAR`ADMINFLAGS)),|,|),|,CRM,switch,switch;@include u(ex-proj)/INC`[strfirstof(%q<switch>,MAIN)]=%2,%3
@set u(ex-proj)/CMD`+PROJECT=regexp

&VAR`PLAYFLAGS u(ex-proj)=NEW|DESC|OLD|LOG|FINISH|UNFINISH|GET|RENAME|DELETE|ADDPARTNER|REMPARTNER|DURATION|START|STOP|ADD|TIMEDIV
&VAR`ADMINFLAGS u(ex-proj)=APPROVE|UNAPPROVE

&INC`TARGET u(ex-proj)=@switch/inline strmatch(%0,*/*)=1,{@include u(ccs)/INC`CHECKPC=before(%0,/),1,INFO;th setq(target,%q<t1>)},0,{th setq(target,%#)}

&INC`FILENAME u(ex-proj)=th setq(filename,if(strmatch(%0,*/*),after(%0,/),%0))

&FUN`LIST u(ex-proj)=sort(iter(if(or(isadmin(%1),strmatch(objid(%0),objid(%1))),lattr(%0/D`PROJECT`*),filterbool(#lambda/match(get(%0/\%0`PARTNERS),objid(%1)),lattr(%0/D`PROJECT`*))),last(%i0,`),%b,|),i,|,|)
&FUN`LIST`UNFINISHED u(ex-proj)=filterbool(#lambda/not(getstat(%0/D`PROJECT`\%0`FLAGS,Finished)),u(FUN`LIST,%0,%1),|,|)
&FUN`LIST`FINISHED u(ex-proj)=filterbool(#lambda/getstat(%0/D`PROJECT`\%0`FLAGS,Finished),u(FUN`LIST,%0,%1),|,|)

&INC`APPROVE u(ex-proj)=@include u(ex-proj)/INC`TARGET;@include u(ex-proj)/INC`FILENAME;@assert strlen(%q<filename>)=@nspemit %#=msghead(PROJECT) ERROR: Info name empty.;@dolist/delimit | %q<filename>={@include u(ex-proj)/INC`PARTIALMATCH=%i0;@break getstat(%q<target>/%q<attr>`FLAGS,Approved)=@nspemit %#=msghead(PROJECT) ERROR: Project %i0 is already approved!;th setstat(%q<target>/%q<attr>`FLAGS,Approved,1);th setstat(%q<target>/%q<attr>`FLAGS,ApprovedBy,%#);th setstat(%q<target>/%q<attr>`FLAGS,ApprovedOn,secs());@switch/inline strmatch(%#,%q<target>)=1,{@nspemit %#=msghead(PROJECT) You approved your %i0 Project},{@nspemit %#=msghead(PROJECT) You approved [name(%q<target>)]'s %i0 '[get(%q<target>/%q<attr>)]' Project!;@nspemit %q<target>=msghead(PROJECT) %n approved your %i0 '[get(%q<target>/%q<attr>)]' Project!};@switch/inline t(v(VAR`ANNOUNCEAPPROVE))=1,{@nscemit/noisy v(VAR`ANNOUNCECHANNEL)=ansi(h,PROJECT:) %n just approved [name(%q<target>)]'s %i0 '[get(%q<target>/%q<attr>)]' Project!};@nspemit/list get(%q<target>/D`PROJECT`%q<filename>`PARTNERS)=msghead(PROJECT) %n approved [name(%q<target>)]'s %q<filename> '[get(%q<target>/D`PROJECT`%q<filename>)]' Project!;@include u(ex-proj)/INC`MAKELOG=%q<target>,%#,%q<filename>,{[name(%#)] approved the Project!}}

&INC`UNAPPROVE u(ex-proj)=@include u(ex-proj)/INC`TARGET;@include u(ex-proj)/INC`FILENAME;@assert strlen(%q<filename>)=@nspemit %#=msghead(PROJECT) ERROR: Info name empty.;@dolist/delimit | %q<filename>={@include u(ex-proj)/INC`PARTIALMATCH=%i0;@assert getstat(%q<target>/%q<attr>`FLAGS,Approved)=@nspemit %#=msghead(PROJECT) ERROR: Project %i0 is not approved!;th setstat(%q<target>/%q<attr>`FLAGS,Approved,0);th delstat(%q<target>/%q<attr>`FLAGS,ApprovedBy);th delstat(%q<target>/%q<attr>`FLAGS,ApprovedOn);@switch/inline strmatch(%#,%q<target>)=1,{@nspemit %#=msghead(PROJECT) You approved your %i0 Project},{@nspemit %#=msghead(PROJECT) You unapproved [name(%q<target>)]'s %i0 '[get(%q<target>/%q<attr>)]' Project!;@nspemit %q<target>=msghead(PROJECT) %n unapproved your %i0 '[get(%q<target>/%q<attr>)]' Project!};@switch/inline t(v(VAR`ANNOUNCEAPPROVE))=1,{@nscemit/noisy v(VAR`ANNOUNCECHANNEL)=ansi(h,PROJECT:) %n just unapproved [name(%q<target>)]'s %i0 '[get(%q<target>/%q<attr>)]' Project!};@nspemit/list get(%q<target>/D`PROJECT`%q<filename>`PARTNERS)=msghead(PROJECT) %n unapproved [name(%q<target>)]'s %q<filename> '[get(%q<target>/D`PROJECT`%q<filename>)]' Project!;@include u(ex-proj)/INC`MAKELOG=%q<target>,%#,%q<filename>,{[name(%#)] unapproved the Project!}}

&INC`FINISH u(ex-proj)=@include u(ex-proj)/INC`TARGET;@include u(ex-proj)/INC`FILENAME;@assert strlen(%q<filename>)=@nspemit %#=msghead(PROJECT) ERROR: Info name empty.;@dolist/delimit | %q<filename>={@include u(ex-proj)/INC`PARTIALMATCH=%i0;@break getstat(%q<target>/%q<attr>`FLAGS,Approved)=@nspemit %#=msghead(PROJECT) ERROR: Project %i0 is already approved!;th setstat(%q<target>/%q<attr>`FLAGS,Approved,1);th setstat(%q<target>/%q<attr>`FLAGS,ApprovedBy,%#);th setstat(%q<target>/%q<attr>`FLAGS,ApprovedOn,secs());@switch/inline strmatch(%#,%q<target>)=1,{@nspemit %#=msghead(PROJECT) You approved your %i0 Project},{@nspemit %#=msghead(PROJECT) You approved [name(%q<target>)]'s %i0 '[get(%q<target>/%q<attr>)]' Project!;@nspemit %q<target>=msghead(PROJECT) %n approved your %i0 '[get(%q<target>/%q<attr>)]' Project!};@switch/inline t(v(VAR`ANNOUNCEAPPROVE))=1,{@nscemit/noisy v(VAR`ANNOUNCECHANNEL)=ansi(h,PROJECT:) %n just approved [name(%q<target>)]'s %i0 '[get(%q<target>/%q<attr>)]' Project!};@nspemit/list get(%q<target>/D`PROJECT`%q<filename>`PARTNERS)=msghead(PROJECT) %n approved [name(%q<target>)]'s %q<filename> '[get(%q<target>/D`PROJECT`%q<filename>)]' Project!;@include u(ex-proj)/INC`MAKELOG=%q<target>,%#,%q<filename>,{[name(%#)] approved the Project!}}

&INC`UNFINISH u(ex-proj)=@include u(ex-proj)/INC`TARGET;@include u(ex-proj)/INC`FILENAME;@assert strlen(%q<filename>)=@nspemit %#=msghead(PROJECT) ERROR: Info name empty.;@dolist/delimit | %q<filename>={@include u(ex-proj)/INC`PARTIALMATCH=%i0;@assert getstat(%q<target>/%q<attr>`FLAGS,Approved)=@nspemit %#=msghead(PROJECT) ERROR: Project %i0 is not approved!;th setstat(%q<target>/%q<attr>`FLAGS,Approved,0);th delstat(%q<target>/%q<attr>`FLAGS,ApprovedBy);th delstat(%q<target>/%q<attr>`FLAGS,ApprovedOn);@switch/inline strmatch(%#,%q<target>)=1,{@nspemit %#=msghead(PROJECT) You approved your %i0 Project},{@nspemit %#=msghead(PROJECT) You unapproved [name(%q<target>)]'s %i0 '[get(%q<target>/%q<attr>)]' Project!;@nspemit %q<target>=msghead(PROJECT) %n unapproved your %i0 '[get(%q<target>/%q<attr>)]' Project!};@switch/inline t(v(VAR`ANNOUNCEAPPROVE))=1,{@nscemit/noisy v(VAR`ANNOUNCECHANNEL)=ansi(h,PROJECT:) %n just unapproved [name(%q<target>)]'s %i0 '[get(%q<target>/%q<attr>)]' Project!};@nspemit/list get(%q<target>/D`PROJECT`%q<filename>`PARTNERS)=msghead(PROJECT) %n unapproved [name(%q<target>)]'s %q<filename> '[get(%q<target>/D`PROJECT`%q<filename>)]' Project!;@include u(ex-proj)/INC`MAKELOG=%q<target>,%#,%q<filename>,{[name(%#)] unapproved the Project!}}

&VAR`ANNOUNCEAPPROVE u(ex-proj)=1
&VAR`ANNOUNCECHANNEL u(ex-proj)=Staff Reports

&INC`DELETE u(ex-proj)=@include u(ex-proj)/INC`PREPARE;@break getstat(%q<target>/%q<attr>`FLAGS,Approved)=@nspemit %#=ERROR: Approved Infos must be unapproved by admin first.;@wipe %q<target>/%q<attr>;@switch/inline strmatch(%#,%q<target>)=1,{@nspemit %#=msghead(PROJECT) You deleted your %i0 Project},{@nspemit %#=msghead(PROJECT) You deleted [name(%q<target>)]'s %i0 Project!;@nspemit %q<target>=msghead(PROJECT) %n deleted your %i0 Project!}

&INC`PARTIALMATCH u(ex-proj)=@assert strlen(%0)=@nspemit %#=msghead(PROJECT) ERROR: Info name empty.;@include u(ccs)/INC`PARTIAL=%0,u(u(ex-proj)/FUN`LIST,%q<target>,%#,1),|,INFO,filename,filename;th setq(attr,D`PROJECT`%q<filename>)

&INC`MAIN u(ex-proj)=@include u(ex-proj)/INC`TARGET;@include u(ex-proj)/INC`LIST=%0,%1,UNFINISHED
&INC`OLD u(ex-proj)=@include u(ex-proj)/INC`TARGET;@include u(ex-proj)/INC`LIST=%0,%1,FINISHED

&INC`LIST u(ex-proj)=@include u(ex-proj)/INC`FILENAME;@switch/inline gt(strlen(%q<filename>),0)=1,{@include u(ex-proj)/INC`LIST`CONTENTS},0,{@include u(ex-proj)/INC`LIST`PROJECTS}

&INC`LIST`PROJECTS u(ex-proj)=@nspemit %#=header(name(%q<target>)'s Projects)%R[align(4 17 17 17 13 5,ID,NAME,DURATION,REMAINING,RL FINISH,COM)];@dolist/inline/delimit | [u(FUN`LIST`%2,%q<target>,%#)]={@nspemit %#=align(4 17 17 17 13 5,pueblize(rjust(%i0,3).,setr(comm,+project [if(strmatch(%#,%q<target>),,name(%q<target>)/)]%i0)),pueblize(left(get(%q<target>/D`PROJECT`%i0),17),%q<comm>),if(setr(fin,or(and(eq(0,get(%q<target>/D`PROJECT`%i0`REMAINING)),get(%q<target>/D`PROJECT`%i0`DURATION)),getstat(%q<target>/D`PROJECT`%i0`FLAGS,Finished))),Finished!,u(FUN`DISPTIME,get(%q<target>/D`PROJECT`%i0`DURATION))),if(%q<fin>,Finished!,u(FUN`DISPTIME,get(%q<target>/D`PROJECT`%i0`REMAINING)))[if(strfirstof(getstat(%q<target>/D`PROJECT`%i0`FLAGS,Active),1),,-Halt)],if(%q<fin>,Finished!,if(get(%q<target>/D`PROJECT`%i0`REMAINING),timefmt($I:$M$p $m/$d,add(secs(),div(get(%q<target>/D`PROJECT`%i0`REMAINING),3))))),nattr(%q<target>/D`PROJECT`%i0`COMMENT`*))};@nspemit %#=header(Current Time: [timefmt($I:$M$p $m/$d,secs())])

&INC`LIST`CONTENTS u(ex-proj)=@include u(ex-proj)/INC`PARTIALMATCH=%q<filename>;@nspemit %#=header(name(%q<target>)'s [get(%q<target>/%q<attr>)] Project);@nspemit %#=get(%q<target>/%q<attr>`DESC);@nspemit %#=subheader()%R[ansi(h,Last set by:)] [name(getstat(%q<target>/%q<attr>`FLAGS,SetBy))] [ansi(h,On:)] [convsecs(getstat(%q<target>/%q<attr>`FLAGS,SetOn))];@switch/inline gt(getstat(%q<target>/%q<attr>`FLAGS,Approved),0)=1,{@nspemit %#=ansi(h,Approved by:) [name(getstat(%q<target>/%q<attr>`FLAGS,ApprovedBy))] [ansi(h,On:)] [convsecs(getstat(%q<target>/%q<attr>`FLAGS,ApprovedOn))]};@switch/inline hasattrval(%q<target>/%q<attr>`PARTNERS)=1,{@nspemit %#=ansi(h,Partners:) [itemize(iter(get(%q<target>/%q<attr>`PARTNERS),name(%i0),%b,|),|,and,\,)]};@dolist/inline sortkey(#lambda/last(\%0,`),lattr(%q<target>/%q<attr>`COMMENT`*))={@nspemit %#=subheader(name(get(%q<target>/%i0`BY)) commented on [convsecs(get(%q<target>/%i0`ON))]);@nspemit %#=get(%q<target>/%i0)};@nspemit %#=header()

&INC`MAKELOG u(ex-proj)=@assert isdbref(%0);@assert isdbref(%1);@assert hasattr(%0/D`PROJECT`%2);@assert strlen(%3);&[setr(log,D`PROJECT`%2`LOG`[nextslot(%0,D`PROJECT`%2`LOG)])] %0=secs();&%q<log>`BY %0=objid(%1);&%q<log>`CONTENTS %0=%3
@@ %0 - Project holder. %1 - enactor. %2 - Project slot. %3 - message.

&INC`LOG u(ex-proj)=@include u(ex-proj)/INC`TARGET;@include u(ex-proj)/INC`FILENAME;@include u(ex-proj)/INC`PARTIALMATCH=%q<filename>;@nspemit %#=header(Log for [name(%q<target>)] Project %q<filename>: [get(%q<target>/D`PROJECT`%q<filename>)]);@dolist/inline sortkey(#lambda/last(\%0,`),lattr(%q<target>/D`PROJECT`%q<filename>`LOG`*))={@nspemit %#=subheader(By [name(get(%q<target>/%i0`BY))] on [convsecs(get(%q<target>/%i0))]);@nspemit %#=get(%q<target>/%i0`CONTENTS)};@nspemit %#=header()

&INC`NEW u(ex-proj)=@include u(ex-proj)/INC`TARGET;@include u(ex-proj)/INC`FILENAME;@assert strlen(%q<filename>)=@nspemit %#=msghead(PROJECT) ERROR: Project name empty.;@assert or(isadmin(%#),strmatch(%q<target>,%#))=@nspemit %#=msghead(PROJECT) ERROR: You may not change another's Projects.;@break gt(strlen(%q<filename>),18)=@nspemit %#=ERROR: Info names are limited to 18 characters or less.;@break regmatchi(%q<filename>,\\|)=@nspemit %#=ERROR: Pipe symbols are not allowed in info names.;@break regmatchi(%q<filename>,\/)=@nspemit %#=ERROR: Slashes symbols are not allowed in info names.;@assert strlen(%1)=@nspemit %#=msghead(PROJECT) ERROR: Text field empty.;&[setr(attr,D`PROJECT`[setr(newnum,add(1,default(%q<target>/D`PROJECT,0)))])] %q<target>=%q<filename>;&%q<attr>`DESC %q<target>=%1;&D`PROJECT %q<target>=%q<newnum>;@switch/inline strmatch(%#,%q<target>)=1,{@nspemit %#=msghead(PROJECT) You create the [last(%q<attr>,`)] '%q<filename>'},{@nspemit %#=msghead(PROJECT) You create [last(%q<attr>,`)] '%q<filename>' for [name(%q<target>)]!;@nspemit %q<target>=msghead(PROJECT) %n created the [last(%q<attr>,`)] '%q<filename>' Project for you};th setstat(%q<target>/%q<attr>`FLAGS,SetBy,objid(%#));th setstat(%q<target>/%q<attr>`FLAGS,SetOn,secs());@include u(ex-proj)/INC`MAKELOG=%q<target>,%#,last(%q<attr>,`),{[name(%#)] created the Project: %0-%R%1}

&INC`DESC u(ex-proj)=@include u(ex-proj)/INC`PREPARE;&%q<attr>`DESC %q<target>=%1;@switch/inline strmatch(%#,%q<target>)=1,{@nspemit %#=msghead(PROJECT) You change your %q<filename> '[get(%q<target>/D`PROJECT`%q<filename>)]' Project Desc!},{@nspemit %#=msghead(PROJECT) You change [name(%q<target>)]'s %q<filename> '[get(%q<target>/D`PROJECT`%q<filename>)]' Project desc!;@nspemit %q<target>=msghead(PROJECT) %n changed your %q<filename> '[get(%q<target>/D`PROJECT`%q<filename>)]' Project desc!};@nspemit/list get(%q<target>/D`PROJECT`%q<filename>`PARTNERS)=msghead(PROJECT) %n changed [name(%q<target>)]'s %q<filename> '[get(%q<target>/D`PROJECT`%q<filename>)]' Project's Desc!;@include u(ex-proj)/INC`MAKELOG=%q<target>,%#,%q<filename>,{[name(%#)] changed the Project's Desc to:%R%1}

&INC`VISIBLE u(ex-proj)=@assert words(setr(players,lsearch(all,eplayer,\[words(wildgrepi(##,D`PROJECT`*`PARTNERS,*%:*))\])))=@nspemit %#=msghead(PROJECT) ERROR: No Players have partnered Projects with you.;@nspemit %#=header(Partnered Projects Infos);@nspemit %#=table(iter(%q<players>,left(pueblize(name(%i0),+project [name(%i0)]),22)\([words(wildgrepi(%i0,D`PROJECT`*`PARTNERS,*%:*))]\)),25,78,|);@nspemit %#=header()

&INC`RENAME u(ex-proj)=@include u(ex-proj)/INC`PREPARE;@break gt(strlen(%1),18)=@nspemit %#=ERROR: Info names are limited to 18 characters or less.;@break regmatchi(%1,\\|)=@nspemit %#=ERROR: Pipe symbols are not allowed in info names.;@break regmatchi(%1,\/)=@nspemit %#=ERROR: Slashes symbols are not allowed in info names.;&%q<attr> %q<target>=%1;@switch/inline strmatch(%#,%q<target>)=1,{@nspemit %#=msghead(PROJECT) You change your %q<filename> '[get(%q<target>/D`PROJECT`%q<filename>)]' Project Name to: %1},{@nspemit %#=msghead(PROJECT) You change [name(%q<target>)]'s %q<filename> '[get(%q<target>/D`PROJECT`%q<filename>)]' Project name to: %1;@nspemit %q<target>=msghead(PROJECT) %n changed your %q<filename> '[get(%q<target>/D`PROJECT`%q<filename>)]' Project name to: %1};@nspemit/list get(%q<target>/D`PROJECT`%q<filename>`PARTNERS)=msghead(PROJECT) %n changed [name(%q<target>)]'s %q<filename> '[get(%q<target>/D`PROJECT`%q<filename>)]' Project to: %1;@include u(ex-proj)/INC`MAKELOG=%q<target>,%#,%q<filename>,{[name(%#)] changed the Project's name to:%R%1}

&INC`START u(ex-proj)=@include u(ex-proj)/INC`PREPARE;@break strfirstof(getstat(%q<target>/D`PROJECT`%q<filename>`FLAGS,Active),1)=@nspemit %#=msghead(PROJECT) ERROR: That Project is already active!;th setstat(%q<target>/D`PROJECT`%q<filename>`FLAGS,Active,1);@switch/inline strmatch(%#,%q<target>)=1,{@nspemit %#=msghead(PROJECT) You activate your %q<filename> '[get(%q<target>/D`PROJECT`%q<filename>)]' Project.},{@nspemit %#=msghead(PROJECT) You activate [name(%q<target>)]'s %q<filename> '[get(%q<target>/D`PROJECT`%q<filename>)]' Project;@nspemit %q<target>=msghead(PROJECT) %n activated your %q<filename> '[get(%q<target>/D`PROJECT`%q<filename>)]' Project};@nspemit/list get(%q<target>/D`PROJECT`%q<filename>`PARTNERS)=msghead(PROJECT) %n activated [name(%q<target>)]'s %q<filename> '[get(%q<target>/D`PROJECT`%q<filename>)]' Project;@include u(ex-proj)/INC`MAKELOG=%q<target>,%#,%q<filename>,{[name(%#)] activated the Project}

&INC`STOP u(ex-proj)=@include u(ex-proj)/INC`PREPARE;@assert strfirstof(getstat(%q<target>/D`PROJECT`%q<filename>`FLAGS,Active),1)=@nspemit %#=msghead(PROJECT) ERROR: That Project is not active!;th setstat(%q<target>/D`PROJECT`%q<filename>`FLAGS,Active,0);@switch/inline strmatch(%#,%q<target>)=1,{@nspemit %#=msghead(PROJECT) You pause your %q<filename> '[get(%q<target>/D`PROJECT`%q<filename>)]' Project.},{@nspemit %#=msghead(PROJECT) You pause [name(%q<target>)]'s %q<filename> '[get(%q<target>/D`PROJECT`%q<filename>)]' Project;@nspemit %q<target>=msghead(PROJECT) %n paused your %q<filename> '[get(%q<target>/D`PROJECT`%q<filename>)]' Project};@nspemit/list get(%q<target>/D`PROJECT`%q<filename>`PARTNERS)=msghead(PROJECT) %n paused [name(%q<target>)]'s %q<filename> '[get(%q<target>/D`PROJECT`%q<filename>)]' Project;@include u(ex-proj)/INC`MAKELOG=%q<target>,%#,%q<filename>,{[name(%#)] paused the Project}

&INC`DURATION u(ex-proj)=@include u(ex-proj)/INC`PREPARE;@assert strlen(%1)=@nspemit %#=msghead(PROJECT) ERROR: Duration field empty.;@assert or(strmatch(%1,X),valnum(u(FUN`EVALTIME,%1)))=@nspemit %#=msghead(PROJECT) ERROR: Duration must be in EVALTIME or X for Indefinite!;th setq(entry,);&%q<attr>`DURATION %q<target>=setr(time,if(strmatch(%1,X),X,u(FUN`EVALTIME,%1)));&%q<attr>`REMAINING %q<target>=%q<time>;th setstat(%q<target>/%q<attr>`FLAGS,Reported,0);@switch/inline strmatch(%#,%q<target>)=1,{@nspemit %#=msghead(PROJECT) You schedule your %q<filename> '[get(%q<target>/%q<attr>)]' Project Duration to: [u(FUN`DISPTIME,u(FUN`EVALTIME,%1))]},{@nspemit %#=msghead(PROJECT) You change [name(%q<target>)]'s %q<filename> '[get(%q<target>/%q<attr>)]' Project Duration to: [u(FUN`DISPTIME,u(FUN`EVALTIME,%1))];@nspemit %q<target>=msghead(PROJECT) %n changed your %q<filename> '[get(%q<target>/%q<attr>)]' Project Duration to: [u(FUN`DISPTIME,u(FUN`EVALTIME,%1))]};@nspemit/list get(%q<target>/%q<attr>`PARTNERS)=msghead(PROJECT) %n changed the Duration of [name(%q<target>)]'s %q<filename> '[get(%q<target>/%q<attr>)]' Project to: [u(FUN`DISPTIME,u(FUN`EVALTIME,%1))];@include u(ex-proj)/INC`MAKELOG=%q<target>,%#,%q<filename>,{[name(%#)] changed the Project's Duration to: [u(FUN`DISPTIME,u(FUN`EVALTIME,%1))]}

&INC`PREPARE u(ex-proj)=@include u(ex-proj)/INC`TARGET;@include u(ex-proj)/INC`FILENAME;@include u(ex-proj)/INC`PARTIALMATCH=%q<filename>;@assert strlen(%q<filename>)=@nspemit %#=msghead(PROJECT) ERROR: Project name empty.;@assert or(isadmin(%#),strmatch(%q<target>,%#),%0)=@nspemit %#=msghead(PROJECT) ERROR: You may not change another's Projects.;@assert or(%2,isadmin(%#),not(getstat(%q<target>/%q<attr>`FLAGS,Approved)))=@nspemit %#=msghead(PROJECT) ERROR: That Project may not be changed by you.

&INC`ADD u(ex-proj)=@include u(ex-proj)/INC`PREPARE=%0,%1,1;@assert strlen(%1)=@nspemit %#=msghead(PROJECT) ERROR: Comment field empty.;&[setr(attr,D`PROJECT`%q<filename>`COMMENT`[nextslot(D`PROJECT`%q<filename>`COMMENT)])] %q<target>=%1;&%q<attr>`ON %q<target>=secs();&%q<attr>`BY %q<target>=%:;@switch/inline strmatch(%#,%q<target>)=1,{@nspemit %#=msghead(PROJECT) You added a comment to your %q<filename> '[get(%q<target>/D`PROJECT`%q<filename>)]' Project},{@nspemit %#=msghead(PROJECT) You add a comment to [name(%q<target>)]'s %q<filename> '[get(%q<target>/D`PROJECT`%q<filename>)]' Project;@nspemit %q<target>=msghead(PROJECT) %n added a comment to your %q<filename> '[get(%q<target>/D`PROJECT`%q<filename>)]' Project};@nspemit/list setdiff(get(%q<target>/D`PROJECT`%q<filename>`PARTNERS),%:)=msghead(PROJECT) %n added a comment to [name(%q<target>)]'s %q<filename> '[get(%q<target>/D`PROJECT`%q<filename>)]' Project;@include u(ex-proj)/INC`MAKELOG=%q<target>,%#,%q<filename>,{[name(%#)] added a comment: %1}

&INC`ADDPARTNER u(ex-proj)=@include u(ex-proj)/INC`PREPARE;@assert strlen(%1)=@nspemit %#=msghead(PROJECT) ERROR: Name field empty.;@include u(ccs)/INC`CHECKPC=%1,1,PROJECT;@break match(get(%q<target>/D`PROJECT`%q<filename>`PARTNERS),%q<t1objid>)=@nspemit %#=msghead(PROJECT) ERROR: [subj(%q<t1>)] is already a Project partner!;&D`PROJECT`%q<filename>`PARTNERS %q<target>=filterbool(#lambda/isobjid(\%0),setunion(get(%q<target>/D`PROJECT`%q<filename>`PARTNERS),%q<t1objid>));@switch/inline strmatch(%#,%q<target>)=1,{@nspemit %#=msghead(PROJECT) You added a partner to your %q<filename> '[get(%q<target>/D`PROJECT`%q<filename>)]' Project: %q<t1name>},{@nspemit %#=msghead(PROJECT) You add a partner to [name(%q<target>)]'s %q<filename> '[get(%q<target>/D`PROJECT`%q<filename>)]' Project: %q<t1name>;@nspemit %q<target>=msghead(PROJECT) %n added a partner to your %q<filename> '[get(%q<target>/D`PROJECT`%q<filename>)]' Project: %q<t1name>};@nspemit/list get(%q<target>/D`PROJECT`%q<filename>`PARTNERS)=msghead(PROJECT) %n added a partner to [name(%q<target>)]'s %q<filename> '[get(%q<target>/D`PROJECT`%q<filename>)]' Project: %q<t1name>;@include u(ex-proj)/INC`MAKELOG=%q<target>,%#,%q<filename>,{[name(%#)] added a partner: %q<t1name>}

&INC`REMPARTNER u(ex-proj)=@include u(ex-proj)/INC`PREPARE;@assert strlen(%1)=@nspemit %#=msghead(PROJECT) ERROR: Name field empty.;@include u(ccs)/INC`CHECKPC=%1,1,PROJECT;@assert match(get(%q<target>/D`PROJECT`%q<filename>`PARTNERS),%q<t1objid>)=@nspemit %#=msghead(PROJECT) ERROR: [subj(%q<t1>)] is not a Project partner!;@switch/inline strmatch(%#,%q<target>)=1,{@nspemit %#=msghead(PROJECT) You removed a partner from your %q<filename> '[get(%q<target>/D`PROJECT`%q<filename>)]' Project: %q<t1name>},{@nspemit %#=msghead(PROJECT) You remove a partner from [name(%q<target>)]'s %q<filename> '[get(%q<target>/D`PROJECT`%q<filename>)]' Project: %q<t1name>;@nspemit %q<target>=msghead(PROJECT) %n removed a partner from your %q<filename> '[get(%q<target>/D`PROJECT`%q<filename>)]' Project: %q<t1name>};@nspemit/list get(%q<target>/D`PROJECT`%q<filename>`PARTNERS)=msghead(PROJECT) %n removed a partner from [name(%q<target>)]'s %q<filename> '[get(%q<target>/D`PROJECT`%q<filename>)]' Project: %q<t1name>;&D`PROJECT`%q<filename>`PARTNERS %q<target>=filterbool(#lambda/isobjid(\%0),setdiff(get(%q<target>/D`PROJECT`%q<filename>`PARTNERS),%q<t1objid>));@include u(ex-proj)/INC`MAKELOG=%q<target>,%#,%q<filename>,{[name(%#)] added a partner: %q<t1name>}

&INC`FINISH u(ex-proj)=@include u(ex-proj)/INC`PREPARE;@break getstat(%q<target>/%q<attr>`FLAGS,Finished)=@nspemit %#=msghead(PROJECT) ERROR: That project is already finished!;th setstat(%q<target>/%q<attr>`FLAGS,Finished,1);@switch/inline strmatch(%#,%q<target>)=1,{@nspemit %#=msghead(PROJECT) You finished your %q<filename> '[get(%q<target>/D`PROJECT`%q<filename>)]' Project},{@nspemit %#=msghead(PROJECT) You finished [name(%q<target>)]'s %q<filename> '[get(%q<target>/D`PROJECT`%q<filename>)]' Project;@nspemit %q<target>=msghead(PROJECT) %n finished your %q<filename> '[get(%q<target>/D`PROJECT`%q<filename>)]' Project};@nspemit/list setdiff(get(%q<target>/D`PROJECT`%q<filename>`PARTNERS),%:)=msghead(PROJECT) %n finished [name(%q<target>)]'s %q<filename> '[get(%q<target>/D`PROJECT`%q<filename>)]' Project;@include u(ex-proj)/INC`MAKELOG=%q<target>,%#,%q<filename>,{[name(%#)] finished the project.}

&INC`UNFINISH u(ex-proj)=@include u(ex-proj)/INC`PREPARE;@assert getstat(%q<target>/%q<attr>`FLAGS,Finished)=@nspemit %#=msghead(PROJECT) ERROR: That project is not finished!;th setstat(%q<target>/%q<attr>`FLAGS,Finished,0);@switch/inline strmatch(%#,%q<target>)=1,{@nspemit %#=msghead(PROJECT) You Unfinished your %q<filename> '[get(%q<target>/D`PROJECT`%q<filename>)]' Project},{@nspemit %#=msghead(PROJECT) You Unfinished [name(%q<target>)]'s %q<filename> '[get(%q<target>/D`PROJECT`%q<filename>)]' Project;@nspemit %q<target>=msghead(PROJECT) %n Unfinished your %q<filename> '[get(%q<target>/D`PROJECT`%q<filename>)]' Project};@nspemit/list setdiff(get(%q<target>/D`PROJECT`%q<filename>`PARTNERS),%:)=msghead(PROJECT) %n Unfinished [name(%q<target>)]'s %q<filename> '[get(%q<target>/D`PROJECT`%q<filename>)]' Project;@include u(ex-proj)/INC`MAKELOG=%q<target>,%#,%q<filename>,{[name(%#)] Unfinished the project.}

&INC`TIMEDIV u(ex-proj)=@assert strlen(%0)=@nspemit %#=msghead(PROJECT) No time entered to divide!;@assert valnum(setr(time,u(FUN`EVALTIME,%0)))=@nspemit %#=msghead(PROJECT) ERROR: Entered time must be in DURATION format!;@assert strlen(%1)=@nspemit %#=msghead(PROJECT) ERROR: No value entered to divide the duration by.;@assert valnum(%1)=@nspemit %#=msghead(PROJECT) ERROR: Divisor must be a whole, positive number.;@nspemit %#=msghead(PROJECT) [u(FUN`DISPTIME,u(FUN`EVALTIME,%0))] Divided by %1: [u(FUN`DISPTIME,div(u(FUN`EVALTIME,%0),%1))]

&FUN`EVALTIME u(ex-proj)=lmath(add,iter(%0,switch(%i0,*s,before(%i0,s),*m,mul(before(%i0,m),u(mi)),*h,mul(before(%i0,h),u(ho)),*d,mul(before(%i0,d),u(da)),*w,mul(before(%i0,w),u(we)),*mo,mul(before(%i0,mo),u(mo)),*se,mul(before(%i0,se),u(se)),*y,mul(before(%i0,y),u(ye)),0)))

&FUN`DISPTIME u(ex-proj)=if(strmatch(%0,X),Indefinite,trim(if(setr(ye,div(setr(newsec,%0),u(ye))),%q<ye>ye [setq(newsec,sub(%0,mul(u(ye),%q<ye>)))])[if(and(%q<newsec>,setr(mo,div(%q<newsec>,u(mo)))),%q<mo>mo [setq(newsec,sub(%q<newsec>,mul(u(mo),%q<mo>)))])][if(and(%q<newsec>,setr(we,div(%q<newsec>,u(we)))),%q<we>w [setq(newsec,sub(%q<newsec>,mul(u(we),%q<we>)))])][if(and(%q<newsec>,setr(da,div(%q<newsec>,u(da)))),%q<da>d [setq(newsec,sub(%q<newsec>,mul(u(da),%q<da>)))])][if(and(%q<newsec>,setr(ho,div(%q<newsec>,u(ho)))),%q<ho>h [setq(newsec,sub(%q<newsec>,mul(u(ho),%q<ho>)))])][if(and(%q<newsec>,setr(mi,div(%q<newsec>,u(mi)))),%q<mi>m [setq(newsec,sub(%q<newsec>,mul(u(mi),%q<mi>)))])][if(%q<newsec>,%q<newsec>s)]))

&YE u(ex-proj)=mul(u(da),425)
&SE u(ex-proj)=mul(u(mo),3)
&MO u(ex-proj)=mul(u(we),4)
&WE u(ex-proj)=mul(u(da),7)
&DA u(ex-proj)=mul(u(ho),25)
&HO u(ex-proj)=mul(u(mi),60)
&MI u(ex-proj)=60

&TRG`TICKDOWN u(ex-proj)=@dolist lsearch(all,eplayer,\[and(approved(##),nattr(##/D`PROJECT`*))\])={@dolist/inline/nobreak filterbool(#lambda/and(not(strfirstof(getstat(%i0/\%0`FLAGS,Finished),0)),strfirstof(getstat(%i0/\%0`FLAGS,Active),1)),lattr(%i0/D`PROJECT`*))={&%i0`REMAINING %i1=bound(sub(get(%i1/%i0`REMAINING),%0),0);@trigger u(ex-proj)/TRG`CHECKFINISH=%i1,%i0}}

&TRG`CHECKFINISH u(ex-proj)=@assert get(%0/%1`DURATION);@break get(%0/%1`REMAINING);@break getstat(%0/%1`FLAGS,Reported);@nspemit/list setunion(get(%0/%1`PARTNERS),%0)=msghead(PROJECT) [name(%0)]'s Project [last(%1,`)] '[default(%0/%1,Unset)]' has finished its Duration.[if(getstat(%0/%1`FLAGS,Approved),%BIt has been submitted for review!)];@include u(ex-proj)/INC`MAKELOG=%0,%0,last(%1,`),{Project duration finished![if(getstat(%0/%1`FLAGS,Approved),%BIt has been submitted for review!)]};@switch/inline t(getstat(%0/%1`FLAGS,Approved))=1,{@assert isdbref(setr(bucket,locate(get(u(job)/vc),PROJ,TXxi)))=@nscemit/noisy Staff Reports=Project Bucket not found, cannot finish [name(%0)]'s pending Project [last(%0,`)] '[default(%0/%1,Unset)]';@trigger get(u(job)/va)/TRIG_CREATE=%0,%q<bucket>,strfirstof(getstat(u(rst)/PRIORITY,%q<category>),1),name(%0) - [last(%1,`)] '[default(%0/%1,Unset)]',The Project is ready for admin review.,iter(filterbool(#lambda/isobjid(\%0),setunion(objid(%0),get(%0/%1`PARTNERS))),num(%i0)),,};th setstat(%0/%1`FLAGS,Reported,1)



