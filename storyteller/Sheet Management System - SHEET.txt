@@ DEPENDENCIES - CORE
th u(newcobj,STORYTELLER: Sheet Management System <SHEET>,sheet,,,,WIZARD SAFE !NO_COMMAND,INHERIT SIDEFX SAFE)

th u(newconf,CONFIG,STORYTELLER,SHEET_PUBLIC,0,+sheets are public?,BOOL)
th u(newconf,PCONF,STORYTELLER,SHEET_PUBLIC,0,Make +sheet public?,BOOL)

&CMD`+SHEET`PENNMUSH [u(cobj,sheet)]=$^\+sheet(?\:/(\S+))?(?\: +(.*))?$:@attach %!/CMD`+SHEET`MAIN
@set [u(cobj,sheet)]/CMD`+SHEET`PENNMUSH=regexp
&CMD`+SHEET`RHOSTMUSH [u(cobj,sheet)]=$^\+sheet(?\:/(\\S+))?(?\: +(.*))?$:@attach %!/CMD`+SHEET`MAIN
@set [u(cobj,sheet)]/CMD`+SHEET`RHOSTMUSH=regexp
&CMD`+SHEET`MAIN [u(cobj,sheet)]=@attach %!/INC`GETSWITCH=%1;@attach %!/INC`CHECKPC=u(strfirstof`%va,%2,%#),1;@select/inline cor(strmatch(%q<t1>,%#),u(isadmin`%va,%#),u(player_config`%va,%q<t1>,STORYTELLER,SHEET_PUBLIC),match(u(alts,%#),%q<t1>),u(game_config,STORYTELLER,SHEET_PUBLIC))=0,{@break 1=@attach %!/INC`MSG=ERROR: Permission denied.};@attach %!/INC`[u(strfirstof`%va,%q<switch>,MAIN)]=%q<t1>,getclass(%q<t1>)
@set [u(cobj,sheet)]/CMD`+SHEET`[switch(v(game),PennMUSH,RHOSTMUSH,RhostMUSH,PENNMUSH)]=no_command

&SWITCHES`PLAYER [u(cobj,sheet)]=EXTRA

&FUN`COLOR [u(cobj,sheet)]=ansi(u(strfirstof`%va,v(%0`%1),v(DEFAULT`%1),n),%2)

&FUN`HEADER [u(cobj,sheet)]=u(FUN`COLOR,%1,BORDER,if(%4,|,\}))[if(strlen(%2),center(u(FUN`COLOR,%1,SLASH,/)[u(FUN`COLOR,%1,FIRSTLETTER,%2)][u(FUN`COLOR,%1,RESTWORD,%3)][u(FUN`COLOR,%1,SLASH,/)],sub(u(width`%va,%#),2),u(FUN`COLOR,%1,BORDER,-)),u(FUN`COLOR,%1,BORDER,repeat(-,sub(u(width`%va,%#),2))))][u(FUN`COLOR,%1,BORDER,if(%4,|,\{))]

&FUN`SHEETHEADER [u(cobj,sheet)]=[space(2)][u(FUN`COLOR,%1,BORDER,.)][u(FUN`COLOR,%1,BORDER,repeat(-,sub(u(width`%va,%#),6)))][u(FUN`COLOR,%1,BORDER,.)]%R[space(1)][u(FUN`COLOR,%1,BORDER,/)][center(u(FUN`COLOR,%1,TITLE,mudname()),sub(u(width`%va,%#),4))][u(FUN`COLOR,%1,BORDER,\\)]%R[u(FUN`HEADER,%0,%1)][u(FUN`CLASSINFO`%1,%0,%1)]

&FUN`CLASSINFO`MAIN [u(cobj,sheet)]=%R[u(FUN`BORDER,%0,%1,align(floor(fdiv(sub(u(width`%va,%#),6),2)) [ceil(floor(fdiv(sub(u(width`%va,%#),6),2)))],u(FUN`INFOCOLUMN,%0,%1,%2),u(FUN`INFOCOLUMN,%0,%1,%3)))]

&FUN`INFOCOLUMN [u(cobj,sheet)]=localize(u(setq`%va,colwidth,u(lmax`%va,iter(%2,strlen(elements(%i0,2,~)),|,%b)))[iter(%2,rjust(u(FUN`COLOR,%1,TEXTHEAD,elements(%i0,2,~)),%q<colwidth>)[u(FUN`COLOR,%1,COLON,:)]%B[u(FUN`COLOR,%1,TEXTFIELD,u(capnames,u(strfirstof`%va,switch(elements(%i0,1,~),NAME,u(moniker`%va,%0),SEX,u(getproperty,%0,SEX),u(getstat,%0,D`INFO,elements(%i0,1,~))),elements(%i0,3,~))))],|,%R)])

&FUN`BORDER [u(cobj,sheet)]=align(1. [sub(u(width`%va,%#),4)] 1.,u(FUN`COLOR,%1,BORDER,|),%2,u(FUN`COLOR,%1,BORDER,|)%R)

&FUN`ATTRIBUTES [u(cobj,sheet)]=localize(u(FUN`3HEADER,%0,%1,Physical Attributes|Social Attributes|Mental Attributes)%R[align(1. [u(setr`%va,c1,floor(u(setr`%va,c0,fdiv(sub(u(width`%va,%#),4),3))))] 1. [u(setr`%va,c2,ceil(%q<c0>))] 1. [u(setr`%va,c3,sub(u(width`%va,%#),4,%q<c1>,%q<c2>))] 1.,u(FUN`COLOR,%1,BORDER,|),u(FUN`ATTRCASCADE,%0,%1,PHYSICAL,%q<c1>),u(FUN`COLOR,%1,BORDER,|),u(FUN`ATTRCASCADE,%0,%1,SOCIAL,%q<c2>),u(FUN`COLOR,%1,BORDER,|),u(FUN`ATTRCASCADE,%0,%1,MENTAL,%q<c3>),u(FUN`COLOR,%1,BORDER,|),,,%R)])

@@ %0 = Player. %1 = Class. %2 = Attribute. %3 = Entry. %4 = Length. %5 = manual value. %6 = Manual bonus. %7 = Don't show bonus. %8 = No favoreds. %9 = Truncate.

&FUN`SECTIONHEADER [u(cobj,sheet)]=u(FUN`COLOR,%1,BORDER,\})[center(u(FUN`COLOR,%1,SLASH,/)[u(FUN`COLOR,%1,SECTIONNAME,edit(%2,_,%b))][u(FUN`COLOR,%1,SLASH,/)],sub(u(width`%va,%#),2),u(FUN`COLOR,%1,BORDER,-))][u(FUN`COLOR,%1,BORDER,\{)]

&FUN`ADVSUBHEADER [u(cobj,sheet)]=u(FUN`COLOR,%1,BORDER,|)[center(u(FUN`ADVSUBNAME,%0,%1,%2),sub(u(width`%va,%#),2))][u(FUN`COLOR,%1,BORDER,|)]

&FUN`ADVSUBNAME [u(cobj,sheet)]=u(FUN`COLOR,%1,ADVBORDER,====)[u(FUN`COLOR,%1,ADVNAME,%2)][u(FUN`COLOR,%1,ADVBORDER,====)]

&FUN`3HEADER [u(cobj,sheet)]=[u(FUN`COLOR,%1,BORDER,\})][center(iter(%2,center(u(FUN`COLOR,%1,SLASH,/)[u(FUN`COLOR,%1,3COLUMNNAME,%i0)][u(FUN`COLOR,%1,SLASH,/)],div(sub(u(width`%va,%#),2),3),u(FUN`COLOR,%1,BORDER,-)),|,),sub(u(width`%va,%#),2),u(FUN`COLOR,%1,BORDER,-))][u(FUN`COLOR,%1,BORDER,\{)]

&FUN`ATTRCASCADE [u(cobj,sheet)]=iter(v(ATTRIBUTES`%2),u(FUN`STATFORMAT`STAT,%0,%1,ATTRIBUTES,%i0,%3),|,%R)

&FUN`SPECIALTIES [u(cobj,sheet)]=u(FUN`DOTFORMAT,%0,%1,SPECIALTIES,36,Specialties,D`SPECIALTIES,sort(u(u(cobj,data)/FUN`RET`SPECIALTIES,%0),i,|,|))

&FUN`MERITS [u(cobj,sheet)]=u(FUN`MERFORMAT,%0,%1,MERITS,+merits)
&FUN`FLAWS [u(cobj,sheet)]=u(FUN`MERFORMAT,%0,%1,FLAWS,+flaws)

&FUN`DOTFORMAT [u(cobj,sheet)]=localize(u(FUN`SECTIONHEADER,%0,%1,%4)%R[u(setq`%va,sorted,if(strlen(%7),u(filter,DOTMATCH,%7,|,|,%6),%6))][if(%8,u(setq`%va,first,u(filter,LTSTRLEN,%q<sorted>,|,|,%3),second,u(filter,GTESTRLEN,%q<sorted>,|,|,%3)),u(setq`%va,first,%q<sorted>))][if(strlen(%q<first>),u(setq`%va,firstsec,u(FUN`STATFORMAT,%0,%1,%2,%3,%5,%q<first>)))][if(strlen(%q<second>),u(setq`%va,secondsec,u(FUN`STATFORMAT2,%0,%1,%2,sub(u(width`%va,%#),2),%5,%q<second>)))][if(strlen(%q<firstsec>),%q<firstsec>)][if(strlen(%q<secondsec>),if(strlen(%q<firstsec>),%R%q<secondsec>,%q<secondsec>))])

&FIL`DOTMATCH [u(cobj,sheet)]=t(match(%1,%0,|))
&FIL`LTSTRLEN [u(Cobj,sheet)]=lt(strlen(before(%0,~)),sub(%1,10))
&FIL`GTESTRLEN [u(cobj,sheet)]=gte(strlen(%0),%1)
&FIL`LTESTRLEN [u(cobj,sheet)]=lte(strlen(%0),%1)

&FUN`STATFORMAT [u(cobj,sheet)]=u(FUN`BORDER,%0,%1,[u(setq`%va,wid,floor(fdiv(mod(sub(u(width`%va,%#),4),%3),floor(fdiv(sub(u(width`%va,%#),4),%3)))))][u(table`%va,iter(%5,u(FUN`STATFORMAT`STAT,%0,%1,%2,%i0,%3),|,|),add(%q<wid>,%3),sub(u(width`%va,%#),4),|)])
&FUN`STATFORMAT2 [u(cobj,sheet)]=u(FUN`BORDER,%0,%1,trimpenn(iter(%5,u(FUN`STATFORMAT`STAT,%0,%1,%2,%i0,%3),|,%R),%R))

&FUN`MERFORMAT [u(cobj,sheet)]=localize(u(FUN`SECTIONHEADER,%0,%1,u(capnames,u(strfirstof`%va,%4,%2)))%R[u(setq`%va,sorted,sortkey(#lambda/u(u(cobj,merit)/FUN`MERNAME,%0,\%0),u(lattr`%va,%0/D`%2`*),i,%b,|))][u(setq`%va,first,filterbool(#lambda/lt(strlen(u(u(cobj,merit)/FUN`MERNAME,%0,\%0)),sub(36,10)),%q<sorted>,|,|))][u(setq`%va,second,filterbool(#lambda/gte(strlen(u(u(cobj,merit)/FUN`MERNAME,%0,\%0)),sub(36,10)),%q<sorted>,|,|))][if(strlen(%q<first>),u(setq`%va,firstsec,u(FUN`MERFORMAT`2,%0,%1,%2,%q<first>,%3)))][if(strlen(%q<second>),u(setq`%va,secondsec,u(FUN`MERFORMAT`3,%0,%1,%2,%q<second>,%3)))][if(strlen(%q<firstsec>),%q<firstsec>)][if(strlen(%q<secondsec>),if(strlen(%q<firstsec>),%R%q<secondsec>,%q<secondsec>))])

&FUN`MERFORMAT`2 [u(cobj,sheet)]=u(FUN`BORDER,%0,%1,u(table`%va,iter(%3,u(FUN`STATFORMAT`STAT,%0,%1,%2,u(pueblize,u(u(cobj,merit)/FUN`MERNAME,%0,%i0),%4 [name(%0)]/[u(u(cobj,merit)/FUN`MERNAME,%0,%i0)]),36,get(%0/%i0`RANK),,1,1),|,|),36,sub(u(width`%va,%#),4),|))

&FUN`MERFORMAT`3 [u(cobj,sheet)]=u(FUN`BORDER,%0,%1,trimpenn(iter(%3,u(FUN`STATFORMAT`STAT,%0,%1,%2,u(pueblize,u(u(cobj,merit)/FUN`MERNAME,%0,%i0),%4 [name(%0)]/[u(u(cobj,merit)/FUN`MERNAME,%0,%i0)]),sub(u(width`%va,%#),4),get(%0/%i0`RANK),,1,1),|,%R),%R))

&FUN`STATFORMAT`STAT [u(cobj,sheet)]=localize([u(setq`%va,namedisp,if(%9,left(%3,%9),%3))][u(setq`%va,val,u(strfirstof`%va,%5,u(getstat,%0,D`%2,%3)))][u(setq`%va,bon,u(strfirstof`%va,%6,getbonus(%0,%3)))][u(setq`%va,totval,add(%q<val>,if(%7,%q<bon>)))][if(%7,u(setq`%va,valdisp,if(%7,u(FUN`COLOR,%1,STATDOT,repeat(*,%q<val>)))),[u(setq`%va,astlen,if(lte(%q<bon>,-1),bound(add(%q<val>,%q<bon>),0),%q<val>))][u(setq`%va,minlen,if(lte(%q<bon>,-1),abs(%q<bon>)))][u(setq`%va,pluslen,if(gte(%q<bon>,1),%q<bon>))][u(setq`%va,valdisp,[if(%q<astlen>,u(FUN`COLOR,%1,STATDOT,repeat(*,%q<astlen>)))][if(%q<minlen>,u(FUN`COLOR,%1,STATDOT,repeat(-,%q<minlen>)))][if(%q<pluslen>,u(FUN`COLOR,%1,STATDOT,repeat(+,%q<pluslen>)))])])][if(%8,,u(setq`%va,fav,t(match(get(%0/D`FAVORED`%2),switch(%3,MARTIAL ARTS,%3,%3),|)),sup,t(match(get(%0/D`SUPERNAL`%2),%3,|))))][u(setq`%va,statlen,strlen(%q<namedisp>))][if(lte(sub(%4,1,%q<statlen>,strlen(%q<valdisp>)),0),u(setq`%va,valdisp,%q<totval>,dotlen,strlen(%q<totval>)),u(setq`%va,dotlen,strlen(%q<valdisp>)))][u(setq`%va,linelen,sub(%4,1,%q<dotlen>,%q<statlen>))][switch(1,%q<sup>,u(FUN`COLOR,%1,SUPMARK,+),%q<fav>,u(FUN`COLOR,%1,FAVMARK,+),%b)][u(FUN`COLOR,%1,STATNAME,if(%5,%q<namedisp>,u(capnames,%q<namedisp>)))][if(gt(%q<linelen>,0),repeat(u(FUN`COLOR,%1,STATFILL,.),%q<linelen>))]%q<valdisp>)
@@ %0 = Player. %1 = Splat. %2 = Attribute. %3 = Entry. %4 = Length. %5 = manual value. %6 = Manual bonus. %7 = Don't show bonus. %8 = No favoreds. %9 = Truncate.

&FUN`WORDLIST [u(cobj,sheet)]=if(strlen(u(setr`%va,short,u(table`%va,sort(u(filter,LTESTRLEN,u(setr`%va,capped,iter(u(renderwords,%1),%i0,|,|)),|,|,36),i,|,|),36,sub(u(width`%va,%#),4),|))),%q<short>)[if(words(u(setr`%va,toolong,u(filter,GTESTRLEN,%q<capped>,|,|,37)),|),if(strlen(%q<short>),%R)[iter(%q<toolong>,%i0,|,%R)])]

&RENDERWORDS [u(cobj,sheet)]=regeditalli(u(capnames,%0),~(\\d+),switch(u(setr`%va,wordnum,$1),>1,%b%(%q<wordnum>%)))

&FUN`FORMATPOOLS [u(cobj,sheet)]=iter(u(capnames,u(u(cobj,pool)/FUN`LISTPOOLS,%0,%1)),rjust(%i0: [rjust(add(u(u(cobj,pool)/FUN`CUR,%0,%i0),0),3)]/[u(u(cobj,pool)/FUN`MAX,%0,%i0)],switch(%1,Pool,21,Channel,19,Track,19,21)),|,%R)

&FUN`FOOTER [u(cobj,sheet)]=u(FUN`SECTIONHEADER,%0,%1,Advancement)%R[u(FUN`BORDER,%0,%1,u(table`%va,iter(XP~Experience,after(%i0,~): [rjust(u(u(cobj,xp)/FUN`AVAILABLE,%0,before(%i0,~)),3,0)]/[rjust(u(u(cobj,xp)/FUN`GAINED,%0,before(%i0,~)),3,0)],|,|),36,sub(u(width`%va,%#),4),|))]%R%B[u(FUN`COLOR,%1,BORDER,\\)][space(sub(u(width`%va,%#),4))][u(FUN`COLOR,%1,BORDER,/)]%R%B%B[u(FUN`COLOR,%1,BORDER,')][center([u(FUN`COLOR,%1,BORDER,*)]%B[u(FUN`GETSERIES,%1)]%B[u(FUN`COLOR,%1,BORDER,*)],sub(u(width`%va,%#),6),u(FUN`COLOR,%1,BORDER,-))][u(FUN`COLOR,%1,BORDER,')]

+help/add +sheet=[u(cobj,sheet)]/HLP`+SHEET
+help/category +sheet=Roleplaying
&HLP`+SHEET [u(cobj,sheet)]=[ansi(hc,Commands)]%R[align(5 [sub(u(width`%va,%#),6)],,[ansi(h,+sheet)] - Shows your sheet.%R[ansi(h,+sheet <player>)] - View another's +sheet. They must have allowed it or you must be staff.%R%RYou can allow others to view your sheet via [ansi(h,+Config storyteller/sheet_public=1)] \(set to 0 to disable\))]
