@@ DEPENDENCIES - CORE

@switch/inline isdbref(u(ex-cso))=0,{@tel create(Calendar System Object <EX-CSO>)=config(master_room)}
&ex-cso u(coi)=locate(config(master_room),Calendar System Object <EX-CSO>,TXxi)
@parent u(ex-cso)=u(coi)
@set u(ex-cso)=WIZARD !NO_COMMAND

&CMD`+TIME u(ex-cso)=$^(?s)\+time(?\:/(\S+))?(?\: +(.+?))?(?\:=(.+))?$:@include u(ccs)/INC`PARTIAL=%1,setunion(get(u(ex-cso)/VAR`PLAYFLAGS),if(isadmin(%#),get(u(ex-cso)/VAR`ADMINFLAGS)),|),|,TIME,switch,switch;@include u(ex-cso)/INC`[strfirstof(%q<switch>,MAIN)]=%2,%3
@set u(ex-cso)/CMD`+TIME=regexp

&FUN`GETSEASON u(ex-cso)=ucstr(if(lte(v(VAR`CURDAY),420),reswitchi(stripansi(elements(u(VAR`MONTHNAMES),ceil(fdiv(v(VAR`CURDAY),28)),|)),^(DESCENDING WATER)$,FRIGID,^(ASCENDING EARTH|RESPLENDENT EARTH)$,SPRING,^(DESCENDING EARTH|ASCENDING WOOD|RESPLENDENT WOOD)$,CROP,^(DESCENDING WOOD|ASCENDING FIRE|RESPLENDENT FIRE)$,SUMMER,^(DESCENDING FIRE)$,BLAZING,^(ASCENDING AIR|RESPLENDENT AIR)$,AUTUMN,^(DESCENDING AIR|ASCENDING WATER|RESPLENDENT WATER)$,WINTER),AUTUMN))

&FUN`SHOWDATE u(ex-cso)=It is [u(FUN`SHOWTIME)]\,%B[if(lte(v(VAR`CURDAY),420),[u(FUN`SHOWDAYNAME)] the [u(FUN`SHOWDAY)] of [u(FUN`SHOWMONTH)]\, [v(VAR`CURYEAR)] R.Y.,the [numth(sub(v(VAR`CURDAY),420))] Day of [ansi(hx,Calibration)] between [v(VAR`CURYEAR)] and [add(1,v(VAR`CURYEAR))] R.Y.)]

&FUN`SHOWDAY u(ex-cso)=if(lte(v(VAR`CURDAY),420),numth(add(mod(v(VAR`CURDAY),28),1)),numth(sub(v(VAR`CURDAY),420)))

&FUN`SHOWDAYNAME u(ex-cso)=s(elements(v(VAR`DAYNAMES),if(mod(v(VAR`CURDAY),7),mod(v(VAR`CURDAY),7),7),|))

&FUN`SHOWHOUR u(ex-cso)=rjust(div(v(VAR`CURSECS),3600),2,0):[rjust(div(mod(v(VAR`CURSECS),3600),60),2,0)]

&FUN`SHOWMONTH u(ex-cso)=if(lte(v(VAR`CURDAY),420),s(elements(v(VAR`MONTHNAMES),ceil(fdiv(v(VAR`CURDAY),28)),|)),ansi(hx,Calibration))

&FUN`SHOWMOON u(ex-cso)=if(lte(v(VAR`CURDAY),420),The Moon is currently in its [elements(u(VAR`MOONNAMES),if(mod(v(VAR`CURDAY),28),mod(v(VAR`CURDAY),28),28),|)] Phase,The Moon and Stars are [ansi(hx,GONE!)])

&FUN`SHOWTIME u(ex-cso)=rjust(div(v(VAR`CURSECS),3600),2,0):[rjust(div(mod(v(VAR`CURSECS),3600),60),2,0)]

&INC`MAIN u(ex-cso)=@nspemit %#=header(Current IC Time);@nspemit %#=u(u(ex-cso)/FUN`SHOWDATE);@nspemit %#=u(u(ex-cso)/FUN`SHOWMOON);@nspemit %#=header()

&TRG`ANNOUNCE u(ex-cso)=@switch/inline v(VAR`ANNOUNCE)=1,{@nscemit/noisy v(VAR`ANNOUNCECHANNEL)=u(u(ex-cso)/FUN`SHOWDATE) - [u(u(ex-cso)/FUN`SHOWMOON)]}

&TRG`TICKER u(ex-cso)=@switch/inline t(v(VAR`ENABLED))=1,{@switch/inline gt(setr(newsecs,add(v(VAR`CURSECS),setr(secsadd,round(mul(60,v(VAR`MULTIPLIER)),0)))),86400)=1,{@trigger u(ex-proj)/TRG`TICKDOWN=%q<secsadd>;&VAR`CURSECS u(ex-cso)=sub(%q<newsecs>,86400);@switch/inline gt(setr(newdays,add(v(VAR`CURDAY),1)),425)=1,{&VAR`CURDAY u(ex-cso)=sub(%q<newdays>,425);&VAR`CURYEAR u(ex-cso)=add(v(VAR`CURYEAR),1);@trigger u(ex-cso)/TRG`ANNOUNCE},{&VAR`CURDAY u(ex-cso)=%q<newdays>;@trigger me/TRG`ANNOUNCE}},{@trigger u(ex-proj)/TRG`TICKDOWN=%q<secsadd>;&VAR`CURSECS u(ex-cso)=%q<newsecs>}};@wait 60={@trigger u(ex-cso)/TRG`TICKER}

&VAR`ADMINFLAGS u(ex-cso)=STOP|START
&VAR`ANNOUNCE u(ex-cso)=1
&VAR`ANNOUNCECHANNEL u(ex-cso)=RP Alerts
&VAR`CURDAY u(ex-cso)=1
&VAR`CURSECS u(ex-cso)=1
&VAR`CURYEAR u(ex-cso)=775
&VAR`DAYNAMES u(ex-cso)=[ansi(hy,Sunday)]|[ansi(hw,Moonday)]|[ansi(hr,Marsday)]|[ansi(y,Mercuryday)]|[ansi(hg,Jupiterday)]|[ansi(hc,Venusday)]|[ansi(hm,Saturnday)]
&VAR`ENABLED u(ex-cso)=1
&VAR`MONTHNAMES u(ex-cso)=[ansi(hc,Ascending Air)]|[ansi(hc,Resplendent Air)]|[ansi(hc,Descending Air)]|[ansi(hb,Ascending Water)]|[ansi(hb,Resplendent Water)]|[ansi(hb,Descending Water)]|[ansi(y,Ascending Earth)]|[ansi(y,Resplendent Earth)]|[ansi(y,Descending Earth)]|[ansi(hg,Ascending Wood)]|[ansi(hg,Resplendent Wood)]|[ansi(hg,Descending Wood)]|[ansi(hr,Ascending Fire)]|[ansi(hr,Resplendent Fire)]|[ansi(hr,Descending Fire)]
&VAR`MOONNAMES u(ex-cso)=[ansi(hx,Waxing Cresc)][ansi(hw,ent)]|[ansi(hx,Waxing Cresc)][ansi(hw,ent)]|[ansi(hx,Waxing Cresc)][ansi(hw,ent)]|[ansi(hx,Waxing Cresc)][ansi(hw,ent)]|[ansi(hx,Waxing Cresc)][ansi(hw,ent)]|[ansi(hx,Waxing)]%B[ansi(hw,Half)]|[ansi(hx,Wax)][ansi(hw,ing Gibbous)]|[ansi(hx,Wax)][ansi(hw,ing Gibbous)]|[ansi(hx,Wax)][ansi(hw,ing Gibbous)]|[ansi(hx,Wax)][ansi(hw,ing Gibbous)]|[ansi(hx,Wax)][ansi(hw,ing Gibbous)]|[ansi(hw,Full)]|[ansi(hw,Full)]|[ansi(hw,Full)]|[ansi(hw,Waning Gibb)][ansi(hx,ous)]|[ansi(hw,Waning Gibb)][ansi(hx,ous)]|[ansi(hw,Waning Gibb)][ansi(hx,ous)]|[ansi(hw,Waning Gibb)][ansi(hx,ous)]|[ansi(hw,Waning Gibb)][ansi(hx,ous)]|[ansi(hw,Waning)]%B[ansi(hx,Half)]|[ansi(hw,Wan)][ansi(hx,ing Crescent)]|[ansi(hw,Wan)][ansi(hx,ing Crescent)]|[ansi(hw,Wan)][ansi(hx,ing Crescent)]|[ansi(hw,Wan)][ansi(hx,ing Crescent)]|[ansi(hw,Wan)][ansi(hx,ing Crescent)]|[ansi(hx,No Moon)]|[ansi(hx,No Moon)]|[ansi(hx,No Moon)]
&VAR`MULTIPLIER u(ex-cso)=3

&STARTUP u(ex-cso)=@trigger u(ex-cso)/TRG`TICKER

@@ Help
+help/addmain Roleplaying/+time=[u(ex-cso)]/HLP`+TIME
&HLP`+TIME u(ex-cso)=align(5 [sub(width(%#),6)],,{[ansi(h,+time)] - Shows the current IC time.})