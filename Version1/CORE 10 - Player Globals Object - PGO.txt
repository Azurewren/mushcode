@@ DEPENDENCIES - CORE

@switch/inline isdbref(u(pgo))=0,{@tel create(Player Globals Object <PGO>)=config(master_room)}
&pgo u(coi)=locate(config(master_room),Player Globals Object <PGO>,TXxi)
@parent u(pgo)=u(coi)
@set u(pgo)=WIZARD !NO_COMMAND

&RFN`MSGHEAD u(pgo)=msghead(%0)

&CMD`CHARSET u(pgo)=$+charset:@nspemit %#=wrap(iter(lnum(32,256),if(comp(first(chr(%i0)),#-1,A),ljust([ljust(%i0:,4)] [ansi(hy,chr(%i0))],7)), ,),78)

&CMD`+BEEP u(pgo)=$^(?\:\+)beep(?\: +(.*))?$:@include u(ccs)/INC`JAILCHECK;@include u(ccs)/INC`CHECKPC=%1,1,BEEP;@assert hasflag(%q<t1>,CONNECTED)=@nspemit %#=That player is offline.;@nspemit %q<t1>=beep()[ansi(hr,%n wishes for your attention! BEEP!)];@nspemit %#=You have sent %q<t1name> a beep!
@set u(pgo)/CMD`+BEEP=regexp

&CMD`+FLIP u(pgo)=$+flip:th setq(roll,rand(0,100));@switch/inline %q<roll>=50,{@include u(ccs)/INC`RMSG=FLIP,lplayers(%l),{[ansi(h,%n)] flipped a coin: [switch(rand(1,6),1,it landed on its side!,2,a Demon grabbed it!,3,it never comes back down...,4,when it lands\, both sides are blank.,5,Reality Error 404: Coin not found!,6,it's drawn into a parallel universe!)]}},>50,{@include u(ccs)/INC`RMSG=FLIP,lplayers(%l),{[ansi(h,%n)] flipped a coin: Heads!}},{@include u(ccs)/INC`RMSG=FLIP,lplayers(%l),{[ansi(h,%n)] flipped a coin: Tails!}}

&CMD`+TZ u(pgo)=$+tz *:@assert setr(accid,accid(%#))=@nspemit %#=u(RFN`MSGHEAD,TZ) ERROR: You have no account set.;@assert strlen(%0)=@nspemit %#=u(RFN`MSGHEAD,TZ) ERROR: No Timezone field entered!;@break regmatchi(%0,^#\\d+)=@nspemit %#=u(RFN`MSGHEAD,TZ) ERROR: None of that chicanery, please.;@break strmatch(time(%0),#-*)=@nspemit %#=u(RFN`MSGHEAD,TZ) ERROR: Entered time zone was not recognized. Timezone entries are case sensitive and can be found in help timezones.;@nspemit %#=u(RFN`MSGHEAD,TZ) Your account Timezone was set to: %0;&%q<accid>`TZ u(adb)=%0

&CMD`+OOC u(pgo)=$^(?s)(\+ooc|osay|\+osay)(?\: +(.*))?$:@assert elock(%l/speech,%#)=@nspemit %#=Permission denied.;@assert strlen(%2)=@nspemit %#=What will you say?;@break strmatch(left(%2,1),|)=@nspemit %#=msghead(CHAT) Emit feature is disabled.;@message lplayers(%l)=%2,u(pgo)/FUN`MSGOOC,%#,##,%2
@set u(pgo)/CMD`+OOC=regexp

&CMD`QUIT u(pgo)=$^(quit|logoff|logout)$:@boot/silent %#
@set u(pgo)/CMD`QUIT=regexp

&FUN`MSGOOC u(pgo)=ansi(custcolor(%1,OOC`BORDER),-<)[ansi(custcolor(%1,OOC`HEADER),OOC)][ansi(custcolor(%1,OOC`BORDER),>-)]%B[if(isadmin(%0),ansi(h,\[Staff\]%B))][speak(&[ansi(custcolor(%1,OOC`NAME),name(%0))],%2,ansi(custcolor(%1,OOC`SAYSTRING),says\,),#lambda/\[ansi(custcolor(%1,OOC`QUOTES),")\]\[ansi(custcolor(%1,OOC`SPEECH),\%0)\]\[ansi(custcolor(%1,OOC`QUOTES),")\])]

th attrib_set(u(cco-db)/VAR`CATEGORIES,setunion(get(u(cco-db)/VAR`CATEGORIES),OOC,|,|))
&VAR`CATEGORIES`OOC u(cco-db)=SPEECH|NAME|HEADER|BORDER|QUOTES|SAYSTRING

&COLOR`OOC`BORDER u(pco)=hx
&COLOR`OOC`HEADER u(pco)=hr