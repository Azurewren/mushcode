@switch/inline isdbref(u(rom))=0,{@tel create(Room Objects Manager <ROM>)=config(master_room)}
&rom u(coi)=locate(config(master_room),Room Objects Manager <ROM>,TXxi)
@parent u(rom)=u(coi)
@set u(rom)=WIZARD !NO_COMMAND

@switch/inline isdbref(u(romp))=0,{@switch/inline isdbref(u(rdso))=1,{@tel create(Room Objects Manager Parent <ROMP>)=u(rom)}}
&romp u(coi)=locate(u(rom),Room Objects Manager Parent <ROMP>,TXxi)
@parent u(romp)=u(coi)
@set u(romp)=visual
@lock/parent u(romp)=true
@describe u(romp)=This Room Object has no been @desc'd yet!
&RFN`HEADER u(romp)=header(%0,%1,ROOM`BORDER,ROOM`BORDERDOT,ROOM`BORDERTEXT)
&RFN`SEPARATOR u(romp)=separator(%0,,ROOM`BORDER,ROOM`BORDERDOT,ROOM`BORDERTEXT)
&NAMEFORMAT u(romp)=endtag(a)[u(RFN`HEADER,pueblize(moniker(%0)[if(cor(controls(%#,me),isadmin(%#)),%([num(%0)][flags(%0)]%))],look),1)]
&CONFORMAT u(romp)=[u(FUN`CONFORMAT`EXTRA)][trim(if(words(lvplayers(me)),u(FUN`CONFORMAT`PLAYERS`[default(me/VAR`MODE,DEFAULT)]))[if(words(lvthings(me)),u(FUN`CONFORMAT`THINGS`[default(me/VAR`MODE,DEFAULT)]))],%r)]
&FUN`CONFORMAT`PLAYERS`DEFAULT u(romp)=u(RFN`SEPARATOR,Players)%R[align(19,Name)]%R[trim(iter(sort(lvplayers(me),namei),align(19,pueblize(ansi(custcolor(%#,ROOM`PLAYERNAME),name(%i0)),look [name(%i0)])),%b,%R),%r)]
&FUN`CONFORMAT`THINGS`DEFAULT u(romp)=u(RFN`SEPARATOR,Things)%R[align(35 35,Name,Owner)]%R[trim(iter(sort(lvthings(me),namei),align(35 35,pueblize(name(%i0),look [name(%i0)]),pueblize(name(owner(%i0)),+finger [name(owner(%i0))])),%b,%R),%r)]
&FUN`CONFORMAT`EXTRA u(romp)=if(cor(nattr(me/VIEW`*),nattr(me/MUSIC`*)),separator(Extra)%R[iter(filterbool(#lambda/nattr(me/\%0`*),VIEW MUSIC),%T[u(FUN`EXTRA`%i0)],%b,%R)]%R)

&CMD`+ROOMSETUP u(rom)=$+roomsetup:@assert hastype(%l,THING)=@nspemit %#=u(RFN`MSGHEAD) ERROR: You must be in a THING to use +roomsetup.;@assert cor(controls(%l,%#),isadmin(%#))=@nspemit %#=u(RFN`MSGHEAD) ERROR: Permission denied. This room object is not yours.;@trigger u(rom)/TRG`SETUPROOM=%l;@nspemit %#=u(RFN`MSGHEAD) Setup complete!

&CMD`+MAKEROOM u(rom)=$+makeroom *:@assert valid(name,%0)=@nspemit %#=u(RFN`MSGHEAD) ERROR: That's not a very good name for a room object. Try something simpler or shorter.;@break locate(%#,%0,TXx)=@nspemit %#=u(RFN`MSGHEAD) ERROR: An object of that name already exists. Enter it and use +roomsetup instead.;@assert lte(nsearch(%#),quota(%#))=@nspemit %#=u(RFN`MSGHEAD) ERROR: Insufficient quota. Destroy some of your existing objects first!;@chown/preserve setr(obj,create(%0))=%#;@tel %q<obj>=%#;@trigger u(rom)/TRG`SETUPROOM=%q<obj>;@nspemit %#=u(RFN`MSHEAD) Setup complete!;

&TRG`SETUPROOM u(rom)=@assert hastype(%0,ROOM);@lock/basic %0==[owner(%0)];&IC %0=1;@set %0=enter_ok;@parent %0=u(romp)

