@@ DEPENDENCIES - CORE

th u(NEWCOBJ,Game Configuration SYSTEM <GCS>,gcs,,,,WIZARD SAFE !NO_COMMAND,INHERIT SIDEFX SAFE)
th u(NEWCOBJ,Game Configuration Data Storage <CONFIG>,config,u(cobj,gcs),,1,WIZARD SAFE,INHERIT SIDEFX SAFE)

@@ %0 - @name. %1 - alias. %2 - location to store. %3 - parent, defaults to MCO. %4 - No parent. %5 - PennMUSH flags. %6 - RhostMUSH flags.

@lock/use [u(cobj,gcs)]=ISADMIN2/1

&CMD`+GAMECONFIG`PENNMUSH [u(cobj,gcs)]=$^\+gameconfig(?\:/(\S+)?)?(?\: +(.+?))?(?\:/(.+?))?(?\:=(.*))?$:@attach %!/CMD`+GAMECONFIG`MAIN
@set [u(cobj,gcs)]/CMD`+GAMECONFIG`PENNMUSH=regexp
&CMD`+GAMECONFIG`RHOSTMUSH [u(cobj,gcs)]=$^\+gameconfig(?\:/(\\S+)?)?(?\: +(.+?))?(?\:/(.+?))?(?\:=(.*))?$:@attach %!/CMD`+GAMECONFIG`MAIN
@set [u(cobj,gcs)]/CMD`+GAMECONFIG`RHOSTMUSH=regexp
&CMD`+GAMECONFIG`MAIN [u(cobj,gcs)]=@attach %!/INC`GETSWITCH=%1;@attach %!/INC`[u(strfirstof`%va,%q<switch>,MAIN)]=%2,%3,%4
@set [u(cobj,gcs)]/CMD`+GAMECONFIG`[switch(%va,PennMUSH,RHOSTMUSH,RhostMUSH,PENNMUSH)]=no_command

&SWITCHES`PLAYER [u(cobj,gcs)]=
&SWITCHES`ADMIN [u(cobj,gcs)]=GET
&SYSTEM`NAME [u(cobj,gcs)]=GAMECONFIG

&INC`MAIN [u(cobj,gcs)]=@switch/inline strlen(%0)=0,{@attach %!/INC`LIST},{@attach %!/INC`SET}

&INC`LIST [u(cobj,gcs)]=@pemit %#=u(header,mudname() Configuration);@dolist/inline/delimit | [u(FUN`CATEGORIES)]={th u(setq`%va,category,##);@pemit %#=u(subheader,u(capnames,%q<category>));@pemit %#=ansi(u(color`%va,%#,u(SYSTEM`NAME),COLUMN_NAMES),align(14 19 43,Setting,Value,Type and Description));@pemit %#=iter(u(FUN`SETTINGS,%q<category>),align(14 19 43,%i0,if(gt(strlen(u(setr`%va,found,u(game_config,%q<category>,%i0))),20),left(%q<found>,17)... %(use /get%),%q<found>)[switch(u(setr`%va,type,get(u(cobj,config)/TYPE`%q<category>`%i0)),BOOL,%B[if(t(%q<found>),%(True%),%(False%))],DURATION,%B%([u(smalltime`%va,%q<found>)]%))],<%q<type>> [default(u(cobj,config)/DESC`%q<category>`%i0,Unset)]),|,%R)};@pemit %#=u(subheader)

&FUN`CATEGORIES [u(cobj,gcs)]=sort(get(u(cobj,config)/CATEGORIES),|,|)

&FUN`SETTINGS [u(cobj,gcs)]=sort(get(u(cobj,config)/CATEGORIES`%0),|,|)

&INC`GET [u(cobj,gcs)]=@check strlen(%0)=@attach %!/INC`MSG={ERROR: Category field empty.};@attach %!/INC`PARTIAL=%0,u(FUN`CATEGORIES),|,category,category;@check strlen(%1)=@attach %!/INC`MSG={ERROR: Option field empty.};@attach %!/INC`PARTIAL=%1,u(FUN`SETTINGS,%q<category>),|,option,option;@pemit %#=u(decompose`%va,u(game_config,%q<category>,%q<option>))

&INC`SET [u(cobj,gcs)]=@check u(iswizard`%va,%#)=@attach %!/INC`MSG=ERROR: Permission denied. Only wizards can set options.;@check strlen(%0)=@attach %!/INC`MSG={ERROR: Category field empty.};@attach %!/INC`PARTIAL=%0,u(FUN`CATEGORIES),|,category,category;@check strlen(%1)=@attach %!/INC`MSG={ERROR: Option field empty.};@attach %!/INC`PARTIAL=%1,u(FUN`SETTINGS,%q<category>),|,option,option;@switch/inline strlen(%2)=0,{@attach %!/INC`MSG=Cleared/reset %q<category> %q<option>!;@attach %!/WIPE=u(cobj,config),CONFIG`%q<category>`%q<option>;@attach %!/INC`MSG`CHAN={Cleared/Reset %q<category> %q<option>}},{@attach %!/INC`VALID`[default(u(cobj,config)/TYPE`%q<category>`%q<option>,WORD)]=%2;th u(attrib_set`%va,u(cobj,config),CONFIG`%q<category>`%q<option>,%q<value>);@attach %!/INC`MSG=u(setr`%va,msg,Game Option %q<category> %q<option> Set to: %q<value>);@attach %!/INC`MSG`CHAN={%q<msg>}}



@@ STARTUP EXTRA is used by individual games.
&PLAYER`CREATE [u(cobj,gcs)]=@dolist/inline u(lattr`%va,%!/PLAYER`CREATE`*)={@trigger %!/##=%0,%1,%2,%3,%4,%5,%6,%7,%8,%9}
&PLAYER`CREATE`CHANNELS [u(cobj,gcs)]=@select/inline 1=u(isguest,%0),{@@ @dolist/inline/delimit | [u(game_config,CHANNELS,JOIN_GUESTS)]=@select/inline %va=PennMUSH,{@chan/on %i0=%0},RhostMUSH,{}},{@dolist/inline/delimit | [u(game_config,CHANNELS,JOIN_PLAYERS)]=@select/inline %va=PennMUSH,{@chan/on %i0=%0},RhostMUSH,{}}

@@ CONFIG DEFAULTS - Don't change these! +config can change them.

th u(NEWCONF,config,SYSTEM,SQL_PREFIX,mushcode_,Used by SQL.,WORD)
th u(NEWCONF,config,SYSTEM,PUBLIC_EMAIL,example@example.com,Used in system messages.,WORD)
th u(NEWCONF,config,SYSTEM,ANONYMOUS_NOTICES,0,Hide admin names in sys-messages?,BOOL)
th u(NEWCONF,config,SYSTEM,HEADER_FILL,=,Border characters.,WORD)
th u(NEWCONF,config,SYSTEM,SUBHEADER_FILL,=,Border characters.,WORD)
th u(NEWCONF,config,SYSTEM,SEPARATOR_FILL,-,Border characters.,WORD)
th u(NEWCONF,config,SYSTEM,FOOTER_FILL,=,Border characters.,WORD)
th u(NEWCONF,config,SYSTEM,REQUIRE_APPROVAL,0,Game requires chargen approval?,BOOL)
th u(NEWCONF,config,SYSTEM,TIMEZONE,UTC,Default Timezone?,TZ)
th u(NEWCONF,config,SYSTEM,NAMELINK,+finger,|-delimited Commands for names?,LIST)
th u(NEWCONF,config,SYSTEM,BORDER_MODE,0,0 = No edges. 1 = + edges. 2 = slash-edges,WORD)

th u(NEWCONF,config,CHANNELS,JOIN_PLAYERS,,Default Channels for Players.,LIST)
th u(NEWCONF,config,CHANNELS,JOIN_ADMIN,,Default Channels for new Admin.,LIST)
th u(NEWCONF,config,CHANNELS,STAFF_ALERTS,,Default Channels for Staff Alerts.,LIST)
th u(NEWCONF,config,COLOR,COLUMN_NAMES,g,For column names.,COLOR)
th u(NEWCONF,config,COLOR,HEADER_TEXT,hw,For header names.,COLOR)
th u(NEWCONF,config,COLOR,HEADER_STAR,hm,For header stars.,COLOR)
th u(NEWCONF,config,COLOR,BORDER,m,For borders.,COLOR)
th u(NEWCONF,config,COLOR,MSG,hm,For system messages borders.,COLOR)
th u(NEWCONF,config,COLOR,MSGTEXT,hw,For system message headers.,COLOR)

th u(NEWCONF,config,SYSTEM,WIZTAG,hr,For showing WIZ in systems,COLOR)
th u(NEWCONF,config,SYSTEM,ROYTAG,hb,For showing ROY in systems,COLOR)
