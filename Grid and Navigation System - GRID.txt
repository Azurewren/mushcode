@switch/first/inline isdbref(u(grid))=0,{@tel create(Grid Management System <GRID>)=config(master_room)}
&grid u(coi)=locate(config(master_room),Grid Management System <GRID>,TXxi)
@parent u(grid)=u(coi)
@set u(grid)=WIZARD SAFE !NO_COMMAND

&RFN`HEADER u(grid)=header(%0,,GRID`BORDER,GRID`BORDERDOT,GRID`BORDERTEXT)
&RFN`SUBHEADER u(grid)=subheader(%0,,GRID`BORDER,GRID`BORDERDOT,GRID`BORDERTEXT)
&RFN`SEPARATOR u(grid)=separator(%0,,GRID`BORDER,GRID`BORDERDOT,GRID`BORDERTEXT)
&RFN`MSGHEAD u(grid)=msghead(v(VAR`MSGHEAD))
&VAR`MSGHEAD u(grid)=GRID

@switch/first/inline isdbref(u(district))=0,{@tel create(District Parent <DISTRICT>)=u(grid)}
&district u(coi)=locate(u(grid),District Parent <DISTRICT>,TXxi)
@parent u(district)=u(ar)

@lock/enter u(district)=FUN`ENTER/1
@lset u(district)/enter=!no_inherit
&FUN`ENTER u(district)=cand(approved(%#),not(hasflag(%#,JAILED)))

@lock/teleport u(district)=FUN`TELEPORT/1
@lset u(district)/teleport=!no_inherit
&FUN`TELEPORT u(district)=cand(approved(%#),not(hasflag(%#,JAILED)))

&CMD`+GRID u(grid)=$^(?s)\+(district|dig|open|convert)(?\:/(\S+)?)?(?\: +(.+?))?(?\:=(.*))?$:@attach u(ccs)/INC`PARTIAL=%2,setunion(get(u(grid)/VAR`%1`PLAYFLAGS),if(isadmin(%#),get(u(grid)/VAR`%1`ADMINFLAGS)),|,|),|,v(VAR`MSGHEAD),switch,switch;@attach u(grid)/INC`%1`[strfirstof(%q<switch>,MAIN)]=%3,%4
@set u(grid)/CMD`+GRID=regexp

&VAR`DISTRICT`PLAYFLAGS u(grid)=LOCK|UNLOCK|DESCRIBE
&VAR`DISTRICT`ADMINFLAGS u(grid)=CREATE|RENAME|ORDER

&VAR`DIG`PLAYFLAGS u(grid)=TELEPORT

&FUN`LISTDISTRICTS u(grid)=sortkey(#lambda/get(\%0/ORDER),sort(children(u(district)),namei))

&INC`DISTRICT`MAIN u(grid)=@switch/first/inline strlen(%0)=>0,{@attach u(grid)/INC`DISTRICT`CHOOSE},{@attach u(grid)/INC`DISTRICT`LIST}

&INC`DISTRICT`LIST u(grid)=@pemit %#=u(RFN`HEADER,mudname() District Parents);@pemit %#=align(5 20 9 41,DBREF,Name,Rooms,Description);@pemit %#=u(RFN`SEPARATOR);@dolist/inline u(FUN`LISTDISTRICTS)={@pemit %#=align(5 20 9 41,pueblize(ansi(if(controls(%#,%i0),hg,hr),%i0),+district %i0),pueblize(name(%i0),+district %i0),nchildren(%i0),default(%i0/DESCRIBE,???))};@pemit %#=u(RFN`SUBHEADER)

&INC`DISTRICT`CHOOSE u(grid)=@attach u(grid)/INC`FINDDIST;@assert/inline controls(%#,%q<dist>)=@pemit %#=u(RFN`MSGHEAD) ERROR: Permission denied.;@pemit %#=u(RFN`MSGHEAD) Now managing district %q<distname>.;&D`DISTRICT %#=%q<dist>

&INC`DISTRICT`CREATE u(grid)=@assert/inline strlen(%0)=@pemit %#=u(RFN`MSGHEAD) ERROR: District name empty.;@assert/inline valid(name,%0)=@pemit %#=u(RFN`MSGHEAD) ERROR: '%0' is not a valid object name.;@break/inline locate(u(district),%0,TXxi)=@pemit %#=u(RFn`MSGHEAD) ERROR: A District already uses that name.;@assert/inline u(setr,dist,create(%0))=@pemit %#=u(RFN`MSGHEAD) ERROR: Could not create district.;@tel %q<dist>=u(district);@parent %q<dist>=u(district);@pemit %#=u(RFN`MSGHEAD) District Created! You have been set to managing it.;&D`DISTRICT %#=%q<dist>;@attach u(ccs)/INC`CMSG={Created District %q<dist> '[name(%q<dist>)]'}

&INC`DISTRICT`RENAME u(grid)=@attach u(grid)/INC`FINDDIST;@assert/inline strlen(%1)=@pemit %#=u(RFN`MSGHEAD) ERROR: New name empty.;@assert/inline valid(name,%1)=@pemit %#=u(RFN`MSGHEAD) ERROR: '%1' is not a valid object name.;@break/inline cand(u(setr,found,locate(u(district),%1,TXxi)),not(strmatch(%q<found>,%q<dist>)))=@pemit %#=u(RFN`MSGHEAD) ERROR: A District already uses that name.;@pemit %#=u(RFN`MSGHEAD) District Renamed!;@attach u(ccs)/INC`CMSG={Renamed District %q<dist> '%q<distname>' to '%1'}

&INC`DISTRICT`DESCRIBE u(grid)=@attach u(grid)/INC`FINDDIST;@assert/inline controls(%#,%q<dist>)=@pemit %#=u(RFN`MSGHEAD) ERROR: Permission denied.;@assert/inline strlen(%1)=@pemit %#=u(RFN`MSGHEAD) ERROR: Nothing entered for the description!;@pemit %#=u(RFN`MSGHEAD) Description set.;@desc %q<dist>=%1;@attach u(ccs)/INC`CMSG={Changed Description for %q<dist> '%q<distname>'}

&INC`DISTRICT`ORDER u(grid)=@attach u(grid)/INC`FINDDIST;@assert/inline strlen(%1)=@pemit %#=u(RFN`MSGHEAD) ERROR: Order value empty.;@assert/inline valnum(%1)=@pemit %#=u(RFN`MSGHEAD) ERROR: Order values must be whole, positive numbers.;&ORDER %q<dist>=%1;@set %q<dist>/ORDER=no_inherit;@pemit %#=u(RFN`MSGHEAD) Order set.;@attach u(ccs)/INC`CMSG={Changed order for %q<dist> '%q<distname>'}

&INC`FINDDIST u(grid)=@assert/inline strlen(%0)=@pemit %#=u(RFN`MSGHEAD) ERROR: No District entered to check.;@assert/inline words(u(setr,dist,namegraball(u(FUN`LISTDISTRICTS),%0)))=@pemit %#=u(RFN`MSGHEAD) ERROR: District '%0' not found.;@break/inline gt(words(%q<dist>),1)=@pemit %#=u(RFN`MSGHEAD) ERROR: '%0' matched [itemize(iter(%q<dist>,pueblize(name(%i0),+district %i0),%b,|),|,and,\,)]. Please be more specific!;th u(setq,distname,name(%q<dist>))

&INC`DISTRICT`LOCK u(grid)=@attach u(grid)/INC`FINDDIST=before(%0,/);@assert/inline controls(%#,%q<dist>)=@pemit %#=u(RFN`MSGHEAD) ERROR: Permission denied.;@attach u(kls)/INC`DOLOCK=%q<dist>,after(%0,/),%1,v(VAR`MSGHEAD),1
&INC`DISTRICT`UNLOCK u(grid)=@pemit %#=u(RFN`MSGHEAD) To Unlock, use the @unlock command directly.

&INC`DISTCHECK u(grid)=@assert/inline strlen(u(setr,dist,get(%#/D`DISTRICT)))=@pemit %#=u(RFN`MSGHEAD) Please choose a District using +district <name>.;@assert/inline match(u(FUN`LISTDISTRICTS),%q<dist>)=@pemit %#=u(RFN`MSGHEAD) ERROR: District no longer valid. Please choose a District using +district <name>.;@assert/inline controls(%#,%q<dist>)=@pemit %#=u(RFN`MSGHEAD) ERROR: Permission denied.;th u(setq,distname,name(%q<dist>))

&INC`DIG`TELEPORT u(grid)=@attach u(grid)/INC`DIG`MAIN=%0,%1,1
&INC`DIG`MAIN u(grid)=@attach u(grid)/INC`DISTCHECK;@assert/inline hastype(%l,ROOM)=@pemit %#=u(RFN`MSGHEAD) ERROR: +dig can only be used from a proper room.;@assert/inline strlen(%0)=@pemit %#=u(RFN`MSGHEAD) ERROR: No room name entered.;@assert/inline valid(name,%0)=@pemit %#=u(RFN`MSGHEAD) ERROR: '%0' is not a good name for a Room.;@assert/inline u(setr,room,dig(%0))=@pemit %#=u(RFN`MSGHEAD) ERROR: Could not create room.;th u(setq,source,%l);@parent %q<room>=%q<dist>;@pemit %#=u(RFN`MSGHEAD) Room created successfully!;@switch/first/inline strlen(u(setr,exname,before(%1,\,)))=>0,{@switch/first/inline isdbref(u(setr,there,open(%q<exname>,%q<room>,%q<source>)))=1,{@pemit %#=u(RFN`MSGHEAD) Exit to new room created successfully!},0,{@pemit %#=u(RFN`MSGHEAD) ERROR: Exit to new room creation failed.}};@switch/first/inline strlen(u(setr,exname,after(%1,\,)))=>0,{@switch/first/inline isdbref(u(setr,back,open(%q<exname>,%q<source>,%q<room>)))=1,{@pemit %#=u(RFN`MSGHEAD) Exit from new room created successfully!},0,{@pemit %#=u(RFN`MSGHEAD) ERROR: Exit from new room creation failed.}};@switch/first/inline %2=1,{@teleport %#=%q<room>};@attach u(ccs)/INC`CMSG={Room %q<room> '%0' created for %q<dist> '%q<distname>'}

&INC`CONVERT`MAIN u(grid)=@attach u(grid)/INC`DISTCHECK;@assert/inline hastype(%l,ROOM)=@pemit %#=u(RFN`MSGHEAD) ERROR: +convert can only be used from a proper room.;@parent %l=%q<dist>;@assert/inline controls(%#,%l)=@pemit %#=u(RFN`MSGHEAD) ERROR: Permission denied.;@parent %l=%q<dist>;@pemit %#=u(RFN`MSGHEAD) Room converted to %q<dist> '%q<distname>'.;@attach u(ccs)/INC`CMSG={Room %l '[name(%l)]' converted to %q<dist> '%q<distname>'}


&PLAYER`DISCONNECT`TELGUEST [u(cobj,gcs)]=@break/inline %1;@select/inline u(isguest,%0)=1,{@tel/silent %0=u(game_config,NAVIGATION,GUEST_START,#0)}

&DEFAULT`NAVIGATION`GUEST_START [u(cobj,config)]=#-1
&DESC`NAVIGATION`GUEST_START [u(cobj,config)]=Room guests return to after logoff.
&TYPE`NAVIGATION`GUEST_START [u(cobj,config)]=DBREF

&DEFAULT`NAVIGATION`IC_NEXUS [u(cobj,config)]=#-1
&DESC`NAVIGATION`IC_NEXUS [u(cobj,config)]=Room +nexus leads to or default +ic
&TYPE`NAVIGATION`IC_NEXUS [u(cobj,config)]=DBREF

&DEFAULT`NAVIGATION`OOC_NEXUS [u(cobj,config)]=#-1
&DESC`NAVIGATION`OOC_NEXUS [u(cobj,config)]=Room +ooc leads to.
&TYPE`NAVIGATION`OOC_NEXUS [u(cobj,config)]=DBREF




&CMD`+IC u(navi)=$ic:@break isic(%#)=@nspemit %#=u(RFN`MSGHEAD) You're already IC.;@assert approved(%#)=@nspemit %#=u(RFN`MSGHEAD) You are not approved!;@switch/inline cand(isobjid(setr(dest,get(%#/D`ICLOC))),isic(%q<dest>))=0,{@nspemit %#=u(RFN`MSGHEAD) Your last IC location is no longer valid. Choose a new one from the exits in the starting nexus.;th setq(dest,v(LOC`ICNEXUS))};@tel[if(hasflag(%#,DARK),/silent)] %#=%q<dest>;@switch/inline hasflag(%#,DARK)=0,{@nsremit %q<dest>=%N arrives from the OOC Nexus.}

&CMD`+OOC u(navi)=$ooc:@assert elock(%l/leave,%#)=@nspemit %#=Permission denied. You do not pass the leave lock.;th setq(dest,v(LOC`OOCNEXUS));@switch/inline isic(%#)=0,{@break strmatch(%l,%q<dest>)=@nspemit %#=You are already in the OOC Nexus.;@nspemit %#=You are already OOC. Returning you to the OOC Nexus.};@switch/inline isic(%#)=1,{&D`ICLOC %#=objid(%l)};@tel[if(hasflag(%#,DARK),/silent)] %#=%q<dest>

&CMD`+NEXUS u(navi)=$+nexus:@assert approved(%#)=@nspemit %#=u(RFN`MSGHEAD) You are not approved!;@break strmatch(%l,setr(m,v(LOC`ICNEXUS)))=@pemit %#=You are already there.;@assert elock(%l/leave,%#)=@pemit %#=Permission denied. You do not pass the leave lock.;@assert elock(%qm/teleport,%#)=@pemit %#=Permission denied. You do not pass the teleport lock.;@tel[if(hasflag(%#,DARK),/silent)] %#=%qm

&CMD`+GOHOME u(navi)=$+gohome:@force/inline %#=home
&HOOK`IGNORE`HOME u(navi)=if(elock(u(navi)/Command,%#),1,0[nspemit(%#,Permission denied.)])

&CMD`+SETHOME u(navi)=$+sethome:@assert elock(%l/link,%#)=@pemit %#=You do not pass the link lock for this room.;@link %#=%l;@nspemit %#=Home set to your current location.

&FUN`TELDISTS u(navi)=filterbool(#lambda/elock(\%0/TELEPORT,strfirstof(%1,%#)),u(u(grid)/FUN`LISTDISTRICTS))
&FUN`FINDROOM u(navi)=namegraball(u(FUN`LISTROOMS),%0)
&FUN`LISTROOMS u(navi)=squish(iter(u(FUN`TELDISTS,,%1),u(FUN`LISTDISTROOMS,%i0))%B[u(FUN`LISTTHINGS)])
&FUN`LISTDISTROOMS u(navi)=sort(children(%0),namei)
&FUN`LISTTHINGS u(navi)=filterbool(#lambda/cor(elock(\%0/TELEPORT,strfirstof(%1,%#)),isadmin(strfirstof(%1,%#))),sort(lsearch(all,type,thing,elock,PORT:>0),namei))

&CMD`+PORT u(navi)=$+port *:@assert words(setr(dest,u(FUN`FINDROOM,%0)))=@nspemit %#=u(RFN`MSGHEAD) Nothing matches '%0'.;@break gt(words(%q<dest>),1)=@nspemit %#=u(RFN`MSGHEAD) Multiple Matches:%R[iter(%q<dest>,pueblize(ansi(hx,%i0) - [ansi(c,name(%i0))],+port %i0),%b,%r)];@break strmatch(%l,%q<dest>)=@nspemit %#=u(RFN`MSGHEAD) You are already there.;@assert elock(%l/leave,%#)=@pemit %#=Permission denied. You do not pass the leave lock.; @assert elock(%q<dest>/teleport,%#)=@pemit %#=Permission denied. You do not pass the teleport lock.;@break cor(hasflag(%#,dark),hidden(%#))=@tel/silent %#=%q<dest>; @tel %#=%q<dest>

&CMD`+ROOMLIST u(navi)=$+roomlist:@nspemit %#=header(mudname() Grid Rooms);@dolist/inline u(FUN`TELDISTS)={@nspemit %#=u(RFN`SEPARATOR,name(%i0));th step(FUN`ROOMLIST,u(FUN`LISTDISTROOMS,%i0),30)};@switch/inline words(setr(things,u(FUN`LISTTHINGS)))=>0,{@nspemit %#=u(RFN`SEPARATOR,Player Things);th step(FUN`ROOMLIST,%q<things>,30)};@nspemit %#=subheader()

&CMD`+ROOMLIST`ARG u(navi)=$+roomlist *:think setq(l,u(FUN`FINDROOM)); @assert strlen(setr(m,strfirstof(grab(%ql,%0),namegraball(%ql,%0))))=@pemit %#=Nothing matches '%0'.;@nspemit %#=header('%0');th step(FUN`ROOMLIST,%qm,30);@nspemit %#=subheader()

&FUN`ROOMLIST u(navi)=nspemit(%#,table(iter(lnum(0,sub(%+,1)),ljust(ansi(c,pueblize(v(%i0),+port [v(%i0)])),6) [ansi(hx,pueblize(name(v(%i0)),+port [v(%i0)]))],%b,|),38,width(%#),|))

&STARTUP u(navi)=@hook/ignore home=u(navi),HOOK`IGNORE`HOME;@trigger me/TRG`SELFCLEAN
&TRG`SELFCLEAN u(navi)=@dolist/inline filterbool(FILTER_TIMEOUT,reglattr(me/^#\\d+`#\\d+$))={@wipe me/%i0};@wait 600=@trigger me/TRG`SELFCLEAN

&CMD`MEETME u(navi)=$^(?s)(?\:\+)?(meet|meetme|invite|join|mjoin|summon|msummon)(?\:/(\S+)?)?(?\: +(.+?))?(?\:=(.*))?$:@include u(navi)/INC`%1=%3,%4
@set u(navi)/CMD`MEETME=regexp

&INC`INVITE u(navi)=@include u(navi)/INC`MEETME
&INC`MEET u(navi)=@include u(navi)/INC`MEETME
&INC`MEETME u(navi)=@include u(ccs)/INC`CHECKPC=%0,1,MEET;@break strmatch(%#,%q<t1>)=@nspemit %#=u(RFN`MSGHEAD) ERROR: You can't Meet yourself.;@assert hasflag(%q<t1>,CONNECTED)=@nspemit %#=u(RFN`MSGHEAD) ERROR: %q<t1name> is not online.;@break haspower(%q<t1>,GUEST)=@nspemit %#=u(RFN`MSGHEAD) ERROR: Guests cannot be invited.;@break strmatch(%l,loc(%q<t1>))=@nspemit %#=u(RFN`MSGHEAD) ERROR: You are already there!;@break lte(sub(secs(),get(u(navi)/%#`%q<t1>)),600)=@nspemit %#=u(RFN`MSGHEAD) An invitation to %q<t1name> is already pending.;@nspemit %q<t1>=u(RFN`HEADER,Meetme,%q<t1>);@nspemit %q<t1>=%n sent you a Meetme request. You have about 10 minutes to respond.%r%rTo join %n, use '[pueblize(ansi(h,join %n),join %n)]'%rTo bring %n to you, use '[pueblize(ansi(h,summon %n),summon %n)]'%rTo decline the request you needn't do a thing.;@nspemit %q<t1>=u(RFN`HEADER,,%q<t1>);@nspemit %#=u(RFN`MSGHEAD) You have sent %q<t1name> a Meetme request.;&%#`%q<t1> me=secs()

&INC`JOIN u(navi)=@include u(navi)/INC`MJOIN
&INC`MJOIN u(navi)=@assert approved(%#)=@nspemit %#=Permission denied.;@include u(ccs)/INC`CHECKPC=%0,1,MEET;@break strmatch(%#,%q<t1>)=@nspemit %#=u(RFN`MSGHEAD) You can't Meet yourself.;@assert hasflag(%q<t1>,CONNECTED)=@nspemit %#=u(RFN`MSGHEAD) %q<t1name> is not online.;@assert cor(hasattr(u(navi)/%q<t1>`%#),isadmin(%#),elock(loc(%q<t1>)/teleport,%#))=@pemit %#=u(RFN`MSGHEAD) %q<t1name> has not sent you a meetme request.;@break strmatch(loc(%q<t1>),%l)=@nspemit %#=u(RFN`MSGHEAD) You are both here!;@wipe me/%q<t1>`%#;@switch/inline hasflag(%#,DARK)=0,{@include u(ccs)/INC`RMSG=v(VAR`MSGHEAD),lplayers(%l),{[ansi(h,%n)] has joined %q<t1name>!}};@switch/inline isic(%#)=1,{&D`RETURNTO %#=%l};@switch/inline hasflag(%#,DARK)=0,{@tel %#=loc(%q<t1>)},1,{@tel/silent %#=loc(%q<t1>)};@nspemit %#=u(RFN`MSGHEAD) You join %q<t1name>

&INC`SUMMON u(navi)=@include u(navi)/INC`MSUMMON
&INC`MSUMMON u(navi)=@include u(ccs)/INC`CHECKPC=%0,1,MEET;@break strmatch(%#,%q<t1>)=@nspemit %#=u(RFN`MSGHEAD) You can't Meet yourself.;@assert hasflag(%q<t1>,CONNECTED)=@nspemit %#=u(RFN`MSGHEAD) %q<t1name> is not online.;@assert cor(hasattr(u(navi)/%q<t1>`%#),isadmin(%#))=@nspemit %#=u(RFN`MSGHEAD) %q<t1name> has not sent you a meetme request.;@break strmatch(loc(%q<t1>),%l)=@nspemit %#=u(RFN`MSGHEAD) You are both here!;@wipe me/%q<t1>`%#;@include u(ccs)/INC`RMSG=v(VAR`MSGHEAD),lvplayers(%l),{[ansi(h,%q<t1name>)] is drawn away by %n's Summons};@switch/inline isic(%q<t1>)=1,{&D`RETURNTO %q<t1>=loc(%q<t1>)};@tel %q<t1>=%l;@nspemit %#=u(RFN`MSGHEAD) You summon %q<t1name>.

&INC`RETURN u(navi)=$+return:@assert isdbref(setr(l,get(%#/D`RETURNTO)))=@pemit %#=ansi(r,You have nowhere to return to.); @break cand(not(isadmin(%#)),cor(hasflag(%ql,no_tel),hasflag(%l,no_tel)))=@pemit %#=ansi(r,You can not do that.); @assert cor(isic(%ql),isadmin(%#))=@pemit %#=ansi(r,You can not return to a non-IC location.);@wipe %#/D`RETURNTO;@tel[if(hasflag(%#,DARK),/silent)] %#=%ql;@pemit %#=ansi(h,You return to your previous location.)
&CMD`+RETURN`OTHER u(navi)=$+return *:@break haspower(%#,guest)=@pemit %#=ansi(r,You can not use this.); @assert isadmin(%#)=@pemit %#=ansi(r,You can not do that.); @break findplayer(%0); @assert isdbref(setr(l,get(%qp/D`RETURNTO)))=@pemit %#=ansi(r,ansi(nc,name(%qp)) has nowhere to return to.);@wipe %qp/D`RETURNTO;@tel %qp=%ql; @pemit %#=ansi(h,ansi(nc,name(%qp)) has been returned to [ansi(nc,name(%ql))].); @pemit %qp=ansi(h,You've been returned to your previous location[if(cor(isadmin(%qp),not(hasflag(%#,dark))),%bby [ansi(nc,%N)])].)

&FILTER_TIMEOUT u(navi)=gt(sub(secs(),v(%0)),600)

&OBJECT`DESTROY`PURGE u(navi)=@switch/inline %2=PLAYER,{@wipe u(navi)/[before(%0,:)]`*;@wipe u(navi)/*`[before(%0,:)]}


@@ NAVIGATION - MEET
+help/addmain Navigation/+meetme=[u(navi)]/HLP`MEETME
&HLP`MEETME u(navi)=The [ansi(h,+meetme)] system lets players warp across the game to directly join other players no matter where they are.%R[ansi(hc,Meetme Aliases:)] Meetme, +meetme, meet, +meet, invite, +invite%R[ansi(hc,Join aliases:)] Join, +join, mjoin, +mjoin%R[ansi(hc,Summon Aliases:)] Summon, +msummon, msummon%R%R[ansi(hc,Commands)]%R[align(5 [sub(width(%#),6)],,{[ansi(h,+meetme <player>)] - Extends an invitation for another player to +join or +summon you. Invites expire after 10 minutes.%R[ansi(h,+join <player>)] - Warp to a player.%R[ansi(h,+summon <player>)] - Bring <player> to you.%R%RStaff can skip the invite process and simply join or summon players. However they can still extend invites.})]