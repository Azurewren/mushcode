@@ DEPENDENCIES - Core

@switch/inline isdbref(u(wso))=0,{@tel create(Watch System Object <WSO>)=globalroom()}
&wso [u(coi)]=locate(globalroom(),Watch System Object <WSO>,Ti)
@wait 0=@parent u(wso)=[u(coi)]
@set u(wso)=WIZARD !NO_COMMAND

&CMD`+WATCH u(wso)=$^(?s)(?\:\+)?(?\:watch|wf|friend|friends)(?\:/(\\S+)?)?(?\: +(.+?))?(?\:=(.*))?$:@include u(ccs)/INC`PARTIAL=%1,setunion(get(u(wso)/VAR`PLAYFLAGS),if(isadmin(%#),get(u(wso)/VAR`ADMINFLAGS)),|,|),|,WATCH,switch,switch;@include u(wso)/INC`[strfirstof(%q<switch>,MAIN)]=%2,%3
@set u(wso)/CMD`+WATCH=regexp

&INC`MAIN u(wso)=@assert/inline words(petr(list,filter(u(wso)/FILTER_isdbref,get(%#/D`WATCH),%B,|)),|)=@pemit %#=ERROR: You have no friends on watch!;@pemit %#=header(Your Friends);th iter(lnum(1,ceil(fdiv(words(%q<list>,|),30))),nspemit(%#,iter(elements(%q<list>,lnum(add(1,mul(sub(inum(0),1),30)),mul(inum(0),30)),|,|),ljust(pueblize(name(%i0),finger [name(%i0)]),60)[if(gte(conn(%i0),0),ansi(hg,ago(idle(%i0))),ansi(hx,elements(get(%i0/LAST),2 3)))],|,%R)));@pemit %#=header();

&INC`ADD u(wso)=@assert/inline strlen(%0)=@pemit %#=ERROR: No player entered to add.;@assert/inline isdbref(petr(p,pmatch(%0)))=@pemit %#=ERROR: %0 does not match a player.;@break/inline match(get(%#/D`WATCH),objid(%qp))=@pemit %#=ERROR: They are already a friend.;&D`WATCH %#=filter(FILTER_isdbref,setunion(get(%#/D`WATCH),objid(%qp)));@pemit %#=name(%qp) added to your Friends list!

&INC`DEL u(wso)=@assert/inline strlen(%0)=@pemit %#=ERROR: No player entered to remove.;@assert/inline isdbref(petr(p,pmatch(%0)))=@pemit %#=ERROR: %0 does not match a player.;@assert/inline match(get(%#/D`WATCH),objid(%qp))=@pemit %#=ERROR: They are not a friend.;&D`WATCH %#=filter(FILTER_isdbref,setdiff(get(%#/D`WATCH),objid(%qp)));@pemit %#=name(%qp) removed from your Friends list!

&FILTER_isdbref u(wso)=isdbref(%0)

&VAR`PLAYFLAGS u(wso)=ADD|DEL

&PLAYER`CONNECT`WATCH u(wso)=@break/inline or(hasflag(%0,DARK),hidden(%0));@break/inline gt(%1,1);@pemit/list lsearch(all,eplayer,\[match(get(##/D`WATCH),[lit(%0)])\])=announce(FRIEND)%B[name(%0)] has connected.

&PLAYER`DISCONNECT`WATCH u(wso)=@break/inline or(hasflag(%0,DARK),hidden(%0));@break/inline gte(%1,1);@pemit/list lsearch(all,eplayer,\[match(get(##/D`WATCH),[lit(%0)])\])=announce(FRIEND)%B[name(%0)] has disconnected.

