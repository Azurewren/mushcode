@@ DEPENDENCIES - CORE

@switch/inline isdbref(u(pgo))=0,{@tel create(Player Globals Object <PGO>)=globalroom()}
&pgo [u(coi)]=locate(globalroom(),Player Globals Object <PGO>,Ti)
@wait 0=@parent [u(pgo)]=[u(coi)]
@set [u(pgo)]=INHERIT SIDEFX !NO_COMMAND

&CMD`CHARSET [u(pgo)]=$+charset:@pemit %#=wrap(iter(lnum(32,256),if(comp(first(chr(%i0)),#-1,A),ljust([ljust(%i0:,4)] [ansi(hy,chr(%i0))],7)), ,),78)

&CMD`+BEEP [u(pgo)]=$^(?\:\+)beep(?\: +(.*))?$:@include u(ccs)/INC`CHECKPC=%1,1,BEEP;@assert/inline hasflag(%q<t1>,CONNECTED)=@pemit %#=That player is offline.;@pemit %q<t1>=beep()[ansi(hr,%n wishes for your attention! BEEP!)];@pemit %#=You have sent %q<t1name> a beep!
@set [u(pgo)]/CMD`+BEEP=regexp

&CMD`+WHO [u(pgo)]=$^(?\:\+)?who$:@pemit %#=header(mudname() Connected Players)%R[align(30 -10 -3 -4 3 23,ansi(hc,Name),ansi(hc,Alias),ansi(hc,Sex),ansi(hc,Idle),ansi(hc,Con),ansi(hc,Location))]%R[subheader()];@dolist/inline petr(list,sort(setunion(lwho(%#),lwho(%#)),d))={@pemit %#=u(FUN`WHO,##,%#)};@switch/inline gt(petr(max,words(setunion(iter(%q<list>,get(%i0/LASTIP)),))),v(VAR`MAXPLAYERS))=1,{&VAR`MAXPLAYERS [u(pgo)]=%q<max>};@pemit %#=header(words(%q<list>) Characters from %q<max> IPs Connected - Record is [v(VAR`MAXPLAYERS)])
@set u(pgo)/CMD`+WHO=regexp

&FUN`WHO [u(pgo)]=align(30 -10 -3 -4 3 23,left(if(and(orflags(%0,HUD),isadmin(%1)),ansi(c,name(%0)),color(%1,name(%0),WHO`NAME,n)),28),color(%1,left(get(%0/ALIAS),10),WHO`ALIAS,n),color(%1,left(default(%0/SEX,?),1),WHO`SEX,n),color(%1,lastidle(%0,%1),WHO`IDLE,n),color(%1,ago(conn(%0)),WHO`CON,n),switch(1,and(isadmin(%1),or(hasflag(%0,UNFINDABLE),hasflag(room(loc(%0)),UNFINDABLE))),ljc(name(room(%0)),23),or(hasflag(%0,UNFINDABLE),hasflag(room(loc(%0)),UNFINDABLE)),*UNFINDABLE*,ljc(name(room(%0)),23)))

&CMD`+FLIP [u(pgo)]=$+flip:th petq(roll,rand(0,100));@switch/inline %q<roll>=50,{@remit %l=announce(FLIP) [ansi(h,%n)] flipped a coin: [switch(rand(1,6),1,it landed on its side!,2,a Demon grabbed it!,3,it never comes back down...,4,when it lands\, both sides are blank.,5,Reality Error 404: Coin not found!,6,it's drawn into a parallel universe!)]},>50,{@remit %l=announce(FLIP) [ansi(h,%n)] flipped a coin: Heads!},{@remit %l=announce(FLIP) [ansi(h,%n)] flipped a coin: Tails!}

&CMD`QUIT [u(pgo)]=$^(quit|logoff|logout)$:@boot/silent %#
@set u(pgo)/CMD`QUIT=regexp

&CMD`+OOC [u(pgo)]=$^(?s)(?\:\+)?(?\:ooc|osay)(?\: +(.*))?$:@assert/inline strlen(%1)=@pemit %#=What will you say?;@remit %l=parsestr(%1,%0,",",,&[ansi(hy,<,hc,OOC,hy,>)] %k)
@set u(pgo)/CMD`+OOC=regexp

th attrib_set(u(cco)/VAR`CATEGORIES,setunion(get(u(cco)/VAR`CATEGORIES),OOC|WHO,|,|))
&VAR`CATEGORIES`OOC [u(cco)]=SPEECH|NAME|HEADER|BORDER|QUOTES|SAYSTRING
&VAR`CATEGORIES`WHO [u(cco)]=NAME|ALIAS|SEX|IDLE|CON|LOCATION