@@ DEPENDENCIES - CORE

@switch/inline isdbref(u(fcl))=0,{@tel create(FC List Object <FCL>)=config(master_room)}
&fcl u(coi)=locate(config(master_room),FC List Object <FCL>,TXxi)
@parent u(fcl)=u(coi)
@set u(fcl)=WIZARD SAFE !NO_COMMAND

@switch/inline isdbref(u(fcdb))=0,{@tel create(FC List Database <FCDB>)=u(fcl)}
&fcdb u(coi)=locate(u(fcl),FC List Database <FCDB>,TXxi)
@parent u(fcdb)=u(coi)
@set u(fcdb)=WIZARD SAFE
@power u(fcdb)=many_attribs

@@ @switch/inline gt(words(lattr(u(fcl)/DAT*)),0)=1,{@dolist/inline/nobreak sortkey(#lambda/last(\%0,`),lattr(u(fcl)/DAT`*))={@stop hasattr(u(fcdb)/[last(%i0,`)]);@attach u(ccs)/INC`CPTREE=u(fcl),%i0,u(fcdb),last(%i0,`)}}
@@ Use this only if switching from the old version of FClist!

&CMD`+FCLIST u(fcl)=$^(?s)(?\:\+)?(fclist|themes)(?\:/(\S+)?)?(?\: +(.+?))?(?\:=(.*))?$:@attach u(ccs)/INC`PARTIAL=%2,setunion(get(u(fcl)/VAR`PLAYFLAGS),if(isadmin(%#),get(u(fcl)/VAR`ADMINFLAGS)),|,|),|,v(VAR`MSGHEAD),switch,switch;th u(setq,com,lcstr(+%1));@attach u(fcl)/INC`[strfirstof(%q<switch>,MAIN)]=%3,%4
@set u(fcl)/CMD`+FCLIST=regexp

&VAR`PLAYFLAGS u(fcl)=INFO
&VAR`ADMINFLAGS u(fcl)=ADDGROUP|RENGROUP|DELGROUP|ADD|REM|DESC

&RFN`MSGHEAD u(fcl)=msghead(v(VAR`MSGHEAD))
&VAR`MSGHEAD u(fcl)=FCLIST
&RFN`HEADER u(fcl)=header(%0,,FCLIST`BORDER,FCLIST`BORDERDOT,FCLIST`BORDERTEXT)
&RFN`SUBHEADER u(fcl)=subheader(%0,,FCLIST`BORDER,FCLIST`BORDERDOT,FCLIST`BORDERTEXT)

&INC`MAIN u(fcl)=@switch/inline gt(strlen(%0),0)=1,{@attach u(fcl)/INC`LIST},0,{@attach u(fcl)/INC`LISTALL}

&INC`LISTALL u(fcl)=@pemit %#=u(HEADER,mudname()'s Themes);@pemit %#=table(iter(sortkey(#lambda/get(u(fcdb)/\%0),lattr(u(fcdb)/*)),pueblize(get(u(fcdb)/%i0),%q<com> [get(u(fcdb)/%i0)]),%b,|),26,78,|);@pemit %#=u(SUBHEADER,%q<com> <group> for listing - %q<com>/info <group> for Info)

&INC`LIST u(fcl)=@attach u(fcl)/INC`FIND=%0;@pemit %#=u(HEADER,%q<gname> Roster)%R[ljust(NAME,26)][ljust(FACTION,22)][ljust(LAST ON,10)][ljust(TYPE,9)][ljust(AVAILABLE,5)]%R[u(SUBHEADER)];@dolist/inline filterbool(#lambda/isobjid(\%0),get(u(fcdb)/%q<group>`LIST))={@pemit %#=ljust(left(name(%i0),25),26)[ljust(default(%i0/D`FINGER`GROUP,Unaffiliated),20)][ljust(left(rjust(if(gt(conn(%i0),0),ansi(hg,ago(conn(%i0))),ansi(hx,elements(get(%i0/LAST),2 3))),9),9),10)]%B%B[ljust(left(default(%i0/D`FINGER`TYPE,N/A),4),9)][ljust(left(if(gt(get(%i0/D`FINGER`DEAD),0),ansi(hx,DEAD),if(approved(%i0),ansi(hx,No),ansi(hg,Yes))),4),5)]};@pemit %#=u(HEADER)

@@ &INC`LIST u(fcl)=@attach u(fcl)/INC`FIND=%0;@pemit %#=u(HEADER,%q<gname> Roster)%R[ljust(NAME,26)][ljust(HENSHIN,22)][ljust(LAST ON,10)][ljust(TYPE,9)][ljust(AVAILABLE,5)]%R[u(SUBHEADER)];@dolist/inline filterbool(#lambda/isobjid(\%0),get(u(fcdb)/%q<group>`LIST))={@pemit %#=ljust(left(name(%i0),25),26)[ljust(default(%i0/D`ATTRIBUTE`NAME`1,None Yet),20)][ljust(left(rjust(if(gt(conn(%i0),0),ansi(hg,ago(conn(%i0))),ansi(hx,elements(get(%i0/LAST),2 3))),9),9),10)]%B%B[ljust(left(default(%i0/D`FINGER`TYPE,N/A),4),9)][ljust(left(if(gt(get(%i0/D`FINGER`DEAD),0),ansi(hx,DEAD),if(approved(%i0),ansi(hx,No),ansi(hg,Yes))),4),5)]};@pemit %#=u(SUBHEADER,+themes <group> for listing - +themes/info <group> for Info)
@@ Mahou MUSH version

&INC`INFO u(fcl)=@attach u(fcl)/INC`FIND=%0;@pemit %#=u(HEADER,Theme Info: %q<gname>);@pemit %#=get(u(fcdb)/%q<group>`DESC);@pemit %#=u(SUBHEADER)

&INC`DESC u(fcl)=@attach u(fcl)/INC`FIND=%0;@check strlen(%1)=@attach %!/INC`MSG=ERROR: Theme description empty!;@attach %!/INC`MSG=Theme description changed!;@attach u(ccs)/INC`CMSG=FCLIST Group '%q<gname>' Description changed to: [left(%1,80)]...;&%q<group>`DESC u(fcdb)=%1

&INC`ADDGROUP u(fcl)=@stop u(FUN`FINDEXACT,%0)=@attach %!/INC`MSG=ERROR: Group already exists!;&[nextslot(u(fcdb))] u(fcdb)=%0;@attach %!/INC`MSG=Group '%0' created!;@attach u(ccs)/INC`CMSG={FCLIST Group '%0' created!}

&INC`FIND u(fcl)=@check strlen(%0)=@attach %!/INC`MSG=ERROR: No group name entered!;@switch/inline gt(strlen(u(setr,group,u(FUN`FINDEXACT,%0))),0)=0,{@check words(u(setr,group,u(FUN`FINDWILD,%0)))=@attach %!/INC`MSG=ERROR: Group not found!;@stop gt(words(%q<group>),1)=@attach %!/INC`MSG=ERROR: '%0' matched [itemize(iter(%q<group>,pueblize(get(u(fcdb)/%i0),%q<com> [get(u(fcdb)/%i0)]),%b,|),|,and,\,)]. Please be more specific.};th u(setq,gname,get(u(fcdb)/%q<group>))

&INC`RENGROUP u(fcl)=@attach u(fcl)/INC`FIND=%0;@check strlen(%1)=@attach %!/INC`MSG=ERROR: New name field empty.;@stop strlen(u(FUN`FINDEXACT,%1))=@attach %!/INC`MSG=ERROR: Group '%1' already exists.;@attach %!/INC`MSG=Group '%q<gname>' Renamed to '%1'!;@attach u(ccs)/INC`CMSG={FCLIST Group '%q<gname>' Renamed to '%1'};&%q<group> u(fcdb)=%1

&INC`DELGROUP u(fcl)=@attach u(fcl)/INC`FIND=%0;@attach u(ccs)/INC`VERIFY={WARNING: This will delete Group '%q<gname>' and all data associated with it. Are you SURE you want to do this? Enter the same command again wtihin ten seconds to verify.},FCLIST DELETE %q<group>,v(VAR`MSGHEAD);@attach %!/INC`MSG=Group '%q<gname>' deleted!;@attach u(ccs)/INC`CMSG=FCList Group '%q<gname>' Deleted!;@wipe u(fcdb)/%q<group>

&INC`ADD u(fcl)=@attach u(fcl)/INC`FIND=%0;@attach u(ccs)/INC`CHECKPC=%1,1,v(VAR`MSGHEAD);@stop match(get(u(fcdb)/%q<group>`LIST),%q<t1objid>)=@attach %!/INC`MSG=ERROR: %q<t1name> is already listed under '%q<gname>'.;@attach u(fcl)/INC`ADDTOLIST=%q<group>,%q<t1objid>;@attach %!/INC`MSG=You added %q<t1name> to FCLIST Group '%q<gname>'!;@attach u(ccs)/INC`CMSG=%q<t1name> now listed under '%q<gname>'

&INC`REM u(fcl)=@attach u(fcl)/INC`FIND=%0;@attach u(ccs)/INC`CHECKPC=%1,1,v(VAR`MSGHEAD);@check match(get(u(fcdb)/%q<group>`LIST),%q<t1objid>)=@attach %!/INC`MSG=ERROR: %q<t1name> is not listed in that group!;@attach u(fcl)/INC`REMFROMLIST=%q<group>,%q<t1objid>;@attach %!/INC`MSG=You removed %q<t1name> from FCLIST Group '%q<gname>'!;@attach u(ccs)/INC`CMSG=%q<t1name> removed from list '%q<gname>'

&INC`ADDTOLIST u(fcl)=&%0`LIST u(fcdb)=filterbool(#lambda/isobjid(\%0),setunion(get(u(fcdb)/%0`LIST),%1,%b,namei))
&INC`REMFROMLIST u(fcl)=&%0`LIST u(fcdb)=filterbool(#lambda/isobjid(\%0),setdiff(get(u(fcdb)/%0`LIST),%1,%b,namei))

&FUN`FINDEXACT u(fcl)=wildgrepi(u(fcdb),*,%0)
&FUN`FINDWILD u(fcl)=grepi(u(fcdb),*,%0)

&CMD`SET u(sgo)=$^\+set(profile|sex|group|race|job|quote|skills)(?\: +(.+?))?(?\:=(.*))?$:@attach u(ccs)/INC`CHECKPC=%2,1,SETFINGER;@switch/inline %1=SEX,{@sex %q<t1>=%3},SKILLS,{&SKILLS %q<t1>=%3},{&D`FINGER`%1 %q<t1>=%3};@pemit %#=announce(SETFINGER) %q<t1name>'s [ucstr(%1)] set to: %3;@nscemit/noisy Staff Reports=ansi(h,%n) set %q<t1name>'s [ucstr(%1)] to: %3
@set u(sgo)/CMD`SET=regexp

+shelp/addmain Character/+fclist=The FCList system allows characters to be added to one or more groups for listing purposes.%R%R[ansi(h,+fclist \[<group>\])]%RBy itself, lists all groups. Given a group as an argument, shows characters in that group.%R%R[ansi(h,+fclist/addgroup <group>)]%RCreates a new group. Group names must be unique.%R%R[ansi(h,+fclist/delgroup <group>)]%RDeletes a group. Be careful with it!%R%R[ansi(h,+fclist/rengroup <group>=<newname>)]%RRenames a group.%R%R[ansi(h,+fclist/add <group>=<character>)]%RAdds a character to a group. Sorting is done by character's name, alphabetical and case-insensitive.%R%R[ansi(h,+fclist/rem <group>=<character>)]%RRemoves a character from a group.

+shelp/addmain Character/+finger=Configuring fingers is actually a series of different commands based off of +set.%R%R[ansi(h,+set<whatever> <target>=<value>)]%R<whatever> can be: profile, sex, group, race, job, quote, skills%R%RAs an example:%R+setsex Volund=Male