@teleport create(Intimacy Tracker Object <EX-ITO>)=config(master_room)
&ex-ito u(coi)=locate(config(master_room),Intimacy Tracker Object <EX-ITO>,TXxi)
@parent u(ex-ito)=u(coi)
@set u(ex-ito)=WIZARD !NO_COMMAND

&CMD`+INTIMACY u(ex-ito)=$^\+intimacy(?\:/(\S+))?(?\: +(.*))?$:@check or(not(strlen(%1)),match(get(u(ex-ito)/VAR`PLAYFLAGS),%1,|))=@pemit %#=ERROR: %1 is not a valid flag for +intimacy.;@select/inline and(strlen(%2),not(strlen(%1)),isadmin(%#))=1,{@attach u(ccs)/INC`CHECKPC=%2,1,INTIMACY},0,{@attach u(ccs)/INC`CHECKPC=%#,1,INTIMACY};@attach u(ex-ito)/INC`[strfirstof(%1,MAIN)]=%2,%q<t1>
@set u(ex-ito)/CMD`+INTIMACY=regexp

&VAR`PLAYFLAGS u(ex-ito)=BUILD|ERODE|GAIN|LOSE|HELP|URGE|MOTIVATION

&INC`CHECKINT u(ex-ito)=@check strlen(%0)=@pemit %#=msghead(INTIMACY) ERROR: Intimacy field empty.;@check valnum(%0)=@pemit %#=msghead(INTIMACY) ERROR: Intimacies may only be addressed by their slot number.;@check hasattr(%1/D`INTIMACIES`%0)=@pemit %#=msghead(INTIMACY) ERROR: Intimacy not found.

&INC`BUILD u(ex-ito)=@attach u(ex-ito)/INC`CHECKINT;@stop gt(u(setr,new,add(1,get(%1/D`INTIMACIES`%0`CUR))),getstat(%1/D`VIRTUES,Conviction))=@pemit %#=msghead(INTIMACY) ERROR: That would exceed your Conviction.;&D`INTIMACIES`%0`CUR %1=%q<new>;@pemit %#=msghead(INTIMACY) You reinforced your '[get(%1/D`INTIMACIES`%0`CONTEXT)]' Intimacy.

&INC`ERODE u(ex-ito)=@attach u(ex-ito)/INC`CHECKINT;@select/inline u(setr,new,sub(1,bound(get(%1/D`INTIMACIES`%0`CONTEXT),0,getstat(%1/D`VIRTUES,Conviction))))=0,{@pemit %#=msghead(INTIMACY) Your '[get(%1/D`INTIMACIES`%0`CONTEXT)]' Intimacy was eroded to 0, and removed.;@wipe %1/D`INTIMACIES`%0},{&D`INTIMACIES`%0 %1=%q<new>;@pemit %#=msghead(INTIMACY) You have Eroded your '[get(%1/D`INTIMACIES`%0`CONTEXT)]' Intimacy.}

&INC`GAIN u(ex-ito)=@check strlen(%0)=@pemit %#=msghead(INTIMACY) ERROR: Intimacy context field empty.;&[u(setr,attr,D`INTIMACIES`[nextslot(%1,D`INTIMACIES)])]`CONTEXT %1=%0;&%q<attr>`CUR %1=getstat(%1/D`VIRTUES,Conviction);@pemit %#=msghead(INTIMACY) Intimacy '%0' added.

&INC`LOSE u(ex-ito)=@attach u(ex-ito)/INC`CHECKINT;@pemit %#=msghead(INTIMACY) You have removed the Intimacy '[get(%1/D`INTIMACIES`%0`CONTEXT)]';@wipe %1/D`INTIMACIES`%0

&INC`MOTIVATION u(ex-ito)=@attach u(ex-ito)/INC`MOTURGE=%0,%1,MOTIVATION
&INC`URGE u(ex-ito)=@attach u(ex-ito)/INC`MOTURGE=%0,%1,URGE
&INC`MOTURGE u(ex-ito)=@select/inline strlen(%0)=0,{@pemit %#=msghead(INTIMACY) Your [capnames(%2)] has been cleared.;@wipe %1/D`INTIMACIES`%2},{@check strlen(%0)=@pemit %#=msghead(INTIMACY) ERROR: [capnames(%1)] field empty.;&D`INTIMACIES`%2 %1=%0;@pemit %#=msghead(INTIMACY) Your [capnames(%2)] has been set to '%0'}

&INC`MAIN u(ex-ito)=@pemit %#=header(+intimacies for [name(%1)]);@dolist/inline MOTIVATION URGE={@select/inline gt(strlen(get(%1/D`INTIMACIES`%i0)),0)=1,{@pemit %#=rjust(ansi(h,%i0):,11) [get(%1/D`INTIMACIES`%i0)]}};@dolist/inline filterbool(#lambda/valnum(\%0),iter(u(lattr,%1/D`INTIMACIES`*),last(%i0,`)))=@pemit %#=ljust(rjust(ansi(h,%i0),3): [get(%1/D`INTIMACIES`%i0`CONTEXT)],60)[ljust([bound(get(%1/D`INTIMACIES`%i0`CUR),0,getstat(%1/D`VIRTUES,Conviction))]/[getstat(%1/D`VIRTUES,Conviction)],5)];@pemit %#=header()
