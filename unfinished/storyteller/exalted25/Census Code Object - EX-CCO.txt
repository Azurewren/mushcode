@@ DEPENDENCIES - CORE

@select/inline isdbref(u(ex-cco))=0,{@tel create(Census Code Object <ex-cco>)=config(master_room)}
&ex-cco u(coi)=locate(config(master_room),Census Code Object <ex-cco>,TXxi)
@set u(ex-cco)=WIZARD !NO_COMMAND
@parent u(ex-cco)=u(coi)

&CMD`+CENSUS u(ex-cco)=$^\+census(?\:/(\S+))?(?\: +(.*))?$:@check words(u(setr,list,sort(lsearch(all,eplayer,\[and(not(haspower(##,GUEST)),not(isadmin(##)),approved(##))\]),namei)))=@pemit %#=msghead(CENSUS) No active players in the +census!;@attach u(ex-cco)/INC`[if(strlen(%2),SUB,MAIN)]=%2
@set u(ex-cco)/CMD`+CENSUS=regexp

&INC`MAIN u(ex-cco)=@pemit %#=header(mudname() Census Data);@dolist/inline/delimit | [get(u(ex-cdo)/VAR`SPLATS)]={@select/inline gt(words(u(setr,racelist,filterbool(#lambda/strmatch(%i0,getstat(\%0/D`INFO,Splat)),%q<list>))),0)=1,{@pemit %#=ljust(ansi(h,pueblize(capnames(%i0),+census [capnames(%i0)])),70,.)[rjust(words(%q<racelist>),2,0)];@select/inline words(%q<racelist>)=0,{},{@pemit %#=itemize(iter(%q<racelist>,name(%i0),%b,|),|,and,\,)%R}}};@pemit %#=header(Total Player Characters: [words(%q<list>)])

&INC`SUB u(ex-cco)=@attach u(ccs)/INC`PARTIAL=%0,get(u(ex-cdo)/VAR`SPLATS),|,CENSUS,choice,Splat;@check words(u(setr,racelist,filterbool(#lambda/strmatch(%q<choice>,getstat(\%0/D`INFO,Splat)),%q<list>)))=@pemit %#=msghead(CENSUS) No active [capnames(%q<choice>)] Players!;@pemit %#=header(mudname() Census Data);@dolist/inline/delimit | [get(u(ex-cdo)/VAR`SPLATS`%q<choice>)]={@select/inline gt(words(u(setr,castelist,filterbool(#lambda/strmatch(%i0,getstat(\%0/D`INFO,Caste)),%q<racelist>))),0)=1,{@pemit %#=ljust(ansi(h,capnames(%i0)),70,.)[rjust(words(%q<castelist>),2,0)];@select/inline words(%q<castelist>)=0,{},{@pemit %#=itemize(iter(%q<castelist>,name(%i0),%b,|),|,and,\,)%R}}};@pemit %#=header(Total [capnames(%q<splat>)] Player Characters: [words(%q<racelist>)])