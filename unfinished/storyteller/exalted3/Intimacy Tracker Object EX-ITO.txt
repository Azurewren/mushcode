@teleport create(Intimacy Tracker Object <EX-ITO>)=config(master_room)
&ex-ito u(coi)=locate(config(master_room),Intimacy Tracker Object <EX-ITO>,TXxi)
@parent u(ex-ito)=u(coi)
@set u(ex-ito)=WIZARD !NO_COMMAND

&CMD`+INTIMACY u(ex-ito)=$^\+intimacy(?\:/(\S+))?(?\: +(.*))?$:@attach u(ccs)/INC`PARTIAL=%1,setunion(get(u(ex-ito)/VAR`PLAYFLAGS),if(isadmin(%#),get(u(ex-ito)/VAR`ADMINFLAGS)),|,|),|,INFO,switch,switch;@select/inline and(strlen(%2),not(strlen(%1)),isadmin(%#))=1,{@attach u(ccs)/INC`CHECKPC=%2,1,INTIMACY},0,{@attach u(ccs)/INC`CHECKPC=%#,1,INTIMACY};@attach u(ex-ito)/INC`[strfirstof(%q<switch>,MAIN)]=trim(%2),%q<t1>
@set u(ex-ito)/CMD`+INTIMACY=regexp

&VAR`MSGHEAD u(ex-ito)=INTIMACY
&RFN`MSGHEAD u(ex-ito)=msghead(v(VAR`MSGHEAD))
&VAR`PLAYFLAGS u(ex-ito)=GAIN|LOSE|STRENGTHEN|WEAKEN|RENAME
&RFN`HEADER u(ex-ito)=header(%0,,INTIMACY`BORDER,INTIMACY`BORDERDOT,INTIMACY`BORDERTEXT,1)
&RFN`SEPARATOR u(ex-ito)=separator(%0,,INTIMACY`BORDER,INTIMACY`BORDERDOT,INTIMACY`BORDERTEXT,1)
&RFN`SUBHEADER u(ex-ito)=subheader(%0,,INTIMACY`BORDER,INTIMACY`BORDERDOT,INTIMACY`BORDERTEXT,1)


&INC`GAIN u(ex-ito)=@check strlen(%0)=@pemit %#=u(RFN`MSGHEAD) ERROR: Intimacy context field empty.;&[u(setr,attr,D`INTIMACIES`[nextslot(%1,D`INTIMACIES)])] %1=%0;&%q<attr>`CUR %1=2;@pemit %#=u(RFN`MSGHEAD) Intimacy '%0' added.

&INC`LOSE u(ex-ito)=@attach u(ex-ito)/INC`CHECKINT;@pemit %#=u(RFN`MSGHEAD) You have removed the Intimacy '[get(%1/D`INTIMACIES`%0)]';@wipe %1/D`INTIMACIES`%0

&INC`CHECKINT u(ex-ito)=@check strlen(%0)=@pemit %#=u(RFN`MSGHEAD) ERROR: Intimacy field empty.;@check valnum(%0)=@pemit %#=u(RFN`MSGHEAD) ERROR: Intimacies may only be addressed by their slot number.;@check hasattr(%1/D`INTIMACIES`%0)=@pemit %#=u(RFN`MSGHEAD) ERROR: Intimacy not found.

&INC`STRENGTHEN u(ex-ito)=@attach u(ex-ito)/INC`CHECKINT;@stop gt(get(%1/D`INTIMACIES`%0`CUR),4)=@pemit %#=u(RFN`MSGHEAD) Intimacies cannot be strengthened past Defining.;&D`INTIMACIES`%0`CUR %1=u(setr,new,add(get(%1/D`INTIMACIES`%0`CUR),2));@pemit %#=u(RFN`MSGHEAD) You reinforced your '[get(%1/D`INTIMACIES`%0)]' Intimacy to [switch(%q<new>,2,Minor,4,Major,6,Defining)].

&INC`WEAKEN u(ex-ito)=@attach u(ex-ito)/INC`CHECKINT;@stop lt(get(%1/D`INTIMACIES`%0`CUR),4)=@pemit %#=u(RFN`MSGHEAD) Intimacies cannot be weakened past Minor. Use /lose for that.;&D`INTIMACIES`%0`CUR %1=u(setr,new,sub(get(%1/D`INTIMACIES`%0`CUR),2));@pemit %#=u(RFN`MSGHEAD) You weakened your '[get(%1/D`INTIMACIES`%0)]' Intimacy to [switch(%q<new>,2,Minor,4,Major,6,Defining)].

&INC`MAIN u(ex-ito)=@pemit %#=u(RFN`HEADER,+intimacies for [name(%1)]);@pemit %#=align(2 60 15,ID,NAME,STATUS);@pemit %#=u(RFN`SEPARATOR);@dolist/inline sortkey(#lambda/last(\%0,`),u(lattr,%1/D`INTIMACIES`*),n)=@pemit %#=align(2 60 15,last(%i0,`),get(%1/%i0),switch(get(%1/%i0`CUR),2,Minor,4,Major,6,Defining));@pemit %#=u(RFN`HEADER)

@@ ROLEPLAYING - +INTIMACY
+help/addmain Roleplaying/+intimacy=[u(ex-ito)]/HLP`+INTIMACY
&HLP`+INTIMACY u(ex-ito)=The Intimacy system keeps track of all your intimacies!%R%R[ansi(hc,Commands)]%R[align(5 [sub(width(%#),6)],,{[ansi(h,+intimacy)] - Lists current Intimacies.%R[ansi(h,+intimacy/gain <text>)] - Gain a new Intimacy.%R[ansi(h,+intimacy/lose <id>)] - Lose a specified Intimacy.%R[ansi(h,+intimacy/strengthen <id>)] - Raise Intimacy status to next stage.%R[ansi(h,+intimacy/weaken <id>)] - Erode Intimacy status by one stage.})]

+shelp/addmain Characters/+intimacy=[u(ex-ito)]/SHLP`+INTIMACY
&SHLP`+INTIMACY u(ex-ito)=[ansi(hc,Staff Commands)]%R[align(5 [sub(width(%#),6)],,{[ansi(h,+intimacy <player>)] - Shows a player's Intimacy information.})]