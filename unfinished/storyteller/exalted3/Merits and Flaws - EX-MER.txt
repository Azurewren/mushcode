@@ DEPENDENCIES - CORE

@select/inline isdbref(u(ex-mer))=0,{@tel create(Merits and Flaws <EX-MER>)=config(master_room)}
&ex-mer u(coi)=locate(config(master_room),Merits and Flaws <EX-MER>,TXxi)
@parent u(ex-mer)=u(coi)
@set u(ex-mer)=WIZARD !NO_COMMAND

&CMD`+MERITS u(ex-mer)=$^(?s)(?\:\+)?(merits|flaws|pacts)(?\:/(\S+)?)?(?\: +(.+?))?(?\:=(.*))?$:@attach u(ccs)/INC`PARTIAL=%2,setunion(get(u(ex-mer)/VAR`PLAYFLAGS),if(isadmin(%#),get(u(ex-mer)/VAR`ADMINFLAGS)),|,|),|,INFO,switch,switch;@attach u(ex-mer)/INC`[strfirstof(%q<switch>,MAIN)]=squish(trim(%3)),trim(%4),%1,switch(%1,merits,Merit,flaws,Flaw,pacts,Pact)
@set u(ex-mer)/CMD`+MERITS=regexp

&VAR`PLAYFLAGS u(ex-mer)=ADD|RANK|GET|DESC|CONTEXT|DELETE
&VAR`ADMINFLAGS u(ex-mer)=

&RFN`HEADER u(ex-mer)=header(%0,,MERITS`BORDER,MERITS`BORDERDOT,MERITS`BORDERTEXT)
&RFN`SUBHEADER u(ex-mer)=subheader(%0,,MERITS`BORDER,MERITS`BORDERDOT,MERITS`BORDERTEXT)
&RFN`SEPARATOR u(ex-mer)=separator(%0,,MERITS`BORDER,MERITS`BORDERDOT,MERITS`BORDERTEXT)
&RFN`MSGHEAD u(ex-mer)=msghead(v(VAR`MSGHEAD))
&VAR`MSGHEAD u(ex-mer)=MERITS

&VAR`MERITS u(ex-mer)=ALLIES|AMBIDEXTROUS|ARTIFACT|BACKING|BOUNDLESS ENDURANCE|COMMAND|CONTACTS|CULT|DANGER SENSE|DEMESNE|DIRECTION SENSE|EIDETIC MEMORY|FAMILIAR|FAST REFLEXES|FLEET OF FOOT|FOLLOWERS|GIANT|HEARTHSTONE|HIDEOUS|INFLUENCE|IRON STOMACH|LANGUAGE|MANSE|MENTOR|MARTIAL ARTIST|MIGHTY THEW|NATURAL IMMUNITY|PAIN TOLERANCE|QUICK DRAW|RETAINERS|RESOURCES|RESOURCES|SELECTIVE CONTRACEPTION|STRONG LUNGS|TEMPERED BY THE ELEMENTS|TOXIN RESISTANCE|CHAMELEON|CLAWS|FANGS|HOOVES|HORNS|ENHANCED SENSE|EXALTED HEALING|EXTRA LIMBS|GILLS|POISONED BODY|QUILLS|SUBTLETY|TAIL|THAUMATURGIST|UNUSUAL HIDE|VENOMOUS|WALL WALKING|WINGS

&VAR`FLAWS u(ex-mer)=ADDICTION|AMPUTEE|BLIND|DEAF|DERANGEMENT|MUTE|STERILE|WYLD MUTANT

&VAR`PACTS u(ex-mer)=INFERNAL NOBILITY|DARK PARAMOUR|DEMONIC TATTOOS|UNBURNT MAJESTY|THE BURNING NAME|SUZERAIN OF ENDLESS FLAME|CHILD OF MADNESS|MIRACLES OF SHADOW AND CHAOS|DEEP BREATH CULTIVATION|FLOWING INTENTION CULTIVATION|LIVING SPIRIT CULTIVATION|PURE HEART CULTIVATION|VITAL FOCUS CULTIVATION|ASTRAL MEDITATION|EYE OF CRIMSON WARNING

th attrib_set(u(cco-db)/VAR`CATEGORIES,setunion(get(u(cco-db)/VAR`CATEGORIES),MERITS,|,|))
&VAR`CATEGORIES`MERITS u(cco-db)=BORDER|BORDERDOT|BORDERTEXT|PRIVATE
&COLOR`MERITS`PRIVATE u(pco)=hx

&DO`ANNOUNCE u(ex-mer)=@select/inline t(strlen(%1))=1,{@attach u(ccs)/INC`CMSG=%1,%#,v(VAR`MSGHEAD),u(cmo`staffrep)};@select/inline strmatch(%#,%0)=0,{@attach u(ccs)/INC`MSG=v(VAR`MSGHEAD),%0,%2;@attach u(ccs)/INC`MSG=v(VAR`MSGHEAD),%#,%3},1,{@attach u(ccs)/INC`MSG=v(VAR`MSGHEAD),%0,%2}
@@ %0 - player being modified. %1 - message for admin channel. %2 - message for player. %3 - message for admin.

&INC`TARGET u(ex-mer)=@select/inline strmatch(%0,*/*)=1,{@attach u(ccs)/INC`CHECKPC=before(%0,/),1,v(VAR`MSGHEAD);th u(setq,target,%q<t1>,filename,after(%0,/))},0,{th u(setq,target,%#,filename,%0)}

&INC`MERNAME u(ex-mer)=@check strlen(before(%0,:))=@pemit %#=u(RFN`MSGHEAD) ERROR: [capstr(%3)] field empty.;@attach u(ccs)/INC`PARTIAL=before(%0,:),v(VAR`%2),|,v(VAR`MSGHEAD),merit,%3;th u(setq,context,squish(trim(after(%0,:))));@stop charin(%q<context>,| ~)=@pemit %#=u(RFN`MSGHEAD) ERROR: Contexts may not contain a ~ or |.

&INC`MERNAME2 u(ex-mer)=@check strlen(before(%0,:))=@pemit %#=u(RFN`MSGHEAD) ERROR: [capstr(%3)] field empty.;@attach u(ccs)/INC`PARTIAL=before(%0,:),setunion(iter(u(lattr,%q<target>/D`%2`*),get(%q<target>/%i0),%b,|),,|,i,|),|,v(VAR`MSGHEAD),merit,%3;th u(setq,context,squish(trim(after(%0,:))));@stop charin(%q<context>,| ~)=@pemit %#=u(RFN`MSGHEAD) ERROR: Contexts may not contain a ~ or |.;th u(setq,context,u(FUN`PARTCON,%q<target>,%2,%q<merit>,%q<context>))

&FUN`FINDMER u(ex-mer)=first(filterbool(#lambda/strmatch(%3,get(%0/\%0`CONTEXT)),u(FUN`FILTERMER,%0,%1,%2)))
&FUN`PARTCON u(ex-mer)=localize(strfirstof(if(match(u(setr,cons,iter(u(FUN`FILTERMER,%0,%1,%2),get(%0/%i0`CONTEXT),%b,|)),%3,|),%3),if(strlen(%3),grab(%q<cons>,%3*,|)),%3))
&FUN`FILTERMER u(ex-mer)=filterbool(#lambda/strmatch(%2,get(%0/\%0)),u(lattr,%0/D`%1`*))

&FUN`MERNAME u(ex-mer)=localize(capnames(get(%0/%1)[if(strlen(u(setr,context,get(%0/%1`CONTEXT))),:%b%q<context>)]))

&INC`BEGIN u(ex-mer)=@attach u(ex-mer)/INC`TARGET;@select/inline strmatch(%#,%q<target>)=0,{@check isadmin(%#)=@pemit %#=u(RFN`MSGHEAD) Permission denied.};@stop cand(approved(%q<target>),not(isadmin(%#)))=@pemit %#=u(RFN`MSGHEAD) Permission denied.;@attach u(ex-mer)/INC`MERNAME=%q<filename>,%1,%2;

&INC`BEGIN2 u(ex-mer)=@attach u(ex-mer)/INC`TARGET;@select/inline strmatch(%#,%q<target>)=0,{@check isadmin(%#)=@pemit %#=u(RFN`MSGHEAD) Permission denied.};@stop cand(approved(%q<target>),not(isadmin(%#)))=@pemit %#=u(RFN`MSGHEAD) Permission denied.;@attach u(ex-mer)/INC`MERNAME2=%q<filename>,%1,%2;

&INC`ADD u(ex-mer)=@attach u(ex-mer)/INC`BEGIN;@stop strlen(u(FUN`FINDMER,%q<target>,%2,%q<merit>,%q<context>))=@pemit %#=u(RFN`MSGHEAD) ERROR: Conflicting Merit. Contexts must be unique.;th u(setq,attr,D`%2`[nextslot(%q<target>,D`%2)]);@check cand(isint(%1),gte(%1,0))=@pemit %#=u(RFN`MSGHEAD) [capstr(%2)] must have a rank. Ranks must be 0 or greater.;@attach u(ex-mer)/DO`ADD=%q<attr>,%q<target>,%q<merit>,%q<context>,%1;th u(setq,msg,gained a new %3: [u(FUN`MERNAME,%q<target>,%q<attr>)] (%1));@select/inline strmatch(%#,%q<target>)=0,{th u(setq,amsg,ansi(h,name(%q<target>)) %q<msg>)};@attach u(ex-mer)/DO`ANNOUNCE=%q<target>,{%q<amsg>},{You %q<msg>},{ansi(h,name(%q<target>)) %q<msg>}

&DO`ADD u(ex-mer)=&%0 %1=%2;&%0`CONTEXT %1=%3;&%0`RANK %1=%4;@attach u(ex-mer)/DO`UPD=%0,%1

&DO`UPD u(ex-mer)=&%0`ON %1=secs();&%0`BY %1=%n;&%0`BYDB %1=%:;&%0`ON %1=secs()

&INC`DELETE u(ex-mer)=@attach u(ex-mer)/INC`BEGIN2;@check strlen(u(setr,attr,u(FUN`FINDMER,%q<target>,%2,%q<merit>,%q<context>)))=@pemit %#=u(RFN`MSGHEAD) ERROR: Merit not found.;@attach u(ccs)/INC`VERIFY={ansi(hr,WARNING:)%BDeleting the [u(FUN`MERNAME,%q<target>,%q<attr>)] %3. Are you sure? Enter the same command again within ten seconds to verify.},%3 DELETE %q<attr>,v(VAR`MSGHEAD);th u(setq,msg,lost a %3: [u(FUN`MERNAME,%q<target>,%q<attr>)] ([get(%q<target>/%q<attr>`RANK)]));@select/inline strmatch(%#,%q<target>)=0,{th u(setq,amsg,ansi(h,name(%q<target>)) %q<msg>)};@attach u(ex-mer)/DO`ANNOUNCE=%q<target>,{%q<amsg>},{You %q<msg>},{ansi(h,name(%q<target>)) %q<msg>};@wipe %q<target>/%q<attr>

&INC`RANK u(ex-mer)=@attach u(ex-mer)/INC`BEGIN2;@check strlen(u(setr,attr,u(FUN`FINDMER,%q<target>,%2,%q<merit>,%q<context>)))=@pemit %#=u(RFN`MSGHEAD) ERROR: Merit not found.;@check cand(isint(%1),gte(%1,0))=@pemit %#=u(RFN`MSGHEAD) [capstr(%2)] must have a rank. Ranks must be 0 or greater.;&%q<attr>`RANK %q<target>=%1;@attach u(ex-mer)/DO`UPD=%q<target>,%q<attr>;th u(setq,msg,had a %3 Rank Changed: [u(FUN`MERNAME,%q<target>,%q<attr>)] to [ansi(h,%1)]);@select/inline strmatch(%#,%q<target>)=0,{th u(setq,amsg,ansi(h,name(%q<target>)) %q<msg>)};@attach u(ex-mer)/DO`ANNOUNCE=%q<target>,{%q<amsg>},{You %q<msg>},{ansi(h,name(%q<target>)) %q<msg>}

&INC`CONTEXT u(ex-mer)=@attach u(ex-mer)/INC`BEGIN2;@check strlen(u(setr,attr,u(FUN`FINDMER,%q<target>,%2,%q<merit>,%q<context>)))=@pemit %#=u(RFN`MSGHEAD) ERROR: Merit not found.;@check strlen(u(setr,newcon,squish(trim(%1))))=@pemit %#=u(RFN`MSGHEAD) ERROR: Context empty.;@stop strlen(u(FUN`FINDMER,%q<target>,%2,%q<merit>,%q<newcon>))=@pemit %#=u(RFN`MSGHEAD) ERROR: Conflicting Merit. Contexts must be unique.;;&%q<attr>`CONTEXT %q<target>=%q<newcon>;@attach u(ex-mer)/DO`UPD=%q<target>,%q<attr>;th u(setq,msg,had a %3 Context Changed: [u(FUN`MERNAME,%q<target>,%q<attr>)] to %q<newcon>);@select/inline strmatch(%#,%q<target>)=0,{th u(setq,amsg,ansi(h,name(%q<target>)) %q<msg>)};@attach u(ex-mer)/DO`ANNOUNCE=%q<target>,{%q<amsg>},{You %q<msg>},{ansi(h,name(%q<target>)) %q<msg>}

&INC`DESC u(ex-mer)=@attach u(ex-mer)/INC`BEGIN2;@check strlen(u(setr,attr,u(FUN`FINDMER,%q<target>,%2,%q<merit>,%q<context>)))=@pemit %#=u(RFN`MSGHEAD) ERROR: Merit not found.;@check strlen(%1)=@pemit %#=u(RFN`MSGHEAD) ERROR: Desc empty!;&%q<attr>`DESC %q<target>=%1;@attach u(ex-mer)/DO`UPD=%q<target>,%q<attr>;th u(setq,msg,had a %3 Desc Changed: [u(FUN`MERNAME,%q<target>,%q<attr>)] to %1);@select/inline strmatch(%#,%q<target>)=0,{th u(setq,amsg,ansi(h,name(%q<target>)) %q<msg>)};@attach u(ex-mer)/DO`ANNOUNCE=%q<target>,{%q<amsg>},{You %q<msg>},{ansi(h,name(%q<target>)) %q<msg>}

&INC`GET u(ex-mer)=@attach u(ex-mer)/INC`TARGET;@attach u(ex-mer)/INC`MERNAME2;@check strlen(u(setr,attr,u(FUN`FINDMER,%q<target>,%2,%q<merit>,%q<context>)))=@pemit %#=u(RFN`MSGHEAD) ERROR: Merit not found.;@pemit %#=u(RFN`HEADER,name(%q<target>)'s [u(FUN`MERNAME,%q<target>,%q<attr>)] Desc);@pemit %#=decompile(get(%q<target>/%q<attr>`DESC));@pemit %#=u(RFN`MSGHEAD)

&INC`MAIN u(ex-mer)=@attach u(ex-mer)/INC`TARGET;@select/inline t(strlen(%q<filename>))=1,{@attach u(ex-mer)/INC`MERNAME2=%q<filename>,%1,%2;@attach u(ex-mer)/INC`DISPLAY},0,{@attach u(ex-mer)/INC`LIST}

&INC`LIST u(ex-mer)=@pemit %#=u(RFN`HEADER,name(%q<target>)'s [capstr(%2)]);@pemit %#=align(30 30 8,Name,Desc,Rank);@pemit %#=u(RFN`SEPARATOR);@dolist/inline/delimit | [sortkey(#lambda/u(FUN`MERNAME,%q<target>,\%0),u(lattr,%q<target>/D`%2`*),i,%b,|)]={@pemit %#=align(30 30 8,pueblize(u(FUN`MERNAME,%q<target>,%i0),+%2 [name(%q<target>)]/[u(FUN`MERNAME,%q<target>,%i0)]),left(get(%q<target>/%i0`DESC),20),repeat(*,get(%q<target>/%i0`RANK)))};@pemit %#=u(RFN`HEADER)

&INC`DISPLAY u(ex-mer)=@check strlen(u(setr,attr,u(FUN`FINDMER,%q<target>,%2,%q<merit>,%q<context>)))=@pemit %#=u(RFN`MSGHEAD) ERROR: Merit not found.;@pemit %#=u(RFN`HEADER,name(%q<target>)'s %3: [u(FUN`MERNAME,%q<target>,%q<attr>)]);@pemit %#=get(%q<target>/%q<attr>`DESC);@pemit %#=u(RFN`SEPARATOR);@pemit %#=align(20 20,Setby,Seton);@pemit %#=align(20 20,get(%q<target>/%q<attr>`BY),fancytime(get(%q<target>/%q<attr>`ON),%#));@pemit %#=u(RFN`HEADER)

ADD|RANK|GET|DESC|CONTEXT|DELETE

@@ CHARACTER - +INFO
+help/addmain Character/+merits=[u(ex-mer)]/HLP`+MERITS
&HLP`+MERITS u(ex-mer)=The Merit system allows Players to mark down their character's Merits, Flaws, and other notes.%R%R[ansi(hc,Concepts)]%R[align(5 [sub(width(%#),6)],,{[ansi(h,Types)] - Only official Merits and Flaws may be entered. The system has a database of valid Merits/Flaws and will Partial match them.%R[ansi(h,context)] - Merits and flaws can have a CONTEXT, such as the name of an Artifact or the source of one's resources. When a Merit name is neded, the context is added in the following style: Artifact: Volcano Cutter. Contexts must be unique for each Merit type!%R[ansi(h,modes)] - This command can also be called as [ansi(h,+flaws)] to address Flaws and [ansi(h,+pacts)] for Sorcerous Pact-based merits like Dark Paramour.})]%R%R[ansi(hc,Managing Merits)]%RNote: Merits and Flaws may only be edited freely while in Chargen. Only admin may edit them otherwise.%R%R[align(5 [sub(width(%#),6)],,{[ansi(h,+merits)] - Lists your current Merits.%R[ansi(h,+merits <merit>)] - View details about a Merit.%R[ansi(h,+merits/add <merit>=<value>)] - Creates new Merit. Value must be a number.%R[ansi(h,+merits/rank <merit>=<value>)] - Change's a merit's value.%R[ansi(h,+merits/desc <merit>=<text>)] - Describes a merit with a text entry visible when it's viewed.%R[ansi(h,+merits/context <merit>=<new context>)] - Change a merit's context.%R[ansi(h,+merits/get <merit>)] - Retrieve an unformatted version of a Merit's /desc for easy editing.})]

+shelp/addmain Character/+merits=[u(ex-mer)]/SHLP`+MERITS
&SHLP`+MERITS u(ex-mer)=Staff commands with +merit are identical to the player commands. To target specific players, use <player>/ to view a player with +merit, and <player>/<merit> to use any other commands. For example, +merits/add <person>/Resources: Tribute=3