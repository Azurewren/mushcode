@@ DEPENDENCIES: Core
@@ RECOMMENDED: Group System, enables Factional fields.

th u(NEWCOBJ,Finger Management System <FINGER>,finger,,,,WIZARD SAFE !NO_COMMAND,INHERIT SIDEFX SAFE)

&CMD`+FINGER`PENNMUSH [u(cobj,finger)]=$^(?s)(?\:\+)?(oocfinger|finger)(?\:/(\S+))?(?\: +(.+?))?(?\:=(.*?))?$:@attach %!/CMD`+FINGER`MAIN
@set [u(cobj,finger)]/CMD`+FINGER`PENNMUSH=regexp
&CMD`+FINGER`RHOSTMUSH [u(cobj,finger)]=$^(?s)(?\:\+)?(oocfinger|finger)(?\:/(\\S+))?(?\: +(.+?))?(?\:=(.*?))?$:@attach %!/CMD`+FINGER`MAIN
@set [u(cobj,finger)]/CMD`+FINGER`RHOSTMUSH=regexp
&CMD`+FINGER`MAIN [u(cobj,finger)]=th u(setq,sysname,%1);@attach %!/INC`GETSWITCH=%2;@include %!/INC`%1`[u(strfirstof,%q<switch>,MAIN)]=%3,%4
@set [u(cobj,finger)]/CMD`+FINGER`[switch(v(game),PennMUSH,RHOSTMUSH,RhostMUSH,PENNMUSH)]=no_command

&SYSTEM`NAME [u(cobj,finger)]=FINGER

&SYSTEM`SWITCHES [u(cobj,finger)]=setunion(v(SWITCHES`%q<sysname>`PLAYER),if(u(isadmin,%#),v(SWITCHES`%q<sysname>`ADMIN)),|,|)

&SWITCHES`FINGER`PLAYER [u(cobj,finger)]=SET|GET
&SWITCHES`FINGER`ADMIN [u(cobj,finger)]=

&INC`FINGER`MAIN [u(cobj,finger)]=@attach %!/INC`CHECKPC=u(strfirstof,%0,%#),1;@pemit %#=u(HEADER,u(getproperty,%q<t1>,ALIASNAME),,,1);th u(setq,col1,u(FINGER`COLUMN1));th u(setq,rjust1,lmath(max,iter(%q<col1>,strlen(elements(##,2,~)),|,%b)));th u(setq,col2,u(FINGER`COLUMN2));th u(setq,rjust2,lmath(max,iter(%q<col2>,strlen(elements(##,2,~)),|,%b)));@pemit %#=align(1. [floor(fdiv(sub(u(width,%#),7),2))] 1. [ceil(fdiv(sub(u(width,%#),7),2))] 1.,ansi(u(color,%#,FINGER,BORDER),|),iter(%q<col1>,rjust(elements(##,2,~),%q<rjust1>):%B[u(strfirstof,u(getproperty,%q<t1>,elements(##,1,~)),elements(##,3,~))],|,%R),ansi(u(color,%#,FINGER,BORDER),|),iter(%q<col2>,rjust(elements(##,2,~),%q<rjust2>):%B[u(strfirstof,u(getproperty,%q<t1>,elements(##,1,~)),elements(##,3,~))],|,%R),ansi(u(color,%#,FINGER,BORDER),|));@dolist/inline/delimit | [v(FINGER`EXTRA)]=@select/inline t(strlen(u(setr,found,u(getproperty,%q<t1>,before(##,~)))))=1,{@pemit %#=u(SEPARATOR,u(capnames,u(strfirstof,after(##,~),##)),,,1);@pemit %#=align(1. [sub(u(width,%#),4)] 1.,ansi(u(color,%#,FINGER,BORDER),|),edit(%q<found>,%t,space(8)),ansi(u(color,%#,FINGER,BORDER),|))};@pemit %#=u(SUBHEADER,,,,1);@select/inline cand(nor(u(isadmin,%#),strmatch(%#,%q<t1>)),get(%q<t1>/afinger))=1,{@trigger/spoof %q<t1>/AFINGER=%#,u(moniker,%#)}

&INC`FINGER`SET [u(cobj,finger)]=@attach %!/INC`CHECKPC=if(cand(u(isadmin,%#),strmatch(%0,*/*)),before(%0,/),%#),1;@attach %!/INC`PARTIAL=if(cand(u(isadmin,%#),strmatch(%0,*/*)),after(%0,/),%0),setunion(v(FINGER`PLAYERSET),if(u(isadmin,%#),v(FINGER`ADMINSET))),|,|),|,field,field;@select/inline strlen(%1)=0,{@attach %!/INC`VERIFY={[ansi(hr,WARNING:)] Setting %q<field> to nothing will clear it. Are you sure? Enter that again to verify!},CLEAR FINGER %q<field>},>0,{@attach %!/INC`CHECK`%q<field>=%1};&D`FINGER`%q<field> %q<t1>=%1;@attach %!/INC`MSG=You set [if(strmatch(%#,%q<t1>),your,%q<t1name>'s)] [u(capnames,%q<field>)] Finger Field to: %1;@select/inline strmatch(%#,%q<t1>)=0,{@attach %!/INC`MSG`NOTICE=Your [u(capnames,%q<field>)] Finger Field is now: %1,%q<t1>}

&INC`FINGER`GET [u(cobj,finger)]=@attach %!/INC`CHECKPC=if(strmatch(%0,*/*),before(%0,/),%#),1;@attach %!/INC`PARTIAL=if(strmatch(%0,*/*),after(%0,/),%0),setunion(v(FINGER`PLAYERSET),v(FINGER`ADMINSET),|,|),|,field,field;@pemit %#=u(getproperty,%q<T1>,%q<field>)

&FINGER`COLUMN1 [u(cobj,finger)]=[if(isdbref(u(cobj,gms)),FACTION~Faction~Unaffiliated|)]SEX~Sex|SPECIES~Species
&FINGER`COLUMN2 [u(cobj,finger)]=[if(isdbref(u(cobj,gms)),RANK~Rank~---|)]JOB~Job
&FINGER`EXTRA [u(cobj,finger)]=GROUPNAMES2~Groups|QUOTE~Quote|PROFILE~Profile|SKILLS~Skills|WIKI~Wiki
&FINGER`PLAYERSET [u(cobj,finger)]=JOB|SPECIES|QUOTE|PROFILE|SKILLS
&FINGER`ADMINSET [u(cobj,finger)]=

&SWITCHES`OOCFINGER`PLAYER [u(cobj,finger)]=SET
&SWITCHES`OOCFINGER`ADMIN [u(cobj,finger)]=

&INC`OOCFINGER`MAIN [u(cobj,finger)]=@attach %!/INC`CHECKPC=u(strfirstof,%0,%#),1,OOCFINGER;@pemit %#=u(HEADER,%q<t1name>);@dolist/inline/delimit | [v(OOCFINGER`COLUMN)]={@pemit %#=align(1. >[v(OOCFINGER`WIDTH)] 1. [sub(u(width,%#),v(OOCFINGER`WIDTH),7)] 1.,ansi(u(color,%#,FINGER,BORDER),|),elements(##,2,~),ansi(u(color,%#,FINGER,BORDER),|),u(getproperty,%q<t1>,elements(##,1,~)),ansi(u(color,%#,FINGER,BORDER),|))};@dolist/inline/delimit | [v(OOCFINGER`EXTRA)]={@select/inline t(strlen(u(setr,found,u(getproperty,%q<t1>,##))))=1,{@pemit %#=u(subheader,u(capnames,##));@pemit %#=align(1. [sub(u(width,%#),4)] 1.,ansi(u(color,%#,FINGER,BORDER),|),edit(%q<found>,%t,space(8)),ansi(u(color,%#,FINGER,BORDER),|))}};@pemit %#=u(SUBHEADER);@select/inline nor(u(isadmin,%#),strmatch(%#,%q<t1>))=1,{@trigger %q<t1>/AOOCFINGER=%#,u(moniker,%#)}

&INC`OOCFINGER`SET [u(cobj,finger)]=@attach %!/INC`CHECKPC=if(cand(u(isadmin,%#),strmatch(%0,*/*)),before(%0,/),%#),1;@attach %!/INC`PARTIAL=if(cand(u(isadmin,%#),strmatch(%0,*/*)),after(%0,/),%0),setunion(v(OOCFINGER`PLAYERSET),if(u(isadmin,%#),v(OOCFINGER`ADMINSET)),|,|),|,OOCFINGER,field,field;@select/inline strlen(%1)=0,{@attach %!/INC`VERIFY={[ansi(hr,WARNING:)] Setting %q<field> to nothing will clear it. Are you sure? Enter that again to verify!},CLEAR OOCFINGER %q<field>};&D`OOCFINGER`%q<field> %q<t1>=%1;@attach %!/INC`MSG=You set [if(strmatch(%#,%q<t1>),your,%q<t1name>'s)] [u(capnames,%q<field>)] Finger Field to: %1;@select/inline strmatch(%#,%q<t1>)=0,{@attach %!/INC`MSG={Your [u(capnames,%q<field>)] OOCFinger Field is now: %1},%q<t1>}

&OOCFINGER`COLUMN [u(cobj,finger)]=ALIAS~Alias|MAIL~Mush Mail|LAST~Last On|EMAIL~Email|CONTACT~Contact|HOMEPAGE~Homepage|THEME~Theme Song|ALTS~Alts|OTHER~Other|ACTOR~Actor|VOICEACTOR~Voice Actor
&OOCFINGER`WIDTH [u(cobj,finger)]=11
&OOCFINGER`EXTRA [u(cobj,finger)]=NOTES
&OOCFINGER`PLAYERSET [u(cobj,finger)]=EMAIL|CONTACT|HOMEPAGE|THEME|OTHER|VOICEACTOR|ACTOR|NOTES[if(isdbref(u(cobj,ams)),,|ALTS)]
&OOCFINGER`ADMINSET [u(cobj,finger)]=

@@ HELP SECTION
+help/add +finger=[u(cobj,finger)]/HLP`FINGER
+help/category +Finger=Character

+help/add +oocfinger=[u(cobj,finger)]/HLP`OOCFINGER
+help/category +oocfinger=Character

&HLP`FINGER [u(cobj,finger)]=The +finger system keeps track of and presents in-character information for some community flavor. By reading someone's finger, you can gain a brief overview of what the character is about!%R%R[ansi(hc,Commands)]%R[align(5 72,,[ansi(h,+finger)] - Show your own finger.%R[ansi(h,+finger <name>)] - Show someone else's +finger.%R[ansi(h,+finger/set <field>=<text>)] - Set a finger field. Setting to nothing may clear it.%R[ansi(h,+finger/get <field>)] - Retrieves a field for editing. Can be used on other players as <player>/<field>)]%R%RFields settable by players: [u(itemize,get(u(cobj,finger)/FINGER`PLAYERSET),|,and,\,)]%RFields Only settable by Admin: [u(itemize,get(u(cobj,finger)/FINGER`ADMINSET),|,and,\,)]%R%RFields not listed here are controlled by other factors.[if(u(isadmin,%#),%R%R[u(subheader,Admin Section)]%RAdmin may set the fields of other characters using [ansi(h,+finger/set <name>/<field>=<text>)])]

&HLP`OOCFINGER [u(cobj,finger)]=The +oocfinger system keeps track of and presents out of character information for your fellow players' convenience.%R%R[ansi(hc,Commands)]%R[align(5 72,,[ansi(h,+oocfinger)] - Show your own OOCfinger.%R[ansi(h,+oocfinger <name>)] - Show someone else's +oocfinger.%R[ansi(h,+oocfinger/set <field>=<text>)] - Set an oocfinger field. Setting to nothing may clear it.)]%R%RFields settable by players: [u(itemize,get(u(cobj,finger)/OOCFINGER`PLAYERSET),|,and,\,)]%RFields Only settable by Admin: [u(itemize,get(u(cobj,finger)/OOCFINGER`ADMINSET),|,and,\,)]%R%RFields not listed here are controlled by other factors.%RThe EMAIL field is special. If you wish to hide your email from everyone but admin, prefix it with a ! - for example\, !user@site.com. This is different from your Account email\, which is seen only by admin.[if(u(isadmin,%#),%R%R[u(subheader,Admin Section)]%RAdmin may set the fields of other characters using [ansi(h,+oocfinger/set <name>/<field>=<text>)])]