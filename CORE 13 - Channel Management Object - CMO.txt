@@ DEPENDENCIES - KLS (Key Lock System)
@@ Without the KLS, the LOCK/UNLOCK flags will NOT function.

@switch/inline isdbref(u(cmo))=0,{@tel create(Channel Management Object <CMO>)=config(master_room)}
&cmo u(coi)=locate(config(master_room),Channel Management Object <CMO>,TXxi)
@parent u(cmo)=u(coi)
@set u(cmo)=WIZARD !NO_COMMAND
&cmo`staffrep u(coi)=Staff Reports
&cmo`public u(coi)=Public

@switch/inline isdbref(u(cmo))=1,{@switch/inline isdbref(u(cdb))=0,{@tel create(Channel Database Object <CDB>)=u(cmo)}}
&cdb u(coi)=locate(u(cmo),Channel Database Object <CDB>,TXxi)
@parent u(cmo)=u(coi)
@set u(cmo)=WIZARD !NO_COMMAND

&CMD`+CHANNEL u(cmo)=$^(?s)(?\:\+)?(?\:channel|chan)(?\:/(\S+)?)?(?\: +(.+?))?(?\:/(.+?))?(?\:=(.*))?$:@include u(ccs)/INC`PARTIAL=%1,setunion(get(u(cmo)/VAR`CHANNEL`PLAYFLAGS),if(isadmin(%#),get(u(cmo)/VAR`CHANNEL`ADMINFLAGS)),|,|),|,CHANNEL,switch,switch;@include u(cmo)/INC`CHANNEL`[strfirstof(%q<switch>,MAIN)]=%2,%4,%3
@set u(cmo)/CMD`+CHANNEL=regexp

&VAR`CHANNEL`PLAYFLAGS u(cmo)=MUZZLE|UNMUZZLE|ALIAS|RECALL
&VAR`CHANNEL`ADMINFLAGS u(cmo)=ADD|DEL|TAG|RENAME|CONFIG

&CMD`+CLOCK u(cmo)=$^(?\:\+)?(?\:chanlock|clock)(?\:/(\S+)?)?(?\: +(.+?))?(?\:/(.+?))?(?\:=(.*))?$:@include u(ccs)/INC`PARTIAL=%1,setunion(get(u(cmo)/VAR`CLOCK`PLAYFLAGS),if(isadmin(%#),get(u(cmo)/VAR`CLOCK`ADMINFLAGS)),|,|),|,CHANNEL,switch,switch;@include u(cmo)/INC`CLOCK=%2,%3,%4,%5
@set u(cmo)/CMD`+CLOCK=regexp

&VAR`CLOCK`PLAYFLAGS u(cmo)=
&VAR`CLOCK`ADMINFLAGS u(cmo)=JOIN|SPEAK|SEE|HIDE|MOD

&INC`CHANNEL`MAIN u(cmo)=@switch/inline strlen(%0)=0,{@nspemit %#=header(mudname() Channel Status)%RON?%B[ljust(NAME,32)]USERS%B%B%B%B%BDESCRIPTION;@dolist/inline/delimit | [objeval(%#,channels(|))]={th setq(id,u(u(cmo)/FUN`FINDCHANNEL,%i0));@nspemit %#=align(3 31 9 30,rjust(cstatus(%i0,%#),3),ljust(pueblize(%i0,+channel [stripansi(%i0)]),31),ljust(rjust(words(filtervis(%#,cwho(%i0,on),CHANNEL,%i0)),3,0)/[rjust(words(filtervis(%#,cwho(%i0,all),CHANNEL,%i0)),3,0)],8),cdesc(%i0))};@nspemit %#=header()},{@include u(ccs)/INC`PARTIAL=%0,objeval(%#,channels(|)),|,CHANNEL,channel,channel;@include u(cmo)/INC`CHECKNAME=%q<channel>,1,1;@nspemit %#=header(Channel Info for: %q<channel>);@nspemit %#=OWNER: [name(cowner(%q<channel>))]%RBUFFER: [cbuffer(%q<channel>)] - [cmsgs(%q<channel>)] Messages stored!%RFLAGS: [clflags(%q<channel>)]%RDESC: [cdesc(%q<channel>)];@switch/inline t(get(u(cdb)/%q<id>`CONFIG`ALIAS))=0,{@nspemit %#=ansi(hg,ONLINE USERS:) [words(filtervis(%#,cwho(%q<channel>,on),CHANNEL,%q<channel>))] - [itemize(iter(filtervis(%#,cwho(%q<channel>,on),CHANNEL,%q<channel>),if(valnum(conn(%i0)),name(%i0),ansi(hx,name(%i0)))[if(strlen(clflags(%q<channel>,%i0)),\([clflags(%q<channel>,%i0)]\))],%b,|),|,and,\,)]%R;@nspemit %#=ansi(hx,OFFLINE USERS:) [words(filtervis(%#,cwho(%q<channel>,off),CHANNEL,%q<channel>))] - [itemize(iter(filtervis(%#,cwho(%q<channel>,off),CHANNEL,%q<channel>),name(%i0)[if(strlen(clflags(%q<channel>,%i0)),\([clflags(%q<channel>,%i0)]\))],%b,|),|,and,\,)]},1,{@nspemit %#=ONLINE USERS (ALIAS): [words(filtervis(%#,cwho(%q<channel>,all),CHANNEL,%q<channel>))] - [itemize(iter(filtervis(%#,cwho(%q<channel>,all),CHANNEL,%q<channel>),strfirstof(get(%i0/D`CHANNEL`%q<id>`ALIAS),name(%i0))[if(strlen(clflags(%q<channel>,%i0)),\([clflags(%q<channel>,%i0)]\))],%b,|),|,and,\,)];@switch/inline isadmin(%#)=1,{@nspemit %#=subheader(Staff Only Section);@nspemit %#=USERS: [words(cwho(%q<channel>,all))] - [itemize(iter(cwho(%q<channel>,all),if(valnum(conn(%i0)),name(%i0),ansi(hx,name(%i0)))[if(strlen(get(%i0/D`CHANNEL`%q<id>`ALIAS)),%b-%b[get(%i0/D`CHANNEL`%q<id>`ALIAS)])][if(strlen(clflags(%q<channel>,%i0)),\([clflags(%q<channel>,%i0)]\))],%b,|),|,and,\,)]}};@nspemit %#=header()}

&INC`CHANNEL`MUZZLE u(cmo)=@switch/inline strlen(%0)=0,{@include u(cmo)/INC`CHANNEL`MUZZLE`SHOWALL},{@include u(ccs)/INC`CHECKPC=%0,1,CHANNEL;@break isadmin(%q<t1>)=@nspemit %#=announce(CHANNEL) ERROR: %q<t1name> is staff.;@include u(ccs)/INC`PARTIAL=%2,objeval(%#,channels(|)),|,CHANNEL,channel,channel;@assert or(isadmin(%#),objeval(%#,testlock(clock(%q<channel>/Mod),%#)))=@nspemit %#=announce(CHANNEL) ERROR: Permission denied.;@include (cmo)/INC`CHECKNAME=%2,1,1;@break gt(get(%q<t1>/D`CHANNEL`%q<id>`MUZZLE),0)=@nspemit %#=announce(ERROR) %q<t1name> is already Muzzled from %q<channel>.;@assert strlen(%1)=@nspemit %#=announce(CHANNEL) ERROR: No Muzzle duration entered.;@assert gt(stringsecs(%1),0)=@nspemit %#=announce(CHANNEL) ERROR: Invalid duration entered. Please see [pueblize(help stringsecs(),help stringsecs())] for more information.;&D`CHANNEL`%q<id>`MUZZLE %q<t1>=add(secs(),stringsecs(%1));@nspemit %#=announce(MUZZLE) You have Muzzled %q<t1name> from %q<channel> for [timestring(stringsecs(%1))] - Until [convsecs(add(secs(),stringsecs(%1)))].;@nspemit %q<t1>=announce(MUZZLE) You have been Muzzled from [get(u(cmo)/D`CHANNEL`%q<channel>)] for [timestring(stringsecs(%1))] - Until [convsecs(add(secs(),stringsecs(%1)))].;@switch/inline gt(v(VAR`ALERTMODE),0)=1,{@nscemit/noisy u(VAR`ALERTSCHANNEL)=[ansi(h,%n)] has Muzzled %q<t1name> from %q<channel> for [timestring(stringsecs(%1))] - Until [convsecs(add(secs(),stringsecs(%1)))].}}

&INC`CHANNEL`MUZZLE`SHOWALL u(cmo)=th setq(cids,filterbool(#lambda/valnum(\%0),iter(objeval(%#,channels(|)),u(FUN`FINDCHANNEL,%i0),|,|),|,|));@nspemit %#=header(mudname() Channel Muzzles);@dolist/inline/delimit %q<cids>={@nspemit %#=pueblize(setr(cname,elements(channels(|),match(channels(|),get(u(cmo)/%i0),|),|)),+channel [stripansi(%q<cname>)]): [itemize(iter(lsearch(all,eplayer,\[gt(get(##/D`CHANNEL`%i0`MUZZLE),secs())\]),ansi(h,name(%i0)) for [convsecs(get(%i0/D`CHANNEL`%i1`MUZZLE))] \([timestring(sub(get(%i0/D`CHANNEL`%i1`MUZZLE),secs()))]\),%b,|),|,and,\,)]};@nspemit %#=header()

&INC`CHANNEL`UNMUZZLE u(cmo)=@include u(ccs)/INC`CHECKPC=%0,1,CHANNEL;@break isadmin(%q<t1>)=@nspemit %#=announce(CHANNEL) ERROR: %q<t1name> is staff.;@include u(ccs)/INC`PARTIAL=%2,objeval(%#,channels(|)),|,CHANNEL,channel,channel;@assert or(isadmin(%#),objeval(%#,testlock(clock(%q<channel>/Mod),%#)))=@nspemit %#=announce(CHANNEL) ERROR: Permission denied.;@include (cmo)/INC`CHECKNAME=%2,1,1;@assert gt(get(%q<t1>/D`CHANNEL`%q<id>`MUZZLE),0)=@nspemit %#=announce(ERROR) %q<t1name> is already Muzzled from %q<channel>.;@wipe %q<t1>/D`CHANNEL`%q<id>`MUZZLE;@nspemit %#=announce(CHANNEL) You have unMuzzled %q<t1name> from %q<channel>.;@nspemit %q<t1>=announce(MUZZLE) You have been unMuzzled from %q<channel>.;@switch/inline gt(v(VAR`ALERTMODE),0)=1,{@nscemit/noisy u(VAR`ALERTSCHANNEL)=[ansi(h,%n)] has unMuzzled %q<t1name> from %q<channel>.}

&INC`CHANNEL`RECALL u(cmo)=@assert strlen(%0)=@nspemit %#=announce(CHANNEL) ERROR: No channel entered to recall.;@include u(ccs)/INC`PARTIAL=%0,objeval(%#,channels(|)),|,CHANNEL,channel,channel;@force/inline %#=@chan/recall %q<channel>=sub(secs(),convtime(get(%#/lastlogout)))s

&INC`CHANNEL`ADD u(cmo)=@include u(cmo)/INC`CHECKNAME=%0,0,0;@break match(channels(|),%q<name>,|)=@nspemit %#=announce(CHANNEL) ERROR: A Channel of that name already exists.;@channel/add %0=v(VAR`CHANNEL`DEFAULTPRIVS);@channel/buffer %q<name>=10;@channel/mogrifier %q<name>=u(cmo);@include u(cmo)/INC`CHECKNAME=%q<name>,1,1;@nspemit %#=announce(CHANNEL) Channel %q<name> created!;@switch/inline gt(v(VAR`ALERTMODE),0)=1,{@nscemit/noisy u(VAR`ALERTSCHANNEL)=ansi(h,%n) has created the %0 channel!}

&VAR`CHANNEL`DEFAULTPRIVS u(cmo)=PQ

&INC`CHANNEL`DEL u(cmo)=@include u(ccs)/INC`PARTIAL=%0,objeval(%#,channels(|)),|,CHANNEL,channel,channel;@include u(cmo)/INC`CHECKNAME=%q<channel>,1,1;@include u(ccs)/INC`VERIFY={[ansi(hr,WARNING:)] This will delete channel %q<name>. Are you sure? Enter the same command within ten seconds to continue!},CHANNEL DELETE %q<cname>,CHANNEL;@wipe u(cmo)/%q<id>;@channel/delete %q<cname>;@switch/inline gt(v(VAR`ALERTMODE),0)=1,{@nscemit/noisy u(VAR`ALERTSCHANNEL)=ansi(h,%n) has deleted the %q<name> channel!}

&INC`CHANNEL`RENAME u(cmo)=@include u(ccs)/INC`PARTIAL=%0,objeval(%#,channels(|)),|,CHANNEL,channel,channel;@assert or(isadmin(%#),objeval(%#,testlock(clock(%q<channel>/Mod),%#)))=@nspemit %#=announce(CHANNEL) ERROR: Permission denied.;@assert strlen(%1)=@nspemit %#=announce(CHANNEL) ERROR: New Name field Empty.;@include u(cmo)/INC`CHECKNAME=%1,0,0;@break match(channels(|),%q<name>,|,|),%1,|)=@nspemit %#=announce(CHANNEL) ERROR: A channel by that name already exists.;@include u(cmo)/INC`CHECKNAME=%q<channel>,1,1;@channel/name %q<channel>=trim(%1);&%q<id> u(cmo)=trim(stripansi(%1));@nspemit %#=announce(CHANNEL) You have renamed channel %q<channel> to: [trim(%1)]!;@switch/inline gt(v(VAR`ALERTMODE),0)=1,{@nscemit/noisy u(VAR`ALERTSCHANNEL)=ansi(h,%n) has renamed the %q<name> channel to %1!}

&INC`CHANNEL`CONFIG u(cmo)=@include u(ccs)/INC`PARTIAL=%0,objeval(%#,channels(|)),|,CHANNEL,channel,channel;@assert or(isadmin(%#),objeval(%#,testlock(clock(%q<channel>/Mod),%#)))=@nspemit %#=announce(CHANNEL) ERROR: Permission denied.;@include u(cmo)/INC`CHECKNAME=%q<channel>,1,1;@include u(ccs)/INC`PARTIAL=%2,ALIAS,|,CHANNEL,parameter,parameter;@switch/inline strlen(%1)=0,{&%q<id>`CONFIG`%q<parameter> u(cdb);@nspemit %#=announce(CHANNEL) You cleared Channel %q<cname>'s %q<parameter> Setting.},{@include u(cmo)/INC`CHANNEL`CONFIG`%q<parameter>;&%q<id>`CONFIG`%q<parameter> u(cdb)=%q<entry>;@nspemit %#=announce(CHANNEL) You set Channel %q<cname>'s %q<parameter> Setting to: %q<entry>};@switch/inline gt(v(VAR`ALERTMODE),0)=1,{@nscemit/noisy u(VAR`ALERTSCHANNEL)=ansi(h,%n) [if(strlen(%1),set,cleared)] the %q<cname> Channel's %q<parameter> Setting[if(strlen(%1),%bto %1!,.)]}

&INC`CHANNEL`CONFIG`ALIAS u(cmo)=th setq(entry,t(%1))

&INC`CHANNEL`ALIAS u(cmo)=@include u(ccs)/INC`PARTIAL=%0,objeval(%#,channels(|)),|,CHANNEL,channel,channel;@include u(cmo)/INC`CHECKNAME=%q<channel>,1,1;@assert get(u(cdb)/%q<id>`CONFIG`ALIAS)=@nspemit %#=announce(CHANNEL) ERROR: That channel does not use aliases.;@assert strlen(%1)=@nspemit %#=announce(CHANNEL) ERROR: Nothing entered for an alias!;@assert or(valid(playername,stripansi(%1)),if(pmatch(%1),strmatch(%#,pmatch(%1)),0))=@nspemit %#=announce(CHANNEL) ERROR: Aliases must follow the same name rules as names, and cannot match another player's name.;@break words(setr(list,setdiff(lsearch(all,eplayer,\[strmatch(get(##/D`CHANNEL`%q<id>`ALIAS),stripansi(%1))\]),%#)))=@nspemit %#=announce(CHANNEL) ERROR: Cannot use that Alias: in use by [itemize(iter(%q<list>,name(%i0),%b,|),|,and,\,)];@nspemit %#=announce(CHANNEL) Alias set for %q<cname> to: %1 - make sure this is a reasonable alias!;&D`CHANNEL`%q<id>`ALIAS %#=%1

&INC`CHECKNAME u(cmo)=@assert strlen(setr(name,stripansi(trim(%0))))=@nspemit %#=announce(CHANNEL) Channel Name field empty.;@assert and(lte(strlen(%q<name>),30),eq(words(%q<name>,|),1))=@nspemit %#=announce(CHANNEL) That channel name is not acceptable.;@break strmatch(%q<name>,ansi\(*)=@nspemit %#=announce(CHANNEL) ERROR: Please use brackets around colored channel names.;@switch/inline gt(%1,0)=1,{@assert setr(num,match(objeval(%#,channels(|)),%q<name>,|))=@nspemit %#=announce(CHANNEL) ERROR: Channel not found.;th setq(cname,elements(objeval(%#,channels(|)),%q<num>,|));@switch/inline gt(%2,0)=1,{@switch/inline gt(setr(id,u(u(cmo)/FUN`FINDCHANNEL,%q<name>)),0)=0,{@nspemit %#=announce(CHANNEL) WARNING: Channel exists, but wasn't found in database. Channel was added to database.;&[setr(id,nextslot(u(cdb)))] u(cdb)=stripansi(%q<cname>)}}}

&INC`CLOCK u(cmo)=@switch/inline and(gt(strlen(%0),0),isadmin(%#))=1,{@include u(ccs)/INC`PARTIAL=%0,channels(|),|,CHANNEL,channel,channel;@include u(cmo)/INC`CHECKNAME=%q<channel>,1,1;@switch/inline gt(strlen(%2),0)=1,{@assert eq(words(setr(choices,setdiff(%2,u(u(kls)/FUN`LISTLOCKS)))),0)=@nspemit %#=announce(KEY) ERROR: Following lock types not found: [itemize(%q<choices>,|,and,\,)]};@dolist/inline/delimit | [ucstr(if(strlen(%q<switch>),%q<switch>,v(VAR`CLOCKTYPES)))]={@clock/%i0 %q<name>=if(strlen(%2),iter(%2,@[u(kdb)]/%i0,%B,|));&%q<id>`CLOCK`%i0 u(cdb)=sort(ucstr(%2));@nspemit %#=announce(CHANNEL) Channel %q<cname> %i0 Locked to: [if(strlen(%2),itemize(%2,%b,and,\,),Nothing!)];@switch/inline gt(v(VAR`ALERTMODE),0)=1,{@nscemit/noisy u(VAR`ALERTSCHANNEL)=ansi(h,%n) %i0 Locked Channel %q<cname> to: [if(strlen(%2),itemize(%2,%b,and,\,),Nothing!)]}}},{@nspemit %#=header(mudname() Channel Locks);@dolist/inline/delimit | [objeval(%#,channels(|))]={th setq(id,u(FUN`FINDCHANNEL,stripansi(%i0)));@nspemit %#=%i0%R[ljust(rjust(JOIN,6): [get(u(cdb)/%q<id>`CLOCK`JOIN)],38)][ljust(rjust(SPEAK,6): [get(u(cdb)/%q<id>`CLOCK`SPEAK)],38)]%R[ljust(rjust(SEE,6): [get(u(cdb)/%q<id>`CLOCK`SEE)],38)][ljust(rjust(HIDE,6): [strfirstof(get(u(cdb)/%q<id>`CLOCK`HIDE),clock(%i0/HIDE))],38)]%R[ljust(rjust(MOD,6): [strfirstof(get(u(cdb)/%q<id>`CLOCK`MOD),clock(%i0/MOD))],25)]};@nspemit %#=header()}

&VAR`CLOCKTYPES u(cmo)=join|speak|see

&STARTUP u(cmo)=@hook/ignore @channel=u(cmo),HOOK`IGNORECHECK
&HOOK`IGNORECHECK u(cmo)=if(and(or(strmatch(first(after(%u,/)),a*),strmatch(first(after(%u,/)),ren*)),hastype(%#,PLAYER)),0[nspemit(%#,announce(CHANNEL) ERROR: Please use +channel for this purpose!)],if(and(strmatch(first(after(%u,/)),wh*),not(isadmin(%#))),0[nspemit(%#,announce(CHANNEL) ERROR: @channel/who disabled for ALIAS channel anonymity! Use +channel <name> for that!)],1))

&VAR`ALERTMODE u(cmo)=1
&VAR`ALERTSCHANNEL u(cmo)=u(CMO`STAFFREP)

&FUN`FINDCHANNEL u(cmo)=last(first(grepi(u(cdb),*,stripansi(%0))),`)

&MOGRIFY`PLAYERNAME u(cmo)=if(get(u(cdb)/[setr(id,u(u(cmo)/FUN`FINDCHANNEL,stripansi(%1)))]`CONFIG`ALIAS),strfirstof(get(%#/D`CHANNEL`%q<id>`ALIAS),%0))
&MOGRIFY`TITLE u(cmo)=if(isadmin(%#),\[[ansi(h,Staff)]\][if(strlen(%0),%B%0)],%0)

&MOGRIFY`BLOCK u(cmo)=if(and(gt(setr(until,default(%#/[setr(muzattr,D`CHANNEL`[setr(id,u(u(cmo)/FUN`FINDCHANNEL,stripansi(%1)))]`MUZZLE)],0)),secs()),not(isadmin(%#))),announce(CHANNEL) You have been Muzzled from %1 until [convsecs(%q<until>)],wipe(%#/%q<muzattr>)[if(and(get(u(cdb)/%q<id>`CONFIG`ALIAS),not(strlen(setr(alias,get(%#/D`CHANNEl`%q<id>`ALIAS)))),not(isadmin(%#))),announce(CHANNEL) That Channel uses aliases\, not names. Please set your alias using +channel/alias %1=<alias here>,if(and(get(u(cdb)/%q<id>`CONFIG`ALIAS),and(not(valid(playername,stripansi(%q<alias>))),not(strmatch(%#,pmatch(%q<alias>)))),v(VAR`ALIASNAME2)),announce(CHANNEL) Your alias may not match another player's name!))])



&VAR`ALIASNAME2 u(cmo)=0

&MOGRIFY`MESSAGE u(cmo)=regeditalli(%0,v(VAR`REGEXP`WWW),weblink(strfirstof($<2>,http://,$<1>)$<3>,$<1>))
&VAR`REGEXP`WWW u(cmo)=(?P<1>(?P<2>((http|https|ftp))\://)?(?P<3>(((25[0-5]|2[0-4][0-9]|1[0-9][0-9]|[1-9][0-9]|[0-9])\.){3}(25[0-5]|2[0-4][0-9]|1[0-9][0-9]|[1-9][0-9]|[0-9])|([a-zA-Z0-9_\-\.])+\.(com|net|org|edu|int|mil|gov|arpa|biz|aero|name|coop|info|pro|museum|uk|me))((:[a-zA-Z0-9]*)?/?([a-zA-Z0-9\-\._\?\,\'/\\\+&amp;%\$#\=~])*)))
