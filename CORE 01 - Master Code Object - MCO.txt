@@ COMMENTS WILL GO HERE

@@ RUN THIS ON CODE WIZARD OBJECT!
@switch/first/inline version()=PennMUSH*,{@switch/first/inline hasflag(%#,UNREGISTERED)=1,{@set me=!UNREGISTERED};@power me=no_pay},RhostMUSH*,{@set me=sidefx !wanderer;@admin command_quota_max=10000}

th if(isdbref(u(cobj,mco)),,setq(0,create(Master Code Object <MCO>))[tel(%q0,switch(version(),PennMUSH*,config(master_room),RhostMUSH**,globalroom()))][parent(%#,%q0)][set(%q0,GAME:[first(version())])][set(%q0,COBJ`MCO:[switch(version(),PennMUSH*,objid(%q0),RhostMUSH*,%q0)])][set(%q0,switch(version(),PennMUSH*,WIZARD SAFE,RhostMUSH*,INHERIT SAFE SIDEFX))])
@@ Yeah, it's an awful hodgepodge of side effect functions. For a good cause.

&COBJ [parent(%#)]=v(COBJ`%0)

&ATTRIB_SET [u(cobj,mco)]=u(ATTRIB_SET`[v(game)],%0,%1,%2)
&ATTRIB_SET`PENNMUSH [u(cobj,mco)]=attrib_set(%0/%1,%2)
&ATTRIB_SET`RHOSTMUSH [u(cobj,mco)]=set(%0,%1:%2,strmatch(%1,*`*))

&OBJID [u(cobj,mco)]=u(OBJID`[v(game)],%0)
&OBJID`PENNMUSH [u(cobj,mco)]=objid(%0)
&OBJID`RHOSTMUSH [u(cobj,mco)]=num(%0)

&LATTR [u(cobj,mco)]=u(LATTR`[v(game)],%0,%1)
&LATTR`PENNMUSH [u(cobj,mco)]=lattr(%0,%1)
&LATTR`RHOSTMUSH [u(cobj,mco)]=if(strlen(%1),iter(lattr(%0,,,,,1),%i0,%b,%1),lattr(%0,,,,,1))

&REGLATTR [u(cobj,mco)]==u(REGLATTR`[v(game)],%0,%1)
&REGLATTR`PENNMUSH [u(cobj,mco)]=reglattr(%0,%1)
&REGLATTR`RHOSTMUSH [u(cobj,mco)]=if(strlen(%1),iter(lattr(%0,,,,1,1),%i0,%b,%1),lattr(%0,,,,1,1))

&STRINGSECS [u(cobj,mco)]=u(STRINGSECS`[v(game)],%0)
&STRINGSECS`PENNMUSH [u(cobj,mco)]=stringsecs(%0)
&STRINGSECS`RHOSTMUSH [u(cobj,mco)]=ladd(iter(secure(regeditall(%0,(h|m|s),$1%b)),switch(%i0,*ML,mul(%i0,31536000000),*y,mul(%i0,31471200),*C,mul(%i0,3153600000),*Mo,mul(%i0,2628000),*w,mul(%i0,604800),*d,mul(%i0,86400),*h,mul(%i0,3600),*m,mul(%i0,60),*s,add(%i0,0))) 0)

&SETQ [u(cobj,mco)]=u(SETQ`[v(game)],%0,%1)
&SETQ`PENNMUSH [u(cobj,mco)]=setq(%0,%1)
&SETQ`RHOSTMUSH [u(cobj,mco)]=setq(+,%1,%0)

&SETR [u(cobj,mco)]=u(SETR`[v(game)],%0,%1)
&SETR`PENNMUSH [u(cobj,mco)]=setr(%0,%1)
&SETR`RHOSTMUSH [u(cobj,mco)]=setr(+,%1,%0)

&STARTUP [u(cobj,mco)]=@dolist/inline u(lattr,%!/STARTUP`*)={@include/nobreak %!/##}

&STARTUP`FUNCTIONS [u(cobj,mco)]=@include %!/STARTUP`FUNCTIONS`[v(game)]
&STARTUP`FUNCTIONS`PENNMUSH [u(cobj,mco)]=@dolist/inline game_config header subheader separator isadmin sortname color mxpmenu pueblize={@function %i0=u(cobj,mco),%i0;@function/restrict %i0=localize}
&STARTUP`FUNCTIONS`RHOSTMUSH [u(cobj,mco)]=@dolist/inline align sortkey game_config header subheader separator isadmin moniker lvplayers lvthings lvexits sortname color mxpmenu pueblize strfirstof table={@function/privileged %d0=%!/%d0}

&STARTUP`MAIN [u(cobj,mco)]=@include/nobreak %!/STARTUP`MAIN`[v(game)];@include/nobreak %!/STARTUP`MAIN`EXTRA

&STARTUP`MAIN`PENNMUSH [u(cobj,mco)]=@command/disable kill;@dolist/inline/nobreak @dig @open @link={@command/restrict %i0=!POWER^GUEST&!FLAG^GAGGED&(POWER^BUILDER|FLAG^WIZARD|FLAG^ROYALTY)};@dolist/inline short-desc playstatus={@attribute/access/retroactive %i0=visual};@dolist/inline D={@attribute/access/retroactive %i0=mortal_dark WIZARD}

@DESCRIBE [u(cobj,mco)]=This is Code Wizard territory. No touching!

@@ FILTER SECTION

&FIL`ISOBJID [u(cobj,mco)]=u(isobjid,%0)
&FIL`OLDOBJID [u(cobj,mco)]=not(u(isobjid,before(%0,~)))
&FIL`ISSLOT [u(cobj,mco)]=isint(last(%0,`))
&FIL`ALIASMATCH [u(cobj,mco)]=strmatch(alias(%0),%1*)
&FIL`NOTGUEST [u(cobj,mco)]=not(u(isguest,%0))
&FIL`FINDACCOUNT [u(cobj,mco)]=match(get(%0/CHARACTERS),%q<finddb>)

@@ U-FUNCTION SECTION

&NEWCOBJ [u(cobj,mco)]=localize(if(not(u(cobj,%1)),u(setr,newobj,u(objid,create(%0)))[if(not(%4),parent(%q<newobj>,u(firstof,%3,u(cobj,mco))))][tel(%q<newobj>,u(firstof,%2,u(MASTER_ROOM)))][u(attrib_set,u(cobj,mco),COBJ`%1,%q<newobj>)][set(%q<newobj>,switch(v(game),PennMUSH,%5,RhostMUSH,%6))]))
@@ %0 - @name. %1 - alias. %2 - location to store. %3 - parent, defaults to MCO. %4 - No parent. %5 - PennMUSH flags. %6 - RhostMUSH flags.

&FIND_IN [u(cobj,mco)]=u(FIND_IN`[v(game)],%0,%1)
&FIND_IN`PENNMUSH [u(cobj,mco)]=locate(%0,%1,TXxi)
&FIND_IN`RHOSTMUSH [u(cobj,mco)]=u(firstof,locate(%0,%1,Tia),locate(%0,%1,Ti))

&ACCID [u(cobj,mco)]=localize(if(%1,u(filter,FINDACCOUNT,children(u(cobj,accounts)),%b,%b,u(setq,finddb,%0)),switch(1,isint(%0),u(strfirstof,u(FIND_IN,u(cobj,accounts),Account %0),0),isdbref(%0),switch(type(%0),PLAYER,get(%0/D`ACCOUNT),THING,if(u(FIND_IN,u(cobj,accounts),%0),%0,0)))))

&ALTS [u(cobj,mco)]=filter(ISOBJID,get(u(accid,%0)/CHARACTERS))
&PALTS [u(cobj,mco)]=alts(%@)

&ISGUEST [u(cobj,mco)]=u(ISGUEST`[v(game)],%0)
&ISGUEST`PENNMUSH [u(cobj,mco)]=haspower(%0,GUEST)
&ISGUEST`RHOSTMUSH [u(cobj,mco)]=hasflag(%0,GUEST)

&ISADMIN [u(cobj,mco)]=u(ISADMIN`[v(game)],%0)
&ISADMIN`PENNMUSH [u(cobj,mco)]=orflags(%0,Wr)
&ISADMIN`RHOSTMUSH [u(cobj,mco)]=gte(bittype(%0),2)

&ISADMIN2 [u(cobj,mco)]=u(ISADMIN`[v(game)],%#)

&ISWIZARD [u(cobj,mco)]=u(ISWIZARD`[v(game)],%0)
&ISWIZARD`PENNMUSH [u(cobj,mco)]=hasflag(%0,WIZARD)
&ISWIZARD`RHOSTMUSH [u(cobj,mco)]=gte(bittype(%0),6)

&ISHIDDEN [u(cobj,mco)]=u(ISHIDDEN`[v(game)],%0)
&ISHIDDEN`PENNMUSH [u(cobj,mco)]=hidden(%0)
&ISHIDDEN`RHOSTMUSH [u(cobj,mco)]=ishidden(%0)

&MASTER_ROOM [u(cobj,mco)]=switch(v(game),PennMUSH,config(master_room),RhostMUSH,globalroom())
&OPEN_CREATION [u(cobj,mco)]=switch(v(game),PennMUSH,strmatch(config(player_creation),yes),RhostMUSH,!member(config(register_host),*))

&NATTR [u(cobj,mco)]=u(LATTR`[v(game)],%0)
&NATTR`PENNMUSH [u(cobj,mco)]=nattr(%0)
&NATTR`RHOSTMUSH [u(cobj,mco)]=first(lattr(%0,,l,,,1))

&FILTER [u(cobj,mco)]=u(FILTER`[v(game)],%0,%1,%2,%3,%4,%5,%6,%7,%8,%9)
&FILTER`PENNMUSH [u(cobj,mco)]=filter(FIL`%0,%1,%2,%3)
&FILTER`RHOSTMUSH [u(cobj,mco)]=filter(FIL`%0,%1,%2,%3,%4,%5,%6,%7,%8,%9)

&ISOBJID [u(cobj,mco)]=u(ISOBJID`[v(game)],%0)
&ISOBJID`PENNMUSH [u(cobj,mco)]=isobjid(%0)
&ISOBJID`RHOSTMUSH [u(cobj,mco)]=isdbref(%0)

&CSECS [u(cobj,mco)]=u(CSECS`[v(game)],%0)
&CSECS`PENNMUSH [u(cobj,mco)]=csecs(%0)
&CSECS`RHOSTMUSH [u(cobj,mco)]=convtime(createtime(%0))

&CTIME [u(cobj,mco)]=u(CSECS`[v(game)],%0)
&CTIME`PENNMUSH [u(cobj,mco)]=ctime(%0)
&CTIME`RHOSTMUSH [u(cobj,mco)]=createtime(%0)

&FANCYTIME [u(cobj,mco)]=u(FANCYTIME`[v(game)],%0,%1)
&FANCYTIME`PENNMUSH [u(cobj,mco)]=timefmt($b $d $I:$M$p $Z,%0,strfirstof(u(tz,%1),u(tz,%#)))
&FANCYTIME`RHOSTMUSH [u(cobj,mco)]=timefmt($b $D $02H:$02T$P $i,u(strfirstof,%0,secs()),u(tz,u(strfirstof,%1,%#)))

&TZ [u(cobj,mco)]=localize(default(%0/TZ,if(u(setr,accid,u(accid,%0)),get(%q<accid>/TZ),UTC)))

&CHARSEARCH [u(cobj,mco)]=u(lmax,iter(%1,strmatch(%0,*%i0*)))

&LMAX [u(cobj,mco)]=u(LMAX`[v(game)],%0,%1)
&LMAX`PENNMUSH [u(cobj,mco)]=lmath(max,%0,%1)
&LMAX`RHOSTMUSH [u(cobj,mco)]=lmax(%0,%1)

&LADD [u(cobj,mco)]=u(LADD`[v(game)],%0,%1)
&LADD`PENNMUSH [u(cobj,mco)]=lmath(add,%0,%1)
&LADD`RHOSTMUSH [u(cobj,mco)]=ladd(%0,%1)

&AGO [u(cobj,mco)]=u(AGO`[v(game)],%0)
&AGO`PENNMUSH [u(cobj,mco)]=switch(1,lt(%0,60),%0s,lt(%0,3600),div(%0,60)m,lt(%0,86400),div(%0,3600)h,div(%0,86400)d)
&AGO`RHOSTMUSH [u(cobj,mco)]=singletime(%0)

&SMALLTIME [u(cobj,mco)]=u(SMALLTIME`[v(game)],%0)
&SMALLTIME`PENNMUSH [u(cobj,mco)]=etime(%0,3)
&SMALLTIME`RHOSTMUSH [u(cobj,mco)]=singletime(%0)

&CAPNAMES [u(cobj,mco)]=regeditalli(lcstr(%0),v(REG`CAPNAMES),capstr($1),v(REG`CAPNAMES3),lcstr($0),v(REG`CAPNAMES2),$1[capstr($2)])

&COMMAFY [u(cobj,mco)]=u(COMMAFY`[v(game)],%0)
&COMMAFY`PENNMUSH [u(cobj,mco)]=squish(flip(foreach(#lambda/if(mod(\%1,3),\%0,\\,\%0),flip(%0))),\,)
&COMMAFY`RHOSTMUSH [u(cobj,mco)]=squish(flip(foreach(#lambda/if(mod([lit(%1)],3),[lit(%0)],\\,[lit(%0)]),flip(%0))),\,)

@@ Maybe universal?
@@ [switch(flip(foreach(#lambda/[lit([if(mod(%1,3),%0,%,%0)])],flip(%0))),%,)]

&SETSTAT [u(cobj,mco)]=localize(u(delstat,%0,%1)[u(attrib_set,%0,setunion(get(%0),%1~%2,|))])
&DELSTAT [u(cobj,mco)]=u(attrib_set,%0,setdiff(get(%0),grab(get(%0),%1~*,|),|))
&GETSTAT [u(cobj,mco)]=if(strlen(%2),add(rest(grab(get(%0),%1~*,|),~),rest(grab(get(before(%0,/)/D`%2),%1~*,|),~)),rest(grab(get(%0),%1~*,|),~))
&GRABSTAT [u(cobj,mco)]=rest(grab(%0,%1~*,|),~)

&LISTMAX [u(cobj,mco)]=u(lmax,iter(%0,strlen(%i0),|,|),|)

&NEXTSLOT [u(cobj,mco)]=add(1,u(lmax,iter(u(filter,ISSLOT,u(lattr,%0/[if(strlen(%1),%1`*,*)])),last(%i0,`))))

&MONIKER [u(cobj,mco)]=u(MONIKER`[v(game)],%0)
&MONIKER`PENNMUSH [u(cobj,mco)]=moniker(%0)
&MONIKER`RHOSTMUSH [u(cobj,mco)]=cname(%0)

&NUMTH [u(cobj,mco)]=%0[switch(%0,11,th,12,th,13,th,switch(right(%0,1),1,st,2,nd,3,rd,th))]

&PUEBLIZE [u(cobj,mco)]=u(MXPMENU`[v(game)],%0,%1)

&MXPMENU [u(cobj,mco)]=u(MXPMENU`[v(game)],%0,%1,%2,%3)
&MXPMENU`PENNMUSH [u(cobj,mco)]=if(u(CAN_MXP,u(strfirstof,%3,%#)),tagwrap(send,"[render(u(strfirstof,%1,%0),html)]" hint="[render(switch(words(u(strfirstof,%0,%2),|),>1,Right Click for Menu \(Default: [first(u(strfirstof,%2,%1,%0),|)]\)|[rest(u(strfirstof,%2,%1,%0),|)],u(strfirstof,%0,%2)),html)]",first(%0,|)),if(u(CAN_PUEBLO,u(strfirstof,%3,%#)),tagwrap(a,XCH_CMD="[render(u(strfirstof,first(%1,|),first(%0,|)),html)]",first(%0,|))))
&MXPMENU`RHOSTMUSH [u(cobj,mco)]=%0

&CAN_MXP [u(cobj,mco)]=1
&CAN_PUEBLO [u(cobj,mco)]=1

&TIMESTAMP [u(cobj,mco)]=u(TIMESTAMP`[v(game)],%0)
&TIMESTAMP`PENNMUSH [u(cobj,mco)]=timefmt($Y-$m-$d $H:$M:$S,u(firstof,%0,secs()))
&TIMESTAMP`RHOSTMUSH [u(cobj,mco)]=ptimefmt($Y-$m-$d $H:$M:$S,u(firstof,%0,secs()))

&UNPRIV [u(cobj,mco)]=localize(u(setq,unprivtext,%0)[objeval(u(cobj,ap),s(%q<unprivtext>))])

&VALNUM [u(cobj,mco)]=cand(isint(%0),gt(%0,0))

&WEBLINK [u(cobj,mco)]=u(WEBLINK`[v(game)],%0,%1)
&WEBLINK`PENNMUSH [u(cobj,mco)]=localize(if(strlen(%0),tagwrap(a,href="[render(u(setr,webfix,if(not(strmatch(%0,*://*)),http://%0,%0)),html)]",strfirstof(%1,%q<webfix>))))
&WEBLINK`RHOSTMUSH [u(cobj,mco)]=%0

&RYG [u(cobj,mco)]=<[if(gt(%0,50),255,round(mul(255,fdiv(mul(%0,2),100)),0))] [if(gte(%0,50),sub(mul(255,2),round(mul(255,fdiv(mul(%0,2),100)),0)),255)] 0>

&ALIASGRAB [u(cobj,mco)]=u(ALIASGRAB`[v(game)],%0,%1)
&ALIASGRAB`PENNMUSH [u(cobj,mco)]=if(match(%0,%1),%1,first(filter(#lambda/strmatch(alias(\%0),%1*),u(sortalias,%0))))
&ALIASGRAB`RHOSTMUSH [u(cobj,mco)]=if(match(%0,%1),%1,first(u(filter,ALIASMATCH,u(sortalias,%0),%b,%b,%1)))

&ALIASGRABALL [u(cobj,mco)]=u(ALIASGRABALL`[v(game)],%0,%1)
&ALIASGRABALL`PENNMUSH [u(cobj,mco)]=if(match(%0,%1),%1,filter(#lambda/strmatch(alias(\%0),%1*),sortkey(#lambda/alias(\%0),%0,i)))
&ALIASGRABALL`RHOSTMUSH [u(cobj,mco)]=if(match(%0,%1),%1,u(filter,ALIASMATCH,u(sortalias,%0),%b,%b,%1))

&RANDWORD [u(cobj,mco)]=u(RANDWORD`[v(game)],%0,%1)
&RANDWORD`PENNMUSH [u(cobj,mco)]=randword(%0,%1)
&RANDWORD`RHOSTMUSH [u(cobj,mco)]=randextract(%0,1,%1)

&STRFIRSTOF [u(cobj,mco)]=u(STRFIRSTOF`[v(game)],%0,%1,%2,%3,%4,%5,%6,%7,%8,%9)
&STRFIRSTOF`PENNMUSH [u(cobj,mco)]=strfirstof(%0,%1,%2,%3,%4,%5,%6,%7,%8,%9)
&STRFIRSTOF`RHOSTMUSH [u(cobj,mco)]=ofparse(5,%0,%1,%2,%3,%4,%5,%6,%7,%8,%9)

&FIRSTOF [u(cobj,mco)]=u(FIRSTOF`[v(game)],%0,%1,%2,%3,%4,%5,%6,%7,%8,%9)
&FIRSTOF`PENNMUSH [u(cobj,mco)]=firstof(%0,%1,%2,%3,%4,%5,%6,%7,%8,%9)
&FIRSTOF`RHOSTMUSH [u(cobj,mco)]=ofparse(1,%0,%1,%2,%3,%4,%5,%6,%7,%8,%9)

&ITEMIZE [u(cobj,mco)]=u(ITEMIZE`[v(game)],%0,%1,%2,%3)
&ITEMIZE`PENNMUSH [u(cobj,mco)]=itemize(%0,%1,%2,%3)
&ITEMIZE`RHOSTMUSH [u(cobj,mco)]=elist(%0,%2,%1,%4,%3)

&TABLE [u(cobj,mco)]=u(TABLE`[v(game)],%0,%1,%2,%3,%4)
&TABLE`PENNMUSH [u(cobj,mco)]=table(%0,%1,%2,%3,%4)
&TABLE`RHOSTMUSH [u(cobj,mco)]=columns(%0,switch([gt(words(%1),0)][gt(words(%2),0)],11,min(%1,%2),01,max(10,%2),*,10),switch([gt(words(%1),0)][gt(words(%2),0)],11,max(1,div(%2,%1)),10,max(1,div(78,%1)),01,max(1,div(%2,10)),*,7),L,0,1,,ifelse(strlen(%4),%4,%b),,1,%3)

@@ Rhost Functions
&ALIGN [u(cobj,mco)]=[printf([iter(%0,$[case(mid(%i0,0,1),<,-[delete(%i0,0,1)],>,[delete(%i0,0,1)],_,_[delete(%i0,0,1)],-,^[delete(%i0,0,1)],=,_[delete(%i0,0,1)],-%i0)]|"s)],%1,%2,%3,%4,%5,%6,%7,%8,%9)]
&SORTKEY [u(cobj,mco)]=munge(#lambda/%[sort%(%%0%,%2%,%3%)%],iter(%1,edit(u(%0,itext(0)),%b,),ifelse(!$v(3),%b,%3),ifelse(!$v(3),%b,%3)),%1,ifelse(!$v(3),%b,%3),ifelse(!$v(4),ifelse(!$v(3),%b,%3),%4))
@@

&BASECONV [u(cobj,mco)]=u(BASECONV`[v(game)],%0,%1,%2)
&BASECONV`PENNMUSH [u(cobj,mco)]=baseconv(%0,%1,%2)
&BASECONV`RHOSTMUSH [u(cobj,mco)]=pack(unpack(%0,%1),%2)

&WIDTH [u(cobj,mco)]=u(WIDTH`[v(game)],%0)
&WIDTH`PENNMUSH [u(cobj,mco)]=width(%0)
&WIDTH`RHOSTMUSH [u(cobj,mco)]=78

&CENTER [u(cobj,mco)]=u(CENTER`[v(game)],%0,%1,%2)
&CENTER`PENNMUSH [u(cobj,mco)]=center(%0,%1,%2)
&CENTER`RHOSTMUSH [u(cobj,mco)]=printf($^[u(strfirstof,%1,78)]:%2:s,%0)

&HASATTRVAL [u(cobj,mco)]=u(HASATTRVAL`[v(game)],%0)
&HASATTRVAL`PENNMUSH [u(cobj,mco)]=hasattrval(%0)
&HASATTRVAL`RHOSTMUSH [u(cobj,mco)]=!!$get(%0,%1)

&LWHOID [u(cobj,mco)]=u(LWHOID`[v(game)],%0)
&LWHOID`PENNMUSH [u(cobj,mco)]=lwhoid(%0)
&LWHOID`RHOSTMUSH [u(cobj,mco)]=lwho(0,%0)

&ALIAS [u(cobj,mco)]=u(ALIAS`[v(game)],%0)
&ALIAS`PENNMUSH [u(cobj,mco)]=alias(%0)
&ALIAS`RHOSTMUSH [u(cobj,mco)]=switch(type(%0),EXIT,elements(fullname(%0),2,;),get(%0/ALIAS))

&PUEBLO [u(cobj,mco)]=u(PUEBLO`[v(game)],%0)
&PUEBLO`PENNMUSH [u(cobj,mco)]=pueblo(%0)
&PUEBLO`RHOSTMUSH [u(cobj,mco)]=0

&LVPLAYERS [u(cobj,mco)]=u(LVPLAYERS`[v(game)],%0)
&LVPLAYERS`PENNMUSH [u(cobj,mco)]=lvplayers(%0)
&LVPLAYERS`RHOSTMUSH [u(cobj,mco)]=lcon(%0/CONNECT)

&LPLAYERS [u(cobj,mco)]=u(LPLAYERS`[v(game)],%0)
&LPLAYERS`PENNMUSH [u(cobj,mco)]=lplayers(%0)
&LPLAYERS`RHOSTMUSH [u(cobj,mco)]=lcon(%0/PLAYER)

&LVTHINGS [u(cobj,mco)]=u(LVTHINGS`[v(game)],%0)
&LVTHINGS`PENNMUSH [u(cobj,mco)]=lvthings(%0)
&LVTHINGS`RHOSTMUSH [u(cobj,mco)]=lcon(%0/OBJECT)

&LVEXITS [u(cobj,mco)]=u(LVEXITS`[v(game)],%0)
&LVEXITS`PENNMUSH [u(cobj,mco)]=lvexits(%0)
&LVEXITS`RHOSTMUSH [u(cobj,mco)]=lexits(%0)

@@ Sort section.

&SORTATTR [u(cobj,mco)]=u(SORTATTR`[v(game)],%0,%1,%2)
&SORTATTR`PENNMUSH [u(cobj,mco)]=sortkey(SRT`ATTRSLOT,%0,%1,%2)
&SORTATTR`RHOSTMUSH [u(cobj,mco)]=sortby(#lambda/[lit([ncomp(last(%0,`),last(%1,`))])],%0,%1,%2)

&SORTIDLE [u(cobj,mco)]=u(SORTIDLE`[v(game)],%0,%1,%2)
&SORTIDLE`PENNMUSH [u(cobj,mco)]=sort(%0,idle,%1,%2)
&SORTIDLE`RHOSTMUSH [u(cobj,mco)]=sortby(#lambda/[lit([ncomp(idle(%0),idle(%1))])],%0,%1,%2)

&SORTNAME [u(cobj,mco)]=u(SORTIDLE`[v(game)],%0,%1,%2)
&SORTNAME`PENNMUSH [u(cobj,mco)]=sort(%0,namei,%1,%2)
&SORTNAME`RHOSTMUSH [u(cobj,mco)]=sortby(#lambda/[lit([comp(lcstr(name(%0)),lcstr(name(%1)))])],%0,%1,%2)

&SORTALIAS [u(cobj,mco)]=u(SORTALIAS`[v(game)],%0,%1,%2)
&SORTALIAS`PENNMUSH [u(cobj,mco)]=sortkey(#lambda/lcstr(alias(\%0)),%0,i,%1,%2)
&SORTALIAS`RHOSTMUSH [u(cobj,mco)]=sortby(#lambda/[lit([comp(lcstr(u(alias,%0)),lcstr(u(alias,%1)))])],%0,%1,%2)

&SORTOLDID [u(cobj,mco)]=u(SORTOLDID`[v(game)],%0,%1,%2)
&SORTOLDID`PENNMUSH [u(cobj,mco)]=sortkey(#lambda/after(\%0,~),%0,i,%1,%2)
&SORTOLDID`RHOSTMUSH [u(cobj,mco)]=sortby(#lambda/[lit([ncomp(after(%0,~),after(%1,~))])],%0,%1,%2)

&SORTPRIORITY [u(cobj,mco)]=u(SORTPRIORITY`[v(game)],%0,%1,%2)
&SORTPRIORITY`PENNMUSH [u(cobj,mco)]=sortkey(#lambda/default(\%0/D`ALTS`PRIORITY,0),%0,n,%1,%2)
&SORTPRIORITY`RHOSTMUSH [u(cobj,mco)]=sortby(#lambda/[lit([ncomp(default(%0/D`ALTS`PRIORITY,0),default(%1/D`ALTS`PRIORITY,0))])],%0,%1,%2)

&SORTSTAFF [u(cobj,mco)]=u(SORTSTAFF`[v(game)],%0,%1,%2)
&SORTSTAFF`PENNMUSH [u(cobj,mco)]=sortkey(#lambda/default(\%0/D`STAFF`ORDER,99),%0,n,%1,%2)
&SORTSTAFF`RHOSTMUSH [u(cobj,mco)]=sortby(#lambda/[lit([ncomp(default(%0/D`STAFF`ORDER,99),default(%1/D`STAFF`ORDER,99))])],%0,%1,%2)


&SRT`ATTRSLOT [u(cobj,mco)]=last(%0,`)

@@ REGEX STORAGE
&REG`CAPNAMES [u(cobj,mco)]=(?:^|(?<=[_\/\-\|\s()\+]))([a-z]+)
&REG`CAPNAMES2 [u(cobj,mco)]=(^|(?<=[(]))(of|the|a|and)
&REG`CAPNAMES3 [u(cobj,mco)]=\b(of|the|a|and)\b

&REG`SQL`DATABASE [u(cobj,mco)]=\$DATABASE\$
&REG`SQL`TABLES [u(cobj,mco)]=\$\w+\$
&REG`SQL`ENTRY [u(cobj,mco)]=\?|\!

@@ SQL WRAPPER

&MYSQL [u(cobj,mco)]=if(hasattrp(%!/Q`%0),sql(u(SQL`FORMAT,%0,%1,%2,%3,%4,%5,%6,%7,%8,%9)),#-1 SQL QUERY DATA NOT FOUND)

&MYSQL2 [u(cobj,mco)]=if(hasattrp(%!/Q`%0),sql(u(SQL`FORMAT,%0,%1,%2,%3,%4,%5,%6,%7,%8,%9),|,^),#-1 SQL QUERY DATA NOT FOUND)

&SQL`IN`NUMBER [u(cobj,mco)]=iter(%0,%i0,%1,\,%b)
&SQL`IN`STRING [u(cobj,mco)]=iter(%0,'[sqlescape(%i0)]',%1,\,%b)

&SQL`FORMAT [u(cobj,mco)]=localize(regeditalli(v(Q`%0),v(REG`SQL`DATABASE),u(SQL`DATABASE),v(REG`SQL`TABLES),u(SQL`TABLENAME,$0),v(REG`SQL`ENTRY),if(isint(u(setr,set,v(u(setr,num,add(%q<num>,1))))),%q<set>,switch($0,!,%q<set>,'[sqlescape(%q<set>)]'))))

&SQL`TABLENAME [u(cobj,mco)]=v(SQL`PREFIX)[v(SQL`TABLE`[trim(%0,$)])]

&SQL`DATABASE [u(cobj,mco)]=sql(SELECT DATABASE())
&SQL`PREFIX [u(cobj,mco)]=mushcode_

&INC`DOSQL [u(cobj,mco)]=@break/inline strmatch(u(setr,errcheck,u(mysql,%0,%1,%2,%3,%4,%5,%6,%7,%8,%9)),#-*)=@attach %!/INC`MSG=SQL ERROR: %q<errcheck>

@ COMMON CODE SNIPPETS

&SYSTEM`NAME [u(cobj,mco)]=SYSTEM

&INC`CHECKPC [u(cobj,mco)]=@assert/inline strlen(%0)=@attach %!/INC`MSG=ERROR: You must enter a player name!;@assert/inline strlen(%1);@assert/inline isdbref(u(setr,t%1,pmatch(%0)))=@attach %!/INC`MSG=ERROR: %0 [if(strmatch(%q<t%1>,#-2*),matches multiple players!,does not match a player!)];th u(setq,t%1objid,u(objid,pmatch(%0)));th u(setq,t%1name,name(pmatch(%0)));th u(setq,t%1acc,u(accid,pmatch(%0)));@select/inline t(%2)=1,{th u(setq,t%1id,u(mysql,GET`ID_FROM_OBJID,u(objid,%0)))}
@@ CHECK PC Arguments: %0 = player being checked, %1 = register number to load their data into. %2 - Boolean for also looking up a SQL id.

&INC`IDCHECK [u(cobj,mco)]=@switch/inline t(u(setr,id,u(mysql,GET`ID_FROM_OBJID,u(objid,%0))))=0,{@attach %!/INC`DOSQL=INSERT`PLAYER,u(objid,%0),name(%0);@attach %!/INC`IDCHECK}

&Q`GET`ID_FROM_OBJID [u(cobj,mco)]=SELECT DISTINCT player_id from $PLAYERS$ WHERE objid=?
&Q`INSERT`PLAYER [u(cobj,mco)]=INSERT INTO $PLAYERS$ (objid,player_name) VALUES (?,?)

&INC`JAILCHECK [u(cobj,mco)]=@break/inline u(jailcheck,%#)=@attach %!/INC`MSG=You have been jailed and cannot use this command.

&INC`VERIFY [u(cobj,mco)]=@switch/first/inline strmatch(get(%#/VERIFY_OK),%1)=0,{th u(attrib_set,%#,VERIFY_OK,%1);@wait 10=@wipe %#/VERIFY_OK;@break/inline 1={@attach %!/INC`MSG=%0,,,%2}},1,{@wipe %#/VERIFY_OK}
@@ VERIFY Arguments: %0 = message to display, %1 = code to set for verification.

&INC`CPTREE [u(cobj,mco)]=@cpattr %0/%1=%2/%3;@dolist/inline u(lattr,%0/%1`**)={@cpattr %0/##=%2/%3`[elements(##,lnum(add(words(%1,`),1),add(words(%1,`),11)),`,`)]}

&AGFN`CPTREE [u(cobj,mco)]=null(attrib_set(%2/%3,get(%0/%1))[iter(lattr(%0/%1`**),attrib_set(%2/%3`[elements(%i0,lnum(add(words(%1,`),1),add(words(%1,`),11)),`,`)],get(%0/%i0)))])

@@ CPTREE arguments: %0 = source DBREF, %1 = source root attribute, %2 = target dbref, %3 = new root attr

&INC`MVTREE [u(cobj,mco)]=@cpattr %0/%1=%2/%3;@dolist/inline u(lattr,%0/%1`**)={@cpattr %0/##=%2/%3`[elements(##,lnum(add(1,words(%1,`)),add(words(%1,`),11)),`,`)]};@attach %!/WIPE=%0,%1
@@ MVTREE arguments: %0 = source DBREF, %1 = source root attribute, %2 = target dbref, %3 = new root attr

&INC`GETSWITCH [u(cobj,mco)]=@attach %!/INC`PARTIAL=%0,u(SYSTEM`SWITCHES),|,switch,switch

&SYSTEM`SWITCHES [u(cobj,mco)]=setunion(v(SWITCHES`PLAYER),if(u(isadmin,%#),v(SWITCHES`ADMIN)),|,|)

&INC`PARTIAL [u(cobj,mco)]=@select/inline or(not(strlen(%0)),u(setr,matched,match(%1,u(setr,u(strfirstof,%3,choice),%0),u(strfirstof,%2,%b))))=0,{@assert/inline words(u(setr,u(strfirstof,%3,choice),graball(%1,%0*,u(strfirstof,%2,%b),u(strfirstof,%2,%b))))=@attach %!/INC`MSG={ERROR: Invalid %4! Valid choices are: [u(itemize,%1,u(strfirstof,%2,%b))]};@break/inline gt(words(r(u(strfirstof,%3,choice)),u(strfirstof,%2,%b)),1)=@attach %!/INC`MSG={ERROR: %0 matched [u(itemize,r(u(strfirstof,%3,choice)),u(strfirstof,%2,%b),and,\,)]. Please be more specific.}},1,{@select/inline cand(t(strlen(%0)),t(%q<matched>))=1,{th u(setq,u(strfirstof,%3,choice),elements(%1,%q<matched>,u(strfirstof,%2,%b)))}}
@@ PARTIAL %0 = entry, %1 = choices, %2 = delimiter, %3 = register name, %4 = topic name

&WIPE [u(cobj,mco)]=@select/inline v(game)=PennMUSH,{@select/inline hasflag(%0,SAFE)=1,{@set %0=!SAFE;@wipe %0/%1;@set %0=SAFE},0,{@wipe %0/%1}},RhostMUSH,{@select/inline hasflag(%0,SAFE)=1,{@set %0=!SAFE;@wipe/regexp %0/^%1(`.+)?$;@set %0=SAFE},0,{@wipe/regexp %0/^%1(`.+)?$}}

&DELETE [u(cobj,mco)]=@select/inline v(game)=PennMUSH,{@set %0=!SAFE;@nuke %0;@nuke %0},RhostMUSH,{@destroy/override %0}

@@ MESSAGING SECTION
&INC`MSG [u(cobj,mco)]=@select/inline v(game)=PennMUSH,{@message u(strfirstof,%1,%#)=%0,%!/MSG,##,%0,%1,%2,%3,%4,%5,%6,%7,%8,%9},RhostMUSH,{@pemit/list u(strfirstof,%1,%#)=udefault(%!/MSG,%0,##,%0,%1,%2,%3,%4,%5,%6,%7,%8,%9)}
@@ %0 - Message. %1 - Recipients. %2 - Format obj/attr.

&INC`MSG`ROOM [u(cobj,mco)]=@select/inline v(game)=PennMUSH,{@message u(strfirstof,%1,u(lplayers,%l))=%0,%!/RMSG,##,%0,%1,%2,%3,%4,%5,%6,%7,%8,%9},RhostMUSH,{@pemit/list u(strfirstof,%1,u(lplayers,%l))=udefault(%!/RMSG,%0,##,%0,%1,%2,%3,%4,%5,%6,%7,%8,%9)}

&INC`MSG`NOTICE [u(cobj,mco)]=@attach %!/INC`MSG=[ansi(h,\[[if(cand(u(isadmin,%#),cor(u(ishidden,%#),u(config,ADMIN`ANONYMOUSNOTICES))),SYSTEM,name(%#))]\])]%B%0,%1,%2,%3,%4,%5,%6,%7,%8,%9

&INC`MSG`CHAN [u(cobj,mco)]=th u(setq,chanmsg,if(not(%5),ansi(h,\[[u(moniker,u(firstof,%4,%#))]\])%B)[if(cand(strlen(u(strfirstof,%2,u(SYSTEM`NAME))),not(%3)),u(strfirstof,%2,u(SYSTEM`NAME)):%b)]%0);@select/inline v(game)=PennMUSH,{@dolist/inline/delimit | [u(strfirstof,u(CHANNEL`ALERT),%1)]={@cemit/noisy %i0=%q<chanmsg>}},RhostMUSH,{@dolist/inline/delimit | [u(u(strfirstof,u(CHANNEL`ALERT),%1)]={@cemit %d0=%q<chanmsg>}}
@@ %0 - Message. %1 - Channels (| delimit). %2 - System. %3 - Ignore system boolean. %4 - Enactor. %5 - Ignore enactor.

&INC`MSG`SYSCHAN [u(cobj,mco)]=th u(setq,chanmsg,[if(not(%5),ansi(h,\[[u(strfirstof,%4,SYSTEM)]\]%B))][if(cand(strlen(u(strfirstof,%2,u(SYSTEM`NAME))),not(%3)),u(strfirstof,%2,u(SYSTEM`NAME)):%b)]%0);@select/inline v(game)=PennMUSH,{@dolist/inline/delimit | u(strfirstof([u(CHANNEL`ALERT)]={@cemit/noisy %i0=%q<chanmsg>}},RhostMUSH,{@dolist/inline/delimit | u(strfirstof([u(CHANNEL`ALERT)]={@cemit %d0=%q<chanmsg>}}

&CHANNEL`ALERT [u(cobj,mco)]=u(game_config,CHANNELS,STAFF_ALERTS)

&MSG [u(cobj,mco)]=u(msghead,u(SYSTEM`NAME),%0) %1
&RMSG [u(cobj,mco)]=u(msgroom,u(SYSTEM`NAME),%0) \[[ansi(h,u(moniker,%0))]\] %1

&MSGHEAD [u(cobj,mco)]=localize(ansi(u(setr,msgcolor,u(color,u(firstof,%1,%#),SYSTEM,MSG)),-=<)[ansi(u(color,u(firstof,%1,%#),SYSTEM,MSGTEXT),ucstr(%0))][ansi(%q<msgcolor>,>=-)])
&MSGROOM [u(cobj,mco)]=localize(ansi(u(setr,msgcolor,u(color,u(firstof,%1,%#),SYSTEM,MSG)),-=<*)[ansi(u(color,u(firstof,%1,%#),SYSTEM,MSGTEXT),ucstr(%0))][ansi(%q<msgcolor>,*>=-)])

&COLOR [u(cobj,mco)]=localize(u(strfirstof,get(%0/D`COLOR`%1`%2),if(u(isdbref,u(setr,coltarget,u(firstof,%3,u(accid,%3)))),get(%q<coltarget>/D`COLOR`%1`%2)),u(strfirstof,u(game_config,COLOR,%2),n)))

&HEADER [u(cobj,mco)]=localize(u(HEADER`PREP,%1,%3,HEADER)[u(setq,headertext,if(strlen(%0),[ansi(%q<bordercolor>,<)][if(u(pueblo,%q<target>),endtag(a))][u(setr,borderstar,ansi(u(color,%q<target>,u(SYSTEM`NAME),HEADER_STAR),*))]%B[if(%2,%0,ansi(u(color,%q<target>,u(SYSTEM`NAME),HEADER_TEXT),%0))]%B%q<borderstar>[ansi(%q<bordercolor>,>)]))][u(HEADER`RENDER)])

&SUBHEADER [u(cobj,mco)]=localize(u(HEADER`PREP,%1,%3,SUBHEADER)[u(setq,headertext,if(strlen(%0),[ansi(%q<bordercolor>,<)][if(u(pueblo,%q<target>),endtag(a))]%B[if(%2,%0,ansi(u(color,%q<target>,u(SYSTEM`NAME),HEADER_TEXT),%0))]%B[ansi(%q<bordercolor>,>)]))][u(HEADER`RENDER)])

&SEPARATOR [u(cobj,mco)]=localize(u(HEADER`PREP,%1,%3,SEPARATOR)[u(setq,headertext,if(strlen(%0),[ansi(%q<bordercolor>,<)][if(u(pueblo,%q<target>),endtag(a))]%B[if(%2,%0,ansi(u(color,%q<target>,u(SYSTEM`NAME),HEADER_TEXT),%0))]%B[ansi(%q<bordercolor>,>)]))][u(HEADER`RENDER)])

&HEADER`PREP [u(cobj,mco)]=u(setq,target,u(firstof,owner(u(firstof,%0,%#))))[u(setq,bordercolor,u(color,%q<target>,u(SYSTEM`NAME),BORDER))][u(setq,bordermode,%1)][u(setq,width,sub(u(width,%q<target>),if(%q<bordermode>,2,0)))][u(setq,fill,u(game_config,SYSTEM,%2_FILL))]

&HEADER`RENDER [u(cobj,mco)]=u(setr,end,if(%q<bordermode>,ansi(%q<bordercolor>,+)))[u(center,%q<headertext>,%q<width>,ansi(u(color,%q<target>,u(SYSTEM`NAME),BORDER),%q<fill>))]%q<end>
@@ %0 - display text (optional), %1 - viewer (optional), %2 - Disable headertext coloring. %3 - Bordered mode. %4 - Border color override. %5 - Border star color override. %6 - Header text color override.


@@ Visibility and Connections
&LASTIDLE [u(cobj,mco)]=switch(objeval(%#,conn(%0)),-1,ansi(hx,elements(get(%0/last),2 3)),u(hideidle,%0))

&HIDECONN [u(cobj,mco)]=switch(objeval(%#,conn(%0)),-1,ansi(hx,Off),ansi(if(u(ishidden,%0),hx,hg),u(smalltime,conn(%0),3)))

&HIDEIDLE [u(cobj,mco)]=switch(objeval(%#,idle(%0)),-1,ansi(hx,Off),ansi(if(u(ishidden,%0),hx,u(ryg,round(mul(fdiv(bound(idle(%0),0,3600),3600),100),0))),u(smalltime,idle(%0),3)))

&LASTCONN [u(cobj,mco)]=switch(objeval(%#,conn(%0)),-1,ansi(hx,elements(get(%0/last),2 3)),u(hideconn,%0))

@@ Color and Configuration

&GAME_CONFIG [u(cobj,mco)]=u(strfirstof,if(hasattr(u(cobj,config)/RETRIEVE`%0`%1),u(u(cobj,config)/RETRIEVE`%0`%1)),get(u(cobj,config)/CONFIG`%0`%1),get(u(cobj,config)/DEFAULT`%0`%1))

&PLAYER_CONFIG [u(cobj,mco)]=u(strfirstof,if(hasattr(u(cobj,pconf)/RETRIEVE`%1`%2),u(u(cobj,pconf)/RETRIEVE`%1`%2,%0)),get(%0/D`CONFIG`%1`%2),get(u(cobj,pconf)/DEFAULT`%1`%2))

&ISAPPROVED [u(cobj,mco)]=if(u(game_config,SYSTEM,REQUIRE_APPROVAL),default(%0/D`APPROVED,0),1)

@@ EVENT HANDLING - PENN
&PLAYER`CONNECT [u(cobj,mco)]=@dolist/inline/nobreak children(%!)={@dolist/inline/nobreak u(lattr,%i0/PLAYER`CONNECT`*)={@trigger %i1/%i0=%0,%1,%2,%3,%4,%5,%6,%7,%8,%9}}

&PLAYER`CREATE [u(cobj,mco)]=@dolist/inline/nobreak children(%!)={@dolist/inline/nobreak u(lattr,%i0/PLAYER`CREATE`*)={@trigger %i1/%i0=%0,%1,%2,%3,%4,%5,%6,%7,%8,%9}}

&PLAYER`DISCONNECT [u(cobj,mco)]=@dolist/inline/nobreak children(%!)={@dolist/inline/nobreak u(lattr,%i0/PLAYER`DISCONNECT`*)={@trigger %i1/%i0=%0,%1,%2,%3,%4,%5,%6,%7,%8,%9}}

&SOCKET`LOGINFAIL [u(cobj,mco)]=@dolist/inline/nobreak children(%!)={@dolist/inline/nobreak u(lattr,%i0/SOCKET`LOGINFAIL`*)={@trigger %i1/%i0=%0,%1,%2,%3,%4,%5,%6,%7,%8,%9}}

&SOCKET`CONNECT [u(cobj,mco)]=@assert/inline setr(banid,first(u(u(cao)/FUN`CHECKLOGINS,%1)));@select/inline gt(secs(),get(u(bandb)/%q<banid>`UNTIL))=1,{@cemit/noisy u(u(cao)/VAR`IP`ALERTSCHANNEL)={ansi(hr,WARNING:) EXPIRED Banned address %1 (match: BanID %q<banid>: [get(u(bandb)/%q<banid>)]) has connected.}},0,{@pemit/port %0=setr(message,strfirstof(get(u(bandb)/%q<banid>`MESSAGE),u(u(cao)/VAR`BANLIST`MESSAGE)));@boot/port %0;@cemit/noisy u(u(cao)/VAR`IP`ALERTSCHANNEL)={ansi(hr,WARNING:) Banned address %1 (match: BanID %q<banid>: [get(u(bandb)/%q<banid>)]) attempted to connect. Message sent to them: '%q<message>'}}

@@ @dolist/inline/nobreak setdiff(filterbool(#lambda/isdbref(\%0),iter(lattr(u(coi)/*),u(u(coi)/%i0))),u(eh))={@dolist/inline/nobreak lattr(%i0/SOCKET`CONNECT`*)={@trigger %i1/%i0=%0,%1,%2,%3,%4,%5,%6,%7,%8,%9}}

&OBJECT`RENAME [u(cobj,mco)]=@dolist/inline/nobreak children(%!)={@dolist/inline/nobreak u(lattr,%i0/OBJECT`RENAME`*)={@trigger %i1/%i0=%0,%1,%2,%3,%4,%5,%6,%7,%8,%9}}

&OBJECT`DESTROY [u(cobj,mco)]=@dolist/inline/nobreak children(%!)={@dolist/inline/nobreak u(lattr,%i0/OBJECT`DESTROY`*)={@trigger %i1/%i0=%0,%1,%2,%3,%4,%5,%6,%7,%8,%9}}

&OBJECT`CREATE [u(cobj,mco)]=@dolist/inline/nobreak children(%!)={@dolist/inline/nobreak u(lattr,%i0/OBJECT`CREATE`*)={@trigger %i1/%i0=%0,%1,%2,%3,%4,%5,%6,%7,%8,%9}}

&OBJECT`MOVE [u(cobj,mco)]=@dolist/inline/nobreak children(%!)={@dolist/inline/nobreak u(lattr,%i0/OBJECT`MOVE`*)={@trigger %i1/%i0=%0,%1,%2,%3,%4,%5,%6,%7,%8,%9}}

&SQL`CONNECT [u(cobj,mco)]=@sql SET time_zone = '+00:00';@nscemit/noisy u(cmo`staffrep)=SQL server connect: %0
&SQL`CONNECTFAIL [u(cobj,mco)]=@attach %!/INC`MSG`SYSCHAN={SQL server connect fail: %0}
&SQL`DISCONNECT [u(cobj,mco)]=@attach %!/INC`MSG`SYSCHAN={SQL server disconnect: %0}
&STARTUP`SQL [u(cobj,mco)]=@assert/inline sql(SELECT DATABASE());@trigger %!/LOOP`SQL
&LOOP`SQL [u(cobj,mco)]=@sql SELECT DATABASE();@wait 60=@trigger %!/LOOP`SQL

@@ EVENT HANDLING - RHOST

@ACONNECT [u(cobj,mco)]=@assert/inline strmatch(v(game),RhostMUSH);@select/inline and(eq(totcmds(%#),0),lt(sub(secs(),ctime(%#)),10))=1,{@attach/nobreak %!/PLAYER`CREATE`RHOSTMUSH=%#,words(ports(%#)),first(ports(%#))};@attach/nobreak %!/PLAYER`CONNECT`RHOSTMUSH=%#,words(ports(%#)),first(ports(%#))
@ADISCONNECT [u(cobj,mco)]=@attach/nobreak %!/PLAYER`DISCONNECT`RHOSTMUSH=%#,words(ports(%#)),ishidden(%#)

&PLAYER`CONNECT`RHOSTMUSH [u(cobj,mco)]=@dolist/inline/nobreak children(%!)={@dolist/inline/nobreak u(lattr,##/PLAYER`CONNECT`*)={@trigger %d1/%d0=%0,%1,%2,%3,%4,%5,%6,%7,%8,%9}}
&PLAYER`CREATE`RHOSTMUSH [u(cobj,mco)]=@dolist/inline/nobreak children(%!)={@dolist/inline/nobreak u(lattr,##/PLAYER`CREATE`*)={@trigger %d1/%d0=%0,%1,%2,%3,%4,%5,%6,%7,%8,%9}}
&PLAYER`DISCONNECT`RHOSTMUSH [u(cobj,mco)]=@dolist/inline/nobreak children(%!)={@dolist/inline/nobreak u(lattr,##/PLAYER`DISCONNECT`*)={@trigger %d1/%d0=%0,%1,%2,%3,%4,%5,%6,%7,%8,%9}}
&SOCKET`LOGINFAIL`RHOSTMUSH [u(cobj,mco)]=@dolist/inline/nobreak children(%!)={@dolist/inline/nobreak u(lattr,##/SOCKET`LOGINFAIL`*)={@trigger %d1/%d0=%0,%1,%2,%3,%4,%5,%6,%7,%8,%9}}
&OBJECT`DESTROY`RHOSTMUSH [u(cobj,mco)]=@dolist/inline/nobreak children(%!)={@dolist/inline/nobreak u(lattr,##/OBJECT`DESTROY`*)={@trigger %d1/%d0=%0,%1,%2,%3,%4,%5,%6,%7,%8,%9}}

&STARTUP`RHOSTEVENT [u(cobj,mco)]=@trigger %!/LOOP`RHOSTMUSH`EVENTHANDLER
&B_@NUKE [u(cobj,mco)]=u(RHOST`DELETE`BEFORE)
&A_@NUKE [u(cobj,mco)]=u(RHOST`DELETE`AFTER)
&B_@TOAD [u(cobj,mco)]=u(RHOST`DELETE`BEFORE)
&A_@TOAD [u(cobj,mco)]=u(RHOST`DELETE`AFTER)
&B_@TURTLE [u(cobj,mco)]=u(RHOST`DELETE`BEFORE)
&A_@TURTLE [u(cobj,mco)]=u(RHOST`DELETE`AFTER)

&B_@NAME [u(cobj,mco)]=u(RHOST`NAME`BEFORE)
&A_@NAME [u(cobj,mco)]=u(RHOST`NAME`AFTER)

&RHOST`DELETE`BEFORE [u(cobj,mco)]=if(u(setr,nuketarget,pmatch(before(rest(%m),=))),u(setq,nuketype,type(%q<nuketarget>))[u(setq,nukename,name(%q<nuketarget>))])
&RHOST`DELETE`AFTER [u(cobj,mco)]=if(cand(%q<nuketarget>,strmatch(%q<nuketype>,PLAYER)),if(cor(hasflag(%q<nuketarget>,RECOVERY),not(hastype(%q<target>,PLAYER))),u(attrib_set,u(cobj,mco),DELETE`QUEUE`[u(nextslot,u(cobj,mco),DELETE`QUEUE)],%q<nuketarget>|%q<nukename>|%q<nuketype>)))

&RHOST`NAME`BEFORE [u(cobj,mco)]=u(setq,namename,name(u(setr,nametarget,pmatch(before(rest(%m),=)))))
&RHOST`NAME`AFTER [u(cobj,mco)]=if(%q<nametarget>,if(strmatch(u(setr,newname,name(%q<nametarget>)),after(%m,=)),u(attrib_set,u(cobj,mco),NAME`QUEUE`[u(nextslot,u(cobj,mco),NAME`QUEUE)],%q<nametarget>|%q<newname>|%q<namename>)))

&LOOP`RHOSTMUSH`EVENTHANDLER [u(cobj,mco)]=@assert/inline strmatch(v(game),RhostMUSH);@dolist/inline [u(lattr,%!/TRG`RHOSTMUSH`*)]={@trigger %!/%d0};@wait 60={@trigger me/LOOP`RHOSTMUSH`EVENTHANDLER}

&TRG`RHOSTMUSH`OBJECT`DELETE [u(cobj,mco)]=@dolist/inline u(lattr,u(cobj,mco)/DELETE`QUEUE`*)={@trigger u(cobj,mco)/OBJECT`DESTROY`RHOSTMUSH=elements(get(u(cobj,mco)/%d0),1,|),elements(get(u(cobj,mco)/%d0),2,|),elements(get(u(cobj,mco)/%d0),3,|);@attach %!/WIPE=u(cobj,mco),%d0}

&TRG`RHOSTMUSH`OBJECT`RENAME [u(cobj,mco)]=@dolist/inline u(lattr,u(cobj,mco)/NAME`QUEUE`*)={@trigger u(cobj,mco)/OBJECT`RENAME`RHOSTMUSH=elements(get(u(cobj,mco)/%d0),1,|),elements(get(u(cobj,mco)/%d0),2,|),elements(get(u(cobj,mco)/%d0),3,|);@attach %!/WIPE=u(cobj,mco),%d0}