
@tel create(Player Globals Object <PGO>)=config(master_room)
&pgo u(coi)=locate(config(master_room),Player Globals Object <PGO>,TXxi)
@parent u(pgo)=u(coi)
@set u(pgo)=WIZARD !NO_COMMAND

&CMD`CHARSET u(pgo)=$+charset:@nspemit %#=wrap(iter(lnum(32,256),if(comp(first(chr(%i0)),#-1,A),ljust([ljust(%i0:,4)] [ansi(hy,chr(%i0))],7)), ,),78)

&CMD`+BEEP u(pgo)=$+beep *:@include u(ccs)/INC`CHECKPC=%0,1,BEEP;@assert hasflag(%q<t1>,CONNECTED)=@nspemit %#=That player is offline.;@nspemit %q<t1>=beep()[ansi(hr,%n wishes for your attention! BEEP!)];@nspemit %#=You have sent %q<t1name> a beep!

&CMD`+WHO u(pgo)=$+who:@nspemit %#=header(+Who)%R[align(30 -10 -3 -4 3 23,ansi(hb,Name),ansi(hb,Alias),ansi(hb,Sex),ansi(hb,Idle),ansi(hb,Con),ansi(hb,Location))]%R[subheader()];th null(iter(setr(list,setunion(lwho(%#),lwho(%#),,namei)),nspemit(%#,ljust(left(pueblize(if(and(orflags(%i0,HU),isadmin(%#)),ansi(hx,name(%i0)),name(%i0)),+finger [name(%i0)]),28),28)%B%B[ljust(center(left(alias(%i0),10),10),10)]%B%B[ljust(center(left(get(%i0/SEX),1),3),3)]%B[rjust(ago(idle(%i0)),3)]%B%B[rjust(ago(conn(%i0)),3)]%B%B[pueblize(left(name(loc(%i0)),23),join [name(%i0)])])));@nspemit %#=header(words(%q<list>) Characters from [words(setunion(iter(%q<list>,get(%i0/LASTIP)),iter(%q<list>,get(%i0/LASTIP))))] IPs Connected)

&CMD`FINGER u(pgo)=$^(?\:\+)?finger(?\: +(.+))?$:@include u(ccs)/INC`CHECKPC=strfirstof(%1,%#),1,FINGER;@nspemit %#=header(%q<t1name>[if(strlen(alias(%q<t1>)),%B\([pueblize(alias(%q<t1>),+oocfinger %q<t1name>)]\))]%B\[[get(%q<t1>/D`FINGER`TYPE)]\]);@nspemit %#=align(1. 74 1.,color(%#,|,FINGER`BORDER,m),[ljust(%BSex:%B[switch(get(%q<t1>/SEX),m*,Male,f*,Female,n*,Neuter,???)],37)][color(%#,|,FINGER`BORDER,m)]%B[ljust(Group:%B[left(default(%q<t1>/D`FINGER`GROUP,Unaffiliated),31)],35)]%R[ljust(Race:%B[left(default(%q<t1>/D`FINGER`RACE,Unset),31)],37)][color(%#,|,FINGER`BORDER,m)]%B[ljust(%B%BJob:%B[left(default(%q<t1>/D`FINGER`JOB,Wanderer),33)],36)],color(%#,|,FINGER`BORDER,m)%R);@switch/inline or(strlen(get(%q<t1>/D`FINGER`QUOTE)),strlen(get(%q<t1>/D`FINGER`PROFILE)))=1,{@nspemit %#=subheader(Details);@switch/inline gt(strlen(get(%q<t1>/D`FINGER`QUOTE)),0)=1,{@nspemit %#=align(1. 74 1.,color(%#,|,FINGER`BORDER,m),get(%q<t1>/D`FINGER`QUOTE)%R%R,color(%#,|,FINGER`BORDER,m)%R)};@switch/inline gt(strlen(get(%q<t1>/D`FINGER`PROFILE)),0)=1,{@nspemit %#=align(1. 74 1.,color(%#,|,FINGER`BORDER,m),get(%q<t1>/D`FINGER`PROFILE)%R,color(%#,|,FINGER`BORDER,m)%R)}};@switch/inline gt(strlen(get(%q<t1>/SKILLS)),0)=1,{@nspemit %#=subheader(Skills);@nspemit %#=align(1. 74 1.,color(%#,|,FINGER`BORDER,m),get(%q<t1>/SKILLS),color(%#,|,FINGER`BORDER,m)%R)};@nspemit %#=header()
@set u(pgo)/CMD`FINGER=regexp

th attrib_set(u(cco)/VAR`CATEGORIES,setunion(get(u(cco)/VAR`CATEGORIES),FINGER,|,|))
&VAR`CATEGORIES`FINGER u(cco)=BORDER

&CMD`+WHERE u(pgo)=$where:@nspemit %#=header(Currently Roleplaying);@nspemit %#=iter(if(isadmin(%#),setunion(iter(filterbool(#lambda/strmatch(IC,default(\%0/PLAYSTATUS,OOC)),setunion(lwho(%#),lwho(%#))),loc(%i0)),,,m),filterbool(#lambda/if(orflags(\%0,DU),if(isadmin(%#),1,0),1),setunion(iter(filterbool(#lambda/and(strmatch(IC,default(\%0/PLAYSTATUS,OOC)),not(orflags(\%0,DU))),setunion(lwho(%#),lwho(%#))),loc(%i0)),,,m))),align(40 38,ljust(if(isadmin(%#),pueblize(left(if(orflags(%i0,DU),ansi(hx,name(%i0)),strfirstof(u(%i0/COLORNAME),name(%i0))),sub(35,strlen(%i0[flags(%i0)])))%(%i0[flags(%i0)]%),tel %i0),if(strlen(getstat(u(tco)/VAR`ROOMLIST,%i0)),pueblize(left(strfirstof(u(%i0/COLORNAME),name(%i0)),37),tel %i0),left(strfirstof(u(%i0/COLORNAME),name(%i0)),37))),40,.),itemize(if(isadmin(%#),iter(filterbool(#lambda/and(hasflag(\%0,CONNECTED),strmatch(IC,default(\%0/PLAYSTATUS,OOC))),lplayers(%i0)),pueblize(if(orflags(%i0,DU),ansi(hx,name(%i0)),name(%i0)),finger [name(%i0)]),%b,|),iter(filterbool(#lambda/and(andflags(\%0,c!D!U),strmatch(IC,default(\%0/PLAYSTATUS,OOC))),lplayers(%i0)),pueblize(name(%i0),finger [name(%i0)]),%b,|)),|,and,\,)),,%R);@nspemit %#=header()

&CMD`RPVIS u(pgo)=$rpvis:@switch/inline hasflag(%#,UNFINDABLE)=1,{@nspemit %#=announce(RPVIS) You will now show on the where list.;@set %#=!UNFINDABLE},0,{@nspemit %#=announce(RPVIS) You will no longer appear on the where list.;@set %#=UNFINDABLE}

&CMD`+OOC u(pgo)=$+ooc *:@nsremit %l=<[ansi(hr,OOC)]>%B[speak(%#,%0)]
&CMD`OOC u(pgo)=$ooc *:@nsremit %l=<[ansi(hr,OOC)]>%B[speak(%#,%0)]


&CMD-ALT-OSAY_EMIT u(pgo)=$'*:@include u(pgo)/INC-OSAY_EMIT
&CMD-OSAY_EMIT u(pgo)=$osay *:@include u(pgo)/INC-OSAY_EMIT

&INC-OSAY_EMIT u(pgo)=@dolist lplayers(%l)={@switch/inline %0=:*,{@nspemit %i0=color(%i0,%n,OSAY`NAME,m)%B[color(%i0,after(%0,:),OSAY`ACTION,n)]},\;*,{@nspemit %i0=color(%i0,%n,OSAY`NAME,m)[color(%i0,after(%0,;),OSAY`ACTION,n)]},{@nspemit %i0=color(%i0,%n,OSAY`NAME,m)%B[color(%i0,osays\,,OSAY`TEXT,m)]%B[color(%i0,',OSAY`QUOTES,hm)][color(%i0,%0,OSAY`SPEECH,n)][color(%i0,',OSAY`QUOTES,hm)]}}

&CMD`IC u(pgo)=$ic:@switch/inline get(%#/playstatus)=IC,{@pemit %#=ansi(m,You're already IC!)},{@nscemit/noisy v(VAR`STATUSCHANNEL)=ansi(w,%n) [ansi(<#848484>,is now)] [ansi(m,In Character.)];&playstatus %#=IC;&D`IDLETIME %#=secs();@nsremit %l=ansi(hg,%n goes IC!)}
&CMD`OOC u(pgo)=$ooc:@switch/inline get(%#/playstatus)=OOC,{@pemit %#=ansi(m,You're already OOC!)},{@nscemit/noisy v(VAR`STATUSCHANNEL)=ansi(w,%n) [ansi(<#848484>,is now)] [ansi(m,Out of Character.)];&playstatus %#=OOC;@nsremit %l=ansi(hg,%n goes OOC!)}

&VAR`STATUSCHANNEL u(pgo)=OOC