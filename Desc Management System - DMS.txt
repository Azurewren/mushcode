@@ DEPENDENCIES: Core

th u(NEWCOBJ,Desc Management System <DMS>,dms,,,,WIZARD SAFE !NO_COMMAND,INHERIT SIDEFX SAFE)

&CMD`+DESC`PENNMUSH [u(cobj,dms)]=$^(?s)\+desc(?\:/(\S+)?)?(?\: +(.+?))?(?\:=(.*))?$:@attach %!/CMD`+DESC`MAIN
@set [u(cobj,dms)]/CMD`+DESC`PENNMUSH=regexp
&CMD`+DESC`RHOSTMUSH [u(cobj,dms)]=$^(?s)\+desc(?\:/(\\S+)?)?(?\: +(.+?))?(?\:=(.*))?$:@attach %!/CMD`+DESC`MAIN
@set [u(cobj,dms)]/CMD`+DESC`RHOSTMUSH=regexp
&CMD`+DESC`MAIN [u(cobj,dms)]=@attach %!/INC`GETSWITCH=%1;@attach %!/INC`[u(strfirstof`%va,%q<switch>,MAIN)]=%2,%3
@set [u(cobj,dms)]/CMD`+DESC`[switch(%va,PennMUSH,RHOSTMUSH,RhostMUSH,PENNMUSH)]=no_command

&SWITCHES`PLAYER [u(cobj,dms)]=SAVE|DELETE|NOISY|VIEW|RENAME

&INC`MAIN [u(cobj,dms)]=@select/inline strlen(%0)=0,{@pemit %#=u(header,Your Saved Descriptions);@pemit %#=u(table`%va,iter(u(lattr`%va,%#/D`DESC`*),u(mxpmenu`%va,get(%#/%i0),+desc [get(%#/%i0)]|+desc/noisy [get(%#/%i0)]|+desc/view [get(%#/%i0)],Change|NoisyChange|View),%b,|),26,78,|);@pemit %#=u(subheader)},{@attach %!/INC`PARTIAL=%0,squish(iter(u(lattr`%va,%#/D`DESC`*),get(%#/%i0),%b,|),|),|,DESC,desc,desc;@check strlen(u(setr`%va,attr,u(FUN`LOCDESC,%#,%q<desc>)))=@attach %!/INC`MSG=ERROR: Desc '%0' not found!;@select/inline %2=1,{@attach %!/INC`MSG`ROOM=[ansi(h,%n)] changed to %p '[get(%#/%q<attr>)]' Description!,lcon(%l)},{@attach %!/INC`MSG=You change to your '[get(%#/%q<attr>)]' Description!};@cpattr %#/%q<attr>`DESC=%#/DESCRIBE;&DESC_CURR %#=%q<desc>}

&INC`NOISY [u(cobj,dms)]=@attach %!/INC`MAIN=%0,%1,1

&FUN`LOCDESC [u(cobj,dms)]=regrepi(%0,D`DESC`*,^%1$)

&GFN`MDESC [u(cobj,dms)]=if(strlen(u(FUN`LOCDESC,%0,%1)),get(%0/[u(FUN`LOCDESC,%0,%1)]`DESC))

&INC`SAVE [u(cobj,dms)]=@check strlen(%0)=@attach %!/INC`MSG=ERROR: Desc name field empty!;@stop u(charsearch,%0,|)=@attach %!/INC`MSG=ERROR: | is not allowed in desc names.;@select/inline strlen(u(setr`%va,attr,u(FUN`LOCDESC,%#,%0)))=0,{th u(setq`%va,attr,D`DESC`[u(nextslot,%#,D`DESC)])},{@attach u(ccs)/INC`VERIFY={[ansi(hr,WARNING:)] This will overwrite the existing file. Enter the same command within ten seconds to verify.},DESC %0};@select/inline strlen(%1)=0,{th u(attrib_set`%va,%#,%q<attr>,%0);@cpattr %#/DESCRIBE=%#/%q<attr>`DESC;@attach %!/INC`MSG=Your current Description has been saved as '%0'!},{&%q<attr> %#=%0;&%q<attr>`DESC %#=%1;@attach %!/INC`MSG=The entered description was saved as '%0'!}

&INC`DELETE [u(cobj,dms)]=@check strlen(%0)=@attach %!/INC`MSG=ERROR: Desc name field empty!;@attach %!/INC`PARTIAL=%0,squish(iter(u(lattr`%va,%#/D`DESC`*),get(%#/%i0),%b,|),|),|,DESC,desc,desc;@check strlen(u(setr`%va,attr,u(FUN`LOCDESC,%#,%q<desc>)))=@attach %!/INC`MSG=ERROR: Desc not found!;@attach %!/INC`VERIFY={[ansi(hr,WARNING:)] Deleting Desc '%0'. This cannot be undone. Are you sure you want to do this? Enter the same command again within ten seconds to verify.},DESC DELETE %q<attr>;th u(setq`%va,name,get(%#/%q<attr>));@attach %!/INC`MSG=Desc '%q<name>' DELETED.;@attach %!/WIPE=%#,%q<attr>

&INC`VIEW [u(cobj,dms)]=@check strlen(%0)=@attach %!/INC`MSG=ERROR: Desc name field empty!;@attach %!/INC`PARTIAL=%0,squish(iter(u(lattr`%va,%#/D`DESC`*),get(%#/%i0),%b,|),|),|,DESC,desc,desc;@check strlen(u(setr`%va,attr,u(FUN`LOCDESC,%#,%q<desc>)))=@attach %!/INC`MSG=ERROR: Desc not found!;@pemit %#=u(header,Viewing Description: '[get(%#/%q<attr>)]');@pemit %#=get(%#/%q<attr>`DESC);@pemit %#=u(subheader)

&INC`RENAME [u(cobj,dms)]=@check strlen(%0)=@attach %!/INC`MSG=ERROR: Desc name field empty!;@check strlen(u(setr`%va,attr,u(FUN`LOCDESC,%#,%0)))=@attach %!/INC`MSG=ERROR: Desc not found!;@check strlen(%1)=@attach %!/INC`MSG=ERROR: New desc name field empty!;@stop u(charsearch,%1,|)=@attach %!/INC`MSG=ERROR: | is not allowed in desc names.;@stop strlen(u(setr`%va,attr2,u(FUN`LOCDESC,%#,%1)))=@attach %!/INC`MSG=ERROR: A desc with that name already exists!;@attach %!/INC`MSG=Desc '%0' renamed to '%1'!;&%q<attr> %#=%1

@@ CHARACTER - DESC
+help/add +desc=[u(cobj,dms)]/HLP`@DESC
+help/category +desc=Character
+help/metatags +desc=describe description appearance
&HLP`@DESC [u(cobj,dms)]=Your character's DESCRIPTION (or @desc) is what others will see when they [ansi(h,look)] at you. These commands are used to manage your DESCRIPTION.%R%R[ansi(hc,See Also:)] [u(pueblize,help @desc)], [u(pueblize,help look)]%R%R[ansi(hc,Basic Commands)]%R[align(5 [sub(u(width`%va,%#),6)],,[ansi(h,@desc me=<text>)] - Sets your current description. Use \%R for linebreaks and \%T for indents.%R[ansi(h,&short-desc me=<text>)] - Sets your short-desc shown in the room display.)]%R%R[ansi(hc,Multidescer Commands)]%RThe Multidescer stores your Descriptions for later retrieval. Handy for maintaining a versatile 'wardrobe' of appearances!%R[ansi(hc,Aliases:)] desc%R[align(5 [sub(u(width`%va,%#),6)],,[ansi(h,+desc)] - Shows all your saved descriptions.%R[ansi(h,+desc <name>)] - Sets your desc to a stored one.%R[ansi(h,+desc/noisy <name>)] - Like above but emits to the room. Useful for dramatics!%R[ansi(h,+desc/save <name>)] - Saves your currently worn description to a <name>.%R[ansi(h,+desc/save <name>=<text>)] - Creates a new saved desc with specified text.%R[ansi(h,+desc/rename <name>=<newname>)] - Renames a saved desc.%R[ansi(h,+desc/view <name>)] - Shows you a saved desc.)]

&CONFLICT`DESC [u(cobj,migrate)]=@tel [u(coi,desc)]=u(cobj,oldbox);@set u(coi,desc)=NO_COMMAND HALT