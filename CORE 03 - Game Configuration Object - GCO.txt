@@ DEPENDENCIES - CORE

th u(NEWCOBJ,Game Configuration Object <GCO>,gco,u(cobj,mco),,,WIZARD SAFE,INHERIT SIDEFX SAFE)
th u(NEWCOBJ,Game Configuration Data Storage <CONFIG>,config,u(cobj,gco),,1,WIZARD SAFE,INHERIT SIDEFX SAFE)

@@ %0 - @name. %1 - alias. %2 - location to store. %3 - parent, defaults to MCO. %4 - No parent. %5 - PennMUSH flags. %6 - RhostMUSH flags.

@select/inline v(game)=PennMUSH,@lock/command [u(cobj,gco)]=ISADMIN2/1,RhostMUSH,{@lock/use [u(cobj,gco)]=ISADMIN2/1}

&CMD`+GAMECONFIG`PENNMUSH [u(cobj,gco)]=$^(?\:\+)?gameconfig(?\:/(\S+)?)?(?\: +(.+?))?(?\:/(.+?))?(?\:=(.*))?$:@attach %!/CMD`+GAMECONFIG`MAIN
@set [u(cobj,gco)]/CMD`+GAMECONFIG`PENNMUSH=regexp
&CMD`+GAMECONFIG`RHOSTMUSH [u(cobj,gco)]=$^(?\:\+)?account(?\:/(\\S+)?)?(?\: +(.+?))?(?\:/(.+?))?(?\:=(.*))?$:@attach %!/CMD`+GAMECONFIG`MAIN
@set [u(cobj,gco)]/CMD`+GAMECONFIG`RHOSTMUSH=regexp
&CMD`+GAMECONFIG`MAIN [u(cobj,gco)]=th u(setq,sysname,ACCOUNT);@attach %!/INC`GETSWITCH=%1;@attach %!/INC`[u(strfirstof,%q<switch>,MAIN)]=%2,%3,%4
@set [u(cobj,gco)]/CMD`+GAMECONFIG`[switch(v(game),PennMUSH,RHOSTMUSH,RhostMUSH,PENNMUSH)]=no_command

&SWITCHES`PLAYER [u(cobj,gco)]=
&SWITCHES`ADMIN [u(cobj,gco)]=
&SYSTEM`NAME [u(cobj,gco)]=GAMECONFIG

&INC`MAIN [u(cobj,gco)]=@switch/inline strlen(%0)=0,{@attach %!/INC`LIST},{@attach %!/INC`SET}

&INC`LIST [u(cobj,gco)]=@pemit %#=u(header,mudname() Configuration);@dolist/inline/delimit | [u(FUN`CATEGORIES)]={th u(setq,category,##);@pemit %#=u(subheader,u(capnames,%q<category>));@pemit %#=ansi(u(color,%#,u(SYSTEm`NAME),COLUMN_NAMES),align(18 15 43,Setting,Value,Type and Description));@pemit %#=iter(u(FUN`SETTINGS,%q<category>),align(18 15 43,%i0,u(game_config,%q<category>,%i0),<[default(u(cobj,config)/TYPE`%q<category>`%i0,unknown)]> [default(u(cobj,config)/DESC`%q<category>`%i0,Unset)]),|,%R)};@pemit %#=u(subheader)

&FUN`CATEGORIES [u(cobj,gco)]=sort(setunion(iter(u(lattr,u(cobj,config)/DEFAULT`*),last(%i0,`),%b,|),,|,|),a,|,|)

&FUN`SETTINGS [u(cobj,gco)]=sort(setunion(iter(u(lattr,u(cobj,config)/DEFAULT`%0`*),last(%i0,`),%b,|),,|,|),a,|,|)

&INC`SET [u(cobj,gco)]=@assert/inline strlen(%0)=@attach %!/INC`MSG={ERROR: Category field empty.};@attach %!/INC`PARTIAL=%0,u(FUN`CATEGORIES),|,category,category;@assert/inline strlen(%1)=@attach %!/INC`MSG={ERROR: Option field empty.};@attach %!/INC`PARTIAL=%1,u(FUN`SETTINGS,%q<category>),|,option,option;@switch/inline strlen(%2)=0,{},{@attach %!/INC`VALID`[default(u(cobj,config)/TYPE`%q<category>`%q<option>,WORD)]=%2;th u(attrib_set,u(cobj,config),CONFIG`%q<category>`%q<option>,%q<value>);@attach %!/INC`MSG=u(setr,msg,Game Option %q<category> %q<option> Set to: %q<value>);@attach %!/INC`MSG`CHAN={%q<msg>}}

&INC`VALID`COLOR [u(cobj,gco)]=@break/inline strmatch(ansi(u(setr,value,%0),Test),#-*)=@attach %!/INC`MSG=ERROR: '%0' is not a valid color.
&INC`VALID`WORD [u(cobj,gco)]=th u(setq,value,trim(%0))
&INC`VALID`LIST [u(cobj,gco)]=th u(setq,value,trim(%0))
&INC`VALID`BOOL [u(cobj,gco)]=@assert/inline match(0 1,u(setr,value,%0))=@attach %!/INC`MSG={ERROR: BOOL options can only be 0 (false) or 1 (true).}
&INC`VALID`DBREF [u(cobj,gco)]=@assert/inline isdbref(%0)=@attach %!/INC`MSG={ERROR: DBREF not found.};th u(setq,value,u(objid,%0))

@@ STARTUP EXTRA is used by individual games.
&STARTUP`MAIN [u(cobj,gco)]=@include/nobreak %!/STARTUP`MAIN`[v(game)];@include/nobreak %!/STARTUP`MAIN`EXTRA

&STARTUP`MAIN`PENNMUSH [u(cobj,gco)]=@command/disable kill;@dolist/inline/nobreak @dig @open @link={@command/restrict %i0=!POWER^GUEST&!FLAG^GAGGED&(POWER^BUILDER|FLAG^WIZARD|FLAG^ROYALTY)};@dolist/inline short-desc playstatus={@attribute/access/retroactive %i0=visual};@dolist/inline D={@attribute/access/retroactive %i0=mortal_dark WIZARD}

&PLAYER`DISCONNECT`TELGUEST [u(cobj,gco)]=@break/inline %1;@select/inline u(isguest,%0)=1,{@tel/silent %0=u(game_config,NAVIGATION,GUEST_START,#0)}

&PLAYER`CREATE`CHANNELS [u(cobj,gco)]=@select/inline 1=u(isguest,%0)={@dolist/inline/delimit | [u(game_config,CHANNELS,JOIN_GUESTS)]=@select/inline v(game)=PennMUSH,{@chan/on %i0=%0},RhostMUSH,{}},0,{@dolist/inline/delimit | [u(game_config,CHANNELS,JOIN_PLAYERS)]=@select/inline v(game)=PennMUSH,{@chan/on %i0=%0},RhostMUSH,{}}

&OBJECT`CREATE`SETLOCK [u(cobj,gco)]=@switch/first/inline type(%0)=EXIT,{},{@lock/basic %0==[owner(%0)]}

@@ CONFIG DEFAULTS - Don't change these! +config can change them.
&DEFAULT`NAVIGATION`GUEST_START [u(cobj,config)]=#-1
&DESC`NAVIGATION`GUEST_START [u(cobj,config)]=Room guests return to after logoff.
&TYPE`NAVIGATION`GUEST_START [u(cobj,config)]=DBREF

&DEFAULT`NAVIGATION`IC_NEXUS [u(cobj,config)]=#-1
&DESC`NAVIGATION`IC_NEXUS [u(cobj,config)]=Room +nexus leads to or default +ic
&TYPE`NAVIGATION`IC_NEXUS [u(cobj,config)]=DBREF

&DEFAULT`NAVIGATION`OOC_NEXUS [u(cobj,config)]=#-1
&DESC`NAVIGATION`OOC_NEXUS [u(cobj,config)]=Room +ooc leads to.
&TYPE`NAVIGATION`OOC_NEXUS [u(cobj,config)]=DBREF

&DEFAULT`SYSTEM`PUBLIC_EMAIL [u(cobj,config)]=example@example.com
&DESC`SYSTEM`PUBLIC_EMAIL [u(cobj,config)]=Used in system messages.
&TYPE`SYSTEM`PUBLIC_EMAIL [u(cobj,config)]=WORD

&DEFAULT`SYSTEM`ANONYMOUS_NOTICES [u(cobj,config)]=0
&DESC`SYSTEM`ANONYMOUS_NOTICES [u(cobj,config)]=Hide admin names in sys-messages?
&TYPE`SYSTEM`ANONYMOUS_NOTICES [u(cobj,config)]=BOOL

&DEFAULT`SYSTEM`HEADER_FILL [u(cobj,config)]==
&DESC`SYSTEM`HEADER_FILL [u(cobj,config)]=Border characters.
&TYPE`SYSTEM`HEADER_FILL [u(cobj,config)]=WORD

&DEFAULT`SYSTEM`SUBHEADER_FILL [u(cobj,config)]==
&DESC`SYSTEM`SUBHEADER_FILL [u(cobj,config)]=Border characters.
&TYPE`SYSTEM`SUBHEADER_FILL [u(cobj,config)]=WORD

&DEFAULT`SYSTEM`SEPARATOR_FILL [u(cobj,config)]=-
&DESC`SYSTEM`SEPARATOR_FILL [u(cobj,config)]=Border characters.
&TYPE`SYSTEM`SEPARATOR_FILL [u(cobj,config)]=WORD

&DEFAULT`SYSTEM`REQUIRE_APPROVAL [u(cobj,config)]=0
&DESC`SYSTEM`REQUIRE_APPROVAL [u(cobj,config)]=Game requires chargen approval?
&TYPE`SYSTEM`REQUIRE_APPROVAL [u(cobj,config)]=BOOL

&DEFAULT`CHANNELS`JOIN_GUESTS [u(cobj,config)]=Guest
&DESC`CHANNELS`JOIN_GUESTS [u(cobj,config)]=Default Channels for Guests.
&TYPE`CHANNELS`JOIN_GUESTS [u(cobj,config)]=LIST

&DEFAULT`CHANNELS`JOIN_PLAYERS [u(cobj,config)]=Public|RP|Staff-Help
&DESC`CHANNELS`JOIN_PLAYERS [u(cobj,config)]=Default Channels for Players.
&TYPE`CHANNELS`JOIN_PLAYERS [u(cobj,config)]=LIST

&DEFAULT`CHANNELS`JOIN_ADMIN [u(cobj,config)]=Admin|**Code
&DESC`CHANNELS`JOIN_ADMIN [u(cobj,config)]=Default Channels for new Admin.
&TYPE`CHANNELS`JOIN_ADMIN [u(cobj,config)]=LIST

&DEFAULT`CHANNELS`STAFF_ALERTS [u(cobj,config)]=**Code
&DESC`CHANNELS`STAFF_ALERTS [u(cobj,config)]=Default Channels for Staff Alerts.
&TYPE`CHANNELS`STAFF_ALERTS [u(cobj,config)]=LIST

&DEFAULT`COLOR`COLUMN_NAMES [u(cobj,config)]=g
&DESC`COLOR`COLUMN_NAMES [u(cobj,config)]=For column names.
&TYPE`COLOR`COLUMN_NAMES [u(cobj,config)]=COLOR

&DEFAULT`COLOR`HEADER_TEXT [u(cobj,config)]=hw
&DESC`COLOR`HEADER_TEXT [u(cobj,config)]=For header names.
&TYPE`COLOR`HEADER_TEXT [u(cobj,config)]=COLOR

&DEFAULT`COLOR`HEADER_STAR [u(cobj,config)]=hm
&DESC`COLOR`HEADER_STAR [u(cobj,config)]=For header stars.
&TYPE`COLOR`HEADER_STAR [u(cobj,config)]=COLOR

&DEFAULT`COLOR`BORDER [u(cobj,config)]=m
&DESC`COLOR`BORDER [u(cobj,config)]=For borders.
&TYPE`COLOR`BORDER [u(cobj,config)]=COLOR