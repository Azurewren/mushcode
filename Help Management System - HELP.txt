@@ DEPENDENCIES - CORE

th u(NEWCOBJ,Help Management System <HELP>,help,,,,WIZARD SAFE !NO_COMMAND,INHERIT SIDEFX SAFE)
th u(NEWCOBJ,HELP DATABASE,HELP-HELP,u(cobj,help),,1,WIZARD SAFE,SIDEFX SAFE)
th u(NEWCOBJ,SHELP DATABASE,HELP-SHELP,u(cobj,help),,1,WIZARD SAFE,SIDEFX SAFE)
th u(NEWCOBJ,NEWS DATABASE,HELP-NEWS,u(cobj,help),,1,WIZARD SAFE,SIDEFX SAFE)
th u(NEWCOBJ,DYNAMIC DATABASE,HELP-DYNAMIC,u(cobj,help),,,WIZARD SAFE,SIDEFX SAFE)
@@ %0 - @name. %1 - alias. %2 - location to store. %3 - parent, defaults to MCO. %4 - No parent. %5 - PennMUSH flags. %6 - RhostMUSH flags.

&CMD`+HELP`PENNMUSH [u(cobj,help)]=$^(?s)(?\:\+)?(help|shelp|news)(?\:/(\S+)?)?(?\: +(.+?))?(?\:/(.+?))?(?\:=(.+?))?$:@attach %!/CMD`+HELP`MAIN
@set [u(cobj,help)]/CMD`+HELP`PENNMUSH=regexp
&CMD`+HELP`RHOSTMUSH [u(cobj,help)]=$^(?s)(?\:\+)?(help|shelp|news)(?\:/(\\S+)?)?(?\: +(.+?))?(?\:/(.+?))?(?\:=(.+?))?$:@attach %!/CMD`+HELP`MAIN
@set [u(cobj,help)]/CMD`+HELP`RHOSTMUSH=regexp
&CMD`+HELP`MAIN [u(cobj,help)]=@attach %!/INC`GETSWITCH=%2;@select/inline %1=SHELP,{@check u(isadmin`%va,%#)=@attach %!/INC`MSG=Permission Denied.};@attach %!/INC`INIT`%1;th u(setq`%va,accid,u(strfirstof`%va,u(accid,%#),%#));@include %!/INC`[u(strfirstof`%va,%q<switch>,MAIN)]=trim(%3),trim(%4),trim(%5),%1
@set [u(cobj,help)]/CMD`+HELP`[switch(%va,PennMUSH,RHOSTMUSH,RhostMUSH,PENNMUSH)]=no_command

&INC`INIT`HELP [u(cobj,help)]=th [u(setq`%va,disp,mudname() Help Files)][u(setq`%va,comm,+help)][u(setq`%va,db,num(u(cobj,help-help)))][u(setq`%va,sysname,HELP)]
&INC`INIT`SHELP [u(cobj,help)]=th [u(setq`%va,disp,mudname() Staff Help Files)][u(setq`%va,comm,+shelp)][u(setq`%va,db,num(u(cobj,help-shelp)))][u(setq`%va,sysname,SHELP)]
&INC`INIT`NEWS [u(cobj,help)]=th [u(setq`%va,disp,mudname() News Files)][u(setq`%va,comm,+news)][u(setq`%va,db,num(u(cobj,help-news)))][u(setq`%va,sysname,NEWS)]

&SYSTEM`NAME [u(cobj,help)]=u(strfirstof`%va,%q<sysname>,HELP)
&SWITCHES`PLAYER [u(cobj,help)]=
&SWITCHES`ADMIN [u(cobj,help)]=HELP|ADD|RENAME|DELETE|CATEGORY|RENCATEGORY|METATAGS|VIEWCODE

&FUN`CATEGORIES [u(cobj,help)]=ucstr(setunion(ucstr(iter(u(lattr`%va,%0/H`*`CATEGORY),get(%0/%i0),%b,|)),,|,|))
&FUN`FILESOFCAT [u(cobj,help)]=localize(u(sorthelp,u(filter,if(strlen(u(setr`%va,catname,%1)),ISHELPCATEGORY,ISUNCATEGORIZED),u(lattr`%va,%0/H`*),%b,%b,%0)))
&FUN`FILENAMES [u(cobj,help)]=iter(u(sorthelp,u(lattr`%va,%0/H`*)),get(%0/%i0),%b,|)
&FUN`FINDFILE [u(cobj,help)]=elements(first(u(wildgrepi`%va,%0,H`*,%1)),2,`)
&FUN`FINDSUBFILE [u(cobj,help)]=elements(first(u(wildgrepi`%va,%0,H`%1`SUB`*,%2)),4,`)
&FUN`HAVEREAD [u(cobj,help)]=switch(1,gt(get(%1/H`%2`TIMESTAMP),u(getstat,%0,D`HELPREAD`%1`HAVEREAD,%2)),ansi(hr,*),t(words(iter(u(lattr`%va,%1/H`%2`SUB`*),u(FUN`HAVEREADSUB,%0,%1,%2,last(%i0,`))))),ansi(y,&),%B)
&FUN`HAVEREADSUB [u(cobj,help)]=if(gt(get(%1/H`%2`SUB`%3`TIMESTAMP),u(getstat,%0,D`HELPREAD`%1`HAVEREAD,%2_%3)),ansi(hr,*),%B)

&FIL`ISHELPCATEGORY [u(cobj,help)]=strmatch(%q<catname>,get(%1/%0`CATEGORY))
&FIL`ISUNCATEGORIZED [u(cobj,help)]=not(strlen(get(%q<db>/%0`CATEGORY)))

&SORTHELP [u(cobj,mco)]=u(SORTHELP`%va,%0,%1)
&SORTHELP`PENNMUSH [u(cobj,mco)]=sortkey(#lambda/get(%q<db>/\%0),%0,i)
&SORTHELP`RHOSTMUSH [u(cobj,mco)]=sortby(SRT`HELP,%0,%b,%b)
&SRT`HELP [u(cobj,mco)]=comp(lcstr(get(%q<db>/%0)),lcstr(get(%q<db>/%1)))

&INC`MAIN [u(cobj,help)]=@select/inline t(strlen(%0))[t(strlen(%1))]=00,{@attach %!/INC`SHOW`ALL},10,{@attach %!/INC`SHOW`TOPIC},11,{@attach %!/INC`SHOW`SUB}

&INC`SHOW`ALL [u(cobj,help)]=@pemit %#=u(header,%q<disp>);@dolist/inline/delimit | [ucstr(squish([u(FUN`CATEGORIES,%q<db>)][if(words(u(setr`%va,uncat,u(FUN`FILESOFCAT,%q<db>))),|UNCATEGORIZED)],|))]={@pemit %#=u(separator,u(capnames,##));@pemit %#=u(table`%va,iter(if(strmatch(##,UNCATEGORIZED),%q<uncat>,u(FUN`FILESOFCAT,%q<db>,##)),ljust(u(pueblize,ansi(h,u(setr`%va,name,get(%q<db>/%i0))),[lcstr(%q<comm>)] %q<name>)%B,25)[rjust(u(FUN`HAVEREAD,%q<accid>,%q<db>,last(%i0,`)),1)][u(date`%va,get(%q<db>/%i0`TIMESTAMP),%#)],%b,|),37,u(width`%va,%#),|)};@pemit %#=u(separator)%R%TFor more information on the listed topics, please use '[ansi(h,lcstr(%q<comm>) <topic>)]'[if(u(isadmin`%va,%#),%R%TAdmin\, please see [u(pueblize,ansi(h,lcstr(%q<comm>)/help),lcstr(%q<comm>)/help)] for config information.)];@pemit %#=u(subheader)

&INC`FINDMAIN [u(cobj,help)]=@check strlen(%0)=@attach %!/INC`MSG=ERROR: File name segment empty.;@select/inline t(%1)=0,{@attach %!/INC`PARTIAL=%0,u(FUN`FILENAMES,%q<db>),|,choice,topic;@check u(setr`%va,topid,u(FUN`FINDFILE,%q<db>,%q<choice>))=@attach %!/INC`MSG=ERROR: file %0 not found!},1,{@select/inline words(u(setr`%va,topid,u(strfirstof`%va,after(u(wildgrepi`%va,%q<db>,H`*,%0),`),after(first(u(wildgrepi`%va,%q<db>,H`*,%0*)),`))))=0,{@select/inline words(u(setr`%va,tagsearch,squish(iter(u(lattr`%va,%q<db>/H`*),if(strmatch(get(%q<db>/%i0`METATAGS),*%0*),%i0)))))=>1,{@attach %!/INC`MSG=SEARCH: '%0' not found. Possible candidates by tags: [u(itemize`%va,iter(%q<tagsearch>,u(pueblize,get(%q<db>/%i0),%q<comm> [get(%q<db>/%i0)]),%b,|),|,and,\,)].},0,{@attach %!/INC`MSG=Sorry. '%0' was not found.;@break 1},1,{th u(setq`%va,topid,after(%q<tagsearch>,`));@attach %!/INC`MSG=SEARCH: Found the following using tags.}}}

&INC`FINDSUB [u(cobj,help)]=@attach %!/INC`PARTIAL=%0,iter(u(lattr`%va,%q<db>/H`%q<topid>`SUB`*),get(%q<db>/%i0),%B,|),|,subchoice,subfile;@check u(setr`%va,subid,u(FUN`FINDSUBFILE,%q<db>,%q<topid>,%q<subchoice>))=@attach %!/INC`MSG=ERROR: Subfile not found.

&INC`SHOW`TOPIC [u(cobj,help)]=@attach %!/INC`FINDMAIN=%0,1;@pemit %#=u(header,%q<comm>: [u(setr`%va,name,get(%q<db>/H`%q<topid>))]);@select/inline strlen(u(setr`%va,metatags,get(%q<db>/H`%q<topid>`METATAGS)))=>0,{@pemit %#=ansi(hc,METATAGS:) %q<metatags>};@select/inline isdbref(before(first(u(setr`%va,contents,get(%q<db>/H`%q<topid>`CONTENTS))),/))=1,{@pemit %#=u(before(first(%q<contents>),/)/[after(first(%q<contents>),/)])},0,{@pemit %#=%q<contents>};@select/inline t(u(setr`%va,count,words(u(setr`%va,subs,u(sorthelp`%va,u(lattr`%va,%q<db>/H`%q<topid>`SUB`*))))))=1,{@pemit %#=u(subheader,Subfiles)%R%TTo read subfiles, use '[ansi(h,%q<comm> %q<name>/<file>)]';@pemit %#=u(table`%va,iter(%q<subs>,ljust(u(pueblize,ansi(h,u(setr`%va,subname,get(%q<db>/%i0))),lcstr(%q<comm>) %q<name>/%q<subname>)%B,24)[rjust(u(FUN`HAVEREADSUB,%q<accid>,%q<db>,%q<topid>,last(%i0,`)),1)][u(date`%va,get(%q<db>/%i0`TIMESTAMP),%#)],%b,|),37,u(width`%va,%#),|)};@pemit %#=u(subheader);th u(setstat,%q<accid>,D`HELPREAD`%q<db>`HAVEREAD,%q<topid>,secs())

&INC`SHOW`SUB [u(cobj,help)]=@attach %!/INC`FINDMAIN=%0;@attach %!/INC`FINDSUB=%1;@pemit %#=u(header,%q<disp>: [get(%q<db>/H`%q<topid>)]/[u(setr`%va,name,get(%q<db>/H`%q<topid>`SUB`%q<subid>))]);@select/inline isdbref(before(get(%q<db>/H`%q<topid>`SUB`%q<subid>`CONTENTS),/))=1,{@pemit %#=u(get(%q<db>/H`%q<topid>`SUB`%q<subid>`CONTENTS))},{@pemit %#=get(%q<db>/H`%q<topid>`SUB`%q<subid>`CONTENTS)};@pemit %#=u(subheader);th u(setstat,%q<accid>,D`HELPREAD`%q<db>`HAVEREAD,%q<topid>_%q<subid>,secs())

&INC`ADD [u(cobj,help)]=@select/inline t(strlen(%0))[t(strlen(%1))]=0*,{@break 1=@attach %!/INC`MSG=ERROR: Topic name empty.},10,{@select/inline gt(u(setr`%va,topid,u(FUN`FINDFILE,%q<db>,%0)),0)=0,{th u(setq`%va,topid,add(get(%q<db>/SLOT),1));th u(setq`%va,increase,1)};@check strlen(%2)=@attach %!/INC`MSG=ERROR: Contents field empty.;@select/inline %q<increase>=1,{&SLOT %q<db>=%q<topid>};th u(attrib_set`%va,%q<db>,H`%q<topid>,%0);th u(attrib_set`%va,%q<db>,H`%q<topid>`CONTENTS,%2);th u(attrib_set`%va,%q<db>,H`%q<topid>`TIMESTAMP,secs());@attach %!/INC`MSG=File '%0' [if(%q<increase>,added!,edited!)];@attach %!/INC`MSG`CHAN={File '%0' [if(%q<increase>,added!,edited!)]}},11,{@attach %!/INC`FINDMAIN=%0;@select/inline gt(u(setr`%va,subid,u(FUN`FINDSUBFILE,%q<db>,%q<topid>,%1)),0)=0,{th u(setq`%va,subid,add(get(%q<db>/H`%q<topid>`SUBSLOT),1));th u(setq`%va,increase,1)};@check strlen(%2)=@attach %!/INC`MSG=ERROR: Contents field empty.;@select/inline %q<increase>=1,{&H`%q<topid>`SUBSLOT %q<db>=%q<subid>};th u(attrib_set`%va,%q<db>,H`%q<topid>`SUB`%q<subid>,%1);th u(attrib_set`%va,%q<db>,H`%q<topid>`SUB`%q<subid>`CONTENTS,%2);th u(attrib_set`%va,%q<db>,H`%q<topid>`SUB`%q<subid>`TIMESTAMP,secs());@attach %!/INC`MSG=File '[get(%q<db>/H`%q<topid>)]' Subfile '%1' [if(%q<increase>,added!,edited!)];@attach %!/INC`MSG`CHAN={File '[get(%q<db>/H`%q<topid>)]' Subfile '%1' [if(%q<increase>,added!,edited!)]}}

&INC`METATAGS [u(cobj,help)]=@attach %!/INC`FINDMAIN=%0;@check strlen(%2)=@attach %!/INC`MSG=ERROR: Metatags list empty.;@attach %!/INC`MSG=File '[get(%q<db>/H`%q<topid>)]' Metatags entry is now: %2;@attach %!/INC`MSG`CHAN=File '[get(%q<db>/H`%q<topid>)]' Metatags entry is now: %2;th u(attrib_set`%va,%q<db>,H`%q<topid>`METATAGS,%2)

&INC`RENCATEGORY [u(cobj,help)]=@attach %!/INC`PARTIAL=%0,u(FUN`CATEGORIES,%q<db>),|,choice,category;@check strlen(%2)=@attach %!/INC`MSG=ERROR: New category name empty.;@stop strmatch(%2,UNCATEGORIZED)=@attach %!/INC`MSG=ERROR: 'UNCATEGORIZED' is reserved for system use.;@check words(u(setr`%va,files,u(wildgrepi`%va,%q<db>,H`*`CATEGORY,%q<choice>)))=@attach %!/INC`MSG=ERROR: No files to re-organize.;@dolist/inline %q<files>={&## %q<db>=ucstr(%2)};@attach %!/INC`MSG=All files from Category %q<choice> Re-organized to Category [ucstr(%2)].;@attach %!/INC`MSG`CHAN=All files from Category %q<choice> Re-organized to Category [ucstr(%2)].

&INC`RENAME [u(cobj,help)]=@select/inline t(strlen(%0))[t(strlen(%1))]=0*,{@break 1=@attach %!/INC`MSG=ERROR: Topic name empty.},10,{@attach %!/INC`FINDMAIN=%0;@check strlen(%2)=@attach %!/INC`MSG=ERROR: New name field empty!;@stop u(FUN`FINDFILE,%q<db>,%2)=@attach %!/INC`MSG=ERROR: File names must be unique.;@attach %!/INC`MSG=u(setr`%va,msg,File '[get(%q<db>/H`%q<topid>)]' Renamed to: %2);@attach %!/INC`MSG`CHAN=%q<msg>;th u(attrib_set`%va,%q<db>,H`%q<topid>,%2);th u(attrib_set`%va,%q<db>,H`%q<topid>`TIMESTAMP,secs())},11,{@attach %!/INC`FINDMAIN=%0;@attach %!/INC`FINDSUB=%1;@check strlen(%2)=@attach %!/INC`MSG=ERROR: New name field empty!;@stop u(FUN`FINDSUBFILE,%q<db>,%q<topid>,%2)=@attach %!/INC`MSG=ERROR: Subfile names must be unique.;@attach %!/INC`MSG=u(setr`%va,msg,File '[get(%q<db>/H`%q<topid>)]' Subfile '[get(%q<db>/H`%q<topid>`SUB`%q<subid>)]' Renamed to: %2);@attach %!/INC`MSG`CHAN=%q<msg>;th u(attrib_set`%va,%q<db>,H`%q<topid>`SUB`%q<subid>,%2);th u(attrib_set`%va,%q<db>,H`%q<topid>`SUB`%q<subid>`TIMESTAMP,secs())}

&INC`DELETE [u(cobj,help)]=@select/inline t(strlen(%0))[t(strlen(%1))]=0*,{@break 1=@attach %!/INC`MSG=ERROR: Topic name empty.},10,{@attach %!/INC`FINDMAIN=%0;@attach %!/INC`MSG=u(setr`%va,msg,File '[get(%q<db>/H`%q<topid>)]' Deleted!);@attach %!/INC`MSG`CHAN=%q<msg>;@attach %!/WIPE=%q<db>,H`%q<topid>},11,{@attach %!/INC`FINDMAIN=%0;@attach %!/INC`FINDSUB=%1;@attach %!/INC`MSG=u(setr`%va,msg,File '[get(%q<db>/H`%q<topid>)]' Subfile '[get(%q<db>/H`%q<topid>`SUB`%q<subid>)]' Deleted!);@attach %!/INC`MSG`CHAN=%q<msg>;@attach %!/WIPE=%q<db>,H`%q<topid>`SUB`%q<subid>}

&INC`CATEGORY [u(cobj,help)]=@attach %!/INC`FINDMAIN=%0;@check strlen(%2)=@attach %!/INC`MSG=ERROR: New Category field empty.;@stop strmatch(%2,UNCATEGORIZED)=@attach %!/INC`MSG=ERROR: 'UNCATEGORIZED' is reserved for system use.;th u(attrib_set`%va,%q<db>,H`%q<topid>`CATEGORY,ucstr(%2));th u(attrib_set`%va,%q<db>,H`%q<topid>`TIMESTAMP,secs());@attach %!/INC`MSG=u(setr`%va,msg,File '[get(%q<db>/H`%q<topid>)]' moved to Category: [ucstr(%2)]);@attach %!/INC`MSG`CHAN=%q<msg>

&INC`VIEWCODE [u(cobj,help)]=@select/inline t(strlen(%0))[t(strlen(%1))]=10,{@attach %!/INC`GET`TOPIC},11,{@attach %!/INC`GET`SUB},{@attach %!/INC`MSG=ERROR: What are you retrieving?}

&INC`GET`TOPIC [u(cobj,help)]=@attach %!/INC`FINDMAIN=%0;@pemit %#=decompose(get(%q<db>/H`%q<topid>`CONTENTS))

&INC`GET`SUB [u(cobj,help)]=@attach %!/INC`FINDMAIN=%0;@attach %!/INC`FINDSUB=%1;@pemit %#=decompose(get(%q<db>/H`%q<topid>`SUB`%q<subid>`CONTENTS))

&INC`HELP [u(cobj,help)]=@pemit %#=u(header,Staff %q<comm> Commands);@pemit %#=[ansi(hc,Administrator Commands)]%R[align(5 [sub(u(width`%va,%#),6)],,{[ansi(h,%q<comm>/add <filename>=<text>)] - Creates or update a helpfile. If <text> is #DBREF/ATTRIBUTE format, the given attribute will be u()'d for retrieval.%R[ansi(h,%q<comm>/add <filename>/<subfilename>=<text>)] - Create or update a sub-helpfile..%R[ansi(h,%q<comm>/category <filename>=<category>)] - Assign or re-assign a helpfile to a category. Categories are arbitrary and do not need to be creative.%R[ansi(h,%q<comm>/rencategory <category>=<new name>)] - Re-assign all helpfiles of a given category to a new one.%R[ansi(h,%q<comm>/delete <filename>\[/<subfilename>\])] - Delete a helpfile or subfile.%R[ansi(h,%q<comm>/rename <filename>\[/<subfilename>\])]=<newname> - Rename a file/subfile.%R[ansi(h,%q<comm>/metatags <filename>=<list of tags>)] - Set or overwrite a helpfile's list of metatags used for searching.})];@pemit %#=u(subheader)

@@ MIGRATION TOOL
&MIGRATE`HELP [u(cobj,migrate)]=@check isdbref(u(cobj,help))=@attach %!/INC`MSG=ERROR: Cannot locate 2.0 Help Management System. Cannot continue.;@check isdbref(u(setr`%va,hso,u(%q<index>/hso)))=@attach %!/INC`MSG=ERROR: Cannot locate 1.0 Help System Object <HSO>. Cannot continue.;@dolist/inline HELP NEWS SHELP={@trigger %!/MIGRATE`HELP`INDIV=##};

&MIGRATE`HELP`INDIV [u(cobj,migrate)]=@check isdbref(u(setr`%va,hobj,get(%q<hso>/VAR`%0)));@check isdbref(u(setr`%va,dest,u(cobj,help-%0)));@dolist/inline u(setr`%va,files,u(lattr`%va,%q<hobj>/*))={@attach %!/INC`CPTREE=%q<hobj>,##,%q<dest>,H`#@;@mvattr %q<dest>/H`#@`NAME=%q<dest>/H`#@;&H`#@`CATEGORY %q<dest>=ucstr(get(%q<dest>/H`#@`CATEGORY))};@dolist/inline u(lattr`%va,%q<dest>/H`*`SUB`*)={@select/inline hasattr(%q<dest>/%i0`NAME)=1,{th u(attrib_set`%va,%q<dest>,%i0,get(%q<dest>/%i0`NAME))}};&SLOT %q<dest>=words(%q<files>)

&CLEANUP`HELP [u(cobj,migrate)]=@dolist/inline lsearch(all,type,player)={@attach %!/WIPE=##,D`HELPREAD}

&CONFLICT`HELP [u(cobj,migrate)]=@tel u(%q<index>/HSO)=u(cobj,oldbox)
