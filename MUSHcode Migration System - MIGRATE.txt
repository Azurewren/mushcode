@@ DEPENDENCIES: Core, Version 1
@@ This tool is used to migrate data from Version 1 of the Codesuite to Version 2.
@@ MAKE BACKUPS BEFORE YOU USE IT dammit.

th u(NEWCOBJ,MUSHcode Migration System <MIGRATE>,migrate,,,,WIZARD SAFE !NO_COMMAND,INHERIT SIDEFX SAFE)
@power [u(cobj,migrate)]=queue
@lock/use [u(cobj,migrate)]=ISWIZARD2/1
th u(NEWCOBJ,Old Code Box,oldbox,u(cobj,migrate),,1,WIZARD SAFE,INHERIT SIDEFX SAFE)

&SYSTEM`NAME [u(cobj,migrate)]=MIGRATE

&CMD`+MIGRATE`PENNMUSH [u(cobj,migrate)]=$^(?s)(?\:\+)?(migrate|cleanup|conflict)(?\:/(\S+)?)?(?\: +(.*))?$:@attach %!/CMD`+MIGRATE`MAIN
@set [u(cobj,migrate)]/CMD`+MIGRATE`PENNMUSH=regexp
&CMD`+MIGRATE`RHOSTMUSH [u(cobj,migrate)]=$^(?s)(?\:\+)?(migrate|cleanup|conflict)(?\:/(\\S+)?)?(?\: +(.*))?$:@attach %!/CMD`+MIGRATE`MAIN
@set [u(cobj,migrate)]/CMD`+MIGRATE`RHOSTMUSH=regexp
&CMD`+MIGRATE`MAIN [u(cobj,migrate)]=@attach %!/INC`GETSWITCH=%2;@include %!/INC`[u(strfirstof`%va,%q<switch>,MAIN)]=%3,%1
@set [u(cobj,migrate)]/CMD`+MIGRATE`[switch(%va,PennMUSH,RHOSTMUSH,RhostMUSH,PENNMUSH)]=no_command

&MIGRATE [u(cobj,migrate)]=iter(u(lattr`%va,%!/MIGRATE`*),last(%i0,`),%b,|)
&CLEANUP [u(cobj,migrate)]=iter(u(lattr`%va,%!/CLEANUP`*),last(%i0,`),%b,|)
&CONFLICT [u(cobj,migrate)]=iter(u(lattr`%va,%!/CONFLICT`*),last(%i0,`),%b,|)

&INDEX [u(cobj,migrate)]=u(find_in`%va,u(MASTER_ROOM),Code Object Index <COI>)

&INC`MAIN [u(cobj,migrate)]=@check isdbref(u(setr`%va,index,u(index)))=@attach %!/INC`MSG=ERROR: Version 1 'Code Object Index <COI>' not found in MASTER ROOM. Cannot continue.;th u(setq`%va,options,u(%1));@check strlen(%0)=@attach %!/INC`MSG=ERROR: [u(capnames,%1)] task name empty. Your options are: [u(itemize`%va,%q<options>,|,and,\,)];@attach %!/INC`PARTIAL=%0,%q<options>,|,choice,choice;@select/inline %1=CLEANUP,{@attach %!/INC`VERIFY={[ansi(hr,WARNING:)] This will wipe old attributes from players. Only do this if you've already converted! You want to Cleanup %q<choice>, correct? Type the same command again within ten seconds to continue.},CLEANUP %q<choice>};@attach %!/%1`%q<choice>

&CONFLICT`FINISH [u(cobj,migrate)]=@dolist/inline children(%q<index>)=@tel %i0=u(cobj,oldbox);@tel %q<index>=u(cobj,oldbox);@set %!=NO_COMMAND

&CMD`+OLDBOX [u(cobj,migrate)]=$+oldbox *:@dolist/inline %0={@halt %i0;@set %i0=NO_COMMAND;@tel %i0=u(cobj,oldbox)}