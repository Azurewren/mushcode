@switch/inline isdbref(u(award))=0,{@tel create(Award Tracker)=config(master_room)}
&award u(coi)=locate(config(master_room),Award Tracker,TXxi)
@parent u(award)=u(coi)
@set u(award)=WIZARD SAFE !NO_COMMAND
@lock/command u(award)=APPROVED/1
&APPROVED u(award)=approved(%#)

&CMD`+VOTE u(award)=$^(?s)(?\:\+)?(awards|vote)(?\:/(\S+)?)?(?\: +(.+?))?(?\:/(.+?))?(?\:=(.*))?$:@include u(ccs)/INC`PARTIAL=%2,setunion(get(u(award)/VAR`%1`PLAYFLAGS),if(isadmin(%#),get(u(award)/VAR`%1`ADMINFLAGS)),|,|),|,AWARD,switch,switch;@include u(award)/INC`%1`[strfirstof(%q<switch>,MAIN)]=%3,%4,%5
@set u(award)/CMD`+VOTE=regexp

&VAR`AWARDS`PLAYFLAGS u(award)=
&VAR`AWARDS`ADMINFLAGS u(award)=
&VAR`VOTE`PLAYFLAGS u(award)=
&VAR`VOTE`ADMINFLAGS u(award)=

&INC`AWARDS`MAIN u(award)=@include u(ccs)/INC`CHECKPC=strfirstof(%0,%#),1,AWARDS;@assert approved(%q<t1>)=@nspemit %#=announce(AWARDS) ERROR: %q<t1name> is not approved!;@nspemit %#=header(%q<t1name>'s Awards);@dolist/inline/delimit | [filterbool(#lambda/getstat(%q<t1>/D`VOTE`TALLIES,\%0),v(VAR`CATEGORIES),|,|)]={@nspemit %#=ansi(strfirstof(getstat(u(award)/VAR`CATEGORIES`COLORS,%i0),n),%i0): [strfirstof(ansi(strfirstof(getstat(u(award)/VAR`TIERS`COLORS,setr(award,before(last(filterbool(#lambda/gte(getstat(%q<t1>/D`VOTE`TALLIES,%i0),last(\%0,~)),sortkey(#lambda/last(\%0,~),v(VAR`TIERS),n,|,|),|,|),|),~))),n),%q<award>),N/A)]%RVotes: [getstat(%q<t1>/D`VOTE`TALLIES,%i0)]%R};@nspemit %#=header()

&INC`VOTE`MAIN u(award)=@assert strlen(%0)=@nspemit %#=announce(VOTE) ERROR: Who will you vote for?;@include u(ccs)/INC`CHECKPC=%0,1,VOTE;@assert approved(%q<t1>)=@nspemit %#=announce(VOTE) ERROR: %q<t1name> is not approved!;@break strmatch(%q<t1>,%#)=@nspemit %#=announce(VOTE) ERROR: Not happening even if you mind trick yourself.;@assert strlen(%1)=@nspemit %#=announce(VOTE) ERROR: You must choose a vote category. Your choices are: [itemize(v(VAR`CATEGORIES),|,and,\,)].;@include u(ccs)/INC`PARTIAL=%1,v(VAR`CATEGORIES),|,VOTE,choice,choice;@assert strlen(%2)=@nspemit %#=announce(VOTE) ERROR: You must enter a reason for the nomination!;@assert gt(setr(last,sub(secs(),getstat(%q<t1>/D`VOTE`TIMES,%:))),stringsecs(24h))=@nspemit %#=announce(VOTE) ERROR: You voted for them already [etime(%q<last>)] ago! 24 hours must pass between votes for the same character.;@nspemit %#=announce(VOTE) You have nominated %q<t1name> for a %q<choice> award!;@include u(award)/INC`VOTE`ADDVOTE=%:,%q<t1>,%q<choice>,%2

&INC`VOTE`ADDVOTE u(award)=th setstat(%1/D`VOTE`TALLIES,%2,add(getstat(%1/D`VOTE`TALLIES,%2),if(t(match(get(%1/D`VOTE`VOTEBYCATS`[edit(%2,%b,_)]),%0)),0.25,1)));th setstat(%1/D`VOTE`TIMES,%0,secs());&D`VOTE`VOTEBYCATS`[edit(%2,%b,_)] %1=filterbool(#lambda/isobjid(\%0),setunion(get(%1/D`VOTE`VOTEBYCATS`[edit(%2,%B,_)]),%0));@nspemit %1=announce(VOTE) [name(%0)] nominated you for a %2 award because: %3!;@switch/inline and(t(strlen(setr(award,before(grab(v(VAR`TIERS),*~[floor(getstat(%1/D`VOTE`TALLIES,%2))],|),~)))),not(match(v(VAR`AWARDED`[edit(%2,%b,_)]`%q<award>),objid(%1))))=1,{@mail %1=Award!/Congratulations [name(%1)]! You have just received a [ansi(strfirstof(getstat(u(award)/VAR`TIERS`COLORS,%q<award>),n),%q<award>)]-%2 Award!%R%RComments from the Voter, [name(%0)]: %3;+bbpost Awards/[name(%1)]: %q<award>-%2!=Congratulations to [name(%1)]! [poss(%1)] has been awarded a [ansi(strfirstof(getstat(u(award)/VAR`TIERS`COLORS,%q<award>),n),%q<award>)]-%2 Award!%R%RComments from the Voter, [name(%0)]: %3;&VAR`AWARDED`[edit(%2,%b,_)]`%q<award> u(award)=filterbool(#lambda/isobjid(\%0),setunion(v(VAR`AWARDED`[edit(%2,%b,_)]`%q<award>),objid(%1)))}

&VAR`CATEGORIES u(award)=Roleplayer|Jedi|Sith
&VAR`CATEGORIES`COLORS u(award)=Roleplayer~y|Jedi~hc|Sith~hr

&VAR`TIERS u(award)=Bronze~5|Silver~10|Gold~15|Platinum~20|Diamond~25
&VAR`TIERS`COLORS u(award)=Bronze~y|Silver~hw|Gold~hy|Platinum~h|Diamond~hc